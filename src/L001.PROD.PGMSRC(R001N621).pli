 /*   SIST ENDRET PÅ PROD   2006.07.18 11.16.46 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2006.04.24  9.41.21 AV   SPA2970          */        
 /*   SIST ENDRET PÅ QASS   2006.04.19 12.10.12 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2006.03.24  8.43.25 AV   SPA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.09.13 11.55.53 AV   HLA2970          */        
 /*   SIST ENDRET PÅ PROD   2000.01.06 11.57.35 AV   JDA7339          */        
 /*       SIST ENDRET 10/06-98 09.48.28 AV   TSB7339                  */        
 /*       SIST ENDRET 09/06-98 08.30.49 AV   SPA7339                  */        
 /*       SIST ENDRET 25/05-98 11.40.37 AV   SPA7339                  */        
  /* *************************************************************** */         
  /*IDENTIFIKASJON:                                                  */         
  /*    R001N621 - PROSEDYRE I PLI                                   */         
  /*    PROGRAMMERER: HERMAN           JAN 1991                      */         
  /*HENSIKT:                                                         */         
  /*    OPPRETTE PENSJONSSTATUS FOR SØKER FRA TRANSEN                */         
  /* *************************************************************** */         
  /*ENDRET:                                                          */         
  /*ENDRINGEN GJELDER:                                               */         
  /*ENDRET AV:                                                       */         
  /* *************************************************************** */         
  /* *************************************************************** */         
  /*PROGRAMTILKNYTNING:                                              */         
  /*    PROSEDYREN INNGÅR SOM EN DEL AV BEHANDLINGA AV UFØREPENSJON  */         
  /*    INCLUDES I R001N620 NÅR STATUS_TYPE = A, C, I, K             */         
  /*BRUK:                                                            */         
  /*    CALL OPPRETT_B02_PENSJONSSTATUS_U2_SØKER                     */         
  /* *************************************************************** */         
  %SKIP(2);                                                                     
 OPPRETT_STATUS_U2_SØKER:                                                       
    PROC;                                                                       
                                                                                
          DCL  ÅR_FØR_UFØRHET   PIC '9999';                                     
          DCL  YRK_IND  FIXED BIN(15);                                          
          DCL  M        FIXED BIN(15);                                          
                                                                                
          DCL  VILKÅR_DEF CHAR(2);                                              
          DCL  1 W_VILKÅR_1_2_2AB DEFINED VILKÅR_DEF,                           
                 3 W_VILKÅR_1_2_2A CHAR(1),                                     
                 3 W_VILKÅR_1_2_2B CHAR(1);                                     
                                                                                
                                                                                
 /* ***************************************************************** */        
 /* ENDRET AV KARIN 8/12-84                                           */        
 /* NÅR STATUS_TYPE = A OG STATUS_KODE_HIST = O ÅMD VI FØRST BLANKE   */        
 /* FELTENE SOM LIGGER I STATUS_SEGMENTENE.                           */        
 /* B01 ER FLYTTET OVER TIL B02 I R0010620.                           */        
 /* ***************************************************************** */        
      IF STATUS_TYPE_SØKER                = 'A' &                               
         B01.STATUS_KODE_HIST (SØKER_IND) = 'O' THEN                            
         DO;                                                                    
            B02.STATUS   (SØKER_IND)   = '';                                    
            B02.UFØRPENS (SØKER_IND)   = '';                                    
            B02.UFØRHIST (SØKER_IND,1) = '';                                    
            B02.UFØRHIST (SØKER_IND,2) = '';                                    
            B02.UFØRHIST (SØKER_IND,3) = '';                                    
            B02.UFØRHIST (SØKER_IND,4) = '';                                    
            B02.UFØRHIST (SØKER_IND,5) = '';                                    
            B02.UFØRHIST (SØKER_IND,6) = '';    /*9709 HL*/                     
            B02.UFØRHIST (SØKER_IND,7) = '';    /*9709 HL*/                     
            B02.YRKEPENS (SØKER_IND)   = '';                                    
            DO I = LBOUND(B02.YRKEHIST,2) TO HBOUND(B02.YRKEHIST,2);            
               B02.YRKEHIST (SØKER_IND,I) = '';                                 
            END;                                                                
                                                                                
            B02.ETTEPENS (SØKER_IND)   = '';                                    
            B02.ETTEBARN (SØKER_IND)   = '';                                    
            B02.SPESOPPL (SØKER_IND)   = '';                                    
         END;                                                                   
                                                                                
 /* ***************************************************************** */        
 /* STATUS_SEGMENTET                                                  */        
 /* ***************************************************************** */        
                                                                                
        IF HJ_VIRK_DATO_ÅMD < 19980200            THEN                          
           B02.PENSJON_FØR_9802(SØKER_IND) = 'J';                               
                                                                                
        IF U2S.TILL_EK = 'J' & HJ_VIRK_DATO_ÅMD > 19910400 THEN                 
           U2S.TILL_EK = ' ';                                                   
                                                                                
        IF U2S.BT_ANT > 0    & HJ_VIRK_DATO_ÅMD > 19910400 THEN                 
           U2S.BT_ANT = 0;                                                      
                                                                                
        IF U2S.YSKADE_TILLEGG = 'J' THEN                                        
           B02.PENSJONSTYPE1(SØKER_IND) = 'Y';                                  
        ELSE                                                                    
                                                                                
           B02.PENSJONSTYPE1(SØKER_IND) = 'U';                                  
                                                                                
       IF U2S.TILL_EK = 'J' THEN                                                
          B02.PENSJONSTYPE2(SØKER_IND) = 'F';                                   
       ELSE                                                                     
          B02.PENSJONSTYPE2(SØKER_IND) = 'P';                                   
                                                                                
 /* **************************************************************** */         
 /* TIDLIGERE FORSØRGET EKTEFELLE STOD IGJEN MED PENSJONSTYPE1 = 'F' */         
 /* OGSÅ ETTER AT TOTALGRADEN BLE UNDER 50 PROSENT.  DET SOM ER      */         
 /* RIKTIG, ER AT EKTEFELLEN SKAL HA PENSJONSTYPE1 = 'G'.            */         
 /* PENSJONISTEN SELV SKAL HA PENSJONSTYPE2 = 'P'.                   */         
 /* ERIK, 11.1.1990.                                                 */         
 /* **************************************************************** */         
                                                                                
       IF U2S.TILL_EK = ' ' &                                                   
          B01.PENSJONSTYPE2(SØKER_IND) = 'F' &                                  
          U2S.UFG < 50   THEN                                                   
          DO;                                                                   
             B02.PENSJONSTYPE2(SØKER_IND) = 'P';                                
             B02.PENSJONSTYPE1(EKTEF_IND) = 'G';                                
             B02.PENSJONSTYPE2(EKTEF_IND) = ' ';                                
          END;                                                                  
                                                                                
       HJ_UFT_ÅMD     = U2S.UFT_ÅMD;                                            
       ÅR_FØR_UFØRHET = HJ_UFT_Å - 1;                                           
                                                                                
       IF U2SP.STI_ÅMD > 0      THEN                                            
          HJ_ÅMD = U2SP.STI_ÅMD;                                                
       ELSE                                                                     
          HJ_ÅMD = U2S.UFT_ÅMD;                                                 
                                                                                
                                                                                
       B02.SIVILSTAND(SØKER_IND)           = U2S.SIVILSTAND;                    
       B02.NAVN      (SØKER_IND)           = U2S.NAVN;                          
       B02.SPRÅK     (SØKER_IND)           = U2S.SPRÅK;                         
       B02.TKNR      (SØKER_IND)           = U2S.TKNR;                          
                                                                                
                                                                                
       B02.INNT_DATO_ÅMD(SØKER_IND)  =  U2S.UFT_ÅMD ;                           
                                                                                
       IF U2S.YRKE                      > 0 THEN                                
          B02.YRKES_KODE(SØKER_IND)     = U2S.YRKE;                             
                                                                                
       IF U2S.INNTEKT_FØR_UP            > 0 THEN                                
          B02.INNTEKT_FØR_UP(SØKER_IND) = U2S.INNTEKT_FØR_UP / 100;             
                                                                                
 /* ***************************************************************** */        
 /* VERNEPLIKTSOPPLYSNINGER                                           */        
 /* ***************************************************************** */        
                                                                                
       DO I = 1 TO 4 WHILE (U2S.VP_ÅR(I) > 0);                                  
          B02.VERNEPLIKTÅR(SØKER_IND,I) = U2S.VP_ÅR(I);                         
       END;                                                                     
                                                                                
       B02.UFØRPENS.VILKÅR_8_4_3A(SØKER_IND) = U2S.VILKÅR_8_4_3A;               
       B02.UFØRPENS.UFG          (SØKER_IND) = U2S.UFG;                         
       B02.VIRK_DATO_ÅMD         (SØKER_IND) = HJ_VIRK_DATO_ÅMD;                
       B02.UFØRPENS.UTDANNING    (SØKER_IND) = U2S.UTDANNING;                   
       B02.UFØRPENS.YRKESM_ATTF  (SØKER_IND) = U2S.YRKESM_ATTF;                 
                                                                                
                                                                                
       IF STATUS_TYPE_SØKER = 'I' ! STATUS_TYPE_SØKER = 'K' THEN                
                /* SØKER HAR UFØREHISTORIKK SOM ALLTID ÅMD OVERFØRES */         
                                                                                
     DO;                                                                        
                                                                                
 /*YS---------------------------------------------------------------*/          
 /*YS  OPPHØRSDATO OG KODE SETTES I UFØRHIST-SEGMENTET              */          
 /*YS  DETTE ER BARE AKTUELT FOR TIDLIGER ENGANGSUTLØSNING . TRUDE  */          
 /*YS---------------------------------------------------------------*/          
                                                                                
                                                                                
          DO I = 1 TO 7;            /*0697 HL*/                                 
             IF B01.UFØRHIST.UFT_ÅMD(SØKER_IND,I)         > 0 &                 
                B01.UFØRHIST.OPPHØRSDATO_ÅMD(SØKER_IND,I)  = 0 THEN             
                DO;                                                             
                                                                                
                   IF B01.UFØRHIST.UFT_ÅMD(SØKER_IND,I) =                       
                      HJ_UFT_ÅMD              THEN     /*Y2K*/                  
        L261:            DO;                                                    
                             FEIL_VED_LABEL = '261';                            
                             FEIL_MELD_NR   =  1609;                            
                             GO TO RETUR;                                       
                         END;                                                   
                                                                                
                   ELSE                                                         
                   DO;                                                          
                      B02.UFØRHIST.OPPHØRSDATO_ÅMD(SØKER_IND,I) =               
                                                   HJ_UFT_ÅMD;                  
                      B02.UFØRHIST.OPPHØRSKODE(SØKER_IND,I)    =  'J';          
                      I                         =   7 ;  /*0697 HL*/            
                   END;                                                         
                END;                                                            
          END;                                                                  
                                                                                
 /*YS---------------------------------------------------------------*/          
 /*YS  OPPHØRSDATO OG KODE SETTES I YRKEHIST-SEGMENTET              */          
 /*YS  DETTE ER BARE AKTUELT FOR TIDLIGER ENGANGSUTLØSNING . TRUDE  */          
 /*YS---------------------------------------------------------------*/          
                                                                                
          DO I = LBOUND(B01.YRKEHIST,2) TO HBOUND(B01.YRKEHIST,2);              
                                                                                
             IF B01.YRKEHIST.YUFT_ÅMD(SØKER_IND,I)         > 0 &                
                B01.YRKEHIST.OPPH_DATO_ÅMD(SØKER_IND,I)  = 0 THEN               
                DO;                                                             
                   B02.YRKEHIST.OPPH_DATO_ÅMD(SØKER_IND,I) =                    
                                                HJ_UFT_ÅMD;                     
                   B02.YRKEHIST.OPPH_KODE(SØKER_IND,I)    =  'J';               
                   I  = HBOUND(B01.YRKEHIST,2);                                 
                END;                                                            
          END;                                                                  
                                                                                
          B02.FØRSTE_GANG_REG    (SØKER_IND) = 'N';                             
     END;                                                                       
       ELSE                                                                     
       IF B02.STATUS_KODE_HIST (SØKER_IND)  = 'X'  THEN                         
          B02.FØRSTE_GANG_REG    (SØKER_IND) = 'N';                             
       ELSE                                                                     
          B02.FØRSTE_GANG_REG    (SØKER_IND) = 'J';                             
                                                                                
                                                                                
 /* **************************************************************** */         
 /* ENDRET 14/1-84 AV KARIN                                          */         
 /* STATUS_KODE_HIST SETTES LIK BLANK                                */         
 /* FEIL NÅR TIDLIGERE AVSLUTTET UFØRETILFELLE BLIR UFØR IGJEN       */         
 /* **************************************************************** */         
                                                                                
       B02.STATUS_KODE_HIST (SØKER_IND) = ' ';                                  
                                                                                
       B02.TT_16_66(SØKER_IND) = U2SP.TT_FRA1937_ÅR * 12 +                      
                                 U2SP.TT_FRA1937_MND;                           
                                                                                
       IF B02.TT_16_66(SØKER_IND) > 480     THEN                                
          B02.TT_16_66(SØKER_IND) = 480;                                        
                                                                                
 /* ***************************************************************** */        
 /* SPESIELLE OPPLYSNINGER                                            */        
 /* ***************************************************************** */        
  %SKIP;                                                                        
       IF U2S.NORSK_BOSATT = 'N'  THEN                                          
         DO;                                                                    
           IF U2SP.TT_KONVENSJON_ÅR > 0            THEN                         
              DO;                                                               
                 B02.TT_16_66(SØKER_IND) = U2SP.TT_KONVENSJON_ÅR * 12;          
                 IF B02.TT_16_66(SØKER_IND) > 480     THEN                      
                    B02.TT_16_66(SØKER_IND) = 480;                              
              END;                                                              
                                                                                
          /*-----------------------------------------------------*/             
          /*FRAVIK FLYKTN GAV REDUSERT ANT_BUP_ÅR. TRUDE 091092--*/             
          /*-----------------------------------------------------*/             
                                                                                
          IF U2SP.FRAVIK_FLYKT  = 'J' THEN                                      
             B02.STATUS.TT_FRAMT(SØKER_IND) =                                   
                            (ALDER_67.ÅR * 12) - (HJ_Å4 * 12 + HJ_M);           
          ELSE                                                                  
             B02.STATUS.TT_FRAMT(SØKER_IND) = F_BEREGN_TT_FRAMT_UTLAND          
                     (B02.FNR(SØKER_IND),HJ_ÅMD,SØKER_IND);                     
                                                                                
          B02.SPESOPPL.STATSBORGER(SØKER_IND)   = U2SP.STATSBORGER;             
          B02.SPESOPPL.BOSATT(SØKER_IND)        = U2SP.BOSATT;                  
       B02.SPESOPPL.PENSJONSTRYGDET(SØKER_IND) = U2SP.PENSJONSTRYGDET;          
          B02.SPESOPPL.MINST_20ÅR_BOTID(SØKER_IND) = U2SP.MINST_20_ÅR;          
          B02.SPESOPPL.FRAVIK_BOSATT_KONVENSJON(SØKER_IND) =                    
                                                     U2SP.KONVENSJON;           
          B02.SPESOPPL.FRAVIK_FLYKTN(SØKER_IND) = U2SP.FRAVIK_FLYKT;            
                                                                                
          IF U2SP.OVERKOMP_UTL = 'J'      THEN                                  
             B02.SPESOPPL.OVERKOMP_UTL(SØKER_IND)  = 'J';                       
          ELSE                                                                  
             B02.SPESOPPL.OVERKOMP_UTL(SØKER_IND)  = 'N';                       
          B02.SPESOPPL.IKKE_GP      (SØKER_IND) = U2SP.IKKE_GP;                 
          B02.SPESOPPL.KONVENSJON(SØKER_IND) =  U2SP.BEREGNING_KONV;            
          B02.SPESOPPL.FRAVIK_8_1(SØKER_IND)  =  U2SP.FRAVIK_8_1_NR3;           
          B02.UFØRPENS.STI_ÅMD(SØKER_IND) = U2SP.STI_ÅMD ;                      
                                                                                
         END;                                                                   
                                                                                
       ELSE                                                                     
                                                                                
          B02.STATUS.TT_FRAMT(SØKER_IND) =                                      
              (ALDER_67.ÅR * 12) - (HJ_Å4 * 12 + HJ_M);                         
                                                                                
                                                                                
 /* ***************************************************************** */        
 /*                                                                   */        
 /* YRKESSKADEOPPLYSNINGER.  ERIK 2.7.87                              */        
 /*                                                                   */        
 /* ***************************************************************** */        
                                                                                
  %SKIP;                                                                        
                                                                                
        IF U2S.YSKADE_TILLEGG = 'J' THEN                                        
           DO;                                                                  
                                                                                
              DO I = LBOUND(B01.YRKEHIST,2) TO HBOUND(B01.YRKEHIST,2);          
                                                                                
                IF B01.YRKEHIST.YUFT_ÅMD(SØKER_IND,I) = 0 THEN                  
                   DO;                                                          
                      IF (I > 1)  & (B01.YRKEHIST.OPPH_DATO_ÅMD                 
                                    (SØKER_IND,(I-1)) >                         
                                              U2S.UFT_ÅMD ) THEN                
                         DO;                                                    
                                                                                
         /*  ********************************************* */                   
         /*  Y-UFØRETIDSPUNKT LIGGER FØR OPPHØRSTI_ÅMDDSPUNKT */                
         /*  ********************************************* */                   
                                                                                
                                                                                
        L264:                                                                   
                             FEIL_VED_LABEL = '264';                            
                             FEIL_MELD_NR   =  1719;                            
                             GO TO RETUR;                                       
                         END;                                                   
                      ELSE                                                      
                       DO;                                                      
                          YRK_IND = I;                                          
                          W_VILKÅR_1_2_2A =                                     
                                          U2Y.VILKÅR_1_2_2A;                    
                          W_VILKÅR_1_2_2B =                                     
                                          U2Y.VILKÅR_1_2_2B;                    
                          B02.VILKÅR_1_2_2(SØKER_IND,YRK_IND) =                 
                                                  VILKÅR_DEF;                   
                                    /*    W_VILKÅR_1_2_2AB  */                  
                                                                                
  /* ******************************************************* */                 
  /* HUSK:                                                   */                 
  /* TESTE PÅ OM B02.VILKÅR_1_2_2 = '  '. HVIS JA, SÅ VANLIG */                 
  /* ALDERSKONV.  HVIS NEI, SÅ SPESIELL ALDERSKONV.BEHANDLING*/                 
  /* ERIK .                                                  */                 
  /* ******************************************************* */                 
                                                                                
                          B02.YRKEHIST.YUFT_ÅMD(SØKER_IND,I) =                  
                                                     U2S.UFT_ÅMD ;              
                          B02.YST_ÅMD(SØKER_IND,YRK_IND) =                      
                                                     U2Y.YST_ÅMD ;              
                                                                                
                          B02.YRKEPENS.YSYKDOM(SØKER_IND) =                     
                                          U2Y.YSYKDOM;                          
                          DIV_PARAM_OMR.YSYKDOM  =                              
                                          U2Y.YSYKDOM;                          
                                                                                
                          B02.YRKEPENS.YUG(SØKER_IND) = U2Y.YUG;                
                          B02.YRKEHIST.YUG(SØKER_IND,YRK_IND) =                 
                                          U2Y.YUG;                              
                          B02.KAP_11_7_UTLØST(SØKER_IND,YRK_IND) =              
                                          U2Y.KAP_11_7_UTLØST;                  
                          B02.YRKE11(SØKER_IND,YRK_IND) = U2Y.YRKE11;           
                          B02.YRKEHIST.AÅI(SØKER_IND,YRK_IND)                   
                                                    = U2Y.AÅI;                  
                       END;                                                     
                     I =  HBOUND(B01.YRKEHIST,2)                                
                                                                                
                  END;                                                          
              ELSE                                                              
                                                                                
                    IF I = HBOUND(B01.YRKEHIST,2)THEN                           
                       DO;                                                      
                                                                                
 /* ***************************************************************** */        
 /* FEIL, ALLE YRKEPERIODENE ER UTFYLT, IKKE PLASS TIL EN TIL         */        
 /* ***************************************************************** */        
                                                                                
         L267:                                                                  
                          FEIL_VED_LABEL = '267';                               
                          FEIL_MELD_NR   = 1715;                                
                          GO TO RETUR;                                          
                       END;                                                     
                  END;                                                          
            END;                                                                
                                                                                
                                                                                
 /* ***************************************************************** */        
 /* PENSJONSGIVENDE INNTEKT                                           */        
 /* HER ÅMD VI TESTE PÅ EN PI-KODE FOR Å SE OM VI HAR FÅTT RAPPORTERT */        
 /* INNTEKT = 0                                                       */        
 /* ***************************************************************** */        
                                                                                
          IF (ÅR_FØR_UFØRHET            > 1966)     &                           
             (U2S.PI > 0                      )       THEN                      
            DO;                                                                 
               IF B01.PI_KODE( SØKER_IND,ÅR_FØR_UFØRHET) = ' ' !                
                  B01.PI_KODE( SØKER_IND,ÅR_FØR_UFØRHET) = 'F' THEN             
                  DO;                                                           
                     B02.PI_KODE(SØKER_IND,ÅR_FØR_UFØRHET) = 'F';               
                     B02.PI(SØKER_IND,ÅR_FØR_UFØRHET) = U2S.PI /100;            
                  END;                                                          
               ELSE   /*200103: KODE M BETYR OMSORG + FORELØPIG*/               
               IF B01.PI_KODE(SØKER_IND,ÅR_FØR_UFØRHET) = 'M'  THEN             
                     DO;                                                        
                        B02.PI_KODE(SØKER_IND,ÅR_FØR_UFØRHET) = 'M';            
                        B02.PI(SØKER_IND,ÅR_FØR_UFØRHET) =                      
                                                         U2S.PI / 100;          
                     END;                                                       
               ELSE                                                             
               IF  (B01.PI_KODE(SØKER_IND,ÅR_FØR_UFØRHET) = 'J' !               
                    B01.PI_KODE(SØKER_IND,ÅR_FØR_UFØRHET) = 'K' !               
                    B01.PI_KODE(SØKER_IND,ÅR_FØR_UFØRHET) = 'L' ) &             
                        B01.PI(SØKER_IND,ÅR_FØR_UFØRHET) = 0     THEN           
                     DO;                                                        
                        B02.PI_KODE(SØKER_IND,ÅR_FØR_UFØRHET) = 'M';            
                        B02.PI(SØKER_IND,ÅR_FØR_UFØRHET) =                      
                                                         U2S.PI / 100;          
                     END;                                                       
            END;                                                                
 /* ***************************************************************** */        
 /* UFØREHISTORIKK                                                    */        
 /* ***************************************************************** */        
    DO I = 1 TO 7;        /*0697 HL*/                                           
       IF (B01.UFØRHIST.UFT_ÅMD(SØKER_IND,I) =      U2S.UFT_ÅMD )               
          THEN                                                                  
 /* ***************************************************************** */        
 /* D.V.S VI HAR EN PERIODE FRA FØR MED SAMME UFTIDSPUNKT             */        
 /* ***************************************************************** */        
                                                                                
         DO;                                                                    
            DIV_PARAM_OMR.REGN_1291 = ' ';                                      
                                                                                
            UF_IND = I;                                                         
            B02.UFØRHIST.UFG(SØKER_IND,I)= U2S.UFG;                             
                                                                                
                                                                                
            IF U2S.VILKÅR_8_4_3A = 'J' THEN                                     
               IF U2S.YSKADE_TILLEGG = 'J' THEN                                 
                  IF U2Y.KAP_11_7_UTLØST = 'J'    THEN                          
                     B02.UFKRITERIER(SØKER_IND,I) = 5;                          
                  ELSE                                                          
                     B02.UFKRITERIER(SØKER_IND,I) = 4;                          
               ELSE                                                             
                  B02.UFKRITERIER(SØKER_IND,I) = 2;                             
            ELSE                                                                
               B02.UFKRITERIER(SØKER_IND,I) = 0;                                
            IF B01.BUP_GAR_KODE(SØKER_IND,I) = 'A' THEN                         
               DO;                                                              
                  IF HJ_VIRK_DATO_ÅMD >= TJUEÅRSDATO_ÅMD THEN                   
                     B02.BUP_GAR_KODE(SØKER_IND,I) = 'B';                       
               END;                                                             
            B02.UFØRHIST.OPPHØRSDATO_ÅMD(SØKER_IND,UF_IND) = 0;                 
            B02.UFØRHIST.OPPHØRSKODE   (SØKER_IND,UF_IND) = ' ';                
            I = 7;                                                              
         END;                                                                   
 /*         DO I = 1 TO 7;                                        */            
      ELSE     IF B01.UFØRHIST.UFT_ÅMD(SØKER_IND,I) = 0 THEN                    
                  DO;                                                           
                                                                                
 /* ***************************************************************** */        
 /* D.V.S. FINNER FØRSTE LEDIGE PLASS.                                */        
 /* ***************************************************************** */        
                                                                                
                     UF_IND = I;                                                
                     B02.UFØRHIST.UFT_ÅMD(SØKER_IND,I) =                        
                                                           U2S.UFT_ÅMD ;        
                     B02.UFØRHIST.UFG(SØKER_IND,I) = U2S.UFG;                   
                                                                                
 /*HL : UFKRIT BLE ALLTID SATT > 0 FOR YRKESSKADDE - RETTET 2.4.91*/            
                                                                                
                     IF U2S.VILKÅR_8_4_3A = 'J' THEN                            
                        IF U2S.YSKADE_TILLEGG = 'J' THEN                        
                           IF U2Y.KAP_11_7_UTLØST = 'J'    THEN                 
                              B02.UFKRITERIER(SØKER_IND,I) = 5;                 
                           ELSE                                                 
                              B02.UFKRITERIER(SØKER_IND,I) = 4;                 
                        ELSE                                                    
                           B02.UFKRITERIER(SØKER_IND,I) = 2;                    
                     ELSE;                                                      
                                                                                
 /* ***************************************************************** */        
 /* DETT ER FØRSTE PERIODE : 9708 HL :                                */        
 /* ***************************************************************** */        
                                                                                
            IF (UF_IND = 1                                    !                 
                B01.UFØRHIST.BUP_GAR_KODE(SØKER_IND,1) ^= ' ' ) THEN            
               /* --------------------------------------------------- */        
               /* ENDRET 040100: UNGE UFØRE SOM LÅ PÅ "VENT" NÅR DEI  */        
               /*                OPPHØRTE FIKK ALDRI BUP_GARANTI      */        
               /* --------------------------------------------------- */        
             CALL UNGE_UFØRE;                                                   
                                                                                
              IF UF_IND > 1 THEN                                                
                        DO; /* DET FINNES EN FORRIGE PERIODE */                 
                           IF B02.UFØRHIST.OPPHØRSDATO_ÅMD(                     
                              SØKER_IND,UF_IND - 1 ) >                          
                                                  (U2S.UFT_ÅMD) THEN            
                           DO;                                                  
                                                                                
 /* ***************************************************************** */        
 /* FEIL, UFØRETIDSPUNKTET LIGGER FØR OPPHØRSTI_ÅMDDSPUNKTET.         */        
 /* ***************************************************************** */        
                                                                                
  L265:                                                                         
                              FEIL_VED_LABEL = '265';                           
                              FEIL_MELD_NR   = 1719;                            
                              GO TO RETUR;                                      
                           END;                                                 
                                                                                
 /* ***************************************************************** */        
 /* HER OVERFØRES BUP-GARANTI OG KODE FRA FORRIGE PERIODE             */        
 /* ***************************************************************** */        
                                                                                
                           B02.BUP_GARANTI(SØKER_IND,I) =                       
                                    B02.BUP_GARANTI(SØKER_IND,(I-1));           
                           B02.BUP_GAR_KODE(SØKER_IND,I) =                      
                                   B02.BUP_GAR_KODE(SØKER_IND,(I-1));           
                           END;                                                 
                                                                                
                        I = 7;         /*0697 HL*/                              
                  END;                                                          
               ELSE                                                             
                  DO;                                                           
                                                                                
 /* ***************************************************************** */        
 /* ALLE UFØREPERIODENE ER UTFYLT                                     */        
 /* ***************************************************************** */        
                                                                                
                    IF I = 7 THEN        /*0697 HL*/                            
                       DO;                                                      
                                                                                
 /* ***************************************************************** */        
 /* FEIL, ALLE UFØREPERIODENE ER UTFYLT, IKKE PLASS TIL EN TIL        */        
 /* ***************************************************************** */        
                                                                                
  L270:                                                                         
                          FEIL_VED_LABEL = '270';                               
                          FEIL_MELD_NR   = 1715;                                
                          GO TO RETUR;                                          
                       END;                                                     
                  END;                                                          
            END;                                                                
                                                                                
                                                                                
    IF B02.BOSATT(SØKER_IND)         > 0 &                                      
       B02.VIRK_DATO_ÅMD(SØKER_IND)  > 19921200 &                               
       B02.PENSJONSTRYGDET(SØKER_IND) ^= 'J' &                                  
      (B02.UFØRHIST.BUP_GAR_KODE(SØKER_IND,1) = 'B'   !                         
       B02.UFØRHIST.BUP_GAR_KODE(SØKER_IND,1) = 'E')  THEN                      
         DO;                                                                    
            DO M = 1 TO 7;                                                      
             B02.BUP_GAR_KODE(SØKER_IND,M) = ' ';                               
             B02.BUP_GARANTI(SØKER_IND,M) = 0;                                  
            END;                                                                
          IF B02.UFØRPENS.STI_ÅMD(SØKER_IND) > 0 THEN                           
     L271:                                                                      
              DO;                                                               
                  FEIL_VED_LABEL = '271';                                       
                  FEIL_MELD_NR   =  386;                                        
                  GO TO RETUR;                                                  
              END;                                                              
         END;                                                                   
 /* ***************************************************************** */        
 /* EKTEFELLES STATUS                                                 */        
 /* ***************************************************************** */        
                                                                                
    IF B02.FNR(EKTEF_IND) > 0 THEN                                              
       DO;                                                                      
          B02.SIVILSTAND(EKTEF_IND)   = B02.SIVILSTAND(SØKER_IND);              
          B02.NAVN      (EKTEF_IND)   = B02.NAVN(EKTEF_IND);                    
          B02.SPRÅK     (EKTEF_IND)   = B02.SPRÅK(SØKER_IND);                   
          B02.VIRK_DATO_ÅMD(EKTEF_IND) =           (U2S.VIRK_DATO_ÅMD);         
          /* KS 5/6-84 */                                                       
          IF U2S.MINST4G = 'J' THEN                                             
             B02.PENSJONSTYPE2  (EKTEF_IND) = 'M';                              
          ELSE                                                                  
             B02.PENSJONSTYPE2  (EKTEF_IND) = 'U';                              
          B02.PENSJONSTYPE3  (EKTEF_IND) = ' ';                                 
          B02.SUM_YTELSE     (EKTEF_IND) = 0;                                   
                                                                                
          B02.FØRSTE_GANG_REG(EKTEF_IND) =                                      
          B02.FØRSTE_GANG_REG(SØKER_IND);                                       
                                                                                
          IF B01.STATUS_KODE_HIST(EKTEF_IND) = 'X'  THEN                        
             B02.FØRSTE_GANG_REG(EKTEF_IND) = 'N';                              
                                                                                
          IF U2S.TILL_EK = 'J' THEN                                             
             DO;                                                                
                B02.STATUS.PENSJONSTYPE1(EKTEF_IND) = 'F';                      
                B02.STATUS.PENSJONSTYPE2(EKTEF_IND) = ' ';                      
              END;                                                              
          ELSE                                                                  
             B02.STATUS.PENSJONSTYPE1(EKTEF_IND) = 'G';                         
        END;                                                                    
                                                                                
    IF U2S.ATT_UF = 'A' THEN                                                    
       IF (B01.PENSJONSTYPE1(SØKER_IND)    = 'U' !                              
           B01.PENSJONSTYPE1(SØKER_IND)    = 'Y')  &                            
          B01.PENSJONSTYPE3(SØKER_IND)   ^= 'A'    &                            
          B01.VIRK_DATO_ÅMD(SØKER_IND)   ^=                                     
                    (U2S.VIRK_DATO_ÅMD)         &                               
          B01.STATUS_KODE_HIST(SØKER_IND) = ' ' THEN                            
          DO;                                                                   
                                                                                
 /* ***************************************************************** */        
 /* FEIL,SØKEREN HAR UFØRPENSJON FRA FØR.                             */        
 /* ***************************************************************** */        
                                                                                
  L275:                                                                         
             FEIL_VED_LABEL = '275';                                            
             FEIL_MELD_NR   = 1717;                                             
             GO TO RETUR;                                                       
          END;                                                                  
       ELSE                                                                     
                                                                                
          IF B01.PENSJONSTYPE3(SØKER_IND) = 'A'     &                           
             (B01.PENSJONSTYPE1(SØKER_IND)   ^= 'U' &                           
              B01.PENSJONSTYPE1(SØKER_IND)   ^= 'Y') &                          
              B01.STATUS_KODE_HIST(SØKER_IND) = ' ' THEN                        
             DO;                                                                
                                                                                
 /* ***************************************************************** */        
 /* FEIL,SØKEREN HAR ATTFØRING FRA FØR.                               */        
 /* ***************************************************************** */        
                                                                                
 L277:                                                                          
                FEIL_VED_LABEL = '277';                                         
                FEIL_MELD_NR   = 1718;                                          
                GO TO RETUR;                                                    
             END;                                                               
          ELSE                                                                  
             B02.PENSJONSTYPE3(SØKER_IND) = 'A';                                
                                                                                
  RETUR:                                                                        
  END OPPRETT_STATUS_U2_SØKER;                                                  
