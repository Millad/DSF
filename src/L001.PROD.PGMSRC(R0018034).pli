 /*   SIST ENDRET PÅ PROD   2004.07.16 12.37.11 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.03.12 13.30.31 AV   JDA2970          */        
 /*********************************************************************/        
 /*                                                                   */        
 /* IDENTIFIKASJON:                                                   */        
 /*    R0018034   ET PROGR. :                                         */        
 /*    SPORBARHET               --------------------------            */        
 /*    PROGRAMMER :  SATISH PATHAK                                    */        
 /*    SKRIVET    :  10.01.2004                                       */        
 /*    PROGRAM KONTROLL PROGRAM    FOR                                */        
 /*       SKRIVING AV TRANSER               REVS DATA BASE D001DR0    */        
 /*                                                                   */        
 /*********************************************************************/        
                                                                                
 OPPDATE_DR_TRANSER: PROC(FNR_UB);                                              
                                                                                
 DCL FNR_UB  FIXED DEC(11); /* FNR UNDER BEHANDLING  */                         
                                                                                
 DCL 1 RF0PERSN          BASED(IO_PEKER),                                       
      %INCLUDE P0019927;                                                        
                                                                                
 DCL 1 TRANHIST     BASED (IO_PEKER),                                           
     %INCLUDE P0019940;                                                         
                                                                                
 DCL 1 PERSN,                                                                   
      %INCLUDE P0018011;                                                        
    /* -------------------------------------------------------------- */        
    /*   SøNADBREV INFO           P0018012                            */        
    /* -------------------------------------------------------------- */        
  DCL  1 TRANS_INFO  ,                                                          
      %INCLUDE P0018015;                                                        
                                                                                
 /*-------------------------------------------------------------------*/        
 /*     SSA                                                           */        
 /*-------------------------------------------------------------------*/        
  DCL 1 SSA1_DR0PERSN           STATIC,                                         
        2    HDEL                   CHAR (17)    INIT                           
                               ('DR0PERSN(FNR     '),                           
        2    REL_OP                CHAR (2)     INIT (' =')     ,               
        2    PKEY              FIXED     DEC  (11)    INIT ( 0  ),              
        2    HP                    CHAR (1)     INIT (')' )     ;               
                                                                                
                                                                                
     DCL 1  SSA1_TRANHIST_DR          UNALIGNED,                                
            2    HDEL                            CHAR (17) INIT                 
                 ('TRANHIST(REGDTKEY'),                                         
            2    REL_OP                          CHAR (2)  INIT (' ='),         
            2    TTP_KEY            FIXED        DEC (15)  INIT ( 0 ),          
            2    VTP_KEY            FIXED        DEC  (9)  INIT ( 0 ),          
            2    GRBLKODE                        CHAR (2)  INIT ('  '),         
            2    HP                              CHAR (1)  INIT (')');          
     DCL 1  W01                 UNALIGNED,                                      
            2   FUNC               CHAR     (4)   INIT('    '),                 
            2   GU                 CHAR     (4)   INIT('GU  '),                 
            2   GHU                CHAR     (4)   INIT('GHU '),                 
            2   GNP                CHAR     (4)   INIT('GNP '),                 
            2   DLET               CHAR     (4)   INIT('DLET'),                 
            2   REPL               CHAR     (4)   INIT('REPL'),                 
            2   ISRT               CHAR     (4)   INIT('ISRT'),                 
            2   S_MELDNR           PIC '9'        INIT (0),                     
            2   PARM_CT_1          FIXED BIN(31)  INIT(1),                      
            2   PARM_CT_2          FIXED BIN(31)  INIT(2),                      
            2   PARM_CT_3          FIXED BIN(31)  INIT(3),                      
            2   PARM_CT_4          FIXED BIN(31)  INIT(4),                      
            2   PARM_CT_5          FIXED BIN(31)  INIT(5),                      
            2   PARM_CT_6          FIXED BIN(31)  INIT(6),                      
            2   PARM_CT_7          FIXED BIN(31)  INIT(7);                      
 /*-------------------------------------------------------------------*/        
 /* PCB-OMRÅDER                                                       */        
 /*-------------------------------------------------------------------*/        
    DCL 1  DR1     BASED    (DR1_PCB_PEKER),                                    
    %INCLUDE P0012003;                                 /* PCB-OMRÅDE  */        
                                                                                
    DCL IO_PEKER             POINTER;                                           
    DCL IO_AREA              CHAR(400);                                         
                                                                                
    DCL  DATE_TID              PIC '(15)9';                                     
    DCL  DATO                  PIC '(15)9';                                     
    DCL  DATO_REV              PIC '(15)9';                                     
    DCL  01 DATE_CH               CHAR(15);                                     
    DCL  BLANKET_KODE             CHAR(1);                                      
                                                                                
 /*-------------------------------------------------------------------*/        
 /*                                                                   */        
 /*      S T A R T   P R O G R A M   R0019X30                         */        
 /*                                                                   */        
 /*-------------------------------------------------------------------*/        
       IO_PEKER = ADDR(IO_AREA);                                                
                                                                                
                                                                                
       DATE_CH = DATETIME;                                                      
       DATO      = SUBSTR(DATE_CH,1,15 );                                       
       DATO_REV  = 999999999999999 - DATO    ;                                  
       CALL KONTROLL_ROT;                                                       
                                                                                
       CALL KONTROLL_TRANHIST ;                                                 
 /* ************************************************************ */             
 /* FINNES ROT ELLER SKRIVE NY ROT                              */              
 /* ************************************************************ */             
                                                                                
 KONTROLL_ROT: PROC;                                                            
 /*                                                                             
  SSA1_RF0PERSN_RF.PKEY = FNR_UB;                                               
  */                                                                            
  CALL LES_ROT_RF_DATABASE;                                                     
                                                                                
  SSA1_DR0PERSN.PKEY = FNR_UB;                                                  
                                                                                
  CALL LES_ROT_DR;                                                              
  IF DR1.STATUS_KODE = '  '  THEN; /* ROT         FUNNET */                     
  ELSE                                                                          
  IF DR1.STATUS_KODE = 'GE'  THEN   /* ROT         IKKE FUNNET  */              
     DO;                                                                        
        PERSN.FNR        = RF0PERSN.FNR;                                        
        PERSN.NAVN       = RF0PERSN.NAVN;                                       
        PERSN.TKNR       = RF0PERSN.TKNR;                                       
        PERSN.PERSN_KODE = RF0PERSN.PERSN_KODE;                                 
                                                                                
                                                                                
        SSA_UQUAL     =  'DR0PERSN';                                            
         CALL ISRT_ROT_DR;                                                      
     END;                                                                       
  ELSE                                                                          
      DO;                                                                       
 L065:                                                                          
       FEIL_MELD_NR = 500;                                                      
       FEIL_VED_LABEL = 'L065';                                                 
       DB_STATUS_KODE  = DR1.STATUS_KODE;                                       
       GO TO SLUTT_8034;                                                        
     END;                                                                       
 END KONTROLL_ROT;                                                              
 /* ********************************************************** */               
 KONTROLL_TRANHIST: PROC;                                                       
   BLANKET_KODE   = ' ';                                                        
    CALL LES_TRANHIST_RF_DATABASE ;                                             
                                                                                
    DO WHILE (BLANKET_KODE = '  '  );                                           
      SELECT (RF1.SEGM_NAVN );                                                  
        WHEN ('TRANHIST')                                                       
          DO;                                                                   
             SSA1_TRANHIST_DR.TTP_KEY  = DATO_REV;                              
             SSA1_TRANHIST_DR.VTP_KEY  = TRANHIST.VTP_KEY;                      
             SSA1_TRANHIST_DR.GRBLKODE = TRANHIST.GRBLKODE;                     
                                                                                
             TRANS_INFO.TRANS_DATOTID   =  DATO_REV;                            
             TRANS_INFO.VTP_KEY         =  TRANHIST.VTP_KEY;                    
             TRANS_INFO.GRBLKODE        =  TRANHIST.GRBLKODE;                   
             TRANS_INFO.PRIORITETS_KODE =  TRANHIST.PRIORITETS_KODE;            
             TRANS_INFO.VIRK_DATO_ÅMD   =  TRANHIST.VIRK_DATO_ÅMD;              
             TRANS_INFO.REGDATO_ÅMD     =  TRANHIST.REGDATO_ÅMD;                
             TRANS_INFO.TKNR            =  TRANHIST.TKNR;                       
             TRANS_INFO.SIVILSTAND      =  TRANHIST.SIVILSTAND ;                
             TRANS_INFO.SPRÅK           =  TRANHIST.SPRÅK;                      
             TRANS_INFO.TRANS_RETURKODE =  TRANHIST.TRANS_RETURKODE ;           
             TRANS_INFO.TERMID          =  TRANHIST.TERMID ;                    
             TRANS_INFO.AUTOS           =  TRANHIST.AUTO ;                      
             TRANS_INFO.BRUKER_ID = DIV_PARAM_OMR.BRUKER_ID;                    
             SSA_UQUAL     =  'TRANHIST';                                       
             CALL ISRT_BARN_AV_ROOT_DR;                                         
                                                                                
                 END;                                                           
             OTHERWISE                                                          
                 DO;                                                            
                    CALL ISRT_BARN_AV_TRAN_DR;                                  
                 END;                                                           
            END;                                                                
             CALL LES_BLANKET_RF_GNP;                                           
       END;                                                                     
                                                                                
                                                                                
                                                                                
 END KONTROLL_TRANHIST;                                                         
 /* ********************************************************** */               
    LES_ROT_RF_DATABASE : PROC;                                                 
                                                                                
        CALL PLITDLI                          (W01.PARM_CT_4,                   
                                               W01.GU,                          
                                               RF1,                             
                                               IO_AREA,                         
                                               SSA1_RF0PERSN);                  
                                                                                
                 IF  DLIUIB.UIBRCODE.UIBFCTR ^=       UIB_RC_OK                 
                   ! RF1.STATUS_KODE         ^=       '  '         THEN         
                   DO;                                                          
                     /* ******************************************** */         
                     /* FEIL VED LES   AV :  ROT                     */         
                     /* ******************************************** */         
         L101:                                                                  
                     FEIL_MELD_NR             =       500 ;                     
                     FEIL_VED_LABEL           =      '101';                     
                     DB_STATUS_KODE           =       RF1.STATUS_KODE;          
                     GO   TO  SLUTT_8034;                                       
                   END;                                                         
                                                                                
                                                                                
                                                                                
    END LES_ROT_RF_DATABASE;                                                    
                                                                                
 /* ********************************************************** */               
    LES_TRANHIST_RF_DATABASE : PROC;                                            
           CALL PLITDLI                        (W01.PARM_CT_5,                  
                                                W01.GHU,                        
                                                RF1,                            
                                                IO_AREA,                        
                                                SSA1_RF0PERSN   ,               
                                                SSA1_TRANHIST);                 
                                                                                
               IF  DLIUIB.UIBRCODE.UIBFCTR ^=       UIB_RC_OK                   
                 ! RF1.STATUS_KODE         ^=       '  '         THEN           
                 DO;                                                            
                   /* ******************************************** */           
                   /* FEIL VED LES   AV :  TRANHIST                */           
                   /* ******************************************** */           
       L111:                                                                    
                   FEIL_MELD_NR             =       500 ;                       
                   FEIL_VED_LABEL           =      '111';                       
                   DB_STATUS_KODE           =       RF1.STATUS_KODE;            
                   GO   TO  SLUTT_8034;                                         
                 END;                                                           
              ELSE                                                              
                  BLANKET_KODE   = ' ';                                         
                                                                                
                                                                                
    END LES_TRANHIST_RF_DATABASE ;                                              
 /* ********************************************************** */               
 /* LESE ALLE BLAKET UNDER TRANHIST                            */               
 /* ********************************************************** */               
    LES_BLANKET_RF_GNP : PROC;                                                  
           CALL PLITDLI                        (W01.PARM_CT_3,                  
                                                W01.GNP,                        
                                                RF1,                            
                                                IO_AREA);                       
                                                                                
                                                                                
                                                                                
               IF  DLIUIB.UIBRCODE.UIBFCTR ^=  UIB_RC_OK  THEN                  
                 DO;                                                            
                   /* ******************************************** */           
                   /* FEIL VED LES   AV :  TRANHIST                */           
                   /* ******************************************** */           
       L112:                                                                    
                   FEIL_MELD_NR             =       500 ;                       
                   FEIL_VED_LABEL           =      '112';                       
                   DB_STATUS_KODE           =       RF1.STATUS_KODE;            
                   GO   TO  SLUTT_8034;                                         
                 END;                                                           
               ELSE                                                             
               IF  RF1.STATUS_KODE   =  '  '  !                                 
                   RF1.STATUS_KODE   =  'GA'  THEN                              
                   BLANKET_KODE   = ' ';                                        
               ELSE                                                             
                   BLANKET_KODE   = 'N';                                        
                                                                                
    END LES_BLANKET_RF_GNP;                                                     
 /* ********************************************************** */               
 /* ********************************************************** */               
 LES_ROT_DR: PROC;                                                              
                                                                                
    CALL PLITDLI                                (W01.PARM_CT_4    ,             
                                                 W01.GU           ,             
                                                 DR1          ,                 
                                                 PERSN        ,                 
                                                 SSA1_DR0PERSN );               
                                                                                
                                                                                
                                                                                
 END LES_ROT_DR;                                                                
    /*----------------------------------------------------------------*/        
    /* SKRIVE ROT                                                     */        
    /*----------------------------------------------------------------*/        
 ISRT_ROT_DR: PROC;                                                             
    CALL PLITDLI                                (W01.PARM_CT_4    ,             
                                                 W01.ISRT         ,             
                                                 DR1          ,                 
                                                 PERSN        ,                 
                                                 SSA_UQUAL    );                
                                                                                
                                                                                
                                                                                
    IF DR1.STATUS_KODE        ^=         '  '    THEN                           
      DO;                                                                       
 L067:                                                                          
       FEIL_MELD_NR = 500;                                                      
       FEIL_VED_LABEL = 'L067';                                                 
       DB_STATUS_KODE  = DR1.STATUS_KODE;                                       
       GO TO SLUTT_8034;                                                        
     END;                                                                       
                                                                                
 END ISRT_ROT_DR;                                                               
                                                                                
 /*----------------------------------------------------------------*/           
 /* SKRIVE BARN AV ROT                                             */           
 /*----------------------------------------------------------------*/           
 ISRT_BARN_AV_ROOT_DR:  PROC;                                                   
                                                                                
    CALL PLITDLI                                (W01.PARM_CT_5    ,             
                                                 W01.ISRT   ,                   
                                                 DR1          ,                 
                                                 TRANS_INFO    ,                
                                                 SSA1_DR0PERSN,                 
                                                 SSA_UQUAL);                    
                                                                                
                                                                                
    IF DR1.STATUS_KODE        ^=         '  '    THEN                           
      DO;                                                                       
 L068:                                                                          
       FEIL_MELD_NR = 500;                                                      
       FEIL_VED_LABEL = 'L068';                                                 
       DB_STATUS_KODE  = DR1.STATUS_KODE;                                       
       GO TO SLUTT_8034;                                                        
     END;                                                                       
                                                                                
 END ISRT_BARN_AV_ROOT_DR;                                                      
 /*----------------------------------------------------------------*/           
 /* SKRIVE BARN AV ROT                                             */           
 /*----------------------------------------------------------------*/           
 ISRT_BARN_AV_TRAN_DR:  PROC;                                                   
      SSA_UQUAL = RF1.SEGM_NAVN  ;                                              
      CALL PLITDLI                         (W01.PARM_CT_6,                      
                                            W01.ISRT,                           
                                            DR1,                                
                                            IO_AREA,                            
                                            SSA1_DR0PERSN,                      
                                            SSA1_TRANHIST_DR,                   
                                            SSA_UQUAL);                         
                                                                                
      IF   DLIUIB.UIBRCODE.UIBFCTR    ^=    UIB_RC_OK                           
        !  DR1.STATUS_KODE            ^=   '  '    THEN                         
        DO;                                                                     
           /* ******************************************** */                   
           /* FEIL VED SKRIV AV :  TRANS                   */                   
           /* ******************************************** */                   
  L069:                                                                         
           FEIL_MELD_NR                =    500 ;                               
           FEIL_VED_LABEL              =   '069';                               
           DB_STATUS_KODE              =    DR1.STATUS_KODE;                    
           GO TO SLUTT_8034;                                                    
        END;                                                                    
                                                                                
 END ISRT_BARN_AV_TRAN_DR;                                                      
 SLUTT_8034:                                                                    
  END OPPDATE_DR_TRANSER ;  /*END  OF PROGRAM  */                               
