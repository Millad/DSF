 /*   SIST ENDRET PÅ PROD   2008.08.01  8.36.48 AV   JDA2990          */        
 /*   SIST ENDRET PÅ PROD   2003.10.02  9.11.08 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2003.09.12 11.30.55 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2002.12.20  8.56.23 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2002.04.08  8.44.28 AV   HLA2970          */        
 /*   SIST ENDRET PÅ PROD   2000.08.25 14.50.39 AV   HLA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.07.13 12.59.34 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.05.23 13.09.23 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.01.04  8.50.02 AV   JDA7339          */        
 /*       SIST ENDRET 18/03-99 09.16.37 AV   JDA7339                  */        
 /*       SIST ENDRET 18/03-99 09.13.33 AV   JDA7339                  */        
 /*       SIST ENDRET 04/05-98 13.26.00 AV   JDA7339                  */        
 /*       SIST ENDRET 11/03-98 12.12.04 AV   JDA7339                  */        
 /*       SIST ENDRET 18/01-83 12.10.40 AV   OLSEN                    */        
  /* *************************************************************** */         
  /* IDENTIFIKASJON:   R0017048 - PROSEDYRE I PLI                    */         
  /* PROGRAMMERER:  JAN, JANUAR 1983                                 */         
  /* HENSIKT:                                                        */         
  /*        LEGGER INFORMASJON FRA B02 OVER I ET BERGRUNL-SEGMENT_DØD*/         
  /*        I STØNADSBREV-BASEN . PROGRAMMET KALLES OPP ETTER        */         
  /*        HVER AJOURFØRT TRANSAKSJON.                              */         
  /* PROGRAMTILKNYTNING:                                             */         
  /*        PROGRAMMET INKLUDERES I  R0017001                        */         
  /* BRUK:                                                           */         
  /*        CALL BYGG_OPP_BERGRUNL_SEGMENT_DØD                       */         
  /* *************************************************************** */         
  BYGG_OPP_BERGRUNL_SEGMENT_DØD:                                                
      PROC(IND);                                                                
                                                                                
    DCL W_FNR13       PIC '(13)9'  INIT (0);                                    
    DCL IND           FIXED BIN (15);                                           
    DCL SEKSTI_NIÅR   PIC '9999' INIT (0);                                      
    DCL SEKSTI_SEKSÅR PIC '9999' INIT (0);                                      
    DCL SKR_AVDØD     BIT (1);                                                  
    DCL KODE_FINNES   BIT(1)  INIT ('0'B);                                      
                                                                                
    DCL HJ_SISTE_UFTP PIC '(8)9';  /* JFA */                                    
    DCL SISTE_UFÅR    PIC '(4)9';                                               
    DCL M             FIXED BIN (15);                                           
                                                                                
    DCL HJ_DØDSÅR  PIC '(8)9';                                                  
    DCL DØDSÅR     PIC '(4)9';                                                  
                                                                                
    DCL                                                                         
        1 W_FNR_R13 DEF W_FNR13,                                                
          2 DAG   PIC '99',                                                     
          2 MÅNED PIC '99',                                                     
          2 ÅR    PIC '9999',                                                   
          2 F1900 PIC '999',                                                    
          2 REST  PIC '99';                                                     
                                                                                
  /* *************************************************************** */         
  /*  PROGRAMMET BEGYNNER HER. VI BLANKER W_BERGRUNL_AVDØD           */         
  /* *************************************************************** */         
                                                                                
    HJ_SISTE_UFTP = 0;  /* JFA 7.8.2002 */                                      
    DO M = 1 TO 7 WHILE (B02.UFØRHIST.UFT_ÅMD(IND,M) > 0); /* JFA */            
       HJ_SISTE_UFTP = (B02.UFØRHIST.UFT_ÅMD(IND,M));                           
       SISTE_UFÅR = SUBSTR(HJ_SISTE_UFTP,1,4);                                  
    END;                                                                        
                                                                                
    HJ_DØDSÅR  = 0;                                                             
    DØDSÅR     = 0;                                                             
                                                                                
    W_BERGRUNL_AVDØD = '';                                                      
    W_FNR13          = KONV_FNR11_FNR13((B02.FNR(IND)));                        
    SEKSTI_NIÅR      = W_FNR_R13.ÅR + 69;                                       
    SEKSTI_SEKSÅR    = SEKSTI_NIÅR - 3;                                         
    SKR_AVDØD        = '0'B;                                                    
    HJ_DØDSÅR        = (B02.DØDSDATO_ÅMD(IND));                                 
    DØDSÅR           = SUBSTR(HJ_DØDSÅR,1,4);                                   
                                                                                
    DO I = (SEKSTI_SEKSÅR+1) TO SEKSTI_NIÅR;                                    
       IF POREKKE.KODE(IND,I) ^= 'U91'    &                                     
          POREKKE.KODE(IND,I) ^= ' '      THEN                                  
          KODE_FINNES = '1'B;                                                   
    END;                                                                        
                                                                                
    DO AVDIND    = 1963 TO SEKSTI_NIÅR;                                         
       W_BERGRUNL_AVDØD.ÅR_AVDØD         = AVDIND;                              
       W_BERGRUNL_AVDØD.UFG_AVDØD        = POREKKE.UFG(IND,AVDIND);             
       W_BERGRUNL_AVDØD.POENG_AVDØD      = POREKKE.POENG(IND,AVDIND);           
       W_BERGRUNL_AVDØD.POENG_KODE_AVDØD = POREKKE.KODE(IND,AVDIND);            
       IF AVDIND > 1966 &                                                       
          AVDIND < 2011    THEN                                                 
          W_BERGRUNL_AVDØD.P_INNTEKT_AVDØD  = B02.PI(IND,AVDIND);               
       IF POREKKE.KODE(IND,AVDIND) = 'U91'  &   /* JFA (FRA 7047) */            
          AVDIND = SEKSTI_SEKSÅR       THEN                                     
          W_BERGRUNL.P_INNTEKT   =  0;                                          
       IF POREKKE.KODE(IND,AVDIND) = 'U54' !                                    
          POREKKE.KODE(IND,AVDIND) = 'U60' !                                    
          POREKKE.KODE(IND,AVDIND) = 'U61' !                                    
          POREKKE.KODE(IND,AVDIND) = 'U64' THEN                                 
          DO;                                                                   
             IF W_BERGRUNL_AVDØD.ÅR_AVDØD < 1973      THEN                      
                AVDIND = SEKSTI_NIÅR;                                           
             ELSE                                                               
             IF AVDIND < SEKSTI_SEKSÅR     THEN                                 
                AVDIND = SEKSTI_SEKSÅR;                                         
 /*??*/      W_HENDELSE.EØS_GARANTI = B02.EØS_GARANTI(IND);                     
          END;                                                                  
                                                                                
         IF (W_BERGRUNL_AVDØD.UFG_AVDØD        > 49 &                           
             W_BERGRUNL_AVDØD.POENG_KODE_AVDØD = '' &                           
             AVDIND >= DØDSÅR        &    /* 200807     */                      
             AVDIND >= SISTE_UFÅR        &                                      
             AVDIND >  1991              &   /*200009 HL FRA 7047 */            
             B02.PENSJONSTYPE1(IND) ^= 'A') THEN /* JFA 7.8.2002 */             
                  AVDIND = SEKSTI_NIÅR;                                         
                                                                                
         IF (AVDIND >= SEKSTI_SEKSÅR &                                          
             ^KODE_FINNES            &                                          
             AVDIND >= DØDSÅR        &    /* JFA 6.5.03 */                      
             W_FNR_R13.ÅR > 1905 )   THEN     /* & HL 200012   */               
    /*       B02.TT_67_TIL_70(IND) = 0 )  THEN   JFA 20.8.02 */                 
                  AVDIND = SEKSTI_NIÅR;                                         
                                                                                
       IF (W_BERGRUNL_AVDØD.POENG_KODE_AVDØD = '' &                             
           W_BERGRUNL_AVDØD.POENG_AVDØD      = 0  &                             
           AVDIND >= DØDSÅR )    THEN                                           
             AVDIND = SEKSTI_NIÅR;                                              
                                                                                
  /* JF 21.8.2002 */                                                            
       IF (AVDIND > SEKSTI_SEKSÅR & AVDIND = DØDSÅR) THEN                       
             AVDIND = (SEKSTI_NIÅR + 1);                                        
                                                                                
       IF SKR_AVDØD = '1'B &                                                    
          AVDIND < 2011    &                                                    
          AVDIND <= SEKSTI_NIÅR   THEN  /*FRA < TIL <= JFA 7.8.02 */            
             CALL INSERT_BERGRAVD;                                              
       ELSE                                                                     
       IF (W_BERGRUNL_AVDØD.POENG_AVDØD     > 0 )  !                            
         ((W_BERGRUNL_AVDØD.P_INNTEKT_AVDØD > 0 )  &                            
          (AVDIND < 2011) )                        !                            
          (W_BERGRUNL.POENG_KODE   = 'U55' )       !  /*7.8.02*/                
          (W_BERGRUNL.POENG_KODE   = 'U56' )     THEN                           
          DO;                                                                   
             CALL INSERT_BERGRAVD;                                              
             SKR_AVDØD = '1'B;                                                  
          END;                                                                  
    END;                                                                        
  END  BYGG_OPP_BERGRUNL_SEGMENT_DØD;                                           
                                                                                
