 /*  DETTE PROGRAM ER FOR STOPPE BRUKER Å REGISTERE BLANKETT I PROD  */         
 /*  DETTE PROGRAM ER FOR STOPPE BRUKER Å REGISTERE BLANKETT I PROD  */         
 /*  DETTE PROGRAM ER FOR STOPPE BRUKER Å REGISTERE BLANKETT I PROD  */         
 /*  DETTE PROGRAM ER FOR STOPPE BRUKER Å REGISTERE BLANKETT I PROD  */         
 /*       SIST ENDRET 19/11-97 08.44.08 AV   SPA7339                  */        
 R00103:                                                                        
   PROC(COMMAREA_PEKER) OPTIONS(MAIN);                                          
                                                                                
 /******************************************************************* */        
 /*IDENTIFIKASJON:                                                    */        
 /************************                                            */        
 /*  PROGRAM-IDENT : R0010301 - VALG AV FUNKSJON - HOVEDPROGRAM       */        
 /*  PROGRAM-SPRÅK : PLI / CICS                                       */        
 /*  PROGRAMMERER  : ?                                                */        
 /*  PROGRAMMET BLE LAGET  ?                                          */        
 /*  ENDRINGERSDATO :                                                 */        
 /*  ENDRINGEN GJELDER:                                               */        
 /*  ENDRINGEN BLE UTFØRT AV :                                        */        
 /*                                                                   */        
 /******************************************************************* */        
 /*HENSIKT:                                                           */        
 /************                                                        */        
 /*                                                                   */        
 /*  PROGRAMMET ER MED I STYRINGEN AV MENYEN VED OPPSTARTINGEN        */        
 /*  AV FOLKETRYGDSYSTEMET. PROGRAMMET LESER INN FUNKSJONSKODEN       */        
 /*  OG KONTROLLERER DEN OG SENDER UT NESTE BILDET I DIALOGEN.        */        
 /*  PROGRAMMETS TRANSKODE ER R030.                                   */        
 /*  PROGRAMMET BLIR AKTIVISERT NÅR OPERATØREN HAR TASTET INN         */        
 /*  FUNKSJONSKODEN, MAP S001013 - MAPSET R001013:                    */        
 /*  PROGRAMMET LESER INN FUNKSJONSKODEN, KONTROLLERER DEN OG SENDER  */        
 /*  UT NESTE BILDE SOM ER AVHENGIG AV FUNKSJONSKODEN :               */        
 /*   A --->  NYTT BILDE SENDES FRA R0010420                          */        
 /*   E --->  MAP S00101E MAPSET S001013 - MENY FJERN TRANS           */        
 /*   R --->  MAP S001014 MAPSET S001013 - MENY REGISTRERING          */        
 /*   I --->  MAP S001I01 MAPSET S001I13 - MENY JUSTERING INNTEKTER   */        
 /*   F --->  MAP S001015 MAPSET S001013 - MENY FORESPØRSLER          */        
 /*   V --->  MAP S001481 MAPSET S001V03 - MENY VENTETRANS-BEHANDLING */        
 /*   X --->  MAP S001012 MAPSET S001013 - PAUSEBILDE                 */        
 /*                                                                   */        
 /******************************************************************* */        
 /*PROGRAMTILKNYTTING:                                                */        
 /**********************                                              */        
 /*  PROGRAMMET BLIR AKTIVISERT FRA CICS MED TRANS-ID = R030.         */        
 /*  DENNE TRANSID SETTES I R0010201:                                 */        
 /*    EXEC CICS RETURN TRANSID(TRANSKODE) COMMAREA(KOM_OMR);         */        
 /*  KONTROLLEN GÅR VIA CICS TIL FORSKJELLIGE PROGRAM AVHENGIG AV     */        
 /*  HVILKEN FUNKSJONSKODE SOM BLIR SATT AV OPERATØREN:               */        
 /*  A - ADMINISTASJON                   - R550 - R0010420            */        
 /*  E - FJERNING AV DATA FRA HOVEDBASEN - R045 - R0010450            */        
 /*  R - REGISTRERING                    - R040 - R0010401            */        
 /*  I - JUSTERING AV INNTEKTER          - R0I1 - R001I101            */        
 /*  F - FORESPØRSEL                     - R410 - R0010410            */        
 /*  V - VENTETRANSBEHANDLING            - R048 - R0010480            */        
 /*  H - HJELPERUTINE                    - IKKE LAGET                 */        
 /*  X - FRIGJØRE TERMINAL               -      - AVSLUTTES           */        
 /*                                                                   */        
 /******************************************************************* */        
 /*DATASETTOPPLYSNINGER:                                              */        
 /*************************                                           */        
 /*  DET BLIR IKKE BRUKT NOEN DATASETT I DETTE PROGRAMMET             */        
 /*                                                                   */        
 /******************************************************************* */        
 /*FEILMELDINGER:                                                     */        
 /*********************                                               */        
 /*  'UGYLDIG FUNKSJONSKODE' BLIR SKREVET PÅ SKJERMBILDET             */        
 /*  NÅR KODEN IKKE ER GYLDIG                                         */        
 /* ***************************************************************** */        
 %PAGE;                                                                         
      %INCLUDE S00101;   /* MENY MAPSETTET */                                   
 %PAGE;                                                                         
      %INCLUDE S001V0;   /* VENTE-TRANS MAPSETTET */                            
 %PAGE;                                                                         
      %INCLUDE S001I4;     /*  REG-INNTEKT    MAPSETTET   */                    
 %PAGE;                                                                         
      %INCLUDE S001I1;   /* INNTEKTS    MAPSETTET */                            
 %PAGE;                                                                         
      %INCLUDE S0010R;   /* FJERNING AV SEGMENT   */                            
 %PAGE;                                                                         
      %INCLUDE S001S1;   /* KORRIGERING INNTEKT   */                            
 %PAGE;                                                                         
      %INCLUDE S001S3;   /* KORRIGERING YRKESKODE */                            
      %INCLUDE S00115;   /*    ENDRINGSBLANKETT-2 MAPSETTET   */                
      %INCLUDE S00118;   /* KORRIGERING YRKESKODE */                            
      %INCLUDE S001A0;     /*  SATSVISE KJØRING               */                
 %PAGE;                                                                         
      %INCLUDE P0019906;   /* TRANS_OPPL_OMRÅDE (BASED) */                      
 %PAGE;                                                                         
      %INCLUDE P0019908;   /* KOM_OMRÅDE (BASED) */                             
 %PAGE;                                                                         
      %INCLUDE P0019910;   /* STYRINGS_OMRÅDE (BASED) */                        
 %PAGE;                                                                         
      %INCLUDE P0019912;   /* DIV_PARAM       (BASED) */                        
 %PAGE;                                                                         
 /*   %INCLUDE P0019959;      OMRÅDET FOR FUNKTAB     */                        
 %PAGE;                                                                         
      %INCLUDE DFHBMSCA;                                                        
                                                                                
                                                                                
   DCL                                                                          
      COMMAREA_PEKER POINTER,                                                   
      IDENT_PEKER    POINTER,                                                   
      BMSMAPBR       POINTER;                                                   
   DCL                                                                          
      IDENT_BIT      BIT(32)    BASED (IDENT_PEKER);                            
                                                                                
   DCL                                                                          
      W01_CICSINFO   CHAR(16) ,                                                 
      W01_CICS_NAVN  CHAR(08) ,                                                 
      W01_PSB_NAVN   CHAR(08) ,                                                 
      W01_BRUKER_ID  CHAR(08) ,                                                 
      W01_CICSINDEX  CHAR(02)  ,                                                
      W01_TERMINAL   CHAR(04)  ,                                                
      W01_DATO_2000  PIC '(08)9' ;                                              
   DCL 01 W01_IDENT_DEF,                                                        
          02 W01_IDENT_1_4                  CHAR (4),                           
          02 W01_IDENT_5                    CHAR (1),                           
       01 W01_IDENT DEF W01_IDENT_DEF      CHAR(05) ;                           
                                                                                
   DCL W_MELD    CHAR(45);                                                      
                                                                                
   DCL                                                                          
      (ADDR,CSTG,LOW,STG,NULL,SUBSTR,VERIFY) BUILTIN;                           
                                                                                
   EXEC CICS HANDLE CONDITION ERROR(FEILBEH);                                   
   KOM_OMR.PEKER_LISTE.STYRINGS_PEKER   =  ADDR(KOM_OMR.STYRINGS_OMR)  ;        
   KOM_OMR.PEKER_LISTE.DIV_PARAM_PEKER  =  ADDR(KOM_OMR.DIV_PARAM_OMR) ;        
   KOM_OMR.PEKER_LISTE.TRANS_LISTE_PEKER =                                      
                                          ADDR(KOM_OMR.TRANS_LISTE_OMR);        
   KOM_OMR.PEKER_LISTE.TRANS_OPPL_PEKER =  ADDR(KOM_OMR.TRANS_OPPL_OMR);        
   ATK_KOM_PTR                          =  ADDR(KOM_OMR.ATK_COM_OMR);           
   KOM_OMR.PCB_UIB_PEKER                =  NULL                        ;        
                                                                                
   W01_CICSINDEX                        =  DIV_PARAM_OMR.CICS_IND      ;        
   W01_CICS_NAVN                        =  DIV_PARAM_OMR.CICS_NAVN     ;        
   W01_PSB_NAVN                         =  DIV_PARAM_OMR.PSB_NAVN     ;         
   W01_BRUKER_ID                        =  DIV_PARAM_OMR.BRUKER_ID    ;         
   W01_DATO_2000                        =  DIV_PARAM_OMR.DATO_2000    ;         
   W01_TERMINAL                         =  DIV_PARAM_OMR.TERMINAL_NR  ;         
   KOM_OMR.DIV_PARAM_OMR                =  '';                                  
   DIV_PARAM_OMR                        =  '';                                  
   DIV_PARAM_OMR.CICS_IND               =  W01_CICSINDEX               ;        
   DIV_PARAM_OMR.CICS_NAVN              =  W01_CICS_NAVN               ;        
   DIV_PARAM_OMR.PSB_NAVN               =  W01_PSB_NAVN               ;         
   DIV_PARAM_OMR.BRUKER_ID              =  W01_BRUKER_ID;                       
   DIV_PARAM_OMR.DATO_2000              =  W01_DATO_2000    ;                   
   DIV_PARAM_OMR.TERMINAL_NR            =  W01_TERMINAL     ;                   
   DIV_PARAM_OMR.FRA_UTEN_DIALOG        =  '0'B;                                
   DIV_PARAM_OMR.FRA_MED_DIALOG         =  '0'B;                                
   DIV_PARAM_OMR.FRA_CICS               =  '0'B;                                
   DIV_PARAM_OMR.HENT_FRAM_MAP          =  '0'B;                                
   DIV_PARAM_OMR.BO_TKNR                =   0  ;                                
  /*ATK_KOM_OMR.ACF2_F_KODE              =   ' '; */                            
   DIV_PARAM_OMR.FEIL_MELD_NR           =   0  ;                                
   STYRINGS_OMR.STYREKODE               =  '  ';                                
                                                                                
   IF FEIL_MELD_NR = 0   THEN                                                   
   DIV_PARAM_OMR.PROGRAM_ID             =  'R0010301';                          
                                                                                
                                                                                
   IF TRANSKODE                         =  'R030'                   THEN        
      DO;                                                                       
         /*--------------------------------------------------------*/           
         /* LESER INN BILDE MED FUNKSJONSKODEN SATT                */           
         /*--------------------------------------------------------*/           
                                                                                
         EXEC CICS RECEIVE MAP('S001013') MAPSET ('S001013')                    
         SET (BMSMAPBR);                                                        
         FUNKSJONSKODE            = S001013I.FUNKSJONSKODEI;                    
                                                                                
                                                                                
      END;                                                                      
                                                                                
  IF FUNKSJONSKODE ^= 'X'    &                                                  
     SUBSTR(DIV_PARAM_OMR.CICS_NAVN,5,4) ^= 'UATK' THEN                         
                                                                                
    CALL  KONTROLL_ACF2;                                                        
  ELSE                                                                          
    FEIL_MELD_NR = 0 ;                                                          
                                                                                
 IF FUNKSJONSKODE ^= 'X'   &                                                    
    FEIL_MELD_NR  > 0              THEN                                         
    DO;                                                                         
        IF TRANSKODE    ^=  'R030'        THEN                                  
            DO;                                                                 
               EXEC CICS SEND MAP('S001013') MAPSET('S001013')                  
               MAPONLY SET(BMSMAPBR);                                           
               ALLOCATE S001013O;                                               
             END;                                                               
                                                                                
          W_MELD  =  ACF2_FEIL( FEIL_MELD_NR );                                 
          S001013O.MELDINGO   =  W_MELD;                                        
          S001013O.FUNKSJONSKODEA     =  'I';                                   
          S001013O.FUNKSJONSKODEO     =   FUNKSJONSKODE             ;           
          S001013O.CICS_INFOO         =   DIV_PARAM_OMR.CICS_NAVN ;             
                                                                                
          IF TRANSKODE                =  'R030'                  THEN           
             EXEC CICS SEND MAP ('S001013') MAPSET('S001013')                   
             ERASEAUP DATAONLY;                                                 
          ELSE                                                                  
             EXEC CICS SEND MAP ('S001013') MAPSET('S001013') ERASE;            
                                                                                
          TRANSKODE                   =  'R030';                                
          GOTO      RETURN                     ;                                
    END;                                                                        
                                                                                
        /* ***NY VERSJON Må KOMPILERE FøR SATSVIS KJøRING FOR                   
              ANDRE KAN IKKE KOMME  I CICSP001  *********/                      
     IF VERIFY(FUNKSJONSKODE,'RNEVIZPT') = 0 THEN                               
            FUNKSJONSKODE  = 'Å';                                               
                                                                                
      SELECT (FUNKSJONSKODE);                                                   
         WHEN('A')                                                              
            DO;                                                                 
               TRANSKODE = 'RA20';                                              
               EXEC CICS XCTL PROGRAM('R0010420')COMMAREA(KOM_OMR);             
            END;                                                                
         WHEN('F')                                                              
            DO;                                                                 
                                                                                
               TRANSKODE       = 'F410';                                        
                                                                                
               ALLOCATE S001015O;                                               
               S001015O.FNRO  = TRANS_OPPL_OMR.FØDSNUMMER ;                     
               S001015O.CICS_INFOO = DIV_PARAM_OMR.CICS_NAVN ;                  
                                                                                
               IF SUBSTR(DIV_PARAM_OMR.CICS_IND,2,1) = 'A'   THEN               
               S001015O.CICS_INFOA = DFHBLINK;                                  
                                                                                
            /* SJEKKER FNR OG NULLSTILLER DERSOM UGYLDIG  */                    
                                                                                
               IF ^F_GYLDIG_FNR(S001015O.FNRO) THEN                             
                  DO;                                                           
                     S001015I.FNRI = LOW(11) ;                                  
                     S001015I.FNRL =  -1  ;                                     
                  END;                                                          
                                                                                
               ELSE                                                             
                  DO;                                                           
                     S001015O.OPPLYSNINGSTYPEO  =  'S' ;                        
                     S001015I.FNRL =  -1  ;                                     
                  END;                                                          
                                                                                
            /* SKRIVER UT FORESPØRSEL-BILDET M/ OVERFØRT FNR */                 
                                                                                
               EXEC CICS SEND MAP('S001015') MAPSET('S001013')                  
                                           CURSOR  ERASE;                       
                                                                                
            END;                                                                
         WHEN('R' )                                                             
            DO;                                                                 
               TRANSKODE = 'R040';                                              
               ALLOCATE S001014O ;                                              
               S001014O.CICS_INFOO = DIV_PARAM_OMR.CICS_NAVN ;                  
                                                                                
               EXEC CICS SEND MAP('S001014') MAPSET('S001013')                  
                                             ERASE;                             
               /* SKRIVER MENY-SKJERMBILDE FOR REGISTRERING */                  
            END;                                                                
                                                                                
         WHEN('N' )                                                             
            DO;                                                                 
                                                                                
                DIV_PERIODE                   = '' ;                            
                DIV_PARAM_OMR.FRA_CICS        = '1'B;                           
                DIV_PARAM_OMR.HENT_FRAM_MAP   = '0'B;                           
                DIV_PARAM_OMR.FRA_MED_DIALOG  = '0'B;                           
                DIV_PARAM_OMR.FRA_UTEN_DIALOG = '0'B;                           
                IF STYRINGS_OMR.FUNKSJONSKODE   = ' ' THEN                      
                   STYRINGS_OMR.FUNKSJONSKODE   = 'R';                          
                                                                                
                KOM_OMR.PCB_UIB_PEKER = NULL;                                   
                STYRINGS_OMR.STYREKODE  = 'NI';                                 
                                                                                
                TRANSKODE = 'R0I4';                                             
                ALLOCATE S001I04O ;                                             
                S001I04O.CICS_INFOO =                                           
                                  DIV_PARAM_OMR.CICS_NAVN ;                     
                                                                                
                EXEC CICS SEND MAP('S001I04')                                   
                                    MAPSET('S001I43') ERASE;                    
                                                                                
            END;                                                                
                                                                                
         WHEN('E')                                                              
            DO;                                                                 
               TRANSKODE = 'R045';                                              
               ALLOCATE S00101EO ;                                              
               S00101EO.CICS_INFOO = DIV_PARAM_OMR.CICS_NAVN ;                  
                                                                                
               EXEC CICS SEND MAP('S00101E') MAPSET('S001013')                  
                                             ERASE;                             
               /* SKRIVER MENY-SKJERMBILDE FOR FJERN TRANS */                   
            END;                                                                
         WHEN('I')                                                              
            DO;                                                                 
                                                                                
                 TRANSKODE = 'R0I1';                                            
                 ALLOCATE S001I01O ;                                            
                 S001I01O.CICS_INFOO = DIV_PARAM_OMR.CICS_NAVN ;                
                                                                                
                 EXEC CICS SEND MAP('S001I01') MAPSET('S001I13')                
                                                     ERASE;                     
             END;                                                               
                                                                                
         WHEN('V')                                                              
            DO;                                                                 
               /*-----------------------------------------------*/              
               /* SKRIVER MENY-SKJERMBILDE FOR BEHANDLING AV    */              
               /* TRANSER På VENTEREGISTERET                    */              
               /*-----------------------------------------------*/              
                                                                                
               TRANSKODE   = 'R048';                                            
               ALLOCATE S001481O;                                               
               S001481O.CICS_INFOO = DIV_PARAM_OMR.CICS_NAVN ;                  
                                                                                
               EXEC CICS SEND MAP ('S001481') MAPSET ('S001V03')                
                              ERASE;                                            
            END;                                                                
                                                                                
          WHEN('P')              /* PENSJONSBREV  AU   */                       
             DO;                                                                
                DIV_PERIODE                   = '' ;                            
                DIV_PARAM_OMR.FRA_CICS        = '1'B;                           
                DIV_PARAM_OMR.HENT_FRAM_MAP   = '0'B;                           
                DIV_PARAM_OMR.FRA_MED_DIALOG  = '0'B;                           
                DIV_PARAM_OMR.FRA_UTEN_DIALOG = '0'B;                           
                KOM_OMR.PCB_UIB_PEKER = NULL;                                   
                STYRINGS_OMR.STYREKODE  = 'AU';                                 
                                                                                
                TRANSKODE = 'R180';                                             
                ALLOCATE S001181O ;                                             
                S001181O.CICS_INFOO = DIV_PARAM_OMR.CICS_NAVN ;                 
                                                                                
                /* SKRIVER SKJERMBILDET FOR STYRT AUTOHENDELSE       */         
                EXEC CICS SEND MAP('S001181') MAPSET('S001183')                 
                                              ERASE ;                           
              END;                                                              
                                                                                
                                                                                
          WHEN('S')                  /* SATSVISE KJØRING       */               
             DO;                                                                
                STYRINGS_OMR.STYREKODE  = 'SK';                                 
                  EXEC CICS XCTL PROGRAM('R0010426') COMMAREA(KOM_OMR);         
              END;                                                              
          WHEN('T' )         /* ENDRING TRYGDEKONTOR */                         
             DO;                                                                
                TRANSKODE = 'R150';                                             
                                                                                
                DIV_PERIODE                   = '' ;                            
                DIV_PARAM_OMR.FRA_CICS        = '1'B;                           
                DIV_PARAM_OMR.HENT_FRAM_MAP   = '0'B;                           
                DIV_PARAM_OMR.FRA_MED_DIALOG  = '0'B;                           
                DIV_PARAM_OMR.FRA_UTEN_DIALOG = '0'B;                           
                IF STYRINGS_OMR.FUNKSJONSKODE   = ' ' THEN                      
                   STYRINGS_OMR.FUNKSJONSKODE   = 'R';                          
                                                                                
                KOM_OMR.PCB_UIB_PEKER = NULL;                                   
                                                                                
                STYRINGS_OMR.STYREKODE  = 'E2';                                 
                ALLOCATE S001151O ;                                             
                S001151O.CICS_INFOO = DIV_PARAM_OMR.CICS_NAVN ;                 
              /* SKRIVER REGISTRERINGSBILDE FOR ENDRINGS-           */          
              /* BLANKETT-2                                         */          
                                                                                
                EXEC CICS SEND MAP('S001151') MAPSET('S001153')                 
                                              ERASE ;                           
                                                                                
             END;                                                               
                                                                                
                                                                                
         WHEN('Z')                                                              
            DO;                                                                 
               /*-----------------------------------------------*/              
               /* SKRIVER MENY-SKJERMBILDE FOR BEHANDLING AV    */              
               /* TRANSER På FJERN ROT / SEGMENT                */              
               /*-----------------------------------------------*/              
                                                                                
               TRANSKODE   = 'R0F1';                                            
               ALLOCATE S0010RO;                                                
               S0010RO.CICS_INFOO = DIV_PARAM_OMR.CICS_NAVN ;                   
                                                                                
               EXEC CICS SEND MAP ('S0010R') MAPSET ('S0010R3')                 
                              ERASE;                                            
               HENT_FRAM_MAP = '0'B;                                            
            END;                                                                
   /*                                                                           
         WHEN('K')                                                              
            DO;                                                                 
                *-----------------------------------------------*               
                * SKRIVER MENY-SKJERMBILDE FOR BEHANDLING AV    *               
                * INNTEK_FOR UP                                 *               
                *-----------------------------------------------*               
                                                                                
               TRANSKODE   = 'R0S1';                                            
               ALLOCATE S001S01O;                                               
               S001S01O.CICS_INFOO = DIV_PARAM_OMR.CICS_NAVN ;                  
                                                                                
               EXEC CICS SEND MAP ('S001S01') MAPSET ('S001S13')                
                              ERASE;                                            
                                                                                
               HENT_FRAM_MAP = '0'B;                                            
            END;                                                                
   */                                                                           
         WHEN('X')                                                              
            DO;                                                                 
               /*-----------------------------------------------*/              
               /* SKRIVER PAUSEBILDET                           */              
               /*-----------------------------------------------*/              
                                                                                
               TRANSKODE = '0000';                                              
               EXEC CICS SEND MAP ('S001012') MAPSET ('S001013')                
                    MAPONLY ERASE ;                                             
               EXEC CICS RETURN                                  ;              
                                                                                
            END;                                                                
         OTHERWISE                                                              
            DO;                                                                 
                                                                                
              /*-----------------------------------------------*/               
              /* FEILMELDING: 'UGYLDIG FUNKSJONSKODE' SKRIVES  */               
              /*-----------------------------------------------*/               
              IF TRANSKODE      ^= 'R030' THEN                                  
                 DO;                                                            
                    EXEC CICS SEND MAP('S001013') MAPSET('S001013')             
                    MAPONLY   SET      (BMSMAPBR)                 ;             
                    ALLOCATE  S001013O                            ;             
                 END;                                                           
                                                                                
              IF FUNKSJONSKODE   =  'Å'  THEN                                   
                 DO;                                                            
                  S001013O.MELDINGO  =                                          
             'SATSVIS KJØRING I GANG, FUNKSJON ER IKKE TILGJENGELIG';           
                  S001013O.FUNKSJONSKODEO =  FUNKSJONSKODE;                     
                 END;                                                           
              ELSE                                                              
              IF FUNKSJONSKODE   ^=  'H'  THEN                                  
                DO;                                                             
                    S001013O.MELDINGO  = 'UGYLDIG FUNKSJONSKODE'   ;            
                    S001013O.FUNKSJONSKODEO =  FUNKSJONSKODE;                   
                END;                                                            
              ELSE                                                              
                DO;                                                             
                    S001013O.MELDINGO  = '                     '   ;            
                    S001013O.FUNKSJONSKODEO =  ' ';                             
                END;                                                            
                                                                                
              S001013O.CICS_INFOO     =  DIV_PARAM_OMR.CICS_NAVN  ;             
              S001013O.FUNKSJONSKODEA = 'I'; /* MDT SET,HIGH INT*/              
                                                                                
              IF TRANSKODE            = 'R030'                THEN              
                 EXEC CICS SEND MAP ('S001013') MAPSET ('S001013')              
                 ERASEAUP  DATAONLY;                                            
              ELSE                                                              
                 EXEC CICS SEND MAP    ('S001013')                              
                                MAPSET ('S001013') ERASE          ;             
                                                                                
              TRANSKODE    =        'R030'                        ;             
            END;                                                                
                                                                                
      END;  /*  SELECT  ( ....         )                         */             
  RETURN:                                                                       
                                                                                
                                                                                
   EXEC CICS RETURN TRANSID(TRANSKODE) COMMAREA(KOM_OMR);                       
                                                                                
                                                                                
  FEILBEH:                                                                      
   /*-----------------------------------------------*/                          
   /* SKRIVER PAUSEBILDET                           */                          
   /*-----------------------------------------------*/                          
                                                                                
   EXEC CICS SEND MAP ('S001012') MAPSET ('S001013')                            
                                  MAPONLY  ERASE     ;                          
   EXEC CICS RETURN                                  ;                          
                                                                                
   %INCLUDE R0019904;           /*  F_GYLDIG_FNR            */                  
   %INCLUDE R0019998;           /*  ACF2_FEILMELD           */                  
   %INCLUDE R0019999;           /*  ATK_KONTROLL            */                  
   %PAGE;                                                                       
                                                                                
 END R00103;                                                                    
