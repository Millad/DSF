 /*   SIST ENDRET PÅ PROD   2003.02.27 12.27.30 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2002.11.21 13.17.25 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.10.22 12.34.13 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2000.02.18 14.35.53 AV   JDA7339          */        
 /*       SIST ENDRET 02/09-98 11.17.28 AV   SPA7339                  */        
 /*       SIST ENDRET 22/06-98 10.22.58 AV   JDA7339                  */        
 /*       SIST ENDRET 04/06-98 15.18.44 AV   RFA9991                  */        
 /* ***************************************************************** */        
 /*IDENTIFIKASJON:                                                    */        
 /*    R0010803 - UNDERPROGRAM I PLI                                  */        
 /*    PROGRAMMERER:                                                  */        
 /* ***************************************************************** */        
 /*HENSIKT:                                                           */        
 /*    PROSEDYREN FORETAR KONTROLL AV DATA FOR EP_SØKER               */        
 /*    FRA ETTERLATTPENSJONSGRUNNBLANKETTEN.                          */        
 /*PROGRAMTILKNYTNING:                                                */        
 /*    INCLUDES I  R0010801 - ETTERLATTEPENSJON                       */        
 /*BRUK:                                                              */        
 /*    CALL KONTROLL_EP_SØKER(FEIL_I_EP_SØKER);                       /*        
 ***************************************************************** */           
 %PAGE;                                /*   */                                  
 /* ***************************************************************** */        
 /*                                                                   */        
 /*   MODULEN KONTROLLERER EP_SØKER, OG RETURNERER                    */        
 /*   MED FEIL_FUNNET = '1'B HVIS FEIL, '0'B HVIS RIKTIG              */        
 /*                                                                   */        
 /* ***************************************************************** */        
 %SKIP(2);                                                                      
 KONTROLL_EP_SØKER:                                                             
   PROC (FEIL_FUNNET);                                                          
   DCL                                                                          
      FEIL_FUNNET BIT (1);                                                      
 %SKIP;                                                                         
   DCL                                                                          
      KEY_BIT                    BIT(32) BASED (KEY_PEKER),                     
      KEY_PEKER                  POINTER,                                       
      TK_RECL                    CHAR (101);                                    
   DCL                                                                          
      ALDER FIXED DEC (7),                                                      
      IÅR   FIXED BIN (15),                                                     
      TELL  FIXED BIN (15),                                                     
      DAGENS_DATO_ÅMD PIC '99999999',                                           
      DAGENS_DATO_ÅM DEF DAGENS_DATO_ÅMD PIC '999999',                          
      DAGENS_DATO_Å  DEF DAGENS_DATO_ÅMD PIC '9999';                            
 %PAGE;                                /*   */                                  
 /*  HJELPE DCL FOR Å SNU DATO           */                                     
 %SKIP(1);                                                                      
   DCL                                                                          
      HJ_VIRK_DATO_ÅMD PIC '99999999',                                          
      VIRK_DATO_Å DEF HJ_VIRK_DATO_ÅMD POS(1) PIC '9999',                       
      VIRK_DATO_M DEF HJ_VIRK_DATO_ÅMD POS(5) PIC '99',                         
      VIRK_DATO_ÅM DEF HJ_VIRK_DATO_ÅMD POS(1) PIC '999999',                    
      HJ_FRAMLEGGSDATO_ÅMD                   PIC '99999999',                    
      FRAMLEGGSDATO_Å DEF HJ_FRAMLEGGSDATO_ÅMD POS(1) PIC '9999',               
      FRAMLEGGSDATO_M DEF HJ_FRAMLEGGSDATO_ÅMD POS(5) PIC '99',                 
      HJ_DØDSDATO_ÅMD                       PIC '99999999',                     
      DØDSDATO_Å DEF HJ_DØDSDATO_ÅMD POS(1) PIC '9999',                         
      DØDSDATO_M DEF HJ_DØDSDATO_ÅMD POS(5) PIC '99',                           
      DØDSDATO_D DEF HJ_DØDSDATO_ÅMD POS(7) PIC '99',                           
      DØDSDATO_ÅM DEF HJ_DØDSDATO_ÅMD POS(1) PIC '999999';                      
 %SKIP;                                                                         
   DCL                                                                          
      SEKSTISJU_ÅR                             PIC '999999',                    
      TT_TIL_Å      DEF SEKSTISJU_ÅR POS(1)    PIC '9999',                      
      TT_TIL_M      DEF SEKSTISJU_ÅR POS(4)    PIC '99',                        
      HJ_FNR                                   PIC '(11)9',                     
      HJ_FNR_MND    DEF HJ_FNR          POS(3) PIC '99',                        
      HJ_FNR_ÅR     DEF HJ_FNR          POS(5) PIC '99',                        
      HJ_FNR_PNR    DEF HJ_FNR          POS(7) PIC '999',                       
      HJ_FNR13                                 PIC '(13)9',                     
      HJ_FNR_MND13  DEF HJ_FNR13        POS(3) PIC '99',                        
      HJ_FNR_ÅR13   DEF HJ_FNR13        POS(5) PIC '9999',                      
      HJ_FNR_PNR13  DEF HJ_FNR13        POS(7) PIC '999';                       
    DCL                                                                         
      1 FNR_REG,                                                                
        2 FNR1 FIXED DEC(11),                                                   
        2 FNR2 FIXED DEC(11),                                                   
        2 BRUKERID CHAR (4);                                                    
                                                                                
    DCL TEKST CHAR(4);                                                          
 %PAGE;                                                                         
                                                                                
      HJ_VIRK_DATO_ÅMD = EPS.VIRK_DATO_ÅMD;                                     
      DIV_PARAM_OMR.REGN_1291 = ' ';                                            
      FNR_REG.BRUKERID = DIV_PARAM_OMR.CICS_IND;                                
      FNR_REG.FNR2     = 0;                                                     
      FEIL_FUNNET      = '0'B;                                                  
      KEY_PEKER        = ADDR(EPS.TKNR);                                        
      ANT_FEIL_SKREVET = 0;                                                     
      HJ_DØDSDATO_ÅMD = EPS.DØDSDATO_ÅMD_EK;                                    
      DAGENS_DATO_ÅMD  = DATO_2000;   /* 2000-3 */                              
      ALDER = F_ALDER(EPS.FNR,EPS.VIRK_DATO_ÅMD);                               
                                                                                
      IF ^F_NUMERISK(F_FELT_PIC_CHAR11(EPS.FNR)) THEN                           
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            EPS.FNRNR   = 200;                                                  
         END;                                                                   
      ELSE                                                                      
      IF ^F_GYLDIG_FNR(EPS.FNR) THEN                                            
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            EPS.FNRNR   = 1;                                                    
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
            FNR_REG.FNR1 = EPS.FNR;                                             
            EXEC CICS LINK PROGRAM('R0019906') COMMAREA(FNR_REG);               
            IF FNR_REG.FNR2 > 0 THEN                                            
               DO;                                                              
                  EPS.FNRNR = 203;                                              
                  EPS.FNR_GML = EPS.FNR;                                        
                  EPS.FNR = FNR_REG.FNR2;                                       
               END;                                                             
         END;                                                                   
      IF EPS.NAVN = (25)' ' THEN                                                
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            EPS.NAVNNR = 2;                                                     
         END;                                                                   
      IF VERIFY(EPS.SPRÅK,'NBE ') ^= 0 THEN                                     
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            EPS.SPRÅKNR = 6;                                                    
         END;                                                                   
      IF ^ F_NUMERISK(F_FELT_PIC_CHAR4(EPS.TKNR)) THEN                          
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            EPS.TKNRNR = 200;                                                   
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
               EXEC CICS HANDLE CONDITION NOTFND(NOTFND);                       
               EXEC CICS READ DATASET ('TKNRTAB') RIDFLD(KEY_BIT)               
                                                  INTO(TK_RECL);                
               LEAVE;                                                           
           NOTFND:                                                              
               FEIL_FUNNET = '1'B;                                              
               EPS.TKNRNR  = 3;                                                 
         END;                                                                   
                                                                                
    /* ************************************************** */                    
    /* KONTROLL AV ACF2 FOR TILLGANGE TIL TKNR            */                    
    /*                                                    */                    
          /* ************************************************** */              
    BO_TKNR  = TKNR;                                                            
     CALL ATK_TILGANG_KONTROLL;                                                 
                                                                                
                                                                                
       IF VERIFY(EPS.SIVILSTAND,'SEU') ^= 0 THEN                                
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            EPS.SIVILSTANDNR = 496;     /* KONS RF */                           
         END;                                                                   
      IF VERIFY(EPS.NORSK_BOSATT,'JN') ^= 0 THEN                                
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            EPS.NORSK_BOSATTNR = 8;                                             
         END;                                                                   
      IF ^ F_NUMERISK(F_FELT_PIC_CHAR2(EPS.TP_PROSENT)) THEN                    
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            EPS.TP_PROSENTNR = 200;                                             
         END;                                                                   
      ELSE                                                                      
      IF EPS.TP_PROSENT > 55 THEN                                               
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            EPS.TP_PROSENTNR = 64;                                              
         END;                                                                   
  /* ********************************************************** */              
  /* SIVILSTAND 'U' SKAL BEHANDLES PÅ SAMME MÅTE SOM 'E'.       */              
  /* ERIK 20.1.88. ENDRET 23.9.89 HL                            */              
  /* ********************************************************** */              
         ELSE                                                                   
          IF (EPS.SIVILSTAND = 'U' !                                            
              EPS.SIVILSTAND = 'E'   ) &                                        
              EPS.TP_PROSENT =  0      THEN                                     
              EPS.TP_PROSENT = 55;                                              
         ELSE                                                                   
         IF (EPS.SIVILSTAND = 'U' !                                             
             EPS.SIVILSTAND = 'E'   ) &                                         
             EPS.TP_PROSENT < 55      THEN                                      
             DO;                                                                
                FEIL_FUNNET = '1'B;                                             
                EPS.SIVILSTANDNR = 241;                                         
                EPS.TP_PROSENTNR = 999;                                         
             END;                                                               
  /* ********************************************************** */              
  /* TIL HIT.  ERIK 20.1.88.    HL 23.9.89                      */              
  /* ********************************************************** */              
      IF ^ (EPS.VILKÅR_10_5 = 'J' ! EPS.VILKÅR_10_5 = 'N') THEN                 
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            EPS.VILKÅR_10_5NR = 66;                                             
         END;                                                                   
      IF ^ (EPS.OMSORG_AVD_BA = 'J' ! EPS.OMSORG_AVD_BA = 'N') THEN             
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            EPS.OMSORG_AVD_BANR = 65;                                           
         END;                                                                   
      IF ^ F_NUMERISK(F_FELT_PIC_CHAR7(EPS.FORVENTET)) THEN                     
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            EPS.FORVENTETNR = 200;                                              
         END;                                                                   
      IF ^ F_NUMERISK(F_FELT_PIC_CHAR7(EPS.FAI)) THEN                           
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            EPS.FAINR = 200;                                                    
         END;                                                                   
       IF VERIFY(EPS.YSKADE_TILLEGG,'J ') > 0 THEN                              
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            EPS.YSKADE_TILLEGGNR = 300;                                         
         END;                                                                   
      IF VERIFY(EPS.GP_OS_KODE,'OP') > 0 THEN                                   
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            EPS.GP_OS_KODENR = 68;                                              
         END;                                                                   
  /*  IF VERIFY(EPS.HJEMMEL_10_8,'JN') > 0 THEN   */                            
                                                                                
      /****************************************************/                    
      /*                                                  */                    
      /*  RETTET SLIK AT DET IKKE KREVES SVAR PÅ 10_8 NÅR */                    
      /*  PERSONEN IKKE ER SKILT                          */                    
      /*  VED SIVILSTAND = E GODTAS 'N' SOM SVAR          */                    
      /****************************************************/                    
                                                                                
      IF EPS.SIVILSTAND                = 'S' THEN                               
         DO;                                                                    
            IF VERIFY(EPS.HJEMMEL_10_8,'JN') ^= 0  THEN                         
              DO;                                                               
                 FEIL_FUNNET = '1'B;                                            
                 EPS.HJEMMEL_10_8NR = 70;                                       
              END;                                                              
         END;                                                                   
      ELSE                                                                      
         IF EPS.SIVILSTAND                = 'E' !                               
            EPS.SIVILSTAND                = 'U' THEN                            
            DO;                                                                 
              IF VERIFY(EPS.HJEMMEL_10_8,' N') ^= 0  THEN                       
                 DO;                                                            
                    FEIL_FUNNET        = '1'B;                                  
                    EPS.HJEMMEL_10_8NR = 320;                                   
                    EPS.SIVILSTANDNR   = 999;                                   
                 END;                                                           
            END;                                                                
                                                                                
      IF ^ F_NUMERISK(F_FELT_PIC_CHAR8(EPS.FRAMLEGGSDATO_ÅMD)) THEN             
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            EPS.FRAMLEGGSDATO_ÅMDNR = 200;/* 2000-3 */                          
         END;                                                                   
      ELSE                                                                      
      IF F_FELT_PIC_CHAR8(EPS.FRAMLEGGSDATO_ÅMD) ^= '00000000' THEN             
         IF ^ F_GYLDIG_DATO(EPS.FRAMLEGGSDATO_ÅMD) THEN                         
            DO;                                                                 
               FEIL_FUNNET = '1'B;                                              
               EPS.FRAMLEGGSDATO_ÅMDNR = 71; /* 2000-3 */                       
            END;                                                                
                                                                                
      IF VERIFY(EPS.FRAMLEGGSKODE,' OA') > 0 THEN                               
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            EPS.FRAMLEGGSKODENR = 73;                                           
         END;                                                                   
                                                                                
      IF ^ F_NUMERISK(F_FELT_PIC_CHAR8(EPS.VIRK_DATO_ÅMD)) THEN                 
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            EPS.VIRK_DATO_ÅMDNR = 200;   /* 2000-3 */                           
         END;                                                                   
      ELSE                                                                      
         IF ^ F_GYLDIG_DATO(EPS.VIRK_DATO_ÅMD) THEN   /* 2000-3 */              
            DO;                                                                 
               FEIL_FUNNET = '1'B;                                              
               EPS.VIRK_DATO_ÅMDNR = 9;                                         
            END;                                                                
                                                                                
 /* ***************************************************** */                    
 /* NY PARAGRAF 10-6 FRA 1.1.89.  YTELSEN SKAL ALLTID GIS */                    
 /* MED 55 PROSENT TILLEGGSPENSJON.  ERIK/HL 11.4.89      */                    
 /* ***************************************************** */                    
                                                                                
                                                                                
       IF EPS.TP_PROSENT < 55 & DØDSDATO_Å > 1988 THEN  /* 2000-3 */            
          EPS.TP_PROSENT = 55;                                                  
                                                                                
      /* ENDRET 15/2-84 AV KARIN - UOPPGITT AVDØD EKTEFELLE */                  
      IF ^F_NUMERISK(F_FELT_PIC_CHAR11(EPS.FNR_EK)) THEN                        
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            EPS.FNR_EKNR = 200;                                                 
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
            IF EPS.FNR_EK > 0  THEN                                             
               DO;                                                              
                 /*** AVDØDES FNR ER UTFYLT  ***/                               
                 IF EPS.FNR_EK ^= 11111111111 THEN                              
                    DO;                                                         
                      /*** AVDØDES FNR ER ET VANLIG FNR ***/                    
                      IF ^F_GYLDIG_FNR(EPS.FNR_EK) THEN                         
                         DO;                                                    
                            FEIL_FUNNET = '1'B;                                 
                            EPS.FNR_EKNR = 15;                                  
                         END;                                                   
                      ELSE                                                      
                         DO;                                                    
                            /*** AVD-FNR ER ET GYLDIG FNR ***/                  
                            FNR_REG.FNR1 = EPS.FNR_EK;                          
                            EXEC CICS LINK PROGRAM('R0019906')                  
                            COMMAREA(FNR_REG);                                  
                            IF FNR_REG.FNR2 > 0 THEN                            
                               DO;                                              
                                  /* FNR ER ENDRET */                           
                                  EPS.FNR_EKNR = 203;                           
                                  EPS.FNR_GML_EK = EPS.FNR_EK;                  
                                  EPS.FNR_EK = FNR_REG.FNR2;                    
                               END;                                             
                         END;   /* END GYLDIG FNR */                            
                      IF ^ F_NUMERISK(F_FELT_PIC_CHAR8                          
                           (EPS.DØDSDATO_ÅMD_EK)) THEN    /* 2000-3 */          
                         DO;                                                    
                            FEIL_FUNNET = '1'B;                                 
                            EPS.DØDSDATO_ÅMD_EKNR = 200; /* 2000-3 */           
                         END;                                                   
                      ELSE                                                      
                         IF EPS.DØDSDATO_ÅMD_EK = 0 THEN                        
                            DO;                                                 
                               FEIL_FUNNET = '1'B;                              
                               EPS.DØDSDATO_ÅMD_EKNR = 115; /* 2000-3 */        
                            END;                                                
                      ELSE                                                      
                         IF ^ F_GYLDIG_DATO                                     
                                (EPS.DØDSDATO_ÅMD_EK) THEN  /* 2000-3 */        
                               DO;                                              
                                  FEIL_FUNNET = '1'B;                           
                                  EPS.DØDSDATO_ÅMD_EKNR = 75;                   
                               END;                                             
                                                                                
                      IF VERIFY(EPS.NORSK_BOSATT_EK,' JN') > 0 THEN             
                         DO;                                                    
                            FEIL_FUNNET = '1'B;                                 
                            EPS.NORSK_BOSATT_EKNR = 8;                          
                         END;                                                   
                      IF ^ (EPS.VILKÅR_8_4_EK = 'J' !                           
                            EPS.VILKÅR_8_4_EK = 'N' !                           
                            EPS.VILKÅR_8_4_EK = ' ') THEN                       
                         DO;                                                    
                            FEIL_FUNNET = '1'B;                                 
                            EPS.VILKÅR_8_4_EKNR = 45;                           
                         END;                                                   
                                                                                
                   /* DERSOM VP_ÅR  ER UTFYLT BLIR DISSE NULLSTILLT */          
                                                                                
                      DO J = 1 TO 4 UNTIL                                       
                         (F_FELT_PIC_CHAR4(EPS.VP_ÅR_EK(J) = '0000'));          
                      IF F_FELT_PIC_CHAR4(EPS.VP_ÅR_EK(J)) = '0000' THEN        
                            DO TELL = J TO 4;                                   
                               EPS.VP_ÅR_EK(TELL) = 0;                          
                            END;                                                
                      END;                                                      
                                                                                
                      DO IÅR = 1 TO 4 WHILE                                     
                         (F_FELT_PIC_CHAR4(EPS.VP_ÅR_EK(IÅR)) ^='0000');        
                         IF ^ F_NUMERISK(F_FELT_PIC_CHAR4                       
                              (EPS.VP_ÅR_EK(IÅR))) THEN                         
                            DO;                                                 
                               FEIL_FUNNET = '1'B;                              
                               EPS.VP_ÅR_EKNR(IÅR) = 200;                       
                            END;                                                
                         ELSE                                                   
                            IF ^(EPS.VP_ÅR_EK(IÅR) > 1966                       
                            & EPS.VP_ÅR_EK(IÅR) <= DAGENS_DATO_Å) THEN          
                               DO;                                              
                                  FEIL_FUNNET = '1'B;                           
                                  EPS.VP_ÅR_EKNR(IÅR) = 47;                     
                               END;                                             
                      END;      /* DO IÅR = 1 TO 4 */                           
                    END; /* END AVDØDES FNR ER ET VANLIG FNR */                 
                 ELSE                                                           
                    DO;  /* AVDØDES FNR = 11111111111, DVS UKJENT   */          
                         /* OG INGEN AV FELTENE TIL AVDØDE SKAL     */          
                         /* VÆRE UTFYLT                             */          
 /*******************************************************************/          
 /* FOR Å AVVISE ALLE YRKESSKADE-DØDE MED FNR. 11111111111, HAR JEG */          
 /* SATT INN TESTEN NEDENFOR.                        ERIK.          */          
 /*******************************************************************/          
                                                                                
          /*           IF EPS.YSKADE_TILLEGG = 'J'  THEN  */                    
          /*              DO;                             */                    
          /*                 FEIL_FUNNET = '1'B;          */                    
          /*                 EPS.FNR_EKNR         = 321;  */                    
          /*                 EPS.YSKADE_TILLEGGNR = 999;  */                    
          /*              END;                            */                    
          /*           ELSE                               */                    
                                                                                
                                                                                
                         IF EPS.NAVN_EK               ^= (25)' ' !              
                            EPS.DØDSDATO_ÅMD_EK       ^= 0 !                    
                            EPS.NORSK_BOSATT_EK       ^= ' ' !                  
                            EPS.VILKÅR_8_4_EK         ^= ' ' !                  
                            EPS.PI_EK                 ^= 0 !                    
                            EPS.VP_ÅR_EK(1)           ^= 0 THEN                 
                            DO;                                                 
                               FEIL_FUNNET = '1'B;                              
                               EPS.FNR_EKNR          = 999;                     
                               EPS.NAVN_EKNR         = 999;                     
                               EPS.DØDSDATO_ÅMD_EKNR = 999;                     
                               EPS.NORSK_BOSATT_EKNR = 999;                     
                               EPS.VILKÅR_8_4_EKNR   = 999;                     
                               EPS.PI_EKNR           = 999;                     
                               EPS.VP_ÅR_EKNR(1)     = 999;                     
                            END;                                                
                    END; /* AVDØDES FNR = 11111111111, DVS UKJENT   */          
               END;      /* AVDØDES FNR ULIK 0, DVS UTFYLT          */          
           ELSE                                                                 
               DO;                                                              
                  /* AVDØDES FNR = 0, DVS IKKE UTFYLT */                        
                  FEIL_FUNNET = '1'B;                                           
                  EPS.FNR_EKNR = 201;                                           
               END;                                                             
         END;                                                                   
                                                                                
                                                                                
 /*******************************************************************/          
                                                                                
  /* HER SETTES VERDIER IN I MENY_OMR */                                        
      FØDSNUMMER = EPS.FNR;                                                     
      VIRKNINGSDATO_ÅMD = EPS.VIRK_DATO_ÅMD;                                    
 %SKIP(3);                             /*  */                                   
 /*   KRYSS KONTROLLER       */                                                 
 %SKIP;                                                                         
      IF ^FEIL_FUNNET THEN                                                      
         DO;                                                                    
                                                                                
 /*LOVENDRING MED VIRKNING FRA 1.1.91 SP : */                                   
            IF  EPS.VIRK_DATO_ÅMD > 19920000 &         /* 2000-3 */             
                EPS.DØDSDATO_ÅMD_EK < 19920000    THEN    /*"*/                 
                                                                                
                 DIV_PARAM_OMR.REGN_1291 = 'J';                                 
                                                                                
            /* DIVERSE TESTER VEDR. RETT TIL PENSJON/OVERGANSST. */             
            IF EPS.GP_OS_KODE  = 'O' !                                          
               EPS.GP_OS_KODE  = 'P' THEN                                       
              DO;                                                               
                IF EPS.SIVILSTAND = 'E' THEN                                    
                  DO;                                                           
                     /*** SIVILSTAND = E     ***/                               
                     IF EPS.HJEMMEL_10_8 = 'J' THEN                             
                        DO;                                                     
                           FEIL_FUNNET = '1'B;                                  
                           EPS.SIVILSTANDNR = 242;                              
                           EPS.HJEMMEL_10_8NR = 999;                            
                        END;                                                    
                     ELSE                                                       
                        DO;                                                     
                           /*** HJEMMEL 10-8 = N    ***/                        
                           IF EPS.OMSORG_AVD_BA = 'J' THEN                      
                              DO;                                               
                                 /*** OMSORG = J        ***/                    
                                 IF EPS.VILKÅR_10_5 = 'N' THEN                  
                                    DO;                                         
                                       FEIL_FUNNET = '1'B;                      
                                       EPS.OMSORG_AVD_BANR=255;                 
                                       EPS.VILKÅR_10_5NR=999;                   
                                    END;                                        
                                 ELSE                                           
                                    DO;                                         
                                       /**** VILKÅR 10-5 = J   ***/             
                                       IF EPS.GP_OS_KODE = 'O' THEN             
                                          DO;                                   
                                             FEIL_FUNNET = '1'B;                
                                             EPS.SIVILSTANDNR =254;             
                                             EPS.VILKÅR_10_5NR=999;             
                                             EPS.GP_OS_KODENR=999;              
                                          END;                                  
                                       /***  END VILKÅR 10-5 = J   ***/         
                                    END;                                        
                                 /*** END OMSORG = J   ***/                     
                              END;                                              
                           ELSE                                                 
                              DO;                                               
                                 /*** OMSORG = N ***/                           
                                 IF EPS.VILKÅR_10_5 = 'J' THEN                  
                                    DO;                                         
                                       /*** VILKÅR 10-5 = J ***/                
                                       IF EPS.GP_OS_KODE = 'O' THEN             
                                          DO;                                   
                                             FEIL_FUNNET = '1'B;                
                                             EPS.SIVILSTANDNR =254;             
                                             EPS.VILKÅR_10_5NR=999;             
                                             EPS.GP_OS_KODENR=999;              
                                          END;                                  
                                       /*** END VILKÅR 10-5 = J ***/            
                                    END;                                        
                                 ELSE                                           
                                    DO;                                         
                                       /*** VILKÅR 10-5 = N ***/                
                                       IF EPS.GP_OS_KODE = 'P' THEN             
                                          DO;                                   
                                            /* PENSJON */                       
                                            FEIL_FUNNET = '1'B;                 
                                            EPS.SIVILSTANDNR =253;              
                                            EPS.VILKÅR_10_5NR=999;              
                                            EPS.GP_OS_KODENR=999;               
                                          END;                                  
                                       /*** END VILKÅR 10-5 = N ***/            
                                    END;                                        
                                 /*** END OMSORG = N ***/                       
                              END;                                              
                           /*** END HJEMMEL 10-8 = N ***/                       
                        END;                                                    
                     /*** END SIVILSTAND = E ***/                               
                  END;                                                          
                ELSE                                                            
                  DO;                                                           
                     IF EPS.SIVILSTAND = 'S' THEN                               
                        DO;                                                     
                           /*** SIVILSTAND = S ***/                             
                           IF EPS.OMSORG_AVD_BA = 'J' THEN                      
                              DO;                                               
                                 /*** OMSORG BARN = J ***/                      
                                 IF EPS.VILKÅR_10_5 = 'N' THEN                  
                                    DO;                                         
                                       FEIL_FUNNET = '1'B;                      
                                       EPS.OMSORG_AVD_BANR =255;                
                                       EPS.VILKÅR_10_5NR=999;                   
                                    END;                                        
                                 /*** END OMSORG BARN = J ***/                  
                              END;                                              
                           ELSE                                                 
                              DO;                                               
                                 /*** OMSORG BARN = N ***/                      
                                 IF EPS.VILKÅR_10_5 = 'N' THEN                  
                                    DO;                                         
                                       IF EPS.GP_OS_KODE = 'P' THEN             
                                          DO;                                   
                                             FEIL_FUNNET = '1'B;                
                                             EPS.SIVILSTANDNR=256;              
                                             EPS.VILKÅR_10_5NR=999;             
                                             EPS.GP_OS_KODENR=999;              
                                          END;                                  
                                    END;                                        
                                 /*** END OMSORG BARN = N ***/                  
                              END;                                              
                        END;                                                    
                     /*** END SIVILSTAND = S ***/                               
                     ELSE                                                       
                        /*** SIVILSTAND = U ***/                                
                        DO;                                                     
                           IF EPS.OMSORG_AVD_BA = 'N' THEN                      
                              DO;                                               
                                  FEIL_FUNNET = '1'B;                           
                                  EPS.SIVILSTANDNR = 322;                       
                                  EPS.OMSORG_AVD_BANR = 999;                    
                              END;                                              
                        END;                                                    
                  END;                                                          
              END;   /* GP_OS KODE = O ELLER P */                               
         END;   /* FEIL_FUNNET */                                               
         /**************************/                                           
                                                                                
                                                                                
                                                                                
         IF ^FEIL_FUNNET THEN                                                   
            DO;                                                                 
               IF ((ALDER > 6612 & VIRK_DATO_Å < 1991) !/* 2000-3 */            
 /*HL*/            (ALDER > 6700                   ))      &                    
                    EPS.FORVENTET > 0                THEN                       
                  DO;                                                           
                     FEIL_FUNNET = '1'B;                                        
                     EPS.FORVENTETNR = 67;                                      
                  END;                                                          
               IF ((ALDER < 6700 ! ALDER > 6912) & EPS.FAI > 0   &              
                    VIRK_DATO_Å < 1991 )    !      /* 2000-3 */                 
                  ((ALDER < 6701 ! ALDER > 7000) & EPS.FAI > 0   &              
                    VIRK_DATO_Å > 1990 ) THEN    /* 2000-3 */                   
                  DO;                                                           
                     FEIL_FUNNET = '1'B;                                        
                     EPS.FAINR = 117;                                           
                  END;                                                          
               IF EPS.GP_OS_KODE   = 'O' &                                      
             (EPS.FRAMLEGGSKODE = ' ' ! EPS.FRAMLEGGSDATO_ÅMD = 0) THEN         
                  DO;                                                           
                     FEIL_FUNNET = '1'B;                                        
                     EPS.FRAMLEGGSKODENR = 74;                                  
                     EPS.FRAMLEGGSDATO_ÅMDNR = 999;                             
                     EPS.GP_OS_KODENR = 999;                                    
                  END;                                                          
               IF EPS.GP_OS_KODE   = 'P' &                                      
                  (EPS.FRAMLEGGSKODE ^= ' ' !                                   
                   EPS.FRAMLEGGSDATO_ÅMD > 0) THEN                              
                  DO;                                                           
                     FEIL_FUNNET = '1'B;                                        
                     EPS.FRAMLEGGSKODENR = 263;                                 
                     EPS.FRAMLEGGSDATO_ÅMDNR = 999;                             
                     EPS.GP_OS_KODENR = 999;                                    
                  END;                                                          
                                                                                
               IF ^F_40DAGER(EPS.VIRK_DATO_ÅMD)  THEN  /* 2000-3 */             
                  DO;                                                           
                     FEIL_FUNNET = '1'B;                                        
                     EPS.VIRK_DATO_ÅMDNR = 10;                                  
                  END;                                                          
               IF EPS.VIRK_DATO_ÅMD < 19670100 THEN /* 2000-3 */                
                  DO;                                                           
                     FEIL_FUNNET = '1'B;                                        
                     EPS.VIRK_DATO_ÅMDNR = 9;                                   
                  END;                                                          
               IF EPS.VIRK_DATO_ÅMD   < 19710100   &/* 2000-3 */                
                  EPS.YSKADE_TILLEGG  = 'J'    THEN                             
                  DO;                                                           
                     FEIL_FUNNET = '1'B;                                        
                     EPS.VIRK_DATO_ÅMDNR   = 310;/* 2000-3 */                   
                     EPS.YSKADE_TILLEGGNR = 999;                                
                  END;                                                          
            END;   /* FEIL_FUNNET */                                            
                                                                                
                                                                                
                                                                                
      IF ^ FEIL_FUNNET & EPS.DØDSDATO_ÅMD_EK > 19670000 THEN                    
         DO;                                                                    
            IF EPS.FNR_EK = 0 THEN                                              
               DO;                                                              
                                                                                
                                                                                
                  FEIL_FUNNET = '1'B;                                           
                  EPS.FNR_EKNR = 201;                                           
               END;                                                             
            IF EPS.NAVN_EK = (25)' ' THEN                                       
               DO;                                                              
                                                                                
                                                                                
                  FEIL_FUNNET = '1'B;                                           
                  EPS.NAVN_EKNR = 201;                                          
               END;                                                             
            IF EPS.NORSK_BOSATT_EK = ' ' THEN                                   
               DO;                                                              
                                                                                
                                                                                
                  FEIL_FUNNET = '1'B;                                           
                  EPS.NORSK_BOSATT_EKNR = 201;                                  
               END;                                                             
            IF EPS.VILKÅR_8_4_EK = ' ' THEN                                     
               DO;                                                              
                  FEIL_FUNNET = '1'B;                                           
                  EPS.VILKÅR_8_4_EKNR = 201;                                    
               END;                                                             
         END;                                                                   
                                                                                
 /*******************************************************************/          
 /* ALLE DØDSFALL MED YSKADE_TILLEGG SKAL AVVISES HVIS DØDSDATO ER  */          
 /* FØR 1.1.71.  HUSK F.MELD.NR.  ERIK 19.1.88.                     */          
 /*******************************************************************/          
                                                                                
              IF ^ FEIL_FUNNET & HJ_DØDSDATO_ÅMD < 19710101  &                  
                   HJ_DØDSDATO_ÅMD > 0  &                                       
                   EPS.FNR_EK  = 11111111111  THEN                              
            /*     EPS.YSKADE_TILLEGG = 'J' THEN  */                            
                       DO;                                                      
                          FEIL_FUNNET          = '1'B;                          
                          EPS.DØDSDATO_ÅMD_EKNR    = 323;                       
                          EPS.YSKADE_TILLEGGNR = 999;                           
                       END;                                                     
                                                                                
 /* *************************************************************** */          
 /* TIL HIT.  ERIK 19.1.88.                                         */          
 /* *************************************************************** */          
                                                                                
      IF ^ FEIL_FUNNET & HJ_DØDSDATO_ÅMD < 19670000 THEN                        
         DO;                                                                    
            IF EPS.FNR_EK > 0 &                                                 
               EPS.FNR_EK ^= 11111111111 THEN                                   
               DO;                                                              
                  FEIL_FUNNET = '1'B;                                           
                  EPS.FNR_EKNR = 202;                                           
               END;                                                             
            IF EPS.NAVN_EK ^= (25)' ' THEN                                      
               DO;                                                              
                  FEIL_FUNNET = '1'B;                                           
                  EPS.NAVN_EKNR = 202;                                          
               END;                                                             
            IF EPS.NORSK_BOSATT_EK ^= ' ' THEN                                  
               DO;                                                              
                  FEIL_FUNNET = '1'B;                                           
                  EPS.NORSK_BOSATT_EKNR = 202;                                  
               END;                                                             
               IF EPS.VILKÅR_8_4_EK ^= ' ' THEN                                 
                  DO;                                                           
                     FEIL_FUNNET = '1'B;                                        
                     EPS.VILKÅR_8_4_EKNR = 202;                                 
                  END;                                                          
               IF EPS.PI_EK > 0 THEN                                            
                  DO;                                                           
                     FEIL_FUNNET = '1'B;                                        
                     EPS.PI_EKNR = 202;                                         
                  END;                                                          
               DO IÅR = 1 TO 4;                                                 
                  IF EPS.VP_ÅR_EK(IÅR) > 0 THEN                                 
                     DO;                                                        
                        FEIL_FUNNET = '1'B;                                     
                        EPS.VP_ÅR_EKNR(IÅR) = 202;                              
                     END;                                                       
               END;                                                             
         END;                                                                   
                                                                                
      IF ^ FEIL_FUNNET & HJ_DØDSDATO_ÅMD > 19670000 THEN                        
         DO;                                                                    
            IF EPS.VP_ÅR_EK(1) > 0 &                                            
               EPS.VP_ÅR_EK(1) = EPS.VP_ÅR_EK(2) THEN                           
               DO;                                                              
                  FEIL_FUNNET = '1'B;                                           
                  EPS.VP_ÅR_EKNR(1) = 124;                                      
                  EPS.VP_ÅR_EKNR(2) = 999;                                      
               END;                                                             
                                                                                
            IF HJ_DØDSDATO_ÅMD >= DAGENS_DATO_ÅMD THEN                          
               DO;                                                              
                  FEIL_FUNNET = '1'B;                                           
                  EPS.DØDSDATO_ÅMD_EKNR = 76;                                   
               END;                                                             
                                                                                
            IF (VIRK_DATO_ÅM < DØDSDATO_ÅM  &                                   
                  DØDSDATO_ÅM < 199100) !                                       
               (VIRK_DATO_ÅM < DØDSDATO_ÅM + 1 &                                
                  DØDSDATO_ÅM > 199012)                 THEN                    
               DO;                                                              
                  FEIL_FUNNET = '1'B;                                           
                  EPS.DØDSDATO_ÅMD_EKNR = 77;                                   
                  EPS.VIRK_DATO_ÅMDNR = 999;                                    
               END;                                                             
                                                                                
            IF ^FEIL_FUNNET THEN                                                
               IF F_KJØNN(EPS.FNR_EK) = 'K' THEN                                
                  DO IÅR = 1 TO 4;                                              
                     IF EPS.VP_ÅR_EK(IÅR) > 0 THEN                              
                        DO;                                                     
                           FEIL_FUNNET = '1'B;                                  
                           EPS.VP_ÅR_EKNR(IÅR) = 47;                            
                           EPS.FNR_EKNR = 999;                                  
                        END;                                                    
                  END;                                                          
                                                                                
                                                                                
         IF EPS.NORSK_BOSATT = 'J' THEN                                         
            DO;                                                                 
               HJ_FNR      = EPS.FNR;                                           
               HJ_FNR13    = KONV_FNR11_FNR13(EPS.FNR);                         
               IF HJ_FNR_ÅR13 <= 1911      THEN                                 
                  EPSP.TT_F67 = 40;                                             
               ELSE                                                             
                  EPSP.TT_F67 = 1967 - (HJ_FNR_ÅR13 + 16);                      
                                                                                
               SEKSTISJU_ÅR = (HJ_FNR_ÅR13 + 67) * 100 + HJ_FNR_MND;            
                                                                                
               IF SEKSTISJU_ÅR ^< DØDSDATO_ÅM THEN                              
                  DO;                                                           
                     TT_TIL_Å = DØDSDATO_Å;                                     
                     TT_TIL_M = DØDSDATO_M;                                     
                  END;                                                          
                                                                                
        /* NB TT_TIL_Å REDEFINERER DE 4 FØRSTE POS AV SEKSTISJU_ÅR */           
        /* NB TT_TIL_M REDEFINERER DE 2 SISTE POS AV SEKSTISJU_ÅR  */           
        /* NB SLIK AT TT_TIL_Å OG TT_TIL_M BLIR LIK 67-ÅRSDAGEN    */           
        /* NB NÅR SEKSTISJU_ÅR < DØDSDATO_ÅM                       */           
                                                                                
                                                                                
                IF (HJ_FNR_ÅR13 + 16) > 1967 THEN                               
                   DO;                                                          
                      EPSP.TT_F67 = 0;                                          
                      EPSP.TT_E66_Å = TT_TIL_Å - (HJ_FNR_ÅR13 + 16);            
                   END;                                                         
                ELSE                                                            
                   EPSP.TT_E66_Å = TT_TIL_Å - 1967;                             
                EPSP.TT_E66_M =  TT_TIL_M;                                      
            END;                                                                
                                                                                
                                                                                
         IF EPS.NORSK_BOSATT_EK = 'J' THEN                                      
            DO;                                                                 
               HJ_FNR      = EPS.FNR_EK;                                        
               HJ_FNR13    = KONV_FNR11_FNR13(EPS.FNR_EK);                      
                                                                                
               IF HJ_FNR_ÅR <= 11     THEN                                      
                  EPSP.TT_F67_EK = 40;                                          
               ELSE                                                             
                  EPSP.TT_F67_EK = 1967 - (HJ_FNR_ÅR13 + 16);                   
                                                                                
               SEKSTISJU_ÅR = (HJ_FNR_ÅR13 + 67) * 100 + HJ_FNR_MND;            
                                                                                
               IF SEKSTISJU_ÅR ^< DØDSDATO_ÅM THEN                              
                  DO;                                                           
                     TT_TIL_Å = DØDSDATO_Å;                                     
                     TT_TIL_M = DØDSDATO_M;                                     
                  END;                                                          
        /* NB TT_TIL_Å REDEFINERER DE 4 FØRSTE POS AV SEKSTISJU_ÅR */           
        /* NB TT_TIL_M REDEFINERER DE 2 SISTE POS AV SEKSTISJU_ÅR  */           
        /* NB SLIK AT TT_TIL_Å OG TT_TIL_M BLIR LIK 67-ÅRSDAGEN    */           
        /* NB NÅR SEKSTISJU_ÅR < DØDSDATO_ÅM                       */           
                                                                                
                                                                                
                                                                                
               IF (HJ_FNR_ÅR13 + 16) > 1967 THEN                                
                  DO;                                                           
                     EPSP.TT_F67_EK   = 0;                                      
                     EPSP.TT_E66_Å_EK =                                         
                                        TT_TIL_Å - (HJ_FNR_ÅR13 + 16);          
                  END;                                                          
               ELSE                                                             
                   EPSP.TT_E66_Å_EK = TT_TIL_Å - 1967;                          
               EPSP.TT_E66_M_EK    =  TT_TIL_M;                                 
            END;                                                                
         END; /* IKKE FEIL FUNNET OG DØDSDATO > 1967*/                          
                                                                                
      /**** TRYGDETID NÅR AVD-FNR IKKE OPPGITT   ***/                           
      IF EPS.FNR_EK = (11)'1' & EPS.NORSK_BOSATT = 'J' THEN                     
         DO;                                                                    
            HJ_FNR      = EPS.FNR;                                              
            HJ_FNR13    = KONV_FNR11_FNR13(EPS.FNR);                            
                                                                                
            IF HJ_FNR_ÅR13 <= 11      THEN                                      
               EPSP.TT_F67 = 40;                                                
            ELSE                                                                
               EPSP.TT_F67 = 1967 - (HJ_FNR_ÅR13 + 16);                         
                                                                                
            SEKSTISJU_ÅR = (HJ_FNR_ÅR13 + 67) * 100 + HJ_FNR_MND;               
                                                                                
        /* NB TT_TIL_Å REDEFINERER DE 4 FØRSTE POS AV SEKSTISJU_ÅR */           
        /* NB TT_TIL_M REDEFINERER DE 2 SISTE POS AV SEKSTISJU_ÅR  */           
                                                                                
            IF (HJ_FNR_ÅR13 + 16) > 1967 THEN                                   
               DO;                                                              
                  EPSP.TT_F67 = 0;                                              
                  EPSP.TT_E66_Å = TT_TIL_Å - (HJ_FNR_ÅR13 + 16);                
               END;                                                             
            ELSE                                                                
                EPSP.TT_E66_Å = TT_TIL_Å - 1967;                                
            EPSP.TT_E66_M =  TT_TIL_M;                                          
         END;                                                                   
                                                                                
                                                                                
 END KONTROLL_EP_SØKER;                                                         
                                                                                
       /* ************************************************ */                   
       /* PROSEDYREN KONTROLLERER OPPLYSNINGENE PÅ         */                   
       /* YRKESSKADEBILDET.                                */                   
       /* ************************************************ */                   
                                                                                
       KONTROLL_EP_YRKESSKADE:                                                  
          PROC(FEIL_FUNNET);                                                    
                                                                                
        DCL FEIL_FUNNET         BIT (1);                                        
                                                                                
                                                                                
        DCL 1 FNR_REG,                                                          
              2 FNR1 FIXED DEC(11),                                             
              2 FNR2 FIXED DEC(11);                                             
                                                                                
        ANT_FEIL_SKREVET = 0;                                                   
 /*YS   HVIS INGEN FELT I UFØREDELEN ER UTMYLT, SÅ GÅR VI UT FRA */             
 /*YS   AT DET ER DØDSFALL PGA EN GAMMEL YRKESSKADE - DA SKAL    */             
 /*YS   NEMLIG INGEN OPPLYSNINGER ENDRES                         */             
                                                                                
        IF ^ (                                                                  
            EPY.VILKÅR_1_2_2A   = ' '         &                                 
            EPY.VILKÅR_1_2_2B   = ' '         &                                 
            EPY.YST_ÅMD         = 0           &                                 
            EPY.YSYKDOM         = 'N'         &                                 
            EPY.YUG             = 0           &                                 
            EPY.KAP_11_7_UTLØST = ' '         &                                 
            EPY.YRKE11          = ' '         &                                 
            EPY.AÅI             = 9999999        )   THEN                       
                                                                                
        DO;                                                                     
                                                                                
        IF ^(EPY.VILKÅR_1_2_2A = 'J' ! EPY.VILKÅR_1_2_2A = ' ') THEN            
           DO;                                                                  
              FEIL_FUNNET   ='1'B;                                              
              EPY.VILKÅR_1_2_2ANR = 301;                                        
           END;                                                                 
                                                                                
                                                                                
        IF ^(EPY.VILKÅR_1_2_2B = 'J' ! EPY.VILKÅR_1_2_2B = ' ') THEN            
           DO;                                                                  
              FEIL_FUNNET   ='1'B;                                              
              EPY.VILKÅR_1_2_2BNR = 302;                                        
           END;                                                                 
                                                                                
        IF ^F_NUMERISK(F_FELT_PIC_CHAR8(EPY.YST_ÅMD)) THEN                      
           DO;                                                                  
              FEIL_FUNNET    = '1'B;                                            
              EPY.YST_ÅMDNR = 200;                                              
           END;                                                                 
        ELSE                                                                    
           DO;                                                                  
              IF ^F_GYLDIG_DATO(EPY.YST_ÅMD) THEN                               
                 DO;                                                            
                    FEIL_FUNNET    = '1'B;                                      
                    EPY.YST_ÅMDNR = 304;                                        
                 END;                                                           
              ELSE                                                              
                 IF EPY.YST_ÅMD < 19710100 THEN                                 
                    DO;                               /* 2000-3 */              
                       FEIL_FUNNET    = '1'B;                                   
                       EPY.YST_ÅMDNR = 305;                                     
                    END;                                                        
           END;                                                                 
                                                                                
        IF ^(EPY.YSYKDOM = 'J' ! EPY.YSYKDOM = 'N') THEN                        
           DO;                                                                  
              FEIL_FUNNET   ='1'B;                                              
              EPY.YSYKDOMNR = 306;                                              
           END;                                                                 
                                                                                
        IF F_FELT_PIC_CHAR3(EPY.YUG) ^= '000' THEN                              
           DO;                                                                  
              FEIL_FUNNET = '1'B;                                               
              EPY.YUGNR = 326;                                                  
           END;                                                                 
                                                                                
        IF EPY.KAP_11_7_UTLØST ^= ' ' THEN                                      
           DO;                                                                  
              FEIL_FUNNET   ='1'B;                                              
              EPY.KAP_11_7_UTLØSTNR = 326;                                      
           END;                                                                 
                                                                                
        IF VERIFY(EPY.YRKE11,'FMUEVSA') ^= 0 THEN                               
           DO;                                                                  
              FEIL_FUNNET = '1'B;                                               
              EPY.YRKE11NR =  309 ;                                             
           END;                                                                 
                                                                                
        IF ^F_NUMERISK(F_FELT_PIC_CHAR7(EPY.AÅI)) THEN                          
           DO;                                                                  
              FEIL_FUNNET = '1'B;                                               
              EPY.AÅINR = 200 ;                                                 
           END;                                                                 
                                                                                
        IF EPY.YST_ÅMD > EPS.VIRK_DATO_ÅMD !/* 2000-3 */                        
           EPY.YST_ÅMD > EPS.DØDSDATO_ÅMD_EK  &                                 
            EPS.FNR_EK  > 11111111111  THEN                                     
           DO;                                                                  
                                                                                
                    /* ****************************************** */            
                    /* FEIL,YSKADETIDSPUNKTET > VIRK ELLER        */            
                    /* DØDSDATO PÅ KAP.10-BLANKETTEN.             */            
                    /* ****************************************** */            
                                                                                
              FEIL_FUNNET    = '1'B;                                            
              EPY.YST_ÅMDNR   =  328 ;                                          
           END;                                                                 
        IF ^FEIL_FUNNET THEN                                                    
           DO;                                                                  
                                                                                
              IF (EPY.YRKE11    = 'F' !                                         
                  EPY.YRKE11    = 'V' !                                         
                  EPY.YRKE11    = 'U' !                                         
                  EPY.YRKE11    = 'E' )      THEN                               
                  IF EPY.AÅI      ^= 9999999 THEN                               
                    DO;                                                         
                                                                                
        /* ********************************************************** */        
        /* KODE F = FISKER.  KODE V = ELEV.  INNTEKT SKAL IKKE OPPGIS */        
        /* FOR DISSE.                                                 */        
        /* ********************************************************** */        
                                                                                
                       FEIL_FUNNET     = '1'B;                                  
                       EPY.AÅINR       =  314;                                  
                       EPY.YRKE11NR    =  999;                                  
                    END;                                                        
                 ELSE;                                                          
              ELSE    /*YRKE ER DA M,S,A */                                     
                 IF EPY.AÅI      = 9999999 THEN                                 
                    DO;                                                         
                                                                                
        /* ********************************************************** */        
        /* INNTEKT SKAL REGISTRERES FOR ALLE ANDRE KODER.             */        
        /* ********************************************************** */        
                                                                                
                       FEIL_FUNNET     = '1'B;                                  
                       EPY.AÅINR       =  316;                                  
                       EPY.YRKE11NR    =  999;                                  
                    END;                                                        
           END;                                                                 
                                                                                
        END; /*SLUTT PÅ TEST OM ALLE FELT ER BLANKE*/                           
                                                                                
        IF F_FELT_PIC_CHAR11(EPY.FNR_EK) ^= (11) '0' THEN                       
           IF ^F_NUMERISK(F_FELT_PIC_CHAR11(EPY.FNR_EK)) THEN                   
              DO;                                                               
                 FEIL_FUNNET = '1'B;                                            
                 EPY.FNR_EKNR = 200;                                            
              END;                                                              
           ELSE                                                                 
              IF ^F_GYLDIG_FNR(EPY.FNR_EK) THEN                                 
                 DO;                                                            
                    FEIL_FUNNET = '1'B;                                         
                    EPY.FNR_EKNR = 1;                                           
                 END;                                                           
              ELSE                                                              
                 DO;                                                            
                    FNR_REG.FNR1 = EPY.FNR_EK;                                  
                    EXEC CICS LINK PROGRAM ('R0019906')                         
                                   COMMAREA(FNR_REG   );                        
                    IF FNR_REG.FNR2 > 0 THEN                                    
                       DO;                                                      
                          EPY.FNR_EKNR = 203;                                   
                          EPY.FNR_GML_EK = EPY.FNR_EK;                          
                          EPY.FNR_EK = FNR_REG.FNR2;                            
                       END;                                                     
                    IF F_FELT_PIC_CHAR11(EPY.FNR_SAMBOER) ^= (11)'0'            
                                                             THEN               
                       DO;                                                      
                          FEIL_FUNNET = '1'B;                                   
                          EPY.FNR_EKNR      = 336;                              
                          EPY.FNR_SAMBOERNR = 999;                              
                       END;                                                     
                    IF EPY.FNR_EK ^= EPS.FNR                 THEN               
                       DO;                                                      
                          FEIL_FUNNET = '1'B;                                   
                          EPY.FNR_EKNR = 335;                                   
                       END;                                                     
                 END;                                                           
        ELSE                                                                    
           IF F_FELT_PIC_CHAR11(EPY.FNR_SAMBOER) ^= (11) '0' THEN               
              IF ^F_NUMERISK(F_FELT_PIC_CHAR11(EPY.FNR_SAMBOER)) THEN           
                 DO;                                                            
                    FEIL_FUNNET = '1'B;                                         
                    EPY.FNR_SAMBOERNR = 200;                                    
                 END;                                                           
              ELSE                                                              
                 IF ^F_GYLDIG_FNR(EPY.FNR_SAMBOER) THEN                         
                    DO;                                                         
                       FEIL_FUNNET = '1'B;                                      
                       EPY.FNR_SAMBOERNR = 1;                                   
                    END;                                                        
                 ELSE                                                           
                    DO;                                                         
                       FNR_REG.FNR1 = EPY.FNR_SAMBOER;                          
                       EXEC CICS LINK PROGRAM ('R0019906')                      
                                      COMMAREA( FNR_REG  );                     
                       IF FNR_REG.FNR2 > 0 THEN                                 
                          DO;                                                   
                             EPY.FNR_SAMBOERNR = 203;                           
                             EPY.FNR_GML_SAMBOER = EPY.FNR_SAMBOER;             
                             EPY.FNR_SAMBOER = FNR_REG.FNR2;                    
                          END;                                                  
                       IF F_FELT_PIC_CHAR11(EPY.FNR_EK) ^=                      
                                                     (11)'0' THEN               
                          DO;                                                   
                             FEIL_FUNNET = '1'B;                                
                             EPY.FNR_EKNR      = 336;                           
                             EPY.FNR_SAMBOERNR = 999;                           
                          END;                                                  
                       IF EPY.FNR_SAMBOER ^= EPS.FNR          THEN              
                          DO;                                                   
                             FEIL_FUNNET = '1'B;                                
                             EPY.FNR_SAMBOERNR = 335;                           
                          END;                                                  
                    END;                                                        
           ELSE                                                                 
              /* ****************************************** */                  
              /* VERKEN FNR_EK ELLER FNR_SAMBOER ER FYLT UT */                  
              /* ****************************************** */                  
              DO;                                                               
                 FEIL_FUNNET         = '1'B;                                    
                 EPY.FNR_EKNR        = 327 ;                                    
              END;                                                              
                                                                                
        IF F_FELT_PIC_CHAR11(EPY.FNR_Y_BARN) ^= (11)'0'  THEN                   
           DO;                                                                  
              FEIL_FUNNET         = '1'B;                                       
              EPY.FNR_Y_BARNNR    =  326 ;                                      
           END;                                                                 
                                                                                
        IF EPY.PLEIEBARN ^= ' '  THEN                                           
           DO;                                                                  
              FEIL_FUNNET         = '1'B;                                       
              EPY.PLEIEBARNNR     = 326 ;                                       
           END;                                                                 
                                                                                
                                                                                
                                                                                
        IF ^FEIL_FUNNET THEN                                                    
           DO;                                                                  
              IF ^(EPY.VILKÅR_1_2_2A   = ' '  !                                 
                   EPY.VILKÅR_1_2_2B   = ' '  )  &                              
                   EPS.NORSK_BOSATT = 'J'  THEN                                 
                 DO;                                                            
                                                                                
        /* ********************************************************** */        
        /* VILKÅR 1-2-2 ER UTFYLT.  PERSONEN ER DA IKKE BOSATT I      */        
        /* NORGE, MEN EP-BLANKETTEN ER KRYSSET AV MED JA I FELTET     */        
        /* NORSK BOSATT.  BLANKETTEN AVVISES.                         */        
        /* JEG HAR IKKE SATT OPP NOE FEILMELDINGSNUMMER HER.          */        
        /* ********************************************************** */        
                                                                                
                    FEIL_FUNNET         = '1'B;                                 
                    EPY.VILKÅR_1_2_2ANR =  318 ;                                
                    EPY.VILKÅR_1_2_2BNR =  999 ;                                
                 END;                                                           
           END;                                                                 
                                                                                
        END KONTROLL_EP_YRKESSKADE;                                             
