 /*   SIST ENDRET PÅ PROD   2008.05.31 11.13.30 AV   SPA2990          */        
 /*       SIST ENDRET 04/05-98 14.27.17 AV   JDA7339                  */        
 /*       SIST ENDRET 16/01-98 12.21.42 AV   SPA7339                  */        
 R00120:                                                                        
   PROC(COMMAREA_PEKER) OPTIONS(MAIN);                                          
                                                                                
                                                                                
 /* ***************************************************************** */        
 /*                                                                   */        
 /*IDENTIFIKASJON:                                                    */        
 /*     R0012001 - PROSEDYRE I PLI                                    */        
 /*     PROGRAMMERER: PATHAK S 20.2.92                                */        
 /*                                                                   */        
 /*HENSIKT:                                                           */        
 /*     RUTINEN TAR UTGANGSPUNKT I STYRINGSKODEN (BLANKETT REGISTRERT)*/        
 /*     OG OVERLATER KONTROLL TIL RUTINEN SOM BESTEMMER OM OG         */        
 /*     EVENTUELT HVORDAN "BEHANDLE-TRANS" SKAL ANVENDES.             */        
 /*                                                                   */        
 /*     DEN FORETAR OGSÅ INITIERINGS-KALLET HVOR PCB-ADDRESSENE       */        
 /*     OPPRETTES. FÅR EN FEIL I INITIERINGS-KALLET AVSLUTTES         */        
 /*     BEHANDLINGEN OG KONTROLLEN RETURNERES TIL    C I C S .        */        
 /*                                                                   */        
 /*     TRANSKODEN ER R200.                                           */        
 /*                                                                   */        
 /*                                                                   */        
 /* ***************************************************************** */        
 %PAGE;                                                                         
                                                                                
   DCL                                                                          
      COMMAREA_PEKER PTR;                                                       
   DCL                                                                          
      (CSTG , ADDR , UNSPEC , NULL , DATE )       BUILTIN;                      
                                                                                
 %SKIP(2);                                                                      
      %INCLUDE P0019908;               /*  KOM_OMR         (BASED)  */          
 %SKIP;                                                                         
      %INCLUDE P0019906;               /*  TRANS_OPPL_OMR  (BASED)  */          
 %PAGE;                                                                         
      %INCLUDE P0019910;               /*  STYRINGS_OMR    (BASED)  */          
 %PAGE;                                                                         
      %INCLUDE P0019912;               /*  DIV_PARAM_OMR   (BASED)  */          
 %PAGE;                                                                         
      %INCLUDE P0019924;               /* G_V_TAB                   */          
 %PAGE;                                                                         
      %INCLUDE P0019925;               /* G_TAB                     */          
    /*                                                                          
    */                                                                          
    /* ************************************************************** */        
    /*                                                                */        
    /*   DL1-PARAMETRE SOM BRUKES VED INITIERINGS- OG AVSLUTNINGS-    */        
    /*   KALL                                                         */        
    /*                                                                */        
    /**************************************************************** */        
       DCL                                                                      
         PLITDLI                     ENTRY;                                     
                                                                                
       DCL                                                                      
         1 W001_DLI                  UNALIGNED,                                 
                                                                                
           2  W01_PSB_R001           CHAR (8)       INIT('        '),           
           2  W01_PSB_NAVN           CHAR (8)       INIT('        '),           
           2  W01_PCB_FUNCTION       CHAR (4)       INIT('PCB '),               
           2  W01_TERM_FUNCTION      CHAR (4)       INIT('TERM'),               
           2  S_MELDNR               PIC '9'        INIT( 0 ),                  
           2  W01_PARM_CT_1          FIXED BIN(31)  INIT(1),                    
           2  W01_PARM_CT_3          FIXED BIN(31)  INIT(3);                    
                                                                                
    /* ************************************************************** */        
    /*                                                                */        
    /*   ARBEIDSFELTER SOM BRUKES AV UIB (USER INTERFACE BLOCK)       */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
    %INCLUDE DLIUIB;                                                            
                                                                                
       DCL                                                                      
           UIB_RC_OK                 BIT  (8)             INIT ( 0 );           
                                                                                
                                                                                
    /* ************************************************************** */        
    /*                                                                */        
    /*   PCB- OG UIB-OMR.                                             */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
    %INCLUDE  P0012002;                  /* PCB-UIB-PEKER OMR  BASED  */        
    %INCLUDE  P0014009;                  /* POTALL_OPPL   BASED  */             
                                                                                
                                                                                
    /* ************************************************************** */        
    /*   FEILMELDINGS-LINJE .                                         */        
    /* ************************************************************** */        
                                                                                
    DCL   W01_UTLINJE           CHAR (78)     INIT ( (78)' ' );                 
                                                                                
                                                                                
    DCL GRUNN_OMR1         CHAR      (1440);                                    
    DCL GRUNN_OMR2         CHAR      (1440);                                    
    DCL GRUNN_IDENT        CHAR      ( 8);                                      
                                                                                
                                                                                
    /* ************************************************************** */        
    /*                                                                */        
    /*   SLUTT PÅ DEKLARASJONENE .   EKSEKVERINGEN STARTER            */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
    ALLOCATE                    POTALL_OPPL  ;                                  
    ALLOCATE                    GV_TAB_RE    ;                                  
    ALLOCATE                    G_TAB_RE     ;                                  
    KOM_OMR.GV_PEKER         = ADDR (GRUNN_OMR1 ) ;                             
    KOM_OMR.G_PEKER          = ADDR (GRUNN_OMR2 ) ;                             
    POTALL_OPPL              =  ''           ;                                  
    GV_TAB_RE                =  ''           ;                                  
    G_TAB_RE                 =  ''           ;                                  
                                                                                
    W01_PSB_NAVN             =  DIV_PARAM_OMR.PSB_NAVN;                         
                                                                                
    GRUNN_IDENT                     = 'P0019924';                               
                                                                                
    CALL P9956_BER_G_CICS(GRUNN_IDENT,GRUNN_OMR1);                              
    GRUNN_IDENT                     = 'P0019925';                               
    CALL P9956_BER_G_CICS(GRUNN_IDENT,GRUNN_OMR2);                              
                                                                                
                                                                                
    SELECT(STYREKODE);                                                          
                                                                                
         WHEN ('AU')                                                            
            DO;                                                                 
                                                                                
               CALL ÅPNE_DATABASE;                                              
               KJØRINGS_TYPE = 'O';                                             
               EXEC CICS LINK PROGRAM('R0011820') COMMAREA(KOM_OMR);            
               TRANSKODE = TRANS_RETURKODE;                                     
               EXEC CICS SYNCPOINT;                                             
               CALL      PLITDLI               (W01_PARM_CT_1,                  
                                           W01_TERM_FUNCTION);                  
               KOM_OMR.PCB_UIB_PEKER = NULL;                                    
                                                                                
            END;                                                                
                                                                                
                                                                                
         OTHERWISE;                                                             
                                                                                
      END;     /* SLUTT PÅ 'SELECT (STYREKODE)' */                              
                                                                                
    /* ************************************************************** */        
    /*                                                                */        
    /*   TERMINERINGSKALLET FOR PCB'ENE                               */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
                                                                                
 ÅPNE_DATABASE:                                                                 
   PROC;                                                                        
    CALL   PLITDLI                              (W01_PARM_CT_3,                 
                                                 W01_PCB_FUNCTION,              
                                                 W01_PSB_NAVN,                  
                                                 UIBPTR);                       
                                                                                
                                                                                
    IF DLIUIB.UIBFCTR                      ^=    UIB_RC_OK       THEN           
      DO;                                                                       
        /* ********************************************************** */        
        /*                                                            */        
        /*     FEIL I INITIERINGKALL                                  */        
        /*                                                            */        
        /* ********************************************************** */        
                                                                                
        W01_UTLINJE    =    'FEIL VED INITIERINGS-KALLET :'         !!          
                            '     UIBFCTR = ' !!  UNSPEC (UIBFCTR)  !!          
                            '     UIBDLTR = ' !!  UNSPEC (UIBDLTR)  ;           
                                                                                
        EXEC CICS SEND TEXT FROM ( W01_UTLINJE )  ACCUM  PAGING ERASE           
                       JUSTIFY   ( 14          )  ;                             
        EXEC CICS SEND PAGE;                                                    
        EXEC CICS      RETURN;                                                  
                                                                                
      END;                                                                      
                                                                                
    ALLOCATE  PCB_UIB_PEKER_OMR;                                                
                                                                                
    /* ********************************************************** */            
    /*                                                            */            
    /*  SETTER  PCB_PEKER  =  UIBPCBAL  FRA INITIERINGS-KALLET    */            
    /*                                                            */            
    /* ********************************************************** */            
                                                                                
    PCB_UIB_PEKER_OMR.UIB_PEKER          =    UIBPTR  ;                         
    PCB_UIB_PEKER_OMR.PCB_PEKER          =    UIBPCBAL;                         
                                                                                
 END ÅPNE_DATABASE;                                                             
                                                                                
 %PAGE;                                                                         
 %INCLUDE R0019956;          /*     P9956_BER_G_CICS    */                      
                                                                                
   END R00120;                                                                  
