 /*   SIST ENDRET PÅ PROD   2006.02.13  8.15.04 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2006.02.09  8.03.34 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2005.11.29 11.57.23 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2005.11.29 11.56.30 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2005.11.29  9.55.11 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2005.11.09  8.40.27 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2005.08.12 11.55.04 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2005.08.11  8.39.54 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2004.08.06  8.12.39 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.08.02 11.17.10 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2004.06.21 11.20.26 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2004.05.14 13.16.16 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.05.03 12.32.05 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2003.06.19 15.33.39 AV   SPA2970          */        
 /*   SIST ENDRET PÅ TEST   2003.03.14 10.38.19 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2002.09.19  8.22.08 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.07.26 14.24.57 AV   HLA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.07.17 11.58.32 AV   HLA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.01.30 14.32.28 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.11.14 12.38.00 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.11.10 10.14.59 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.11.07 12.22.49 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.08.02 12.01.58 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.07.17 12.35.47 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.07.14 12.51.11 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.07.07 13.00.41 AV   JDA7339          */        
 /* ***************************************************************** */        
 /* IDENTIFIKASJON:                                                   */        
 /*    R0014254 - PRO_RATA__PROSENT - INTERN PROC I PLI.              */        
 /*    PROGRAMMERER: MARTIN HELLE, MAI 2000.                          */        
 /* HENSIKT:                                                          */        
 /*    PROGRAMMET BEREGNER REDUKSJON I AFP HVOR UTTAK 01.08.2000      */        
 /*    ELLER SENERE.                                                  */        
 /* PROGRAMTILKNYTNING:                                               */        
 /*    PROGRAMMET BLIR INKLUDERT I / CALL FRA                         */        
 /*       4251 / 4251                                                 */        
 /* BRUK:                                                             */        
 /*    CALL PRO_RATA_INNTEKT_AFP                                      */        
 /* PROGRAMENDRING:                                                   */        
 /* ***************************************************************** */        
 PRO_RATA_INNTEKT_AFP: PROC;                                                    
    DCL SUM_PENSJON    FIXED DEC (9,2)  INIT (0),                               
        SUM_BRUTTO     FIXED DEC (7)    INIT (0),                               
        SUM_FAI        FIXED DEC (7)    INIT (0),                               
        SUM_TAI        FIXED DEC (7)    INIT (0),                               
        TAI_PLUSS      FIXED DEC (7)    INIT (0),                               
        ENDRET               BIT (1) ALIGNED INIT ('0'B),                       
        MAX            BUILTIN,                                                 
        PÅ_45          FIXED DEC (3)    INIT (0),                               
        PÅ_42          FIXED DEC (3)    INIT (0),                               
        RED_PROSENT    FIXED DEC (3)    INIT (0),                               
        TOLERANSE      FIXED DEC (7)    INIT (0),                               
        UTB            FIXED DEC (3,2)  INIT (0),                               
        W_BRP          FIXED DEC (7,2)  INIT (0),                               
        W_AFPHIST_DATO PIC '99999999',                                          
        W_SUM_BRUTTO   FIXED DEC (7)    INIT (0),                               
        W_SUM_MINST    FIXED DEC (7)    INIT (0),                               
        W_SUM_PENSJON  FIXED DEC (9,2)  INIT (0),                               
        W_TAI          FIXED DEC (7)    INIT (0),                               
        W_SUM_TPST     FIXED DEC (7)    INIT (0),                               
        W_ST           FIXED DEC (9,2)  INIT (0),                               
        W_ST_BRUTTO    FIXED DEC (9,2)  INIT (0),                               
        W_ST_TOTAL     FIXED DEC (5)    INIT (0),                               
        W_ST_PROSENT   FIXED DEC (5,2)  INIT (0),                               
        W_GP           FIXED DEC (9,2)  INIT (0),                               
        W_GP_PROSENT   FIXED DEC (9,3)  INIT (0),    /*200402 HL */             
        W_AFPT         FIXED DEC (9,2)  INIT (0),                               
        W_MAX_AFP_70   FIXED DEC (9,2)  INIT (0), /*  70% REGELEN */            
        W_MAX_AFP_100  FIXED DEC (9,2)  INIT (0), /* 100% REGELEN */            
        W_TP           FIXED DEC (9,2)  INIT (0);                               
                                                                                
    /* R0014254 STARTER HER ========================================= */        
    ENDRET = '1'B;                                                              
                                                                                
  /*ENDRET 200208 HL : */                                                       
                                                                                
    IF TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD < 20020000  THEN                        
       TOLERANSE = 4000;                                                        
    ELSE                                                                        
       TOLERANSE = 15000;                                                       
                                                                                
    CALL TIDLIGERE_ARBEIDSINNTEKT(W_TAI,W_BRP);       /* 4135 */                
                                                                                
    SUM_FAI = B02.ALDERSP.FAI(BER_SØ_IND) * 100;                                
    SUM_TAI = DIV_PARAM_OMR.TAI(BER_SØ_IND);                                    
    TAI_PLUSS = SUM_TAI + 1000;                                                 
                                                                                
 /* IF SUM_FAI > SUM_TAI    THEN   */                                           
    IF SUM_FAI > TAI_PLUSS  THEN                                                
       IF ( TRANSTYPE = 40   !         /*200407 HL */                           
            TRANSTYPE = 8      )       THEN                                     
  L320:                                                                         
               DO;                                                              
                  FEIL_VED_LABEL = '320';                                       
                  FEIL_MELD_NR   = 1744;                                        
               END;                                                             
                                                                                
                                                                                
    /* -------------------------------------------------------------- */        
    /* NÅ BENYTTES ÅRSBELØP TIL Å BEREGNE DEN UREDUSERTE YTELSEN      */        
    /* -------------------------------------------------------------- */        
    IF (B02.ALDERSP.TP(BER_SØ_IND) > 0) THEN                                    
       DO;  /* HAR TILLEGGSPENSJONEN */                                         
          PÅ_45 = B02.ALDERSP.PÅ(BER_SØ_IND)                                    
                - B02.ALDERSP.PÅ_ETTER91(BER_SØ_IND);                           
          PÅ_42 = B02.ALDERSP.PÅ_ETTER91(BER_SØ_IND);                           
          W_TP = F_TP                                                           
                 (                                                              
                  W_FNR_R13.ÅR,                                                 
                  G,                                                            
                  (B02.ALDERSP.SPT(BER_SØ_IND)),                                
                  (B02.ALDERSP.OPT(BER_SØ_IND)),                                
                  PÅ_45,                                                        
                  PÅ_42,                                                        
                  100,                                                          
                  100,                                                          
                  'J',                                                          
                  'Å',                                                          
                  2                                                             
                 );                                                             
       END; /* HAR TILLEGGSPENSJONEN */                                         
    /* -------------------------------------------------------------- */        
    IF (B02.ALDERSP.GP(BER_SØ_IND) > 0) THEN                                    
       DO;  /* HAR GRUNNPENSJONEN */                                            
          IF (B02.PENSJONSTYPE2 (BER_SØ_IND) = 'S'    !                         
              (B02.PENSJONSTYPE2 (BER_SØ_IND) = 'N' &                           
               B02.PENSJONSTYPE2 (BER_EK_IND) = 'P' &                           
               B02.SIVILSTAND   (BER_SØ_IND) ^= 'A' ) ) THEN                    
             IF (B02.GAMMEL_SAMMENSTØTS_REGEL(BER_SØ_IND) = 'J') THEN           
                DO;                                                             
                   W_GP_PROSENT =                                               
                   1 - REST_PROSENT * B02.UFØRPENS.UFG(BER_EK_IND)/100;         
                   IF (W_GP_PROSENT < GP_PROSENT) THEN                          
                      W_GP_PROSENT = GP_PROSENT;                                
                END;                                                            
             ELSE                                                               
                DO;                                                             
 /*TILLEGG HL */  IF B02.SIVILSTAND      (BER_SØ_IND) = 'V'   &                 
 /*R 1303 JFA */    (B02.PENSJON_FØR_9802(BER_SØ_IND) = 'J' !                   
 /*           */     B02.PENSJON_FØR_9802(BER_SØ_IND) = 'X' ) THEN              
 /*           */     W_GP_PROSENT = 1.00;                                       
 /* 17.8.05   */  ELSE                                                          
                     W_GP_PROSENT = GP_PROSENT;                                 
                END;                                                            
          ELSE                                                                  
             IF (DIV_PARAM_OMR.REDUSERT_GP_2G(BER_SØ_IND) ^= ' ') THEN          
                W_GP_PROSENT = GP_PROSENT;                                      
             ELSE                                                               
                W_GP_PROSENT = 1.00;                                            
                                                                                
          W_GP = W_GP_PROSENT * G * B02.TT_ANV(BER_SØ_IND)/40;                  
       END; /* HAR GRUNNPENSJONEN */                                            
                                                                                
    /* -------------------------------------------------------------- */        
  /*IF (B02.ALDERSP.ST(BER_SØ_IND) > 0) THEN */                                 
       DO;  /* KAN HA SÆRTILLEGG */                                             
          IF ((B02.PENSJONSTYPE2 (BER_SØ_IND) = 'S')  &                         
             ^(B02.PENSJONSTYPE3 (BER_EK_IND) = 'K')  &                         
             ^(B02.PENSJONSTYPE3 (BER_EK_IND) = 'S')     )    !                 
             (B02.PENSJONSTYPE2 (BER_SØ_IND) = 'N' &                            
              B02.PENSJONSTYPE2 (BER_EK_IND) = 'P' &                            
              B02.SIVILSTAND    (BER_EK_IND) = 'G') THEN                        
             W_ST_PROSENT = ST_PROSENT_SAMST;                                   
          ELSE                                                                  
             IF (B02.PENSJONSTYPE2 (BER_SØ_IND) = 'F' ) !                       
                (B02.PENSJONSTYPE2 (BER_SØ_IND) = 'N' &                         
                 B02.PENSJONSTYPE1 (BER_EK_IND) = 'F' ) THEN                    
                DO;  /* PT2 = 'F' */                                            
                   IF (ALDER_EK        >= 6000    &                             
                       W_DATO_ÅMD      >  19890300    &                         
                       W_DATO_ÅMD      <  19910000)        !                    
                      (ALDER_EK        >  6000    &                             
                       W_DATO_ÅMD      >  19910000)      THEN                   
                      W_ST_PROSENT = ST_PROSENT_SAMST * 2;                      
                   ELSE                                                         
                      W_ST_PROSENT = ST_PROSENT_ENSLIG;                         
                                                                                
                   IF  ALDER_EK        >= 6001    &                             
                       W_DATO_ÅMD      >  19931200    THEN                      
                       W_ST_PROSENT = ST_PROSENT_ENSLIG * 2;                    
                END; /* PT2 = 'F' */                                            
             ELSE                                                               
                W_ST_PROSENT = ST_PROSENT_ENSLIG;                               
                                                                                
          W_ST_BRUTTO = G * W_ST_PROSENT/100;                                   
          W_ST_BRUTTO = W_ST_BRUTTO * B02.TT_ANV(BER_SØ_IND) / 40;              
          W_ST        = W_ST_BRUTTO - W_TP;                                     
          IF (0 > W_ST) THEN                                                    
             W_ST = 0;                                                          
       END; /* KAN HA SÆRTILLEGG */                                             
                                                                                
    /* -------------------------------------------------------------- */        
    /* HER REGNES AFP-TILLEGG UT SOM ÅRSBELØP                         */        
    /* -------------------------------------------------------------- */        
    IF B02.PENSJONSTYPE3(BER_SØ_IND)  = 'N' THEN                                
       W_AFPT = 0;                                                              
    ELSE                                                                        
       DO;  /* SKAL HA AFP TILLEGG */                                           
          IF (B02.PENSJONSTYPE3(BER_SØ_IND) = 'B' !                             
              B02.PENSJONSTYPE3(BER_SØ_IND) = 'K' !                             
              B02.PENSJONSTYPE3(BER_SØ_IND) = 'V' !                             
              B02.PENSJONSTYPE3(BER_SØ_IND) = 'S' )       !                     
             (B02.VIRK_DATO_ÅMD(BER_SØ_IND) > 19930900  &                       
              B02.PENSJONSTYPE2(BER_SØ_IND) = 'N'       ) THEN                  
             W_AFPT = 1700;                                                     
          ELSE                                                                  
             W_AFPT = 950;                                                      
          W_AFPT = W_AFPT * 12;                                                 
       END; /* SKAL HA AFP TILLEGG */                                           
                                                                                
    /* -------------------------------------------------------------- */        
    /* HER KONTROLLERES AT AFP IKKE OVERSTIGER 70% AV TEI             */        
    /* 060700: IFLG MONICA PEDERSEN SKAL OGSÅ AFP-TILLEGGET TAS MED   */        
    /*         NÅR VI BEREGNER 70% REGELEN                            */        
    /* -------------------------------------------------------------- */        
    W_SUM_PENSJON = W_TP + W_GP + W_ST + W_AFPT;                                
    W_MAX_AFP_70  = SUM_TAI * .7;                                               
                                                                                
    IF (W_SUM_PENSJON > W_MAX_AFP_70) THEN                                      
       DO;  /* REDUKSJON GRUNNA 70 PROSENTEN */                                 
          DIV_PARAM_OMR.AP_KODE_AEY(BER_SØ_IND) = 'W';                          
          /* FORHINDRER ØKNING AV SÆRTILLEGG I 4010 */                          
          DIV_PARAM_OMR.AFP_REDUSERT_70(BER_SØ_IND) = 'J';                      
          W_SUM_MINST = W_MAX_AFP_70 / 12 + 0.5;                                
          W_TP   =  W_TP    * W_MAX_AFP_70 / W_SUM_PENSJON + 0.005;             
          W_GP   =  W_GP    * W_MAX_AFP_70 / W_SUM_PENSJON + 0.005;             
          W_ST   =  W_ST    * W_MAX_AFP_70 / W_SUM_PENSJON + 0.005;             
          W_ST_BRUTTO = W_ST_BRUTTO *                                           
                        W_MAX_AFP_70 / W_SUM_PENSJON + 0.005;                   
          W_AFPT =  W_AFPT  * W_MAX_AFP_70 / W_SUM_PENSJON + 0.005;             
          W_SUM_PENSJON = W_TP + W_GP + W_ST + W_AFPT;                          
       END; /* REDUKSJON GRUNNA 70 PROSENTEN */                                 
    ELSE                                                                        
       DIV_PARAM_OMR.AP_KODE_AEY(BER_SØ_IND) = 'P';                             
                                                                                
    B02.BEREGN_ALT_AEY(BER_SØ_IND)=                                             
       DIV_PARAM_OMR.AP_KODE_AEY(BER_SØ_IND);                                   
                                                                                
    /* -------------------------------------------------------------- */        
    /* HER KONTROLLERES AT SUM AFP OG INNTEKT IKKE OVERSTIGER 100%    */        
    /* 050700: IFLG MONICA PEDERSEN SKAL OGSÅ AFP-TILLEGGET TAS MED   */        
    /*         NÅR VI BEREGNER 100% REGELEN                           */        
    /* 060700: SKAL IKKE BENYTTES VED PRORATA AFP IFLG KRAVSPEC       */        
    /* -------------------------------------------------------------- */        
  /*W_SUM_PENSJON = W_TP + W_GP + W_ST + W_AFPT;                                
    W_MAX_AFP_100 = W_SUM_PENSJON + SUM_FAI;                                    
    IF (W_SUM_PENSJON > W_MAX_AFP_70) THEN                                      
       DO;  /* REDUKSJON GRUNNA 100 PROSENTEN */                                
    /*    W_TP   =  W_TP    * W_MAX_AFP_100 / W_SUM_PENSJON + 0.005;            
          W_GP   =  W_GP    * W_MAX_AFP_100 / W_SUM_PENSJON + 0.005;            
          W_ST   =  W_ST    * W_MAX_AFP_100 / W_SUM_PENSJON + 0.005;            
          W_AFPT =  W_AFPT  * W_MAX_AFP_100 / W_SUM_PENSJON + 0.005;            
       END; /* REDUKSJON GRUNNA 100 PROSENTEN */                                
                                                                                
    /* -------------------------------------------------------------- */        
    /* HER LEGGES BRUTTO BELØPENE INN                                 */        
    /* -------------------------------------------------------------- */        
    B02.ALDERSP.GP(BER_SØ_IND) = W_GP / 12 + 0.5;                               
    B02.ALDERSP.TP(BER_SØ_IND) = W_TP / 12 + 0.5;                               
    /* B02.ALDERSP.ST(BER_SØ_IND) = W_ST / 12 + 0.5;                            
    /* REMEDY 500, FIKK EN KRONE FOR LITE I NOEN TILFELLER */                   
    W_ST_TOTAL = ROUND(W_ST_BRUTTO/12,0);                                       
    W_SUM_TPST = B02.ALDERSP.TP(BER_SØ_IND)                                     
               + B02.ALDERSP.ST(BER_SØ_IND);                                    
    IF (B02.ALDERSP.TP(BER_SØ_IND) < W_SUM_TPST) THEN                           
       B02.ALDERSP.ST(BER_SØ_IND) = W_ST_TOTAL                                  
                                  - B02.ALDERSP.TP(BER_SØ_IND);                 
    /* REMEDY 500, FIKK EN KRONE FOR LITE I NOEN TILFELLER */                   
    B02.AFP_TILLEGG(BER_SØ_IND) = W_AFPT/ 12 + 0.5;                             
    IF DIV_PARAM_OMR.AP_KODE_AEY(BER_SØ_IND) = 'W'    THEN                      
       DO;                                                                      
          W_SUM_BRUTTO =                                                        
                       B02.ALDERSP.GP(BER_SØ_IND) +                             
                       B02.ALDERSP.TP(BER_SØ_IND) +                             
                       B02.ALDERSP.ST(BER_SØ_IND) +                             
                       B02.AFP_TILLEGG(BER_SØ_IND);                             
          IF W_SUM_BRUTTO < W_SUM_MINST            THEN                         
                       B02.AFP_TILLEGG(BER_SØ_IND) =                            
                       B02.AFP_TILLEGG(BER_SØ_IND) + 1;                         
       END;                                                                     
                                                                                
    /* -------------------------------------------------------------- */        
    /* FINNER VI UT OM VI SKAL REDUSERE                               */        
    /* -------------------------------------------------------------- */        
 /* IF (SUM_FAI > 4000) THEN  */                                                
    IF (SUM_FAI > TOLERANSE)   THEN                                             
       RED_PROSENT = (SUM_FAI / SUM_TAI) * 100;                                 
    ELSE                                                                        
       RED_PROSENT = 0;                                                         
                                                                                
    /* NØDVENDIG SE xxxxxxxxxxx (fnr fjernet)
    IF (RED_PROSENT > 100) THEN                                                 
       UTB = 0;                                                                 
    ELSE                                                                        
       UTB = (100 - RED_PROSENT) / 100; */                                      
    IF (RED_PROSENT > 100) THEN                                                 
       RED_PROSENT = 100;                                                       
    UTB = (100 - RED_PROSENT) / 100;                                            
                                                                                
                                                                                
    /* -------------------------------------------------------------- */        
    /* HER LEGGES NETTO BELØPENE INN                                  */        
    /* -------------------------------------------------------------- */        
    IF RED_PROSENT > 0                            THEN                          
       DO;                                                                      
          B02.AP_GP_NETTO(BER_SØ_IND) =                                         
             W_GP   / 12 * UTB + 0.5;                                           
          B02.AP_TP_NETTO(BER_SØ_IND) =                                         
             W_TP   / 12 * UTB + 0.5;                                           
          B02.AP_ST_NETTO(BER_SØ_IND) =                                         
             W_ST   / 12 * UTB + 0.5;                                           
          B02.AFP_TILLEGG_NETTO(BER_SØ_IND) =                                   
             W_AFPT / 12 * UTB + 0.5;                                           
       END;                                                                     
    ELSE                                                                        
       DO;                                                                      
          B02.AP_GP_NETTO(BER_SØ_IND) =                                         
                       B02.ALDERSP.GP(BER_SØ_IND);                              
          B02.AP_TP_NETTO(BER_SØ_IND) =                                         
                       B02.ALDERSP.TP(BER_SØ_IND);                              
          B02.AP_ST_NETTO(BER_SØ_IND) =                                         
                       B02.ALDERSP.ST(BER_SØ_IND);                              
          B02.AFP_TILLEGG_NETTO(BER_SØ_IND) =                                   
                       B02.AFP_TILLEGG(BER_SØ_IND);                             
       END;                                                                     
                                                                                
    /* -------------------------------------------------------------- */        
    /* OPPDATERER SUM YTELSE                                          */        
    /* -------------------------------------------------------------- */        
    B02.SUM_YTELSE(BER_SØ_IND) =                                                
       +  B02.ALDERSP.AP_GP_NETTO(BER_SØ_IND)                                   
       +  B02.ALDERSP.AP_TP_NETTO(BER_SØ_IND)                                   
       +  B02.ALDERSP.AP_ST_NETTO(BER_SØ_IND)                                   
       +  B02.ALDERSP.AFP_TILLEGG_NETTO(BER_SØ_IND)                             
       +  B02.ALDERSP.ET(BER_SØ_IND);                                           
                                                                                
    B02.APD(BER_SØ_IND) = 100 - RED_PROSENT;                                    
    W_SUM_PENSJON = B02.ALDERSP.GP(BER_SØ_IND)                                  
                  + B02.ALDERSP.TP(BER_SØ_IND)                                  
                  + B02.ALDERSP.ST(BER_SØ_IND)                                  
                  + B02.AFP_TILLEGG(BER_SØ_IND)                                 
                  + B02.ALDERSP.ET(BER_SØ_IND);                                 
                                                                                
    /* -------------------------------------------------------------- */        
    /* FORELØBIG RUTINE FOR HISTORIKK UTTAKSPROSENTER OSB.            */        
    /* -------------------------------------------------------------- */        
 /* IF (B02.UTTAKSDATO_ÅMD(BER_SØ_IND) =                                        
        B02.VIRK_DATO_ÅMD(BER_SØ_IND)  ) THEN                                   
    /* KAN DETTE FUNGERE??????                                                  
    IF (B02.UTTAKSDATO_ÅMD(BER_SØ_IND) =                                        
        B02.VIRK_DATO_ÅMD(BER_SØ_IND)  ) &                                      
       (B02.VIRK_DATO_ÅMD(BER_SØ_IND)  >                                        
        B02.G_DATO_ÅMD(BER_SØ_IND)    ) THEN                                    
       DO;  /* DETTE ER FØRSTE UTTAK AV AFP */                                  
  /*      B02.UTTAKS_APD(BER_SØ_IND) = 100 - RED_PROSENT;                       
          I = 1;                                                                
       END; /* DETTE ER FØRSTE UTTAK AV AFP */                                  
 /* ELSE                                                                        
       DO;                                                                      
          DO I = 2 TO 20 UNTIL (B02.AFPDATO_ÅMD(BER_SØ_IND,I) = 0);             
          END;                                                                  
          IF (B02.AFPDATO_ÅMD(BER_SØ_IND,I-1) >=                                
              B02.VIRK_DATO_ÅMD(BER_SØ_IND)   ) THEN                            
             I = I - 1;                                                         
       END;                                                                     
    /* TEST */                                                                  
 /*FØRST BLANKER VI AFPHIST MED SENERE DATO : */                                
    DCL W_DATO1     PIC '(8)9',                                                 
        W_DATO2     PIC '(8)9',                                                 
        W_DATO3     PIC '(8)9',                                                 
        W_DATO4     PIC '(8)9';                                                 
    DCL W_G         PIC '(5)9';                                                 
                                                                                
    W_G = G;                                                                    
    W_DATO1 = TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD;                                 
    W_DATO2 = B02.VIRK_DATO_ÅMD(BER_SØ_IND);                                    
    W_DATO3 = B02.G_DATO_ÅMD(BER_SØ_IND);                                       
                                                                                
    IF (W_DATO2 > W_DATO3) THEN                                                 
       W_AFPHIST_DATO = W_DATO2;                                                
    ELSE                                                                        
       W_AFPHIST_DATO = W_DATO3;                                                
                                                                                
 /* DO I = 1 TO 20 UNTIL (B02.AFPDATO_ÅMD(BER_SØ_IND,I) = 0);*/                 
    I = 1;                                                                      
    DO WHILE (B02.AFPDATO_ÅMD(BER_SØ_IND,I) > 0 &                               
                                          I < 30);                              
    /* IF (TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD >=                                  
           B02.AFPDATO_ÅMD(BER_SØ_IND,I) ) THEN */                              
       IF (B02.AFPDATO_ÅMD(BER_SØ_IND,I) >= W_AFPHIST_DATO) THEN                
          B02.AFPHIST(BER_SØ_IND,I) = '';                                       
       I = I + 1;                                                               
    END;                                                                        
                                                                                
                                                                                
 /*SÅ FINNER VI INDEX FOR NYTT AFPHIST : */                                     
    DO I = 1 TO 30 UNTIL (B02.AFPDATO_ÅMD(BER_SØ_IND,I) = 0);                   
    END;                                                                        
    /* -------------------------------------------------------------- */        
    /* OPPDATERER AFP HIST SEGMENTET                                  */        
    IF (I > 1) THEN                                                             
       DO;  /* HAR OGSÅ TIDLIGERE SEGMENT - HAR NOE ENDRET SEG ? */             
          ENDRET = '0'B;                                                        
          IF (B02.AFPHIST.TEI_FAKTOR(BER_SØ_IND,I-1) ^=                         
              B02.ALDERSP.AP_TEI_FAKTOR(BER_SØ_IND)   ) THEN                    
             ENDRET = '1'B;                                                     
          IF (B02.AFPHIST.FEI_I_100KR(BER_SØ_IND,I-1) ^=                        
              B02.ALDERSP.FAI(BER_SØ_IND)             ) THEN                    
             ENDRET = '1'B;                                                     
          IF (B02.AFPHIST.GRAD_UTBET(BER_SØ_IND,I-1)  ^=                        
              100 - RED_PROSENT                   )     THEN                    
             ENDRET = '1'B;                                                     
          IF (B02.AFPHIST.FULL_AFP(BER_SØ_IND,I-1)   ^=                         
             W_SUM_PENSJON                       )      THEN                    
             ENDRET = '1'B;                                                     
          IF (B02.AFPHIST.RED_AFP(BER_SØ_IND,I-1)    ^=                         
             B02.SUM_YTELSE(BER_SØ_IND)          )       THEN                   
             ENDRET = '1'B;                                                     
          IF (B02.AFPHIST.ET_REG(BER_SØ_IND,I-1) = 'J' ) THEN                   
             IF ^(B02.PENSJONSTYPE2 (BER_SØ_IND) = 'F'   !                      
                  (B02.PENSJONSTYPE2 (BER_SØ_IND) = 'N' &                       
                   B02.PENSJONSTYPE1 (BER_EK_IND) = 'F' )) THEN                 
                ENDRET = '1'B;                                                  
          IF (B02.AFPHIST.ET_REG(BER_SØ_IND,I-1) = 'N' ) THEN                   
             IF (B02.PENSJONSTYPE2 (BER_SØ_IND) = 'F'   !                       
                 (B02.PENSJONSTYPE2 (BER_SØ_IND) = 'N' &                        
                  B02.PENSJONSTYPE1 (BER_EK_IND) = 'F' )) THEN                  
                ENDRET = '1'B;                                                  
       END; /* HAR OGSÅ TIDLIGERE SEGMENT - HAR NOE ENDRET SEG ? */             
                                                                                
    /* -------------------------------------------------------------- */        
      IF (ENDRET ! I=1) THEN                                                    
         DO;   /* LEGG TIL NYTT ELEMENT */                                      
            B02.AFPDATO_ÅMD(BER_SØ_IND,I) = W_AFPHIST_DATO;                     
            B02.AFPHIST.TEI_FAKTOR(BER_SØ_IND,I) =                              
               B02.ALDERSP.AP_TEI_FAKTOR(BER_SØ_IND);                           
            B02.AFPHIST.FEI_I_100KR(BER_SØ_IND,I) =                             
               B02.ALDERSP.FAI(BER_SØ_IND);                                     
            B02.AFPHIST.GRAD_UTBET(BER_SØ_IND,I) =                              
               100 - RED_PROSENT;                                               
            B02.AFPHIST.FULL_AFP(BER_SØ_IND,I) =                                
               W_SUM_PENSJON;                                                   
            B02.AFPHIST.RED_AFP(BER_SØ_IND,I) =                                 
               B02.SUM_YTELSE(BER_SØ_IND);                                      
    /*      IF (B02.ALDERSP.ET(BER_SØ_IND) > 0) THEN     */                     
            IF (B02.PENSJONSTYPE2 (BER_SØ_IND) = 'F'    !                       
                (B02.PENSJONSTYPE2 (BER_SØ_IND) = 'N' &                         
                 B02.PENSJONSTYPE1 (BER_EK_IND) = 'F' ))  THEN                  
               B02.AFPHIST.ET_REG(BER_SØ_IND,I) = 'J';                          
            ELSE                                                                
               B02.AFPHIST.ET_REG(BER_SØ_IND,I) = 'N';                          
         END;  /* LEGG TIL NYTT ELEMENT */                                      
 /*200208 : */                                                                  
      IF KJØRINGS_TYPE = 'A'         THEN                                       
         DO J = 1 TO 30;                                                        
            B01.AFPHIST(BER_SØ_IND,J) = B02.AFPHIST(BER_SØ_IND,J);              
         END;                                                                   
                                                                                
 END PRO_RATA_INNTEKT_AFP;                                                      
