 /*   SIST ENDRET PÅ PROD   2004.06.18 12.24.06 AV   SPA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.05.03 12.32.08 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2003.06.19 15.33.55 AV   SPA2970          */        
 /*   SIST ENDRET PÅ TEST   2003.03.14 10.36.54 AV   JDA2970          */        
 /*       SIST ENDRET 09/06-98 08.49.07 AV   SPA7339                  */        
 /*       SIST ENDRET 27/05-98 09.51.49 AV   RFA9991                  */        
 /*       SIST ENDRET 24/02-95 11.21.40 AV   JDA0310                  */        
 /*       SIST ENDRET 24/02-95 11.11.56 AV   JDA0310                  */        
 /*       SIST ENDRET 04/10-94 10.55.47 AV   TSB0310                  */        
 /*       SIST ENDRET 31/07-91 12.46.08 AV   DYBVIK                   */        
 /*       SIST ENDRET 26/04-91 11.46.24 AV   DYBVIK                   */        
 /*       SIST ENDRET 24/05-89 12.39.19 AV   DYBVIK                   */        
 /*       SIST ENDRET 14/04-89 13.38.13 AV   HERMAN                   */        
 /*       SIST ENDRET 24/03-87 11.47.38 AV   DYBVIK                   */        
 /*       SIST ENDRET 25/04-86 08.06.40 AV   TASSEN                   */        
 /*       SIST ENDRET 03/01-86 10.41.14 AV   TASSEN                   */        
 /*       SIST ENDRET 17/10-85 15.40.46 AV   HERMAN                   */        
 /*       SIST ENDRET 13/12-83 13.43.10 AV   OLAV                     */        
 /*       SIST ENDRET 05/12-83 13.22.08 AV   OLAV                     */        
 /*       SIST ENDRET 29/04-83 11.35.34 AV   OLAV                     */        
 /*       SIST ENDRET 28/04-83 14.18.12 AV   OLAV                     */        
 /* ***************************************************************** */        
 /* IDENTIFIKASJON                                                    */        
 /*     R0014431 - ÅFT_TILLEGG - PROSEDYRE I PLI.                     */        
 /*     PROGRAMMERER: TOM JØRGENSEN, JULI 82.                         */        
 /* HENSIKT                                                           */        
 /*     OPPDATERER ÅFT (8.5.1-TILLEGG) OG RETURNERER ÅFT_MAX.         */        
 /* PROGRAMTILKNYTNING                                                */        
 /*     PROSEDYREN BLIR INKLUDERT I R0014401 - BEREGN_UFØREPENSJON.   */        
 /* ENDRET 16.10.85 HL : SE FEILRAPPORT 258.  TOTALT ENDRET           */        
 /* ***************************************************************** */        
 ÅFT_TILLEGG:PROC(ÅFT,ÅFT_MAX,IND);                                             
    DCL ÅFT                FIXED    (5) ,                                       
        ÅFT_MAX            FIXED    (5) ,                                       
        RETT_TIL_ÅFT       BIT      (1)  INIT('1'B),                            
        HJELP              FIXED   (9,4)   INIT(0),                             
        GP100              FIXED    (5)     INIT(0),                            
        ET100              FIXED    (5)     INIT(0),                            
        BT100              FIXED    (5)     INIT(0),                            
        ET_UFG             FIXED    (5)     INIT(0),                            
        H1                 FIXED    (5,4)     INIT(0),                          
        H2                 FIXED    (6,4)     INIT(0),                          
        H3                 FIXED    (5,4)     INIT(0),                          
        I                  FIXED BIN(15),                                       
        IND                FIXED BIN(15);                                       
    /* == 4431 ====================================================== */        
                                                                                
   IF B02.UFØRHIST.UFG(BER_SØ_IND,IND) < 70    THEN                             
      RETT_TIL_ÅFT = '0'B;                                                      
                                                                                
   DO I = 1 TO HBOUND(B02.UFØRGRAD,3)                                           
                 WHILE(B02.UFØRGRAD.UFG_ØVRIGE(BER_SØ_IND,IND,I) > 0);          
      IF B02.UFØRGRAD.UFG_ØVRIGE  (BER_SØ_IND,IND,I) < 70    !                  
         (B02.UFØRGRAD.UFG_ØVRIGE (BER_SØ_IND,IND,I) > 65    &                  
          RETT_TIL_ÅFT                               = '0'B  &                  
          B02.UFØRGRAD.UFG_DATO_ÅMD(BER_SØ_IND,IND,I) > 19730600) THEN          
         RETT_TIL_ÅFT = '0'B;                                                   
      ELSE                                                                      
         RETT_TIL_ÅFT = '1'B;                                                   
   END;                                                                         
                                                                                
   IF RETT_TIL_ÅFT & B02.UFØRPENS.UFG(BER_SØ_IND) < 100    THEN                 
      DO;  /* HAR RETT TIL ÅFT */                                               
         DIV_PARAM_OMR.RETT_TIL_ÅFT(BER_SØ_IND) = 'J';                          
         IF B02.PENSJONSTYPE2(BER_SØ_IND) = 'S'         THEN                    
            DO;                                                                 
               IF (B02.PENSJONSTYPE1(BER_EK_IND) = 'A' !                        
                   B02.PENSJONSTYPE1(BER_EK_IND) = 'K' ) THEN                   
                  HJELP = GP_PROSENT;                                           
               ELSE                                                             
                  DO;                                                           
                       /* FAKTOR ER EKTEFELLENS UFØREGRAD  **                   
                       ** FAKTOR ER UTREGNET I 4401. TRUDE 041094**             
                  HJELP = MAX(1 - REST_PROSENT * (FAKTOR / 100),                
                              GP_PROSENT);                                      
                       */                                                       
                  H1 = (FAKTOR / 100)  ;                                        
                  H2 = ROUND(REST_PROSENT  * H1,4);                             
                  H3 = 1 - H2 ;                                                 
                  HJELP = MAX(H3,GP_PROSENT);                                   
                                                                                
                  END;                                                          
            END;                                                                
         ELSE                                                                   
            HJELP = 1;                                                          
                                                                                
         HJELP = HJELP * G;                                                     
         HJELP = HJELP * B02.TT_ANV(BER_SØ_IND) / 40;                           
         HJELP = HJELP / 12 + 0.5;                                              
         GP100 = HJELP;                                                         
                                                                                
         /* LOVENDRING OM BEHOVSPRØVET FORSØRGINGSTILLEGG FRA 1.5.91 :*/        
         /* FORSØRGINGSTILLEGG MÅ DERFOR TREKKES UT AV ÅFT-BEREGNINGEN*/        
         /* BARNETILLEGG ER ENNÅ IKKE BEREGNET - STÅR DERFOR I NULL   */        
         IF (G < 34101) THEN                                                    
            DO;                                                                 
               CALL BEREGN_BT (BT100,100);                                      
               IF B02.PENSJONSTYPE2(BER_SØ_IND) = 'F'      THEN                 
                  DO;                                                           
                     ET100 = 0.5 * G * B02.TT_ANV(BER_SØ_IND)                   
                           /(12 * 40)+ 0.5;                                     
                     ET_UFG = 0.5 * G * B02.TT_ANV(BER_SØ_IND) *                
                            B02.UFØRPENS.UFG(BER_SØ_IND)     /                  
                            (12 * 40 * 100) + 0.5;                              
                  END;                                                          
            END;                                                                
                                                                                
         B02.ÅFT(BER_SØ_IND) = (GP100 + ET100 + BT100 )      -                  
                               (B02.UFØRPENS.GP (BER_SØ_IND) +                  
                                ET_UFG                       +                  
                                B02.UFØRPENS.BT (BER_SØ_IND) +                  
                                B02.UFØRPENS.TP (BER_SØ_IND) +                  
                                B02.YRKEPENS.GP (BER_SØ_IND) +                  
                                B02.YRKEPENS.BT (BER_SØ_IND) +                  
                                B02.YRKEPENS.TP (BER_SØ_IND));                  
                                                                                
         ÅFT_MAX = B02.ÅFT(BER_SØ_IND) + B02.UFØRPENS.TP(BER_SØ_IND) +          
                                         B02.YRKEPENS.TP(BER_SØ_IND);           
                                                                                
         IF (B02.ÅFT(BER_SØ_IND) < 0) THEN                                      
            B02.ÅFT(BER_SØ_IND) = 0;                                            
                                                                                
      END; /* HAR RETT TIL ÅFT */                                               
   ELSE                                                                         
      DO;  /* HAR IKKE RETT TIL ÅFT */                                          
         ÅFT_MAX             = 0;                                               
         B02.ÅFT(BER_SØ_IND) = 0;                                               
      END; /* HAR IKKE RETT TIL ÅFT */                                          
                                                                                
 END ÅFT_TILLEGG;                                                               
