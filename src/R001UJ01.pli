 /*   SIST ENDRET PÅ PROD   2004.12.17 15.41.36 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.10.26 11.41.24 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2004.06.30 13.34.07 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.03.04  9.21.43 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2003.07.29 11.00.44 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2003.07.29 10.47.06 AV   SPA2970          */        
 /*   SIST ENDRET PÅ PROD   2003.02.27 12.38.03 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2002.11.21 13.29.45 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.10.22 13.04.14 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2000.05.18 14.26.17 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.05.18 14.12.37 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.05.18 12.53.19 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.02.21 15.02.14 AV   SPA7339          */        
 /* ***************************************************************** */        
 /*IDENTIFIKASJON:                                                    */        
 /* **********************                                            */        
 /*  PROGRAM-IDENT : R001UJ01 KONTROLL_GRUNNBLANKETT_KFP HOVEDPROG    */        
 /*  PROGRAM-SPRÅK : PLI - CICS                                       */        
 /*  PROGRAMMERER  : SATISH PATHAK                                    */        
 /*  PROGRAMMET BLE LAGET : 1988                                      */        
 /*  ENDRINGERSDATO :                                                 */        
 /*  ENDRINGEN GJELDER:                                               */        
 /*  ENDRINGEN BLE UTFØRT AV :                                        */        
 /*                                                                   */        
 /* ***************************************************************** */        
 /*HENSIKT:                                                           */        
 /* **********                                                        */        
 /*  PROGRAMMET KONTROLLERER KFP (AVTALEFESTET FØRTIDSPENSJON)        */        
 /*  BLANKETTEN KONTROLLERES ISOLERT - IKKE MOT STATUS.               */        
 /*  OPERATØREN KAN BRUKE PF-TASTENE :                                */        
 /*    PF1  FØRER TIL OPPBEVARING AV TRANSEN I VENTEREGISTERET.       */        
 /*    PF2  FØRER TIL AT TRANSAKSJONEN FORKASTES -                    */        
 /*    I BEGGE TILFELLER SKRIVES DET UT MELDING PÅ SKJERM, OG         */        
 /*    PROGRAM R0010401 SØRGER FOR Å LEGGE UT NYTT BILDE FOR          */        
 /*    REGISTRERING AV KFP - TRANSKODE R041.                          */        
 /*  NÅR INGEN FEIL MER FINNES SETTES TRANSKODE = R042, OG DET        */        
 /*  RETURNERES TIL R0010401.                                         */        
 /*                                                                   */        
 /* ***************************************************************** */        
 /*PROGRAMTILKNYTTING:                                                */        
 /* ********************                                              */        
 /*  PROGRAMMETS TRANSKODER ER RUJ0, RUJ1, RUJ2, RUJ3, RUJ4.          */        
 /*  PROGRAMMET KALLES FRA R0010401 - REGISTRERING, MED               */        
 /*  TRANSKODE 'RUJ0'.                                                */        
 /*  PROGRAMMET HAR EXEC CICS XCTL TIL :                              */        
 /*             R0010301 - VALG_AV_FUNKSJON                           */        
 /*             R0010401 - REGISTRERING                               */        
 /*  PROGRAMMET HAR EXEC CICS LINK TIL :                              */        
 /*             R0010490 - SKRIV_UT_PÅ_VENTEREG                       */        
 /*  PROGRAMMET INCLUDER :                                            */        
 /*             R001UJ02  -  BLANK_ARBEIDSOMRÅDE                      */        
 /*             R001UJ03  -  KONTROLL_SØKER                           */        
 /*             R001UJ05  -  KONTROLL_SPESIELLE_OPPLYSNINGER          */        
 /*             R001UJ06  -  FLYTT_FRA_ARBEIDSOMRÅDE_TIL_MAP          */        
 /*             R001UJ08  -  BEREGN_TT_KFP                            */        
 /*             R001UJ09  -  FLYTT_FRA_MAP_TIL_ARBEIDSOMRÅDE          */        
 /*             R0019901  -  TEST OM DATO ER GYLDIG                   */        
 /*             R0019902  -  FINNE KJØNN                              */        
 /*             R0019903  -  ER EKTEFELLES ALDER GYLDIG               */        
 /*             R0019904  -  FØDSELSNUMMERKONTROLL                    */        
 /*             R0019905  -  BEREGN_ALDER                             */        
 /*             R0019907  -  TABELLSØKING                             */        
 /*             R0019910  -  NUMERISK KONTROLL                        */        
 /*             R0019911  -  DATO_KONTROLL                            */        
 /*             R0019912  -  KONVERTERING CHAR ==> PIC OG OMV.        */        
 /*             R0019944  -  SKRIV_FEIL                               */        
 /*             R0019964  -  DAGENS DATO + 6 MDR                      */        
 /*  PROGRAMMET BRUKER MAPSET S001UJ3 - KFP                           */        
 /*         MAP S001UJ1   -  REGISTRERING AV KFP-TRANS                */        
 /* ***************************************************************** */        
 /*DATASETTOPPLYSNINGER:                                              */        
 /* ***********************                                           */        
 /*  FØDSELSNR-ENDRINGSREG BLIR LEST I SUBPROGRAM R001UJ03 VED        */        
 /*  CALL TIL PROGRAM R0019906.                                       */        
 /*                                                                   */        
 /* ***************************************************************** */        
 /*FEILMELDINGER:                                                     */        
 /* *******************                                               */        
 /*                                                                   */        
 /*  SKRIVES UT PÅ SKJERMBILDET                                       */        
 /*                                                                   */        
 /* ***************************************************************** */        
 R001UJ: PROC(COMMAREA_PEKER) OPTIONS(MAIN);                                    
      %INCLUDE S001UJ;                 /* KFP-MAPSETTET   */                    
      %INCLUDE P001UJ01;               /* TRANS-OMR FOR KFP          */         
      %INCLUDE P0019906;               /* TRANS_OPPL_OMR (BASED) */             
      %INCLUDE P0019908;               /* KOM_OMRÅDE (BASED) */                 
      %INCLUDE P0019910;               /* STYRINGS_OMRÅDE (BASED) */            
      %INCLUDE P0019912;               /*  DIV_OMRÅDE (BASED)      */           
      %INCLUDE DFHBMSCA;                                                        
   DCL                                                                          
      (BMSMAPBR,                                                                
      COMMAREA_PEKER) PTR;                                                      
   DCL                                                                          
      (CSTG,                                                                    
      ADDR,                                                                     
      STG,                                                                      
      VERIFY,                                                                   
      UNSPEC,                                                                   
      ONCODE,                                                                   
      SUBSTR) BUILTIN;                                                          
   DCL SYSPRINT     EXTERNAL FILE;                                              
   DCL                                                                          
      (FEIL_FUNNET,FEIL19,FEIL20) BIT(1),                                       
      FEIL_I_SØKER                BIT(1),                                       
   /* FEIL_I_BARN                 BIT(1), */                                    
      FEIL_I_SPES                 BIT(1),                                       
      ONKODE                      PIC'9999',                                    
      CURSOR_POS                  FIXED BIN(15) INIT(-1),                       
      K                           FIXED BIN(15),                                
      ONK DEF ONKODE              CHAR(4),                                      
      FEILKODE                    CHAR(4),                                      
      DSNAVN                      CHAR(8),                                      
      ANT_FEIL_SKREVET            FIXED DEC (3);                                
                                                                                
      ON ERROR SNAP                                                             
      BEGIN;                                                                    
                                                                                
         ON ERROR SYSTEM        ;                                               
         ONKODE=ONCODE          ;                                               
         FEILKODE = ONK         ;                                               
         DSNAVN   = EIBDS       ;                                               
         GO TO FEILBEH          ;                                               
      END;                                                                      
    /* START PGM UJ01 =============================================== */        
                                                                                
    FEILKODE  = 'FEIL';                                                         
    DSNAVN    = '        ';                                                     
                                                                                
    EXEC CICS HANDLE AID PF1(PF1) PF2(PF2);                                     
                                                                                
    EXEC CICS HANDLE CONDITION ERROR(FEILBEH);                                  
                                                                                
    KOM_OMR.PEKER_LISTE.DIV_PARAM_PEKER  = ADDR(KOM_OMR.DIV_PARAM_OMR);         
                                                                                
    IF FRA_CICS THEN                                                            
  DO; /* FRA CICS */                                                            
    KOM_OMR.PEKER_LISTE.TRANS_OPPL_PEKER = ADDR(KOM_OMR.TRANS_OPPL_OMR);        
    KOM_OMR.PEKER_LISTE.TRANS_PEKER      = ADDR(KOM_OMR.TRANS_OMR);             
    KOM_OMR.PEKER_LISTE.STYRINGS_PEKER   = ADDR(KOM_OMR.STYRINGS_OMR);          
    ATK_KOM_PTR           = ADDR  (KOM_OMR.ATK_COM_OMR  ) ;                     
  END;/* FRA CICS */                                                            
                                                                                
    IF HENT_FRAM_MAP  THEN                                                      
       DO;  /* HENT FRAM MAP */                                                 
          EXEC CICS SEND MAP('S001UJ1') MAPSET('S001UJ3') MAPONLY               
                                                   SET (BMSMAPBR);              
          ALLOCATE S001UJ1O;                                                    
          S001UJ1O.CICS_INFOO  = DIV_PARAM_OMR.CICS_NAVN;                       
       END; /* HENT FRAM MAP */                                                 
                                                                                
 RECEIVE_MAP:                                                                   
                                                                                
                                                                                
 IF ^HENT_FRAM_MAP THEN                                                         
    DO;                                                                         
       EXEC CICS RECEIVE MAP('S001UJ1')                                         
                 MAPSET('S001UJ3') SET(BMSMAPBR);                               
       CALL BLANK_KFP_MELDNR;                            /* R001UJ02 */         
       FEIL_MELD_NR = 0;                                                        
    END;                                                                        
                                                                                
 IF FUNKSJONSKODEL > 0 THEN                                                     
    DO;                                                                         
       FUNKSJONSKODE = FUNKSJONSKODEI;                                          
       EXEC CICS XCTL PROGRAM('R0010301') COMMAREA(KOM_OMR);                    
    END;                                                                        
                                                                                
 IF STYRE_KODEL > 0 THEN                                                        
    DO;                                                                         
       STYREKODE = STYRE_KODEI;                                                 
       TRANSKODE = 'R041';                                                      
       EXEC CICS XCTL PROGRAM('R0010401') COMMAREA(KOM_OMR);                    
    END;                                                                        
                                                                                
 IF STYREKODE ^= 'KF'  &  FRA_CICS THEN      /*   CHANGE TO */                  
    TRANSKODE = 'R041';                                                         
                                                                                
 ELSE                                                                           
                                                                                
  DO;                                                                           
   ANT_FEIL_SKREVET = 0;                                                        
   FEIL_FUNNET      = '0'B;                                                     
   FEIL_I_SØKER     = '0'B;                                                     
   FEIL_I_SPES      = '0'B;                                                     
   TRANS_RETURKODE  = TRANSKODE;                                                
                                                                                
            SELECT (TRANSKODE);                                                 
               WHEN('RUJ0')                                                     
                  DO;                                                           
                     CALL BLANK_KFP_SØKER;                /* R001UJ02 */        
                     CALL BLANK_KFP_MELDNR;               /* R001UJ02 */        
                     CALL OVERFØR_KFP_SØKER;              /* R001UJ09 */        
                     CALL KONTROLL_KFP_SØKER(FEIL_I_SØKER);/*R001UJ03*/         
                     CALL KONTROLL_KFP_SPES(FEIL_I_SPES);/*R001UJ05*/           
                  END;                                                          
               WHEN('RUJ1')                                                     
                  DO;                                                           
                     CALL BLANK_KFP_MELDNR;               /* R001UJ02 */        
                     CALL OVERFØR_KFP_SØKER;              /* R001UJ09 */        
                     CALL KONTROLL_KFP_SØKER(FEIL_I_SØKER);/*R001UJ03*/         
                     CALL KONTROLL_KFP_SPES(FEIL_I_SPES);/*R001UJ05*/           
                  END;                                                          
               OTHERWISE                                                        
                  ;                                                             
 /* INGEN UTGANG DA CICS IKKE GODTAR UGYLDIGE TRANSAKSJONSKODER */              
            END;                                                                
                                                                                
            IF FEIL_I_SØKER = '0'B &                                            
               FEIL_I_SPES  = '0'B THEN                                         
               FEIL_FUNNET = '0'B;                                              
            ELSE                                                                
               FEIL_FUNNET = '1'B;                                              
                                                                                
                                                                                
            IF FEIL_FUNNET ! HENT_FRAM_MAP ! FRA_UTEN_DIALOG !                  
            TRANS_RETURKODE ^= TRANSKODE THEN                                   
                                                                                
               DO;                                                              
                  IF TRANSKODE = 'RUJ0' THEN                                    
                     TRANSKODE = 'RUJ1';                                        
                  CALL BLANK_KFP_MAP;                     /* R001UJ09 */        
                  CALL OVERFØR_KFPSØKER_MAP;             /* R001UJ06 */         
                                                                                
                S001UJ1O.CICS_INFOO  = DIV_PARAM_OMR.CICS_NAVN;                 
                IF HENT_FRAM_MAP THEN                                           
                   EXEC CICS SEND MAP('S001UJ1') MAPSET('S001UJ3')              
                                                        ERASE CURSOR;           
                ELSE                                                            
                   EXEC CICS SEND MAP('S001UJ1') MAPSET('S001UJ3')              
                                            DATAONLY ERASEAUP CURSOR;           
                FEIL_MELD_NR  = 666;/* => FEIL_FUNNET ER SATT. */               
                HENT_FRAM_MAP = '0'B;                                           
                                                                                
                IF  FRA_CICS THEN                                               
                    EXEC CICS RETURN TRANSID(TRANSKODE)                         
                                                  COMMAREA(KOM_OMR);            
                ELSE                                                            
                    GO TO RECEIVE_MAP;                                          
               END;                                                             
            ELSE                                                                
              DO;                                                               
                IF TRANSKODE = 'RUJ0' THEN                                      
                  TRANS_RETURKODE = 'RUJ1';                                     
                ELSE                                                            
                  TRANS_RETURKODE = TRANSKODE;                                  
                IF  FRA_CICS  THEN                                              
                  DO;                                                           
                    BLANKETTYPE = STYREKODE;                                    
                    TRANSKODE = 'R042';                                         
                  END;                                                          
              END;                                                              
                                                                                
  END;                                                                          
                                                                                
  CALL UTGANG;                                                                  
                                                                                
  UTGANG: PROC;                                                                 
    IF  FRA_CICS THEN                                                           
      EXEC CICS XCTL PROGRAM('R0010401') COMMAREA(KOM_OMR);                     
    ELSE                                                                        
      EXEC CICS RETURN;                                                         
  END UTGANG;                                                                   
                                                                                
                                                                                
  FEILBEH:                                                                      
                                                                                
     EXEC CICS HANDLE CONDITION ERROR(ABEND);                                   
     IF FEIL_MELD_NR = 0 THEN /* AB 01.08.15 */                                 
        PROGRAM_ID = 'R001UJ01';                                                
                                                                                
     S001UJ1O.MELDING2O =                                                       
              'F E I L  H A R  O P P S T Å T T ! ! !.';                         
                                                                                
     S001UJ1O.MELDING3O =                                                       
              'EIBRCODE : ' !! UNSPEC(EIBRCODE);                                
                                                                                
     S001UJ1O.MELDING4O =                                                       
       'EIBFN    : ' !! UNSPEC(EIBFN) !! '. FEILKODE : ' !! FEILKODE !!         
                                            '. DATASETT : ' !! DSNAVN;          
                                                                                
     S001UJ1O.MELDING5O =                                                       
                   'PROGRAMNAVN :  ' !! PROGRAM_ID    !!                        
                   '.INGEN OPPDATERING ER BLITT UTFØRT.';                       
                                                                                
     S001UJ1O.MELDING6O =                                                       
                   'TA UTSKRIFT!!!. TRYKK DERETTER "ENTER".';                   
                                                                                
                                                                                
     S001UJ1O.CICS_INFOO  = DIV_PARAM_OMR.CICS_NAVN;                            
     EXEC CICS SEND MAP('S001UJ1')    MAPSET('S001UJ3');                        
                                                                                
     EXEC CICS RECEIVE MAP('S001UJ1')                                           
                                    MAPSET('S001UJ3') SET(BMSMAPBR);            
                                                                                
                                                                                
     EXEC CICS SYNCPOINT ROLLBACK;                                              
                                                                                
                                                                                
     TERMINERINGS_IND = 'F';                                                    
                                                                                
     TRANSKODE  = 'R041';                                                       
                                                                                
     CALL UTGANG;                                                               
                                                                                
  ABEND:                                                                        
     EXEC CICS ABEND ABCODE(FEILKODE);                                          
                                                                                
  PF1:                                                                          
                                                                                
 /* LEGGER UNDERKJENT TRANS UT PÅ VENTEDATABASE HVIS GYLDIG     */              
 /* FNR OG VIRKNINGSDATO,ELLERS FORKASTES TRANSAKSJONEN.        */              
                                                                                
     IF ^(KFP.FNRNR = 1          !                                              
          KFP.FNRNR = 200        !                                              
          KFP.VIRK_DATO_ÅMDNR = 9 !                                             
          KFP.VIRK_DATO_ÅMDNR = 200) THEN                                       
        DO;                                                                     
                                                                                
           IF FØDSNUMMER    = 0   THEN                                          
                                                                                
 /* SKRIVER MELDING OM AT TRANSAKSJONEN ER FORKASTET.           */              
                                                                                
              DO;                                                               
                 FEIL_MELD_NR  = 0;                                             
                 S001UJ1O.MELDING6O =                                           
                         'REGISTERINGEN ER IKKE AVSLUTTET MED "ENTER"'!!        
                         '.TRANSAKSJONEN ER FORKASTET.';                        
              END;                                                              
           ELSE                                                                 
              DO;                                                               
                                                                                
                 SEARCH_FNR  = TRANS_OPPL_OMR.FØDSNUMMER;                       
                 IF FRA_CICS THEN                                               
                    BLANKETTYPE = STYREKODE;                                    
                                                                                
                                                                                
                 IF FEIL_MELD_NR = 666 &      /*BLANKETTKONTROLLFEIL*/          
                    ^FRA_MED_DIALOG     THEN  /*IKKE BASEKONTROLLFEIL*/         
                    FUNKSJONSKODE = 'B';                                        
                                                                                
                                                                                
                 EXEC CICS LINK PROGRAM('R0010490') COMMAREA(KOM_OMR);          
                                                                                
 /* SKRIVER MELDING OM AT TRANSAKSJONEN ER LAGT UT PÅ VENTEBASE.*/              
                                                                                
                 S001UJ1O.MELDING6O =                                           
                         'TRANSAKSJONEN ER LAGT UT PÅ VENTEREGISTER.';          
              END;                                                              
                                                                                
                                                                                
           S001UJ1O.CICS_INFOO  = DIV_PARAM_OMR.CICS_NAVN;                      
           EXEC CICS SEND MAP('S001UJ1') MAPSET('S001UJ3')                      
                                                           DATAONLY;            
     /* DUMMY SEND-KALL.                              */                        
                                                                                
           EXEC CICS SEND MAP('S001UJ1')                                        
                                                  MAPSET('S001UJ3');            
           EXEC CICS DELAY INTERVAL(005);                                       
                                                                                
           TERMINERINGS_IND = 'F';                                              
                                                                                
           TRANSKODE  = 'R041';                                                 
                                                                                
           CALL UTGANG;                                                         
                                                                                
        END;                                                                    
     ELSE                                                                       
        DO;                                                                     
           TERMINERINGS_IND = 'F';                                              
                                                                                
           TRANSKODE = 'R041';                                                  
                                                                                
           S001UJ1O.MELDING6O =                                                 
                                                                                
   'NB** TRANSAKSJONEN ER FORKASTET P.G.A. UGYLDIG FNR EL. VDATO **NB';         
                                                                                
           S001UJ1O.CICS_INFOO  = DIV_PARAM_OMR.CICS_NAVN;                      
           EXEC CICS SEND MAP('S001UJ1') MAPSET('S001UJ3')                      
                                                           DATAONLY;            
     /* DUMMY SEND-KALL.                            */                          
                                                                                
           EXEC CICS SEND MAP('S001UJ1')                                        
                                    MAPSET('S001UJ3');                          
                                                                                
           EXEC CICS DELAY INTERVAL(005);                                       
           CALL UTGANG;                                                         
        END;                                                                    
                                                                                
                                                                                
  PF2:                                                                          
     TERMINERINGS_IND   = 'F';                                                  
                                                                                
     S001UJ1O.MELDING6O =                                                       
                                                                                
      'NBNBNBNBNB***    TRANSAKSJONEN ER FORKASTET.    ***NBNBNBNBNB';          
                                                                                
           S001UJ1O.CICS_INFOO  = DIV_PARAM_OMR.CICS_NAVN;                      
     EXEC CICS SEND MAP('S001UJ1') MAPSET('S001UJ3')                            
                                                      DATAONLY ;                
     /* DUMMY SEND-KALL.                                     */                 
                                                                                
     EXEC CICS SEND MAP('S001UJ1')                                              
                                    MAPSET('S001UJ3');                          
     EXEC CICS DELAY INTERVAL(005);                                             
                                                                                
                                                                                
     TRANSKODE = 'R041';                                                        
     CALL UTGANG;                                                               
                                                                                
                                                                                
      %INCLUDE R001UJ02;               /* BLANK  ARBEIDSOMRÅDE      */          
      %INCLUDE R001UJ03;               /* KONTROLL AV SØKERDELEN    */          
      %INCLUDE R001UJ06;               /* ARBEIDSOMRÅDE -> MAP      */          
      %INCLUDE R001UJ09;               /* MAP -> ARBEIDSOMRÅDE      */          
      %INCLUDE R0019901;               /* TESTER OM DATO ER GYLDIG  */          
      %INCLUDE R0019902;               /* FINNE KJØNN               */          
      %INCLUDE R0019903;               /* EKTEFELLES ALDER GYLDIG   */          
      %INCLUDE R0019904;               /* FØDSELSNUMMERKONTROLL     */          
      %INCLUDE R0019905;               /* BEREGNER ALDER            */          
      %INCLUDE R0019910;               /* NUMERISK KONTROLL         */          
      %INCLUDE R0019912;               /* KONVERTERING CHAR ==> PIC */          
      %INCLUDE R0019944;               /* SKRIV_FEIL      */                    
      %INCLUDE R0019959;               /* DAGENS DATO PLUSS 2 MDR   */          
      %INCLUDE R0019964;               /* DAGENS DATO PLUSS 6 MDR   */          
      %INCLUDE R0019988;               /* HÅMD-ÅMD                  */          
      %INCLUDE R0019989;               /* HÅMD-MÅ                   */          
      %INCLUDE R0019983;               /*  ÅMD-HÅMD                 */          
      %INCLUDE R0019984;               /*   MÅ-HÅMD                 */          
      %INCLUDE R0019995;               /*   KONV-FNR11-FNR13        */          
      %INCLUDE R0019945;               /* ATK_TILLGANG_KONTROLL   */            
      %INCLUDE R0019999;               /* KONTROLL_ACF2           */            
   END R001UJ;                                                                  
