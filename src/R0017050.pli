 /*   SIST ENDRET PÅ PROD   2007.12.21 12.20.24 AV   JDA2990          */        
 /*   SIST ENDRET PÅ PROD   2003.10.27  9.17.20 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2002.04.19 12.28.57 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2002.02.25  7.30.50 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2000.07.07 13.01.21 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.05.23 13.09.28 AV   JDA7339          */        
 /*       SIST ENDRET 26/03-99 13.26.02 AV   JDA7339                  */        
 /*       SIST ENDRET 26/03-99 13.11.35 AV   JDA7339                  */        
 /*       SIST ENDRET 04/05-98 13.26.07 AV   JDA7339                  */        
 /*       SIST ENDRET 11/03-98 12.06.26 AV   JDA7339                  */        
 /* **************************************************************** */         
 /*IDENTIFIKASJON:                                                   */         
 /*    R0017050 - PROSEDYRE I PLI                                    */         
 /*    PROGRAMMERER: TRUDE, SEPT 91                                  */         
 /*HENSIKT:                                                          */         
 /*    RUTINEN SETTER INN KODER FOR TEKSTTYPE M I TEKSTTABELLEN FOR  */         
 /*    STØNADSBREV.TEKSTTYPE M FORTELLER OM HVILKE MELDINGER SOM     */         
 /*    SKAL SKRIVES.                                                 */         
 /*PROGRAMTILKNYTNING:                                               */         
 /*    INCLUDES I R0017001                                           */         
 /*BRUK:                                                             */         
 /*   CALL PLASSER_TEKSTTYPE_M_KODE_TABELL(IND)                      */         
 /* **************************************************************** */         
 PLASSER_M_KODE_TABELL: PROC(IND,IND_EK);                                       
    /* -------------------------------------------------------------- */        
    DCL                                                                         
       W_FNR                   PIC'99999999999',                                
       IND                     FIXED BIN (15),                                  
       IND_EK                  FIXED BIN (15),                                  
       ALDER                   FIXED DEC (5),                                   
       ALDER2                  FIXED DEC (5);                                   
    DCL                                                                         
       T_FNR13                 PIC'9999999999999';                              
    DCL                                                                         
       1 T_FNR_R13 DEF T_FNR13,                                                 
        2 DAG       PIC '99',                                                   
        2 MND       PIC '99',                                                   
        2 ÅR        PIC '9999',                                                 
        2 ÅRHUNDRE  PIC '9',                                                    
        2 FILLER    PIC '9999';                                                 
    DCL I                      FIXED BIN(15)INIT(1);                            
    DCL FT_BARN                FIXED DEC(5) INIT(0);                            
    DCL FT_FELLESBARN          FIXED DEC(5) INIT(0);                            
    DCL FT_SÆRBARN             FIXED DEC(5) INIT(0);                            
    DCL FT_ET                  FIXED DEC(5) INIT(0);                            
    DCL FT_TOTAL               FIXED DEC(5) INIT(0);                            
    DCL W_FNR_ÅM               PIC'999999';                                     
    DCL INNTEKT_EK             FIXED DEC(7) INIT(0);                            
                                                                                
    /* ============================================================== */        
    /* 7050 STARTER HER                                               */        
    /* ============================================================== */        
                                                                                
    TEKSTTYPE_M_KODE(*) = ' ';                                                  
    I                = 1;                                                       
    W_FNR            = B02.FNR(IND);                                            
    W_FNR13          = KONV_FNR11_FNR13((B02.FNR(IND)));                        
    T_FNR13          = KONV_FNR11_FNR13(W_FNR);                                 
    W_FNR_ÅM         = T_FNR_R13.ÅR*100+T_FNR_R13.MND;                          
    ALDER            = F_ALDER(W_FNR,TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD);         
    ALDER2           = F_ALDER(W_FNR,B02.SB_DATO_ÅMD(IND));                     
                                                                                
    FT_FELLESBARN    = ( B02.ALDERSP.BT(IND)  +                                 
                         B02.UFØRPENS.BT(IND)  +                                
                         B02.YRKEPENS.BT(IND)  +                                
                         B02.GARANTITILLEGG_BARN(IND));                         
                                                                                
    FT_SÆRBARN       = ( B02.BT_SÆRBARN(IND)  +                                 
                         B02.GARANTITILLEGG_SÆRBARN(IND));                      
                                                                                
    FT_BARN          =   FT_FELLESBARN          +                               
                         FT_SÆRBARN;                                            
                                                                                
    FT_ET            = ( B02.ALDERSP.ET(IND)  +                                 
                         B02.UFØRPENS.ET(IND)  +                                
                         B02.YRKEPENS.ET(IND)  +                                
                         B02.GARANTITILLEGG_EK(IND));                           
                                                                                
    FT_TOTAL         =   FT_BARN                                                
                     +   FT_ET;                                                 
                                                                                
    INNTEKT_EK       = ( B02.ARBEIDSINNTEKT_EK(IND)  +                          
                         B02.PENSJONSINNTEKT_EK(IND) +                          
                         W_HENDELSE.FOLKETRYGD_EK);                             
                                                                                
    /* -------------------------------------------------------------- */        
    /* FØRST SKRIVES OVERSKRIFT OM PLIKTER + TEKST ETTER DENNE        */        
    /* -------------------------------------------------------------- */        
    IF (B02.PENSJONSTYPE2(IND) ^= 'O') THEN                                     
       IF (B02.PENSJONSTYPE1(IND) ^= 'K') THEN                                  
          CALL NY_M('M901');                                                    
       ELSE                                                                     
       IF (DIV_PARAM_OMR.AP_KODE_AEY(IND) = 'W' !                               
           DIV_PARAM_OMR.AP_KODE_AEY(IND) = 'P' ) THEN                          
          CALL NY_M('M200');                                                    
       ELSE                                                                     
          CALL NY_M('M900');                                                    
    ELSE                                                                        
       CALL NY_M('M903');                                                       
                                                                                
    /* -------------------------------------------------------------- */        
    /* BESKJED NÅR DU FLYTTER                                         */        
    /* -------------------------------------------------------------- */        
    CALL NY_M('M904');                                                          
                                                                                
    /* -------------------------------------------------------------- */        
    /* BESKJED NÅR DU ENDRER SIVILSTAND                               */        
    /* -------------------------------------------------------------- */        
    IF DIV_PARAM_OMR.REDUSERT_GP_2G(IND)  = 'X'     THEN                        
       CALL NY_M('M923');          /*200107*/                                   
    ELSE                                                                        
    DO;                                                                         
          SELECT(B02.SIVILSTAND(IND));                                          
             WHEN ('U','E','S') CALL NY_M('M906');                              
             WHEN ('G')         CALL NY_M('M907');                              
             WHEN ('P')         CALL NY_M('M924');                              
             WHEN ('W','V')     CALL NY_M('M923');                              
             WHEN ('A')         CALL NY_M('M908');                              
             OTHER;                                                             
          END; /* SELECT SIVILSTAND */                                          
    END;                                                                        
                                                                                
    /* -------------------------------------------------------------- */        
    /* MELDING OM INNTEKTSENDRING - KUN FOR DEM MED FORSØRGINSTILLEGG */        
    /* -------------------------------------------------------------- */        
  /*IF FT_TOTAL > 0  TESTER FOR FORS-TILL ER ENDRET 01.2002   JD*/              
    IF FT_ET    > 0 THEN                                                        
       DO;  /* HAR FORSØRGINGINGSTILLEGG */                                     
          /* -------------------------------------------------------- */        
          /* MELDING OM INNTEKTSENDRING FOR DEN SOM BLIR FORSØRGET    */        
          /* -------------------------------------------------------- */        
          IF (FT_BARN) > 0 THEN                                                 
             CALL NY_M('M909'); /* ET OG BARN FÅR INNTEKT > G */                
          ELSE                                                                  
             CALL NY_M('M912'); /* ET FÅR INNTEKT > G */                        
          IF DIV_PARAM_OMR.FULLT_ET =  'J' THEN                                 
             CALL NY_M('M910');                                                 
          ELSE                                                                  
             CALL NY_M('M917'); /* INNTEKTENDRING */                            
       END;                                                                     
    ELSE                                                                        
       DO;                                                                      
                                                                                
    /* -------------------------------------------------------------- */        
    /* BESKJED DERSOM EKTEFELLEN KOMMER TIL Å TJENE OVER 2G           */        
    /* -------------------------------------------------------------- */        
         IF VERIFY(B02.SIVILSTAND(IND),'GPVW') = 0 &                            
            (B02.PENSJON_FØR_9802(IND) ^= 'J'   &                               
             B02.EK_INNT_OVER_2G (IND)  = 'N'   &                               
             B02.PENSJONSTYPE2   (IND) ^= 'S') THEN                             
                                  CALL NY_M('M930');                            
          IF FT_FELLESBARN > 0 THEN                                             
             DO;                                                                
               CALL NY_M('M911'); /* BARN FÅR INNTEKT > G */                    
      /*       IF DIV_PARAM_OMR.FULLT_BT_W = 'J' THEN                           
                  CALL NY_M('M918');                                            
               ELSE                                          */                 
                  CALL NY_M('M919');  /* INNTEKTSGRENSE */                      
             END;                                                               
          ELSE                                                                  
          IF FT_SÆRBARN THEN                                                    
             DO;                                                                
               CALL NY_M('M911'); /* BARN FÅR INNTEKT > G */                    
               IF DIV_PARAM_OMR.FULLT_BT_V = 'J' THEN                           
                  CALL NY_M('M910');                                            
               ELSE                                                             
                  CALL NY_M('M917');  /* INNTEKTSENDRING*/                      
             END;                                                               
       END; /* HAR FORSØRGINGINGSTILLEGG */                                     
                                                                                
    /* -------------------------------------------------------------- */        
    /* DIVERSE MELDINGER OM INNTEKTSGRENSE FOR PENSJONEN              */        
    /* -------------------------------------------------------------- */        
    IF (B02.SUM_YTELSE(IND) > 0) THEN                                           
       SELECT(B02.PENSJONSTYPE1(IND));                                          
          WHEN('A','K')                                                         
             IF (ALDER < 7000) THEN                                             
                DO;  /* IKKJE FYLT 70 - OPPLYSNINGER OM INNTEKT */              
                   IF (B02.PENSJONSTYPE1(IND) = 'K'           &                 
                      (DIV_PARAM_OMR.AP_KODE_AEY(IND) = 'W' !                   
                       DIV_PARAM_OMR.AP_KODE_AEY(IND) = 'P' ) ) THEN            
                      CALL NY_M('M931'); /* ARBEIDSINNTEKT ENDR > 4' */         
                   ELSE                                                         
                   IF W_FNR_ÅM < 192501 !                                       
                      (B02.KONV_P_KODE(IND)   = 'K' &                           
                       B02.UTTAKSDATO_ÅMD(IND) < 19920000) THEN                 
                      CALL NY_M('M922');                                        
                   ELSE                                                         
                   IF  (B02.ALDERSP.GP          (IND) >                         
                       B02.ALDERSP.AP_GP_NETTO (IND)  &    /* KONS RF */        
                       B02.ALDERSP.FAI(IND) > 0 )     !                         
                      (B02.ETTEPENS.GP_BRUTTO(IND) >                            
                       B02.ETTEPENS.GP_NETTO (IND)) THEN                        
                      CALL NY_M('M926');                                        
                   ELSE                                                         
                   IF (ALDER2 > 6800) THEN                                      
                      CALL NY_M('M905');                                        
                   IF (B02.PENSJONSTYPE1(IND) = 'K'           &                 
                      (DIV_PARAM_OMR.AP_KODE_AEY(IND) = 'W' !                   
                       DIV_PARAM_OMR.AP_KODE_AEY(IND) = 'P' ) ) THEN            
                      ; /* SKAL IKKE HA DENNE IFLG BENTE 030700                 
                      CALL NY_M('M932'); /* NYE AFP TILFELLER        */         
                   ELSE                                                         
                      CALL NY_M('M925');                                        
                END; /* IKKJE FYLT 70 - OPPLYSNINGER OM INNTEKT */              
          WHEN('B')                                                             
             CALL NY_M('M915');                                                 
          WHEN('U','Y')                                                         
             DO;  /* UFØRE / YRKE    + FOREL.UF.STONAD JD 0602*/                
                IF (B02.PENSJONSTYPE2(IND) ^= 'E') THEN                         
                   IF (B02.PENSJONSTYPE3(IND) = 'A' &                           
                       B02.UFØRPENS.UFG(IND) > 49)  THEN                        
                      CALL NY_M('M933'); /* INNT > LOVLIG */                    
                   ELSE                                                         
                   IF (B02.UFØRPENS.UFG(IND) > 49) THEN                         
                      CALL NY_M('M929'); /* INNT > LOVLIG */                    
                   ELSE;                                                        
                ELSE                                                            
                IF (B02.UFØRPENS.ST(IND) +                                      
                    B02.ETTEPENS.ST(IND) > 0 )  !                               
                    B02.ETTEPENS.TP_BRUTTO(IND) = 0  THEN                       
                   CALL NY_M('M929'); /* INNT > LOVLIG */                       
                ELSE                                                            
                IF (B02.FORVENTET (IND) = 0         &                           
                    DIV_PARAM_OMR.FRADRAG_2G ^= 'J' &                           
                    B02.ETTEPENS.TP_BRUTTO(IND) > 0 ) THEN                      
                   CALL NY_M('M914'); /* INNT > 1/2 G */                        
                ELSE                                                            
                   CALL NY_M('M920'); /* INNT ENDRES */                         
                /* -------------------------------------------------- */        
                CALL NY_M('M925'); /* DERSOM FOR MYE */                         
             END; /* UFØRE / YRKE */                                            
          WHEN('E','J')                                                         
             DO;  /* ETTERLATT EKTEFELLE/SAMBOER/PARTNER/FAMILIEPL. */          
                IF B02.ETTEPENS.FORVENTET(IND) = 0   &                          
                    DIV_PARAM_OMR.FRADRAG_2G ^= 'J'     THEN                    
                   CALL NY_M('M914'); /* INNT > 1/2 G */                        
                ELSE                                                            
                   CALL NY_M('M920'); /* INNT ENDRES */                         
                CALL NY_M('M925'); /* DERSOM FOR MYE */                         
             END; /* ETTERLATT EKTEFELLE/SAMBOER/PARTNER/FAMILIEPL. */          
          OTHER;                                                                
       END; /* SELECT PENSJONSTYPE */                                           
                                                                                
    /* -------------------------------------------------------------- */        
    /* AVSLUTTNING MED BESKJED OM Å LESE VEDLEGGET                    */        
    /* -------------------------------------------------------------- */        
    CALL NY_M('M902');                                                          
                                                                                
    /* ************************************************************** */        
    /* INTERN PROC                                                    */        
    /* ************************************************************** */        
    NY_M : PROC (KODE);                                                         
       DCL KODE CHAR (4);                                                       
       TEKSTTYPE_M_KODE(I) = KODE;                                              
       I                   = I + 1;                                             
    END NY_M;                                                                   
                                                                                
 END PLASSER_M_KODE_TABELL;                                                     
