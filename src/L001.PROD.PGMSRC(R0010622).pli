 /*   SIST ENDRET PÅ PROD   2006.07.18 11.36.00 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2006.04.24  9.41.55 AV   SPA2970          */        
 /*   SIST ENDRET PÅ QASS   2006.04.19 12.10.28 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2006.03.24  8.43.38 AV   SPA2970          */        
 /*   SIST ENDRET PÅ PROD   2005.06.24 12.43.46 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2005.04.26 12.54.04 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2004.12.17 13.57.08 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.07.13 12.58.55 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.09.13 11.56.18 AV   HLA2970          */        
 /*   SIST ENDRET PÅ PROD   2000.01.06 11.57.42 AV   JDA7339          */        
 /*       SIST ENDRET 09/06-98 08.39.26 AV   SPA7339                  */        
 /*       SIST ENDRET 26/05-98 09.42.01 AV   RFA9991                  */        
 /* ***************************************************************** */        
 /*IDENTIFIKASJON:                                                    */        
 /*    R0010622 - PROSEDYRE I PLI                                     */        
 /*    PROGRAMMERER: JAN, OKTOBER 1981                                */        
 /* *****************************************                         */        
 /*ENDRINGSDATO : 31/1-84                                             */        
 /*ENDRINGEN GJELDER :                                                */        
 /*    OPPDATERING AV UFKRIT NÅR SAMME UFØRETIDSPUNKT SOM TIDLIGERE   */        
 /*    RAPPORTERT. FELTET BLE IKKE JUSTERT NÅR VILKÅR8_4 = N, BARE    */        
 /*    NÅR = J.                                                       */        
 /*ENDRINGEN UTFØRT AV KARIN                                          */        
 /* ********************************************************          */        
 /*ENDRINGSDATO : 5/6-84                                              */        
 /*ENDRINGEN GJELDER :                                                */        
 /*    REDUSERT BARNETILLEGG MED VIRKNING FRA 1/7-84                  */        
 /*    DET INNFØRES OPPLYSNINGER OM EKTEFELLENS INNTEKT I             */        
 /*    EKTEFELLENS PENSJONSKODE2.(BARE PT2(EK) = G)                   */        
 /*    PENSJONSKODE2(EKTEF) SETTES LIK 'M' NÅR INNT >= 4G.            */        
 /*    PENSJONSKODE2(EKTEF) SETTES LIK 'U' NÅR INNT <  4G.            */        
 /*ENDRINGEN BLE UTFØRT AV KARIN                                      */        
 /* ********************************************************          */        
 /*ENDRINGSDATO : 18/1-85                                             */        
 /*ENDRINGEN GJELDER :                                                */        
 /*    BARNEPENSJONISTER SOM BLE UFØRE FIKK IKKE MED UFØREPENSJONEN.  */        
 /*    DETTE SKYLTES AT PENSJONSTYPE3 ALLTID BLE SATT TIL BLANK NÅR   */        
 /*    DET IKKE VAR ET ATTFØRINGSTILFELLE.                            */        
 /*ENDRINGEN BLE UTFØRT AV KARIN                                      */        
 /* ********************************************************          */        
 /*ENDRINGSDATO : SOMMEREN 1987.  ERIK.                               */        
 /*ENDRINGEN GJELDER :                                                */        
 /*    UTVIDELSE AV TESTER I FORBINDELSE MED INNBAKING AV             */        
 /*    YRKESSKADEPENSJON I SYSTEMET.                                  */        
 /* *********************************************************         */        
 /*HENSIKT:                                                           */        
 /*    PROSEDYREN AJOURFØRER SØKERS PENSJONSSTATUS MED INFO FRA TRANS */        
 /*    UFØREPENSJON.                                                  */        
 /* ****************************************************************  */        
 /*PROGRAMTILKNYTNING:                                                */        
 /*    PROSEDYREN INCLUDES I R0010620.                                */        
 /*BRUK:                                                              */        
 /*    CALL AJOURFØR_B02_MED_UPTRANS                                  */        
 /*                                                                   */        
 /* ***************************************************************** */        
                                                                                
 %SKIP(2);                                                                      
 AJOURFØR_B02_MED_UPTRANS:                                                      
   PROC OPTIONS(REENTRANT);                                                     
   IF FEIL_MELD_NR = 0 THEN /* AB 01.08.16 */                                   
      PROGRAM_ID = 'R0010622';    /* LETTER FEILSØK, MHA7339, 171296 */         
                                                                                
 %SKIP;                                                                         
   DCL                                                                          
      UF_IND           FIXED BIN(15),                                           
        YUF_IND        FIXED BIN(15),                                           
      J                FIXED BIN(15),                                           
      K                FIXED BIN(15),                                           
      ÅR_FØR_UFØRHET   PIC '9999';                                              
   DCL                                                                          
      HJ_UFT_ÅMD                    PIC '99999999',                             
      HJ_UFT_HÅ DEF HJ_UFT_ÅMD POS(1) PIC '9999',                               
      HJ_UFT_Å DEF HJ_UFT_ÅMD POS(3) PIC '99',                                  
      HJ_UFT_M DEF HJ_UFT_ÅMD POS(5) PIC '99';                                  
                                                                                
    IF UPS.VIRK_DATO_ÅMD < 19980200 THEN             /* 2000-3 */               
       B02.PENSJON_FØR_9802(SØKER_IND) = 'J';                                   
                                                                                
    B02.NAVN(SØKER_IND)            = UPS.NAVN;                                  
    B02.SPRÅK(SØKER_IND)           = UPS.SPRÅK;                                 
    B02.TKNR(SØKER_IND)            = UPS.TKNR;                                  
                                                                                
    IF UPS.YRKE                    > 0   THEN          /*TS171094*/             
       B02.YRKES_KODE(SØKER_IND)   = UPS.YRKE;                                  
                                                                                
                                                                                
    IF UPS.UFT_ÅMD < 19910000    THEN        /*HL*/   /* 2000-3 */              
       B02.PENSJONSRETT_FØR_91(SØKER_IND) = 'J';                                
                                                                                
    B02.FØRSTE_GANG_REG(SØKER_IND) = 'N';                                       
    DO I = 1 TO 13;                                                             
       IF B02.FNR(I) > 0 THEN                                                   
          B02.FØRSTE_GANG_REG(I) = 'N';                                         
    END;                                                                        
                                                                                
 /* ***************************************************************** */        
 /* YTELSE_SEGMENTET                                                  */        
 /* ***************************************************************** */        
                                                                                
 %SKIP;                                                                         
    IF B01.PENSJONSTYPE1(SØKER_IND) = 'N' !                                     
                           B01.PENSJONSTYPE1(SØKER_IND) = 'B' THEN              
       B02.PENSJONSTYPE3(SØKER_IND) = 'U';                                      
    /*    PT1 ER UENDRET                                              */        
    ELSE                                                                        
       B02.PENSJONSTYPE1(SØKER_IND) = 'U';                                      
                                                                                
  L270:                                                                         
         IF UPY.KAP_11_7_UTLØST          = 'J'   &   /*TRUDE, 31.5.89*/         
            B01.PENSJONSTYPE1(SØKER_IND) = 'F'   THEN                           
              DO;                                                               
                FEIL_VED_LABEL           = '270';                               
                FEIL_MELD_NR             = '1732';                              
              END;                                                              
                                                                                
                                                                                
          IF UPS.YSKADE_TILLEGG           = 'J'  !                              
             B01.PENSJONSTYPE1(SØKER_IND) = 'Y'     THEN                        
             B02.PENSJONSTYPE1(SØKER_IND) = 'Y';                                
                                                                                
           IF UPS.ATT_UF = 'A' THEN                                             
              IF (B01.PENSJONSTYPE1(SØKER_IND)     = 'U' !                      
                  B01.PENSJONSTYPE1(SØKER_IND)     = 'Y' ) &                    
                 B01.PENSJONSTYPE3(SØKER_IND)    ^= 'A' &                       
                                                                                
          B01.STATUS_KODE_HIST(SØKER_IND)  = ' ' THEN                           
          DO;                                                                   
                                                                                
             IF  (STATUS_TYPE = 'B' !                                           
                 STATUS_TYPE = 'I')  &                                          
                 TRANS_LISTE_INDEX = 1  THEN                                    
 /* ***************************************************************** */        
 /* FEIL,SØKEREN HAR UFØRPENSJON FRA FØR.                             */        
 /* ***************************************************************** */        
                 DO;                                                            
  L275:                                                                         
                    FEIL_VED_LABEL = '275';                                     
                    FEIL_MELD_NR   = 1717;                                      
                    GO TO RETUR;                                                
                 END;                                                           
 /* ***************************************************************** */        
 /* SØKEREN HAR ATTFØRING FRA FØR.TRANHIST BLIR DA FJERNET.           */        
 /* ***************************************************************** */        
             ELSE                                                               
                 DO;                                                            
                                                                                
                                                                                
                    SEARCH_FNR                  =                               
                                   TRANS_OPPL_OMR.FØDSNUMMER        ;           
                                                                                
                                                                                
                    FJERN_ALLE                  = '0'B              ;           
                                                                                
                                                                                
                    EXEC CICS LINK PROGRAM                                      
                               ('R0015501') COMMAREA(LOKAL_KOM_OMR) ;           
                                                                                
                    IF FEIL_MELD_NR > 0 THEN                                    
                       GO TO RETUR                                  ;           
                                                                                
                                                                                
  /*9712 HL - FJERNET FOR Å RETTE FEIL NR. 3 : *************                    
                    ANTALL_TRANS_I_TRANS_LISTE =                                
                                      ANTALL_TRANS_I_TRANS_LISTE - 1;           
   *********************************************************** */               
                                                                                
                 END;                                                           
          END;                                                                  
       ELSE                                                                     
  /* DENNE TESTEN ER BEGRENSET I FORB. MED FEILM.274. ERIK 16.1.86 */           
                 IF B01.PENSJONSTYPE3(SØKER_IND) = 'A'  &                       
                    (B01.PENSJONSTYPE1(SØKER_IND)    ^= 'Y' &                   
                     B01.PENSJONSTYPE1(SØKER_IND)    ^= 'U' ) &                 
                    B01.STATUS_KODE_HIST(SØKER_IND)  = ' ' THEN                 
                 DO;                                                            
                                                                                
 /* ***************************************************************** */        
 /* FEIL,SØKEREN HAR ATTFØRING FRA FØR.                               */        
 /* ***************************************************************** */        
                                                                                
  L277:                                                                         
                FEIL_VED_LABEL = '277';                                         
                FEIL_MELD_NR   = 1718;                                          
                GO TO RETUR;                                                    
             END;                                                               
          ELSE                                                                  
             B02.PENSJONSTYPE3(SØKER_IND) = 'A';                                
    ELSE                                                                        
       DO;                                                                      
          IF B01.PENSJONSTYPE3(SØKER_IND) = 'A' THEN                            
             DO;                                                                
                                                                                
                DO I = 1 TO 7;               /*0697 HL*/                        
                   IF B01.UFØRHIST.UFT_ÅMD(SØKER_IND,I) = 0 THEN                
                      DO;                                                       
                         B00.UFØRHIST(SØKER_IND,I-1) =                          
                                 B01.UFØRHIST(SØKER_IND,I-1);                   
                         B01.UFØRHIST(SØKER_IND,I-1) = '';                      
                         K = I - 1;                                             
                         I = 8;                 /*0697 HL*/                     
                      END;                                                      
                END;                                                            
             END;                                                               
                                                                                
 /* ***************************************************************** */        
 /* NYE FORS-TILL BARN SKAL IKKE REGISTRERES,BRUK F7-BL  (JD 7.91)    */        
 /* ***************************************************************** */        
                                                                                
    IF UPS.BT_ANT > B01.ANTALL_BARN (SØKER_IND) &                               
       UPS.VIRK_DATO_ÅMD > 19910400 THEN    /* 2000-3 */                        
                 DO;                                                            
  L378:                                                                         
                    FEIL_VED_LABEL = '378';                                     
                    FEIL_MELD_NR   = 1738;                                      
                    GO TO RETUR;                                                
                 END;                                                           
                                                                                
          IF B02.PENSJONSTYPE1(SØKER_IND) = 'U' !                               
             B02.PENSJONSTYPE1(SØKER_IND) = 'Y' THEN                            
             B02.PENSJONSTYPE3(SØKER_IND) = ' ';                                
       END;                                                                     
 /* ***************************************************************** */        
 /* TIDLIGERE REGISTERT SOM ETTERLATT EKTEFELLE                       */        
 /* ***************************************************************** */        
                                                                                
    IF B01.PENSJONSTYPE1(SØKER_IND) = 'E' THEN                                  
       B02.PENSJONSTYPE2(SØKER_IND) = 'E';                                      
                                                                                
 /* ***************************************************************** */        
 /* HVIS SØKER HAR EKTEFELLE I TRANS                                  */        
 /* ***************************************************************** */        
                                                                                
    IF UPS.FNR_EK > 0 THEN                                                      
       DO;                                                                      
          IF  UPS.SIVILSTAND               = 'A' THEN                           
             DO;                                                                
                B02.SIVILSTAND(SØKER_IND)      = 'A';                           
                B02.SIVILSTAND(EKTEF_IND)      = 'A';                           
             END;                                                               
          ELSE                                                                  
          IF  UPS.SIVILSTAND               = 'P' THEN                           
             DO;                                                                
                B02.SIVILSTAND(SØKER_IND)      = 'P';                           
                B02.SIVILSTAND(EKTEF_IND)      = 'P';                           
             END;                                                               
          ELSE                                                                  
          IF  UPS.SIVILSTAND               = 'W' THEN                           
             DO;                                                                
                B02.SIVILSTAND(SØKER_IND)      = 'W';                           
                B02.SIVILSTAND(EKTEF_IND)      = 'W';                           
             END;                                                               
          ELSE                                                                  
          IF  UPS.SIVILSTAND               = 'V' THEN                           
             DO;                                                                
                B02.SIVILSTAND(SØKER_IND)      = 'V';                           
                B02.SIVILSTAND(EKTEF_IND)      = 'V';                           
             END;                                                               
          ELSE                                                                  
             DO;                                                                
                B02.SIVILSTAND(SØKER_IND)      = 'G';                           
                B02.SIVILSTAND(EKTEF_IND)      = 'G';                           
             END;                                                               
                                                                                
 /* ***************************************************************** */        
 /* HVIS FORSØRGINGSTILLEGG EKTEFELLE                                 */        
 /* PENSJONSTYPE1 FOR EKTEFELLEN OG PENSJONSTYPE2 FOR PENSJONISTEN    */        
 /* BLE GALT FOR YRKESKADEPENSJONER.  ENDRET 11.1.1990, ERIK.         */        
 /* ***************************************************************** */        
                                                                                
          IF UPS.TILL_EK = 'J' & UPS.VIRK_DATO_ÅMD > 19910400 &                 
             B01.PENSJONSTYPE2(SØKER_IND) ^= 'F' THEN                           
             UPS.TILL_EK = ' ' ; /* JD 7.91*/                                   
          IF UPS.TILL_EK = 'J' THEN /*TEST PÅ B01.ET FJERNET 7.90 JD */         
             DO;                                                                
                B02.PENSJONSTYPE2(SØKER_IND)   = 'F';                           
                IF UPS.UFG < 50 THEN                                            
                 DO;                                                            
                    B02.PENSJONSTYPE2(SØKER_IND)   = 'P';                       
                    B02.PENSJONSTYPE1(EKTEF_IND)   = 'G';                       
                    B02.PENSJONSTYPE2(EKTEF_IND)   = ' ';                       
                 END;                                                           
               ELSE                                                             
                 B02.PENSJONSTYPE1(EKTEF_IND)   = 'F';                          
                 B02.PENSJONSTYPE2(EKTEF_IND)   = ' ';                          
             END;                                                               
          ELSE                                                                  
             DO;                                                                
                                                                                
 /* ***************************************************************** */        
 /* HVIS EKTEFELLEN HAR EGEN PENSJON                                  */        
 /* ***************************************************************** */        
                                                                                
                                                                                
                IF  B01.PENSJONSTYPE1(EKTEF_IND)    = 'A' !                     
                    B01.PENSJONSTYPE1(EKTEF_IND)    = 'Y' !                     
                    B01.PENSJONSTYPE1(EKTEF_IND)    = 'K' !                     
                    B01.PENSJONSTYPE1(EKTEF_IND)    = 'U' THEN                  
                                                                                
 /* ***************************************************************** */        
 /* EKTEFELLEN HAR AVTALEFESTET PENSJON SOM IKKE UTBETALES            */        
 /* ***************************************************************** */        
                                                                                
                    IF B01.PENSJONSTYPE1(EKTEF_IND)    = 'K'  &                 
                       B01.PENSJONSTYPE2(EKTEF_IND)    = 'N'  THEN              
                       DO;                                                      
                          B02.PENSJONSTYPE2(SØKER_IND)    = 'P';                
                          B02.PENSJONSTYPE2(EKTEF_IND)    = 'N';                
                       END;                                                     
                    ELSE                                                        
                       IF  UPS.SIVILSTAND               = 'A'   !               
                           UPS.SIVILSTAND               = 'V' THEN              
                          DO;                                                   
                             B02.PENSJONSTYPE2(SØKER_IND)   = 'P';              
                             B02.PENSJONSTYPE2(EKTEF_IND)   = 'P';              
                          END;                                                  
                       ELSE                                                     
                          DO;                                                   
                             IF UPY.KAP_11_7_UTLØST = 'J'  THEN                 
                                                                                
                               DO;                                              
                                B02.PENSJONSTYPE2(SØKER_IND) = ' ';             
                                B02.PENSJONSTYPE2(EKTEF_IND) = 'P';             
                               END;                                             
                             ELSE                                               
                               DO;                                              
                             B02.PENSJONSTYPE2(SØKER_IND)   = 'S';              
                             B02.PENSJONSTYPE2(EKTEF_IND)   = 'S';              
                                                                                
                            IF B02.PENSJONSTYPE3(EKTEF_IND) = 'S' &             
                               B02.PENSJONSTYPE2(EKTEF_IND) ^= 'N' THEN         
                                  B02.PENSJONSTYPE2(SØKER_IND)   = 'P';         
                               END;                                             
                          END;                                                  
                ELSE                                                            
                   DO;                                                          
                                                                                
 /* ***************************************************************** */        
 /* EKTEFELLEN HAR IKKE PENSJON                                       */        
 /* ***************************************************************** */        
                                                                                
                       IF UPY.KAP_11_7_UTLØST = 'J'  THEN                       
                          B02.PENSJONSTYPE2(SØKER_IND) = ' ';                   
                       ELSE                                                     
                          B02.PENSJONSTYPE2(SØKER_IND)   = 'P';                 
                      B02.PENSJONSTYPE1(EKTEF_IND)   = 'G';                     
                      IF UPS.EK_INNT = 'J' THEN                                 
                         B02.PENSJONSTYPE2(EKTEF_IND)   = 'M';                  
                      ELSE                                                      
                         B02.PENSJONSTYPE2(EKTEF_IND)   = 'U';                  
                   END;                                                         
            END;                                                                
            B02.NAVN(EKTEF_IND)            = UPS.NAVN_EK;                       
            B02.SPRÅK(EKTEF_IND)           = UPS.SPRÅK;                         
                                                                                
 /* ***************************************************************** */        
 /* TILKN-KODE TIL EKTEFELLENE OPPDATERES MED RESPEKTIVE NYE STATUSER.*/        
 /* ***************************************************************** */        
                                                                                
            DO I = 1 TO 13;                                                     
               IF (B02.FNR_TILKN(SØKER_IND,I)=                                  
                                        B02.FNR(EKTEF_IND))THEN                 
                  DO;                                                           
                     B02.TILKN.TILKNYTNINGSKODE(SØKER_IND,I)                    
                                  =  B02.PENSJONSTYPE1(EKTEF_IND);              
                     I=13;                                                      
                  END;                                                          
            END;                                                                
            DO I = 1 TO 13;                                                     
               IF (B02.FNR_TILKN(EKTEF_IND,I)=                                  
                                        B02.FNR(SØKER_IND))THEN                 
                  DO;                                                           
                     B02.TILKNYTNINGSKODE(EKTEF_IND,I)                          
                            =  B02.PENSJONSTYPE1(SØKER_IND);                    
  /*HL*/             B02.FT_FØR_91(EKTEF_IND,I) = ' ';                          
                     I=13;                                                      
                  END;                                                          
            END;                                                                
       END;                                                                     
    ELSE                                                                        
                                                                                
 /* **************************************************************** */         
 /* SØKER HAR IKKE EKTEFELLE                                         */         
 /* **************************************************************** */         
                                                                                
       IF ^(B01.PENSJONSTYPE1(SØKER_IND) = 'N' !                                
            B01.PENSJONSTYPE1(SØKER_IND) = 'E' !                                
            B01.PENSJONSTYPE1(SØKER_IND) = 'B')    &                            
            B01.PENSJONSTYPE2(SØKER_IND)  ^= 'E'       THEN                     
                                                                                
            IF UPY.KAP_11_7_UTLØST = 'J'  THEN                                  
               B02.PENSJONSTYPE2(SØKER_IND) = ' ';                              
            ELSE                                                                
               B02.PENSJONSTYPE2(SØKER_IND) = 'P';                              
                                                                                
 /* ***************************************************************** */        
 /* UFØREPENSJON                                                      */        
 /* ***************************************************************** */        
                                                                                
    B02.UFØRPENS.UFG(SØKER_IND)           = UPS.UFG;                            
    B02.UFØRPENS.VILKÅR_8_4_3A(SØKER_IND) = UPS.VILKÅR_8_4;                     
    B02.VILKÅR_8_1B(SØKER_IND)            = UPS.VILKÅR_8_1;                     
                                                                                
                                                                                
 /* ***************************************************************** */        
 /* TRYGDETID                                                         */        
 /* ***************************************************************** */        
                                                                                
    B02.STATUS.TT_FØR_1967(SØKER_IND) = UPSP.TT_F67;                            
    B02.TT_ETTER_1966(SØKER_IND)      = UPSP.TT_E66_Å * 12 +                    
                                        UPSP.TT_E66_M;                          
                                                                                
    HJ_UFT_ÅMD     = UPS.UFT_ÅMD;                                               
                                                                                
    IF HJ_UFT_HÅ > 1972 THEN     /* 2000-3 */                                   
       B02.STATUS.TT_FRAMT(SØKER_IND)    =                                      
                                (ALDER_67.ÅR * 12 + ALDER_67.MND) -             
                                (HJ_UFT_HÅ * 12 + HJ_UFT_M);                    
    ELSE                                                                        
       B02.STATUS.TT_FRAMT(SØKER_IND)    =                                      
                                (ALDER_70.ÅR * 12 + ALDER_70.MND) -             
                                 (HJ_UFT_HÅ * 12 + HJ_UFT_M);                   
                                                                                
 /* ***************************************************************** */        
 /* HVIS SPESIELLE OPPLYSNINGER                                       */        
 /* ***************************************************************** */        
                                                                                
    IF UPS.NORSK_BOSATT = 'N' ! UPS.VILKÅR_8_1 = 'N' THEN                       
       DO;                                                                      
          B02.SPESOPPL.STATSBORGER(SØKER_IND) = UPSP.                           
                                                           STATSBORGER;         
          B02.SPESOPPL.BOSATT(SØKER_IND)      = UPSP.BOSATT;                    
          B02.FRAVIK_BOSATT(SØKER_IND)        = UPSP.                           
                                                         FRAVIK_BOSATT;         
          B02.SPESOPPL.FRAVIK_TRYGDET_3ÅR(SØKER_IND) =                          
                                                      UPSP.FRA_TRY_3ÅR;         
          B02.FRAVIK_8_1(SØKER_IND)       = UPSP.FRAVIK_8_1;                    
          B02.SPESOPPL.KONVENSJON(SØKER_IND) = UPSP.KONVENSJON; /*TS*/          
          B02.SPESOPPL.FRAVIK_19_3(SØKER_IND)   = UPSP.                         
                                                           FRAVIK_19_3;         
          B02.SPESOPPL.FRAVIK_FLYKTN(SØKER_IND) =                               
                                                     UPSP.FRAVIK_FLYKT;         
          B02.SPESOPPL.OVERKOMP_UTL(SØKER_IND)  = UPSP.                         
                                                          OVERKOMP_UTL;         
          B02.UFØR_UNDER_BOTID(SØKER_IND)        = UPSP.                        
                                                          UFØR_U_BOTID;         
          IF B02.STATUS.PENSJONSTYPE1(EKTEF_IND) = 'F' THEN                     
             DO;                                                                
                B02.STATUS.TT_FØR_1967(EKTEF_IND) = UPSP.                       
                                                        TT_F67_EK;              
                IF UPSP.TT_E66_Å_EK > 0 THEN                                    
                   B02.TT_ETTER_1966(EKTEF_IND) = UPSP.                         
                                    TT_E66_M_EK + UPSP.TT_E66_Å_EK * 12;        
             END;                                                               
          IF UPSP.TT_ETTER_UFT > 0 THEN                                         
             DO;                                                                
                B02.SPESOPPL.TT_FRAMT_UFT(SØKER_IND) =                          
                                                      UPSP.TT_ETTER_UFT;        
                B02.STATUS.TT_FRAMT(SØKER_IND) =                                
                                                      UPSP.TT_ETTER_UFT;        
             END;                                                               
          ELSE                                                                  
             DO;                                                                
                IF UPSP.TT_FRAMT_8_1_Å > 0 THEN                                 
                   DO;                                                          
                      B02.SPESOPPL.TT_FRAMT_UFT(SØKER_IND) =                    
                                                     UPSP.TT_ETTER_UFT;         
                      B02.STATUS.TT_FRAMT(SØKER_IND) =                          
                                                    UPSP.TT_ETTER_UFT;          
                   END;                                                         
             END;                                                               
          B02.IKKE_GP(SØKER_IND)      = UPSP.IKKE_GP;                           
       END;                                                                     
    ELSE                                                                        
       B02.SPESOPPL(SØKER_IND) = '';                                            
                                                                                
 /* ***************************************************************** */        
 /* PENSJONSGIVENDE INNTEKT                                           */        
 /* ***************************************************************** */        
                                                                                
 %SKIP;                                                                         
    ÅR_FØR_UFØRHET = HJ_UFT_HÅ - 1;                                             
                                                                                
 /* ***************************************************************** */        
 /* HER MÅ VI TESTE PÅ EN KODE FOR Å SE OM VI HAR FÅTT RAPP. INNTEKT  */        
 /* ***************************************************************** */        
                                                                                
                                                                                
  /* ENDRET 200103 HL : */                                                      
                                                                                
          IF (ÅR_FØR_UFØRHET            > 1966)     &                           
             (UPS.PI > 0                      )       THEN                      
            DO;                                                                 
               IF B01.PI_KODE( SØKER_IND,ÅR_FØR_UFØRHET) = ' ' !                
                  B01.PI_KODE( SØKER_IND,ÅR_FØR_UFØRHET) = 'F' THEN             
                  DO;                                                           
                     B02.PI_KODE(SØKER_IND,ÅR_FØR_UFØRHET) = 'F';               
                     B02.PI(SØKER_IND,ÅR_FØR_UFØRHET) = UPS.PI;                 
                  END;                /* FJERNET /100 OVER JFA */               
               ELSE   /*        KODE M BETYR OMSORG + FORELØPIG*/               
               IF B01.PI_KODE(SØKER_IND,ÅR_FØR_UFØRHET) = 'M'  THEN             
                     DO;                                                        
                        B02.PI_KODE(SØKER_IND,ÅR_FØR_UFØRHET) = 'M';            
                        B02.PI(SØKER_IND,ÅR_FØR_UFØRHET) =                      
                                                UPS.PI; /*/ 100;JFA */          
                     END;                                                       
               ELSE                                                             
               IF  (B01.PI_KODE(SØKER_IND,ÅR_FØR_UFØRHET) = 'J' !               
                    B01.PI_KODE(SØKER_IND,ÅR_FØR_UFØRHET) = 'K' !               
                    B01.PI_KODE(SØKER_IND,ÅR_FØR_UFØRHET) = 'L' ) &             
                        B01.PI(SØKER_IND,ÅR_FØR_UFØRHET) = 0     THEN           
                     DO;                                                        
                        B02.PI_KODE(SØKER_IND,ÅR_FØR_UFØRHET) = 'M';            
                        B02.PI(SØKER_IND,ÅR_FØR_UFØRHET) =                      
                                                  UPS.PI; /*/ 100;JFA*/         
                     END;                                                       
            END;                                                                
                                                                                
 /* ***************************************************************** */        
 /* UFØREHISTORIKK                                                    */        
 /* ***************************************************************** */        
                                                                                
 DO I = 1 TO 7;              /*0697 HL*/                                        
    SELECT;                                                                     
       WHEN (B01.UFØRHIST.UFT_ÅMD(SØKER_IND,I) =  UPS.UFT_ÅMD)                  
                                    /*  2000-3 */                               
 /* ***************************************************************** */        
 /* D.V.S VI HAR EN PERIODE FRA FØR MED SAMME UFTIDSPUNKT             */        
 /* ***************************************************************** */        
                                                                                
       DO;                                                                      
       /*  DO J = 1 TO 12; */                                                   
           DO J = 1 TO HBOUND(B02.UFØRGRAD,3); /*JFA30.3.04 0697 HL*/           
              IF B02.UFG_ØVRIGE(SØKER_IND,I,J) > 0 THEN                         
                 DO;                                                            
                    B02.UFG_ØVRIGE(SØKER_IND,I,J) = 0;                          
                    B02.UFG_DATO_ÅMD(SØKER_IND,I,J) = 0;                        
                 END;                                                           
              ELSE                                                              
                 J = HBOUND(B02.UFØRGRAD,3); /*JFA  0697 HL*/                   
           END;                                                                 
          UF_IND = I;                                                           
          B02.UFØRHIST.UFG(SØKER_IND,I)= UPS.UFG;                               
                                                                                
 /*HL : UFKRIT FOR YRKESSKADDE BLE ALLTID SATT > 0 - RETTET 2.4.91*/            
                                                                                
          IF UPS.VILKÅR_8_4 = 'J' THEN                                          
             IF UPS.YSKADE_TILLEGG = 'J' THEN                                   
                IF UPY.KAP_11_7_UTLØST = 'J'    THEN                            
                   B02.UFKRITERIER(SØKER_IND,I) = 5;                            
                ELSE                                                            
                   B02.UFKRITERIER(SØKER_IND,I) = 4;                            
             ELSE                                                               
                B02.UFKRITERIER(SØKER_IND,I) = 2;                               
          ELSE                                                                  
             B02.UFKRITERIER(SØKER_IND,I) = 0;                                  
          IF B01.BUP_GAR_KODE(SØKER_IND,I) = 'A' THEN                           
             DO;                                                                
                IF UPS.VIRK_DATO_ÅMD >= TJUEÅRSDATO_ÅMD  &                      
                   UPS.VIRK_DATO_ÅMD > 19810400 THEN  /* 2000-3 */              
                   B02.BUP_GAR_KODE(SØKER_IND,I) = 'B';                         
             END;                                                               
          I = 7;            /*0697 HL*/                                         
       END;                                                                     
       WHEN (B01.UFØRHIST.UFT_ÅMD(SØKER_IND,I) = 0)                             
       DO;                                                                      
                                                                                
 /* ***************************************************************** */        
 /* D.V.S. DETTE ER FØRSTE LEDIGE PLASS.                              */        
 /* ***************************************************************** */        
                                                                                
          UF_IND = I;                                                           
          B02.UFØRHIST.UFT_ÅMD(SØKER_IND,I) = UPS.UFT_ÅMD;                      
          B02.UFØRHIST.UFG(SØKER_IND,I)    = UPS.UFG;                           
          IF UPS.VILKÅR_8_4 = 'J' THEN                                          
             B02.UFKRITERIER(SØKER_IND,I) = 2;                                  
          IF UF_IND > 1 THEN                                                    
             DO;  /* HAR TIDLIGERE PERIODE - DENNE OPPHØRER */                  
                IF B01.OPPHØRSDATO_ÅMD(SØKER_IND,(I-1)) = 0  THEN               
                   DO;  /* OPPHØRSDATO = 0 */                                   
                      B02.OPPHØRSDATO_ÅMD(SØKER_IND,(       /* 2000-3 */        
                                        I-1)) = UPS.UFT_ÅMD;                    
                      B02.OPPHØRSKODE(SØKER_IND,(I-1)) = 'J';                   
      /*JFA30.3.04*/  DO J = 1 TO HBOUND(B02.UFØRGRAD,3) WHILE                  
                         (B02.UFG_ØVRIGE(SØKER_IND,(I-1),J) > 0);               
                         IF B02.UFG_DATO_ÅMD(SØKER_IND,(I-1),J) =               
                            B02.UFØRHIST.UFT_ÅMD(SØKER_IND,I) THEN              
                                       /* DET NYE UFØRETIDSPT ER LIK */         
                                       /* TIDSPT TIL SISTE UFØREGRAD */         
                                       /* I FORRIGE PERIODE OG MÅ    */         
                                       /* SLETTES                    */         
                            DO;                                                 
                               B02.UFG_ØVRIGE (SØKER_IND,(I-1),J) = 0;          
                               B02.UFG_DATO_ÅMD(SØKER_IND,(I-1),J) = 0;         
                               B01.UFG_ØVRIGE (SØKER_IND,(I-1),J) = 0;          
                               B01.UFG_DATO_ÅMD(SØKER_IND,(I-1),J) = 0;         
                            END;                                                
                         ELSE                 /* 2000-3 */                      
                            IF B02.UFG_DATO_ÅMD(SØKER_IND,(I-1),J) >            
                               B02.UFØRHIST.UFT_ÅMD(SØKER_IND,I) THEN           
                               DO;                                              
 /* ***************************************************************** */        
 /* FEIL, UFØREDATO I FORRIGE PERIODE ER ETTER DET NYE UFØRETIDSPUNKT */        
 /* ***************************************************************** */        
                                                                                
  L278:                                                                         
                                   FEIL_VED_LABEL = '278';                      
                                   FEIL_MELD_NR   = 1711;                       
                                   GO TO RETUR;                                 
                               END;                                             
                      END;  /* DO J = 1 TO 12 */                                
                   END; /* OPPHØRSDATO = 0 */                                   
                                                                                
 /* ***************************************************************** */        
 /* HER OVERFØRES BUP-GARANTI OG KODE FRA FORRIGE PERIODE             */        
 /* ***************************************************************** */        
                                                                                
                B02.BUP_GARANTI(SØKER_IND,I) =                                  
                                    B02.BUP_GARANTI(SØKER_IND,(I-1));           
                B02.BUP_GAR_KODE(SØKER_IND,I) =                                 
                                   B02.BUP_GAR_KODE(SØKER_IND,(I-1));           
                IF B02.BUP_GAR_KODE(SØKER_IND,I) = 'A'        THEN              
                   DO;                                                          
                      IF UPS.VIRK_DATO_ÅMD >= TJUEÅRSDATO_ÅMD  &                
                         UPS.VIRK_DATO_ÅMD > 19810400 THEN /* 2000-3 */         
                         B02.BUP_GAR_KODE(SØKER_IND,I) = 'B';                   
                   END;                                                         
                ELSE                                                            
                IF B02.BUP_GAR_KODE(SØKER_IND,I) = 'C' THEN                     
                   DO;                                                          
                      IF UPS.VIRK_DATO_ÅMD > 19831200 THEN                      
                         B02.BUP_GAR_KODE(SØKER_IND,I) = 'B';                   
                   END;                                                         
             END; /* HAR TIDLIGERE PERIODE - DENNE OPPHØRER */                  
            IF (UF_IND = 1                                    !                 
                B01.UFØRHIST.BUP_GAR_KODE(SØKER_IND,1) ^= ' ' ) THEN            
               /* --------------------------------------------------- */        
               /* ENDRET 040100: UNGE UFØRE SOM LÅ PÅ "VENT" NÅR DEI  */        
               /*                OPPHØRTE FIKK ALDRI BUP_GARANTI      */        
               /* --------------------------------------------------- */        
             CALL UNGE_UFØRE;                                                   
          I = 7;               /*0697 HL*/                                      
       END;                                                                     
       OTHERWISE                                                                
         DO;                                                                    
                                                                                
 /* ***************************************************************** */        
 /* ALLE UFØREPERIODENE ER UTFYLT                                     */        
 /* ***************************************************************** */        
                                                                                
           IF I = 7 THEN         /*0697 HL*/                                    
              DO;                                                               
                                                                                
 /* ***************************************************************** */        
 /* FEIL, ALLE UFØREPERIODENE ER UTFYLT, IKKE PLASS TIL EN TIL        */        
 /* ***************************************************************** */        
                                                                                
  L279:                                                                         
                  FEIL_VED_LABEL = '279';                                       
                  FEIL_MELD_NR   = 1715;                                        
                  GO TO RETUR;                                                  
                  END;                                                          
          END;/* OTHERWISE  */                                                  
    END;  /* SELECT */                                                          
  END; /* DO I = 7  */                                                          
    IF B01.PENSJONSTYPE3(SØKER_IND) = 'A' THEN                                  
       B01.UFØRHIST(SØKER_IND,K) =                                              
                                   B00.UFØRHIST(SØKER_IND,K);                   
                                                                                
 /* ***************************************************************** */        
 /* SØKEREN HAR ATTFØRING FRA FØR.SS = NS OG NS BLIR FJERNET.         */        
 /* ***************************************************************** */        
                                                                                
    IF UPS.ATT_UF = 'A' THEN                                                    
       IF  (STATUS_TYPE = 'B' &                                                 
          TRANS_LISTE_INDEX > 1)  THEN                                          
          IF B01.PENSJONSTYPE1(SØKER_IND)  = 'U' &                              
             B01.PENSJONSTYPE3(SØKER_IND) ^= 'A' THEN                           
             DO;                                                                
                B02                         = B01                  ;            
                B01 = '';                                                       
                B00 = '';                                                       
             END;                                                               
                                                                                
 /* ***************************************************************** */        
 /* VERNEPLIKTOPPLYSNINGER                                            */        
 /* ***************************************************************** */        
                                                                                
    DO I = 1 TO 4 WHILE (UPS.VP_ÅR(I) > 0);                                     
       B02.VERNEPLIKTÅR(SØKER_IND,I) = UPS.VP_ÅR(I);                            
    END;                                                                        
                                                                                
 /* ***************************************************************** */        
 /*                                                                   */        
 /* OPPDATERE INNTEKT_FØR_UP MED 0 PGA ABEND I SEGMENTET.             */        
 /*           INNTEKT_FØR_UP MÅ OPPDATERES I EGET SKJERMBLIDE         */        
 /*           TRUDE 030295                                            */        
 /* ***************************************************************** */        
                                                                                
    IF UPS.INNTEKT_FØR_UP          > 0   THEN          /*SP210296*/             
       B02.INNTEKT_FØR_UP(SØKER_IND) = UPS.INNTEKT_FØR_UP / 100;                
                                                                                
    IF   B02.UFØRHIST.UFT_ÅMD(SØKER_IND,1) > 0 THEN                             
         B02.INNT_DATO_ÅMD(SØKER_IND)  =                                        
                          B02.UFØRHIST.UFT_ÅMD(SØKER_IND,1);                    
    ELSE                                    /* 2000-3 */                        
         B02.INNT_DATO_ÅMD(SØKER_IND) =  UPS.UFT_ÅMD;                           
                                                                                
 /* ***************************************************************** */        
 /*                                                                   */        
 /* YRKESSKADEOPPLYSNINGER.  ERIK 2.7.87.                             */        
 /*                                                                   */        
 /* ***************************************************************** */        
                                                                                
                                                                                
  %SKIP;                                                                        
                                                                                
        IF UPS.YSKADE_TILLEGG = 'J' THEN                                        
                                                                                
           IF B01.YRKEPENS.YUG(SØKER_IND) = UPY.YUG     &                       
              B01.UFØRPENS.UFG(SØKER_IND)^= UPS.UFG     THEN ;                  
           ELSE                                                                 
                                                                                
           IF B01.YRKEPENS.YUG(SØKER_IND) > UPY.YUG     THEN                    
              DO;                                                               
        L280:                                                                   
                 FEIL_VED_LABEL = '280';                                        
                 FEIL_MELD_NR   =  1737;                                        
                 GO TO RETUR;                                                   
              END;                                                              
           ELSE                                                                 
                                                                                
                                                                                
        DO I = LBOUND(B01.YRKEHIST,2) TO HBOUND(B01.YRKEHIST,2);                
                                                                                
           YUF_IND = I;                                                         
        SELECT;                                                                 
           WHEN (B01.YRKEHIST.YUFT_ÅMD(SØKER_IND,I) =                           
                        UPS.UFT_ÅMD)                                            
              DO;                                                               
                 B02.YRKEHIST.YUG(SØKER_IND,I)    = UPY.YUG;                    
                 I = HBOUND(B02.YRKEHIST,2);                                    
                                                                                
              END;                                                              
           WHEN (B01.YRKEHIST.YUFT_ÅMD(SØKER_IND,I) = 0)                        
              DO;                                                               
                 B02.YRKEHIST.YUFT_ÅMD(SØKER_IND,I) =                           
                                       UPS.UFT_ÅMD;     /*2000-3 */             
                 B02.YRKEHIST.YUG(SØKER_IND,I)    = UPY.YUG;                    
                                                                                
                 IF (I > 1)  & (B01.YRKEHIST.OPPH_DATO_ÅMD                      
                          (SØKER_IND,(I-1)) = 0 ) THEN                          
                    DO;                                                         
                                                                                
                       B02.YRKEHIST.OPPH_DATO_ÅMD(SØKER_IND,(I-1)) =            
                                         UPS.UFT_ÅMD;                           
                       B02.YRKEHIST.OPPH_KODE(SØKER_IND,(I-1)) = 'J';           
                                                                                
                       DO J = 1 TO 5 WHILE                                      
                          (B02.YUG_ØVRIGE(SØKER_IND,(I-1),J) > 0);              
                                                                                
                          IF B02.YUG_DATO_ÅMD(SØKER_IND,(I-1),J) =              
                             B02.YRKEHIST.YUFT_ÅMD(SØKER_IND,I) THEN            
                             DO;                                                
                                B02.YUG_ØVRIGE(SØKER_IND,(I-1),J) = 0;          
                                B02.YUG_DATO_ÅMD(SØKER_IND,(I-1),J)= 0;         
                                B01.YUG_ØVRIGE(SØKER_IND,(I-1),J) = 0;          
                                B01.YUG_DATO_ÅMD(SØKER_IND,(I-1),J)= 0;         
                             END;                                               
                          IF B02.YUG_DATO_ÅMD(SØKER_IND,(I-1),J) >              
                             B02.YRKEHIST.YUFT_ÅMD(SØKER_IND,I) THEN            
                             DO;                                                
         /*  ********************************************* */                   
         /*  Y-UFØRETIDSPUNKT LIGGER FØR OPPHØRSTIDSPUNKT  */                   
         /*  ********************************************* */                   
                                                                                
                                                                                
        L281:                                                                   
                                FEIL_VED_LABEL = '281';                         
                                FEIL_MELD_NR   =  1711;                         
                                GO TO RETUR;                                    
                             END;                                               
                       END;                                                     
               /*   I = 5    TRUDE */                                           
                 END;                                                           
                    I = HBOUND(B01.YRKEHIST,2);                                 
                                                                                
              END;                                                              
           OTHERWISE                                                            
              DO;                                                               
                 IF I = HBOUND(B01.YRKEHIST,2) THEN                             
         /*  ********************************************* */                   
         /*  ALLE PERIODENE UTFYLT.  IKKE MER PLASS.       */                   
         /*  ********************************************* */                   
                                                                                
        L282:                                                                   
                    DO;                                                         
                       FEIL_VED_LABEL = '282';                                  
                       FEIL_MELD_NR   =  1715;                                  
                       GO TO RETUR;                                             
                    END;                                                        
              END;                                                              
        END; /*SELECT*/                                                         
        END;    /*DO I= 1 TO 5*/                                                
                                                                                
         IF UPS.YSKADE_TILLEGG = 'J' THEN                                       
                                                                                
           IF B01.YRKEPENS.YUG(SØKER_IND) = UPY.YUG     &                       
              B01.UFØRPENS.UFG(SØKER_IND)^= UPS.UFG     THEN ;                  
           ELSE                                                                 
           DO;                                                                  
              B02.YRKEPENS.YUG(SØKER_IND) = UPY.YUG;                            
              B02.YRKEHIST.YST_ÅMD(SØKER_IND,YUF_IND) = /* 2000-3*/             
                                       UPY.YST_ÅMD;                             
              B02.YRKEHIST.AÅI(SØKER_IND,YUF_IND) = UPY.AÅI;                    
              B02.YRKEHIST.YRKE11(SØKER_IND,YUF_IND) = UPY.YRKE11;              
              B02.YRKEHIST.VILKÅR_1_2_2(SØKER_IND,YUF_IND)=                     
                                 UPY.VILKÅR_1_2_2A !! UPY.VILKÅR_1_2_2B;        
              B02.YRKEHIST.KAP_11_7_UTLØST(SØKER_IND,YUF_IND) =                 
                                                    UPY.KAP_11_7_UTLØST;        
              B02.YRKEPENS.YSYKDOM(SØKER_IND) = UPY.YSYKDOM;                    
              DIV_PARAM_OMR.YSYKDOM        = UPY.YSYKDOM        ;               
 /* ***************************************************************** */        
 /*YS - VI SKAL AVVISE :  1) NY YRKESSKADE FOR EN SOM HAR Y FRA FØR   */        
 /*YS                     2) ENGANGSUTLØSNING FOR EN SOM HAR UFØRHIST */        
 /*YS                                                                 */        
 /* ***************************************************************** */        
 /* DETTE BLE LAGT I PROD FOR FLERE ÅR SIDEN - HL 13.9.01:                      
              IF B01.PENSJONSTYPE1(SØKER_IND) = 'Y'      &                      
                 YUF_IND                      > 1        &                      
                 B02.YRKEHIST.YST_ÅMD(SØKER_IND,YUF_IND) ^=                     
                 B02.YRKEHIST.YST_ÅMD(SØKER_IND,YUF_IND-1) THEN                 
                                                                                
                 DO;                                                            
        L283:                                                                   
                    FEIL_VED_LABEL = '283';                                     
                    FEIL_MELD_NR   = 1728;                                      
                    GO TO RETUR;                                                
                 END;                                                           
   *********************************************** */                           
                                                                                
              IF (B01.PENSJONSTYPE1(SØKER_IND) = 'Y'      !                     
                  B01.PENSJONSTYPE1(SØKER_IND) = 'U'  )   &                     
                 UPY.KAP_11_7_UTLØST ^= ' '             THEN                    
                                                                                
                 DO;                                                            
        L284:                                                                   
                    FEIL_VED_LABEL = '284';                                     
                    FEIL_MELD_NR   = 1729;                                      
                    GO TO RETUR;                                                
                 END;                                                           
                                                                                
                                                                                
                                                                                
                                                                                
                                                                                
                                                                                
           END;                                                                 
                                                                                
                                                                                
  /*RULLERING AV INNTEKTER - 9802 HL : */                                       
    IF B02.FORVENTET(SØKER_IND) > 0          THEN                               
       CALL RULL_FORVENTET(B02.G_DATO_ÅMD(SØKER_IND),     /* 2000-3 */          
                           B02.VIRK_DATO_ÅMD(SØKER_IND),                        
                           B02.FORVENTET(SØKER_IND));                           
                                             /*GJELDER ALLE F_RULL .*/          
    IF B02.FORSI.PENSJONSINNTEKT(SØKER_IND) > 0  THEN                           
       B02.FORSI.PENSJONSINNTEKT(SØKER_IND) =                                   
           F_RULL_FORSI(B02.G_DATO_ÅMD(SØKER_IND),                              
                 B02.VIRK_DATO_ÅMD(SØKER_IND),                                  
                 B02.FORSI.PENSJONSINNTEKT(SØKER_IND));                         
                                                                                
    IF B02.FORSI.ARBEIDSINNTEKT(SØKER_IND) > 0  THEN                            
       B02.FORSI.ARBEIDSINNTEKT(SØKER_IND) =                                    
           F_RULL_FORSI(B02.G_DATO_ÅMD(SØKER_IND),                              
                 B02.VIRK_DATO_ÅMD(SØKER_IND),                                  
                 B02.FORSI.ARBEIDSINNTEKT(SØKER_IND));                          
                                                                                
    IF B02.FORSI.PENSJONSINNTEKT_EK(SØKER_IND) > 0  THEN                        
       B02.FORSI.PENSJONSINNTEKT_EK(SØKER_IND) =                                
           F_RULL_FORSI(B02.G_DATO_ÅMD(SØKER_IND),                              
                  B02.VIRK_DATO_ÅMD(SØKER_IND),                                 
                  B02.FORSI.PENSJONSINNTEKT_EK(SØKER_IND));                     
                                                                                
    IF B02.FORSI.ARBEIDSINNTEKT_EK(SØKER_IND) > 0  THEN                         
       B02.FORSI.ARBEIDSINNTEKT_EK(SØKER_IND) =                                 
           F_RULL_FORSI(B02.G_DATO_ÅMD(SØKER_IND),                              
                 B02.VIRK_DATO_ÅMD(SØKER_IND),                                  
                 B02.FORSI.ARBEIDSINNTEKT_EK(SØKER_IND));                       
                                                                                
 /* RULLERING AV EKTEFELLES INNTEKTER - 9802 HL */                              
    IF B02.ALDERSP.FAI(EKTEF_IND) > 0   THEN                                    
       CALL RULL_FAI(B02.G_DATO_ÅMD(EKTEF_IND),                                 
                     B02.VIRK_DATO_ÅMD(EKTEF_IND),                              
                     B02.ALDERSP.FAI(EKTEF_IND));                               
                                                                                
    IF B02.FORSI.PENSJONSINNTEKT(EKTEF_IND) > 0  THEN                           
       B02.FORSI.PENSJONSINNTEKT(EKTEF_IND) =                                   
           F_RULL_FORSI(B02.G_DATO_ÅMD(EKTEF_IND),                              
                 B02.VIRK_DATO_ÅMD(EKTEF_IND),                                  
                 B02.FORSI.PENSJONSINNTEKT(EKTEF_IND));                         
                                                                                
    IF B02.FORSI.ARBEIDSINNTEKT(EKTEF_IND) > 0  THEN                            
       B02.FORSI.ARBEIDSINNTEKT(EKTEF_IND) =                                    
           F_RULL_FORSI(B02.G_DATO_ÅMD(EKTEF_IND),                              
                 B02.VIRK_DATO_ÅMD(EKTEF_IND),                                  
                 B02.FORSI.ARBEIDSINNTEKT(EKTEF_IND));                          
                                                                                
    IF B02.FORSI.PENSJONSINNTEKT_EK(EKTEF_IND) > 0  THEN                        
       B02.FORSI.PENSJONSINNTEKT_EK(EKTEF_IND) =                                
           F_RULL_FORSI(B02.G_DATO_ÅMD(EKTEF_IND),                              
                  B02.VIRK_DATO_ÅMD(EKTEF_IND),                                 
                  B02.FORSI.PENSJONSINNTEKT_EK(EKTEF_IND));                     
                                                                                
    IF B02.FORSI.ARBEIDSINNTEKT_EK(EKTEF_IND) > 0  THEN                         
       B02.FORSI.ARBEIDSINNTEKT_EK(EKTEF_IND) =                                 
           F_RULL_FORSI(B02.G_DATO_ÅMD(EKTEF_IND),                              
                 B02.VIRK_DATO_ÅMD(EKTEF_IND),                                  
                 B02.FORSI.ARBEIDSINNTEKT_EK(EKTEF_IND));                       
                                                                                
                                                                                
                                                                                
  RETUR:                                                                        
   END AJOURFØR_B02_MED_UPTRANS;                                                
