 /*   SIST ENDRET PÅ PROD   2005.05.23 13.53.27 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2005.05.13  9.55.24 AV   SPA2970          */        
 /*   SIST ENDRET PÅ QASS   2005.05.09 12.23.52 AV   SPA2970          */        
 /*   SIST ENDRET PÅ TEST   2005.05.09 12.22.44 AV   SPA2970          */        
 /*   SIST ENDRET PÅ QASS   2005.04.20 15.02.19 AV   SPA2970          */        
 /*   SIST ENDRET PÅ TEST   2005.04.20 15.01.57 AV   SPA2970          */        
 /*   SIST ENDRET PÅ QASS   2005.04.20 14.34.03 AV   SPA2970          */        
 /*   SIST ENDRET PÅ TEST   2005.04.20 14.32.10 AV   SPA2970          */        
 /*   SIST ENDRET PÅ QASS   2005.04.12 15.04.29 AV   TSB2970          */        
 /*   SIST ENDRET PÅ QASS   2005.04.08 14.38.18 AV   SPA2970          */        
 /*   SIST ENDRET PÅ TEST   2005.04.08 14.37.53 AV   SPA2970          */        
 /*   SIST ENDRET PÅ QASS   2005.04.06  8.36.10 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2005.04.05 13.45.02 AV   JDA2970          */        
 /* ***************************************************************** */        
 /*IDENTIFIKASJON:                                                    */        
 /* **********************                                            */        
 /*  PROGRAM-IDENT : R0012301 -                                       */        
 /*  PROGRAM-SPRÅK : PLI/CICS                                         */        
 /*  PROGRAMMERER  : PATAHK                                           */        
 /*  PROGRAMMET BLE LAGET  ?                                          */        
 /*  ENDRINGERSDATO :                                                 */        
 /*  ENDRINGEN GJELDER:                                               */        
 /*                                                                   */        
 /* ***************************************************************** */        
 /*HENSIKT:                                                           */        
 /* **********                                                        */        
 /*  PROGRAMMET KONTROLLERER REGISTERING AV FRIINNTEKT-DATO           */        
 /*   KONTROLLERE     FNR  OG FRIINNTEKT-DATO                         */        
 /*                                                                   */        
 /* ***************************************************************** */        
 /*PROGRAMTILKNYTTING:                                                */        
 /* ********************                                              */        
 /*  PROGRAMMETS TRANSKODER ER R3S0, R3S1                             */        
 /*  PROGRAMMET KALLES FRA R0010301 - REGISTRERING MED TRANSKODE      */        
 /*  R3S1:                                                            */        
 /*    EXEC CICS RETURN TRANSID(TRANSKODE) COMMAREA(KOM_OMR)          */        
 /*    S0010063:                                                      */        
 /*                                                                   */        
 /* ***************************************************************** */        
 R001230:PROC(COMMAREA_PEKER) OPTIONS(MAIN);                                    
                                                                                
    %INCLUDE S00123;   /* UP-MAPSETTET                                */        
    %INCLUDE P0019906; /* TRANS_OPPL_OMRÅDE (BASED)                   */        
    %INCLUDE P0019908; /* TRANS_OMRÅDE (BASED)                        */        
    %INCLUDE P0019910; /* STYRINGS_OMRÅDE (BASED)                     */        
    %INCLUDE P0019912; /* DIV-PARAM_OMRÅDE (BASED)                    */        
    %INCLUDE DFHBMSCA;                                                          
    DCL                                                                         
       (BMSMAPBR,COMMAREA_PEKER) PTR;                                           
                                                                                
    %INCLUDE P0019A01;               /* TRANS-OMR FOR STYRT AUTOHE.*/           
                                                                                
 %INCLUDE P0012002;   /* PCB - PEKER - OMR    */                                
       DCL 1  RF1              BASED (RF1_PCB_PEKER),                           
                                                                                
 %INCLUDE P0012003;                         /* PCB                 */           
                                                                                
  DCL                                                                           
     W01_PSB_NAVN                CHAR (8)       INIT('B001F001');               
  DCL                                                                           
     PLITDLI ENTRY;                                                             
                                                                                
    /* ************************************************************** */        
    /*                                                                */        
    /*   ARBEIDSFELTER SOM BRUKES AV UIB (USER INTERFACE BLOCK)       */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
    %INCLUDE DLIUIB;                                                            
                                                                                
       DCL 1 RF0PERSN BASED (IO_PEKER),                                         
    %INCLUDE P0019927;                              /* RF0PERS-SEGM. */         
                                                                                
       DCL 1 STATUS BASED (IO_PEKER),                                           
    %INCLUDE P0019930;                              /* STATUSP-SEGM. */         
                                                                                
       DCL 1 UFØRHIST BASED (IO_PEKER),                                         
    %INCLUDE P0019935;                              /* UFØRHIST_SEGM. */        
                                                                                
       DCL 1 GRUNNBU3 BASED (IO_PEKER),                                         
    %INCLUDE P0019U42;                            /* GRUNNBU3-SEGM. */          
    /* ************************************************************** */        
    /*                                                                */        
    /*   DLI CALL-PARAMETRE + ANDRE KODER .                           */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
 DCL 1  W01_DLI_PARAM,                                                          
              2   W02_GET                 CHAR (4)   INIT ('    '),             
              2   W02_GU                  CHAR (4)   INIT ('GU  '),             
              2   W02_GHNP                CHAR (4)   INIT ('GHNP'),             
              2   W02_GHU                 CHAR (4)   INIT ('GHU '),             
              2   W02_REPL                CHAR (4)   INIT ('REPL'),             
              2   W02_GN                  CHAR (4)   INIT ('GN  '),             
              2   W02_PARM_CT_1     FIXED BIN  (31)  INIT (1),                  
              2   W02_PARM_CT_3     FIXED BIN  (31)  INIT (3),                  
              2   W02_PARM_CT_4     FIXED BIN  (31)  INIT (4),                  
              2   W02_PARM_CT_5     FIXED BIN  (31)  INIT (5);                  
                                                                                
                                                                                
    /* ************************************************************** */        
    /*                                                                */        
    /*   DL1 SSA-OMRÅDER FOR ROT OG STATUS-SEGMENTER.                 */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
 DCL    SSA_UQUAL              STATIC      CHAR (9) INIT ((9)' ');              
 DCL    SSA_UQUAL_STATUS       STATIC      CHAR (9) INIT ('STATUS   ');         
                                                                                
 DCL 1  SSA1_RF0PERSN          STATIC,                                          
        2    HDEL                          CHAR (17)  INIT                      
             ('RF0PERSN(FNR     '),                                             
        2    REL_OP                        CHAR (2)   INIT (' ='),              
        2    PKEY               FIXED      DEC  (11)  INIT ( 0  ),              
        2    HP                            CHAR (1)   INIT (')' );              
                                                                                
    DCL 1 SSA1_STATUS,                                                          
           2 SSA_STATUS_FELT   CHAR(9) INIT ('STATUS  ('),                      
           2 FELT1             CHAR(8)  INIT ('STATKODE'),                      
           2 RELOOP1           CHAR(2)  INIT (' ='),                            
           2 SSA_STATUS_KODE   CHAR(1)  INIT ('S'),                             
           2 HØYRE_PARENTES    CHAR(1)  INIT (')');                             
                                                                                
                                                                                
       DCL 1  SSA1_TRANHIST                   ,                                 
              2    HDEL                       CHAR (19)  INIT                   
                                                ('TRANHIST*P(GRBLKODE'),        
              2    REL_OP_1                   CHAR (2)   INIT ('^='),           
              2    GRBLKODE                   CHAR (2)   INIT ('US'),           
              2    HP                         CHAR (1)   INIT (')');            
                                                                                
                                                                                
    DCL                                                                         
       (ADDR,UNSPEC,CSTG,ONCODE,VERIFY,SUBSTR) BUILTIN;                         
   DCL END_OF_ROOT          BIT   (1) INIT ('0'B);                              
   DCL                                                                          
      (FEIL_FUNNET,FEIL19,FEIL20) BIT(1),                                       
      FEIL_I_SØKER                BIT(1),                                       
      FEIL_I_BARN                 BIT(1),                                       
      FEIL_I_SPES                 BIT(1),                                       
      ONKODE                      PIC'9999',                                    
      CURSOR_POS                  FIXED BIN(15) INIT(-1),                       
      ONK DEF ONKODE              CHAR(4),                                      
      FEILKODE                    CHAR(4),                                      
      W_STATUS                    CHAR(4),                                      
      SKODE                       CHAR(1),                                      
      UFT_KODE                    CHAR(1),                                      
      UFT_BLK_KODE                CHAR(1),                                      
      BLKTKODE                    CHAR(1),                                      
      DSNAVN                      CHAR(8),                                      
      IO_AREA                     CHAR(450),                                    
      W_MELD                      CHAR(78),                                     
      ANT_FEIL_SKREVET            FIXED DEC (3),                                
      ANT_BARN                    FIXED BIN(15);                                
                                                                                
    DCL IO_PEKER                 POINTER;                                       
    DCL F_LOGG                   POINTER;                                       
    DCL MAP_MELDING              CHAR (78);                                     
    DCL CUR                      CHAR (78);                                     
                                                                                
    DCL   1 S1,                                                                 
            4  FNRNR                  FIXED DEC (5),                            
            4  FNR                    PIC '(11)9',                              
            4  FNR_GML                PIC '(11)9',                              
            4  UFT_ÅMDNR              FIXED DEC (5),                            
            4  UFT_ÅMD                PIC '(08)9',                              
            4  FRIINNTEKT_DATO_ÅMDNR  FIXED DEC (5),                            
            4  FRIINNTEKT_DATO_ÅMD    PIC '(08)9';                              
   /*                                                                           
   DCL   1  FRI_REC,                                                            
            2 TKNR          PIC '(04)9'   ,                                     
            2 FNR           PIC '(11)9'   ,                                     
            2 SORT_KODE     CHAR (1)     ,                                      
            2 SB_KODE       CHAR (1)     ;                                      
    */                                                                          
                                                                                
 %PAGE;                                                                         
                                                                                
 FEILKODE  = 'FEIL';                                                            
 DSNAVN    = 'DSFYKODE';                                                        
                                                                                
 EXEC CICS HANDLE CONDITION ERROR(FEILBEH);                                     
                                                                                
                                                                                
    KOM_OMR.PEKER_LISTE.DIV_PARAM_PEKER  = ADDR(KOM_OMR.DIV_PARAM_OMR);         
                                                                                
    KOM_OMR.PEKER_LISTE.TRANS_OPPL_PEKER = ADDR(KOM_OMR.TRANS_OPPL_OMR);        
    KOM_OMR.PEKER_LISTE.TRANS_PEKER      = ADDR(KOM_OMR.TRANS_OMR);             
    KOM_OMR.PEKER_LISTE.STYRINGS_PEKER   = ADDR(KOM_OMR.STYRINGS_OMR);          
    IO_PEKER                             = ADDR (IO_AREA);                      
    ATK_KOM_PTR           = ADDR  (KOM_OMR.ATK_COM_OMR  ) ;                     
                                                                                
                                                                                
 IF HENT_FRAM_MAP  THEN                                                         
   DO;                                                                          
     ALLOCATE S001230O;                                                         
     S001230O.CICS_INFOO = DIV_PARAM_OMR.CICS_NAVN;                             
     S001230O.ROLLEO     = DIV_PARAM_OMR.ATK_ROLLE;                             
     EXEC CICS SEND MAP('S001230') MAPSET('S001233')                            
                                              ERASE;                            
     HENT_FRAM_MAP = '1'B;                                                      
   END;                                                                         
                                                                                
 RECEIVE_MAP:                                                                   
                                                                                
                                                                                
 IF ^HENT_FRAM_MAP THEN                                                         
    DO;                                                                         
       EXEC CICS RECEIVE MAP('S001230')                                         
                 MAPSET('S001233') SET(BMSMAPBR);                               
       FEIL_MELD_NR = 0;                                                        
    END;                                                                        
                                                                                
 IF FUNKSJONKODEL > 0 THEN                                                      
    DO;                                                                         
       FUNKSJONSKODE = FUNKSJONKODEI;                                           
       IF FUNKSJONSKODE ^= 'D'  THEN                                            
             FRA_CICS = '1'B;                                                   
                                                                                
             TRANSKODE = 'R031';                                                
             EXEC CICS XCTL PROGRAM('R0010301') COMMAREA(KOM_OMR);              
                                                                                
    END;                                                                        
                                                                                
 IF STYREKODE  ^= 'FI'  &  FRA_CICS THEN                                        
    TRANSKODE = 'R031';                                                         
                                                                                
 ELSE                                                                           
  DO;                                                                           
                                                                                
   ANT_FEIL_SKREVET = 0;                                                        
   FEIL_FUNNET      = '0'B;                                                     
   FEIL_MELD_NR     = 0;                                                        
   TRANS_RETURKODE  = TRANSKODE;                                                
                                                                                
   SELECT (TRANSKODE);                                                          
      WHEN('R230')                                                              
        DO;                                                                     
            CALL BLANK_S1_SØKER;                                                
            CALL BLANK_S1_MELDNR;                                               
            CALL OVERFØR_S1_SØKER;                                              
            CALL KONTROLL_S1_SØKER(FEIL_FUNNET);                                
        END;                                                                    
     WHEN('R231')                                                               
        DO;                                                                     
            CALL BLANK_S1_MELDNR;                                               
            CALL OVERFØR_S1_SØKER;                                              
            CALL KONTROLL_S1_SØKER(FEIL_FUNNET);                                
        END;                                                                    
    OTHERWISE;                                                                  
        END;                                                                    
                                                                                
   IF ^FEIL_FUNNET THEN                                                         
       DO;                                                                      
         UFT_KODE = ' ';                                                        
         UFT_BLK_KODE = ' ';                                                    
         CALL OPPDATE_DATABASE;                                                 
         IF UFT_KODE = ' '  !                                                   
            UFT_BLK_KODE = ' '  THEN                                            
            DO;                                                                 
                FEIL_FUNNET        = '1'B;                                      
                EXEC CICS SYNCPOINT  ROLLBACK;                                  
                IF UFT_KODE = ' ' &                                             
                   UFT_KODE = ' ' THEN                                          
                   W_MELD      =                                                
                      ' FEIL UFØRETIDSPUNKT. ';                                 
                ELSE                                                            
                IF UFT_KODE = ' ' THEN                                          
                     W_MELD      =                                              
                    ' FEIL UFØRETIDSPUNKT. SEGMENT FINNES IKKE. ';              
                ELSE                                                            
                IF UFT_BLK_KODE = ' ' THEN                                      
                           W_MELD      =                                        
                     ' FEIL UFØRETIDSPUNKT. U3 BLANKETT FINNES IKKE';           
                                                                                
            END;                                                                
                                                                                
         IF ^FEIL_FUNNET THEN                                                   
             DO;                                                                
               CALL BLANK_S1_MAP;                                               
               TRANSKODE        = 'R230';                                       
               HENT_FRAM_MAP = '0'B;                                            
               S001230I.FNRL          = CURSOR_POS;                             
               S001230O.CICS_INFOO = DIV_PARAM_OMR.CICS_NAVN;                   
               S001230O.ROLLEO     = DIV_PARAM_OMR.ATK_ROLLE;                   
               EXEC CICS SEND MAP('S001230') MAPSET('S001233')                  
                                   ERASE  CURSOR;                               
            END;                                                                
     END;                                                                       
                                                                                
                                                                                
   IF FEIL_FUNNET THEN                                                          
      DO;                                                                       
         IF TRANSKODE = 'R230' THEN                                             
            TRANSKODE = 'R231';                                                 
         CALL BLANK_S1_MAP;                                                     
         CALL OVERFØR_S1SØKER_MAP;                                              
                                                                                
         S001230O.CICS_INFOO = DIV_PARAM_OMR.CICS_NAVN;                         
         S001230O.ROLLEO     = DIV_PARAM_OMR.ATK_ROLLE;                         
         S001230I.FNRL          = CURSOR_POS;                                   
                                                                                
         EXEC CICS SEND MAP('S001230') MAPSET('S001233')                        
                                   ERASE    CURSOR;                             
         FEIL_MELD_NR  = 666;/* => FEIL_FUNNET ER SATT. */                      
                                                                                
         HENT_FRAM_MAP = '0'B;                                                  
         GO TO RECEIVE_MAP;                                                     
     END;                                                                       
  END;                                                                          
                                                                                
                                                                                
      EXEC CICS RETURN TRANSID(TRANSKODE) COMMAREA(KOM_OMR);                    
                                                                                
  FEILBEH:                                                                      
                                                                                
     EXEC CICS HANDLE CONDITION ERROR(ABEND);                                   
                                                                                
       S001230O.CICS_INFOO = DIV_PARAM_OMR.CICS_NAVN;                           
       S001230O.ROLLEO     = DIV_PARAM_OMR.ATK_ROLLE;                           
     IF FEIL_MELD_NR    =   0  THEN /*AB 10.09.01 */                            
        PROGRAM_ID = 'R0012301';                                                
                                                                                
     S001230O.MELDING2O =                                                       
              'F E I L  H A R  O P P S T Å T T ! ! !.';                         
                                                                                
     S001230O.MELDING3O =                                                       
              'EIBRCODE : ' !! UNSPEC(EIBRCODE);                                
                                                                                
     S001230O.MELDING4O =                                                       
       'EIBFN    : ' !! UNSPEC(EIBFN) !! '. FEILKODE : ' !! FEILKODE !!         
                                            '. DATASETT : ' !! DSNAVN;          
                                                                                
     S001230O.MELDING5O =                                                       
                   'PROGRAMNAVN :  ' !! PROGRAM_ID    !!                        
                   '.INGEN OPPDATERING ER BLITT UTFØRT.';                       
                                                                                
                                                                                
     EXEC CICS SEND MAP('S001230')                                              
                                                 MAPSET('S001233');             
                                                                                
     EXEC CICS RECEIVE MAP('S001230')                                           
                                    MAPSET('S001233') SET(BMSMAPBR);            
                                                                                
                                                                                
     EXEC CICS SYNCPOINT ROLLBACK;                                              
                                                                                
                                                                                
     TERMINERINGS_IND = 'F';                                                    
                                                                                
     TRANSKODE  = 'R041';                                                       
                                                                                
      EXEC CICS RETURN;                                                         
                                                                                
  ABEND:                                                                        
     EXEC CICS ABEND ABCODE(FEILKODE);                                          
                                                                                
     TRANSKODE = 'R041';                                                        
      EXEC CICS RETURN;                                                         
                                                                                
 /* ***************************************************************** */        
 BLANK_S1_SØKER:                                                                
   PROC OPTIONS(REENTRANT);                                                     
      S1.FNR          = 0;                                                      
      S1.UFT_ÅMD      = 0;                                                      
      S1.FRIINNTEKT_DATO_ÅMD = 0;                                               
   END BLANK_S1_SØKER;                                                          
                                                                                
 BLANK_S1_MELDNR:                                                               
   PROC;                                                                        
      S1.FNRNR          = 0;                                                    
      S1.UFT_ÅMDNR     = 0;                                                     
      S1.FRIINNTEKT_DATO_ÅMDNR = 0;                                             
   END BLANK_S1_MELDNR;                                                         
                                                                                
                                                                                
 KONTROLL_S1_SØKER:   PROC (FEIL_S);                                            
 DCL FEIL_S             BIT (1);                                                
   DCL                                                                          
      KEY_BIT                    BIT(32) BASED (KEY_PEKER),                     
      KEY_PEKER                  POINTER,                                       
      TK_RECL                    CHAR (101);                                    
 %SKIP;                                                                         
   DCL                                                                          
      1 FNR_REG,                                                                
        2 FNR1                                  FIXED DEC(11),                  
        2 FNR2                                  FIXED DEC(11),                  
        2 BRUKERID                              CHAR     ( 4),                  
                                                                                
      ALDER                                     FIXED DEC (7);                  
 %SKIP;                                                                         
   DCL                                                                          
      W_FNR                                     PIC'(11)9',                     
      W_UFT_MMÅÅ                                PIC'(04)9',                     
      W_FRIINNTEKT_DATO_ÅMD                     PIC'(8)9';                      
                                                                                
                                                                                
                                                                                
   W_FNR     = S1.FNR;                                                          
   W_UFT_MMÅÅ = S1.UFT_ÅMD;                                                     
   W_FRIINNTEKT_DATO_ÅMD = S1.FRIINNTEKT_DATO_ÅMD;                              
                                                                                
                                                                                
    IF ^F_NUMERISK(S1.FNR) THEN                                                 
       DO;                                                                      
          FEIL_S = '1'B;                                                        
          S1.FNRNR    = 200;                                                    
       END;                                                                     
    ELSE                                                                        
       IF ^F_GYLDIG_FNR(S1.FNR) THEN                                            
          DO;                                                                   
             FEIL_S = '1'B;                                                     
             S1.FNRNR    = 1;                                                   
          END;                                                                  
       ELSE                                                                     
          DO;                                                                   
             FNR_REG.BRUKERID =   DIV_PARAM_OMR.CICS_IND;                       
             DIV_PARAM_OMR.FNR_SØKER = S1.FNR;                                  
             FNR_REG.FNR1     =   S1.FNR;                                       
                                                                                
             EXEC CICS LINK PROGRAM('R0019906') COMMAREA(FNR_REG);              
                                                                                
             IF FNR_REG.FNR2 > 0 THEN                                           
                DO;                                                             
                   S1.FNRNR    = 203;                                           
                   S1.FNR_GML = S1.FNR;                                         
                   S1.FNR      = FNR_REG.FNR2;                                  
                   DIV_PARAM_OMR.FNR_SØKER = S1.FNR;                            
                END;                                                            
          END;                                                                  
    /* -------------------------------------------------------------- */        
    /* 1.19 FRIINNTEKT FRA OG MED DATO                                */        
    /* -------------------------------------------------------------- */        
                                                                                
    /*  KONTROLL UFØRETIDSPUNKT                                  */             
       IF ^F_NUMERISK((S1.UFT_ÅMD)) THEN                                        
           DO;  /* IKKE NUMMERISK */                                            
               FEIL_FUNNET        = '1'B;                                       
               S1.UFT_ÅMDNR = 200;                                              
           END; /* IKKE NUMMERISK */                                            
         ELSE                                                                   
       IF ^F_GYLDIG_DATO(S1.UFT_ÅMD) THEN                                       
           DO;  /* UGYLDIG DATO */                                              
              FEIL_FUNNET        = '1'B;                                        
              S1.UFT_ÅMDNR = 9;                                                 
           END; /* UGYLDIG DATO */                                              
                                                                                
    /* JFA KONTROLL PÅ VIRK - NY REGEL FOR FRIINNTEKT FRA 01.01.2004*/          
    IF (S1.FRIINNTEKT_DATO_ÅMD > 0) THEN                                        
       DO;  /* FRIINTEKT ER FYLT UT */                                          
           IF W_FRIINNTEKT_DATO_ÅMD   > 20040100  THEN                          
              DO;                                                               
                 IF ^F_NUMERISK((S1.FRIINNTEKT_DATO_ÅMD)) THEN                  
                     DO;  /* IKKE NUMMERISK */                                  
                        FEIL_FUNNET        = '1'B;                              
                        S1.FRIINNTEKT_DATO_ÅMDNR = 200;                         
                     END; /* IKKE NUMMERISK */                                  
                ELSE                                                            
                 IF ^F_GYLDIG_DATO(S1.FRIINNTEKT_DATO_ÅMD) THEN                 
                     DO;  /* UGYLDIG DATO */                                    
                        FEIL_FUNNET        = '1'B;                              
                        S1.FRIINNTEKT_DATO_ÅMDNR = 9;                           
                     END; /* UGYLDIG DATO */                                    
              END;                                                              
           ELSE                                                                 
              DO;                                                               
                  FEIL_FUNNET        = '1'B;                                    
                  S1.FRIINNTEKT_DATO_ÅMDNR = 642;                               
              END;                                                              
                                                                                
     END; /* FRIINTEKT ER FYLT UT */                                            
   END KONTROLL_S1_SØKER;                                                       
                                                                                
                                                                                
 OVERFØR_S1SØKER_MAP:                                                           
   PROC;                                                                        
   DCL                                                                          
      NORMAL  CHAR (1) INIT(' '),                                               
      NOR_NUM CHAR (1) INIT('&'),                                               
      BRI_NUM CHAR (1) INIT('Q');                                               
                                                                                
      S001230O.FNRO          = S1.FNR ;                                         
      S001230O.UFT_MMÅÅO =                                                      
                    KONV_HÅMD_MÅ(S1.UFT_ÅMD );                                  
      S001230O.FRIINNTEKT_DATO_DMÅO =                                           
                    KONV_HÅMD_DMÅ(S1.FRIINNTEKT_DATO_ÅMD);                      
 %SKIP(2);                                                                      
      S001230O.DUMMYA = '_';                                                    
                                                                                
                                                                                
       IF ^ FRA_CICS THEN                                                       
          DO;                                                                   
            IF FEIL_MELD_NR > 0 THEN                                            
               CALL SKRIV_FEIL(FEIL_MELD_NR);                                   
             FEIL_MELD_NR = 0;                                                  
          END;                                                                  
                                                                                
                                                                                
      IF S1.FNRNR = 0 THEN                                                      
         S001230O.FNRA = NOR_NUM;                                               
      ELSE                                                                      
         DO;                                                                    
            S001230O.FNRA = BRI_NUM;                                            
            S001230I.FNRL = CURSOR_POS;                                         
            IF S1.FNRNR ^= 999 THEN                                             
               CALL SKRIV_FEIL(S1.FNRNR);                                       
         END;                                                                   
                                                                                
                                                                                
      IF S1.UFT_ÅMDNR = 0 THEN                                                  
         S001230O.UFT_MMÅÅA = NOR_NUM;                                          
      ELSE                                                                      
         DO;                                                                    
            S001230O.UFT_MMÅÅA = BRI_NUM;                                       
            S001230I.UFT_MMÅÅL = CURSOR_POS;                                    
            IF S1.UFT_ÅMDNR ^= 999 THEN                                         
               CALL SKRIV_FEIL(S1.UFT_ÅMDNR);                                   
         END;                                                                   
                                                                                
      IF S1.FRIINNTEKT_DATO_ÅMDNR = 0 THEN                                      
         S001230O.FRIINNTEKT_DATO_DMÅA = NOR_NUM;                               
      ELSE                                                                      
         DO;                                                                    
            S001230O.FRIINNTEKT_DATO_DMÅA = BRI_NUM;                            
            S001230I.FRIINNTEKT_DATO_DMÅL = CURSOR_POS;                         
            IF S1.FRIINNTEKT_DATO_ÅMDNR ^= 999 THEN                             
               CALL SKRIV_FEIL(S1.FRIINNTEKT_DATO_ÅMDNR);                       
         END;                                                                   
                                                                                
      IF FRA_UTEN_DIALOG THEN                                                   
        DO;                                                                     
          S001230O.FNRA          = DFHBMASK;                                    
          S001230O.UFT_MMÅÅA = DFHBMASK;                                        
          S001230O.FRIINNTEKT_DATO_DMÅA = DFHBMASK;                             
        END;                                                                    
                                                                                
                                                                                
   END OVERFØR_S1SØKER_MAP;                                                     
                                       /*    */                                 
                                                                                
 BLANK_S1_MAP:                                                                  
   PROC;                                                                        
    DCL                                                                         
      LOW BUILTIN;                                                              
      FNRO                 = LOW(11);                                           
      UFT_MMÅÅO            = LOW(04);                                           
      FRIINNTEKT_DATO_DMÅO = LOW(2);                                            
      MELDING1O            = (78)' ';                                           
      MELDING2O            = (78)' ';                                           
      MELDING3O            = (78)' ';                                           
      MELDING4O            = (78)' ';                                           
      MELDING5O            = (78)' ';                                           
      MELDING1O            = W_MELD;                                            
      W_MELD               = (78)' ';                                           
   END BLANK_S1_MAP;                                                            
                                                                                
 OVERFØR_S1_SØKER:                                                              
   PROC;                                                                        
      IF FNRL > 0 THEN                                                          
         S1.FNR          = FNRI ;                                               
                                                                                
      IF UFT_MMÅÅL > 0 THEN                                                     
         S1.UFT_ÅMD      =                                                      
                 KONV_MÅ_HÅMD( UFT_MMÅÅI);                                      
                                                                                
      IF FRIINNTEKT_DATO_DMÅL > 0 THEN                                          
         S1.FRIINNTEKT_DATO_ÅMD =                                               
                             KONV_DMÅ_HÅMD( FRIINNTEKT_DATO_DMÅI);              
                                                                                
   END OVERFØR_S1_SØKER;                                                        
                                                                                
                                                                                
  OPPDATE_DATABASE: PROC;                                                       
                                                                                
   CALL DATABASE('OPEN');                                                       
                                                                                
   W02_GET = W02_GU;                                                            
   CALL P_LES_DBD_GU;                                                           
   BO_TKNR  = RF0PERSN.TKNR;                                                    
                                                                                
   /*                                                                           
   IF ^FEIL_FUNNET                   THEN                                       
       CALL ATK_TILGANG_KONTROLL;                                               
    */                                                                          
   W02_GET = W02_GHNP;                                                          
                                                                                
   IF ^FEIL_FUNNET   THEN                                                       
      DO;                                                                       
          DO WHILE (^END_OF_ROOT & ^FEIL_FUNNET);                               
           SELECT (RF1.SEGM_NAVN);                                              
             WHEN ('RF0PERSN')                                                  
                DO;                                                             
                  BLKTKODE = 'J';                                               
                  AUS.TKNR = RF0PERSN.TKNR;                                     
                  IF S1.FNR ^= RF0PERSN.FNR THEN                                
                    END_OF_ROOT = '1'B;                                         
                END;                                                            
              WHEN ('STATUS  ')                                                 
                DO;                                                             
                  IF STATUS.STATUS_KODE = 'S' THEN                              
                   DO;                                                          
                      SKODE = 'J';                                              
                      CALL KONTROLL_VIRK_DATO;                                  
                      IF ^FEIL_FUNNET   THEN                                    
                         DO;                                                    
                             STATUS.FRIINNTEKT_DATO_ÅMD =                       
                                             S1.FRIINNTEKT_DATO_ÅMD ;           
                             CALL REP_SEGMENT;                                  
                         END;                                                   
                   END;                                                         
                 ELSE                                                           
                   SKODE = ' ';                                                 
                END;                                                            
                                                                                
              WHEN ('UFØRHIST')                                                 
                DO;                                                             
                   IF SKODE = 'J' THEN                                          
                      DO;                                                       
                         IF (UFØRHIST.UFT_ÅMD/100) =                            
                                       (S1.UFT_ÅMD/100)   THEN                  
                            DO;                                                 
                                UFØRHIST.FRIINNTEKT_FRA_HIST_ÅMD =              
                                            S1.FRIINNTEKT_DATO_ÅMD ;            
                                CALL REP_SEGMENT;                               
                                SKODE = ' ';                                    
                                UFT_KODE = 'J';                                 
                            END;                                                
                   END;                                                         
                                                                                
                END;                                                            
                                                                                
        WHEN ('GRUNNBU3')                                                       
          DO;                                                                   
             IF BLKTKODE = 'J'  &                                               
                (GRUNNBU3.UFT_ÅMD / 100)  =                                     
                            (S1.UFT_ÅMD / 100)   THEN                           
                 DO;                                                            
                     GRUNNBU3.FRIINNTEKT_DATO_ÅMD =                             
                                      S1.FRIINNTEKT_DATO_ÅMD  ;                 
                     CALL REP_SEGMENT;                                          
                     BLKTKODE = ' ';                                            
                     UFT_BLK_KODE = 'J';                                        
                 END;                                                           
                                                                                
          END;                                                                  
        OTHERWISE;                                                              
          END;                                  /*  END SELECT     */           
                                                                                
       CALL P_LES_DBD_GHNP;                                                     
                                                                                
      END;                                  /*  END DO WHILE    */              
   END;                                         /*  END IF          */          
                                                                                
 /*    **************************************************  */                   
   CALL DATABASE('CLOSE');                                                      
   STYREKODE   = 'AU';                                                          
                                                                                
   CALL  BERGEN_PEN('N');                                                       
     IF ^FEIL_FUNNET THEN                                                       
         CALL  BERGEN_PEN('J');                                                 
                                                                                
   STYREKODE   = 'FI';                                                          
  END OPPDATE_DATABASE;                                                         
                                                                                
  /* *********************************************************** */             
  /* LES ROT MED KVALIFI-FNR                                     */             
  /* *********************************************************** */             
  P_LES_DBD_GU: PROC;                                                           
                                                                                
   SSA1_RF0PERSN.PKEY  = S1.FNR;                                                
   CALL PLITDLI                         (W02_PARM_CT_4,                         
                                         W02_GET,                               
                                         RF1,                                   
                                         IO_AREA,                               
                                         SSA1_RF0PERSN);                        
                                                                                
   W_STATUS = RF1.STATUS_KODE;                                                  
   IF  UIBFCTR                    =     '00000000'B   THEN                      
       DO;                                                                      
          SELECT ( RF1.STATUS_KODE );                                           
                                                                                
            WHEN ( '  ' )                                                       
              DO;                                                               
                 END_OF_ROOT = '0'B;                                            
              END;                                                              
            OTHERWISE                                                           
              DO;                                                               
               END_OF_ROOT = '1'B;                                              
               FEIL_FUNNET = '1'B;                                              
               S1.FNRNR =  0501 ;                                               
              END;                                                              
           END;                                                                 
       END;                                                                     
      ELSE                                                                      
        DO;                                                                     
               END_OF_ROOT = '1'B;                                              
          /* ************************************************** */              
          /*   UIB'S RETUR-CODE ER UAKSEPTABEL                  */              
          /* ************************************************** */              
                                                                                
             MAP_MELDING = 'NOE ER GALT MED DATABASEN PÅ *GU*      '            
                      !!'FRIINNTEKTER,:' !! UNSPEC(UIBFCTR) !! ' '              
                                           !! UNSPEC(UIBDLTR);                  
                                 FEIL_FUNNET = '1'B;                            
        END;                                                                    
                                                                                
                                                                                
  END P_LES_DBD_GU;                                                             
                                                                                
  /* *********************************************************** */             
  /* LES ALLE SEGMENT UNDER ROOT                                 */             
  /* *********************************************************** */             
  P_LES_DBD_GHNP: PROC;                                                         
                                                                                
   CALL PLITDLI                         (W02_PARM_CT_3,                         
                                         W02_GET,                               
                                         RF1,                                   
                                         IO_AREA);                              
                                                                                
   W_STATUS = RF1.STATUS_KODE;                                                  
   IF  UIBFCTR                    =     '00000000'B   THEN                      
       DO;                                                                      
          SELECT ( RF1.STATUS_KODE );                                           
                                                                                
            WHEN ( '  ' , 'GK', 'GA')                                           
              DO;                                                               
                 END_OF_ROOT = '0'B;                                            
              END;                                                              
            WHEN ( 'GE','GB')                                                   
              DO;                                                               
                 END_OF_ROOT = '1'B;                                            
              END;                                                              
            OTHERWISE                                                           
              DO;                                                               
               END_OF_ROOT = '1'B;                                              
               FEIL_FUNNET = '1'B;                                              
               S1.FNRNR =  0501 ;                                               
              END;                                                              
           END;                                                                 
       END;                                                                     
      ELSE                                                                      
        DO;                                                                     
               END_OF_ROOT = '1'B;                                              
          /* ************************************************** */              
          /*   UIB'S RETUR-CODE ER UAKSEPTABEL                  */              
          /* ************************************************** */              
                                                                                
             MAP_MELDING = 'NOE ER GALT MED DATABASEN PÅ GHNP      '            
                      !!'FRIINNTEKTER,:' !! UNSPEC(UIBFCTR) !! ' '              
                                           !! UNSPEC(UIBDLTR);                  
                                 FEIL_FUNNET = '1'B;                            
        END;                                                                    
                                                                                
                                                                                
  END P_LES_DBD_GHNP;                                                           
                                                                                
  /* ******************************************************** */                
  /* REPLACE SISTE LES SEGMENT I DBD                          */                
  /* ******************************************************** */                
                                                                                
  REP_SEGMENT: PROC;                                                            
                                                                                
   CALL PLITDLI                         (W02_PARM_CT_3,                         
                                         W02_REPL,                              
                                         RF1,                                   
                                         IO_AREA);                              
                                                                                
                                                                                
   W_STATUS = RF1.STATUS_KODE;                                                  
   IF  UIBFCTR                    =     '00000000'B   THEN                      
       DO;                                                                      
          SELECT ( RF1.STATUS_KODE );                                           
                                                                                
            WHEN ( '  ' )                                                       
              DO;                                                               
                 END_OF_ROOT = '0'B;                                            
              END;                                                              
            OTHERWISE                                                           
              DO;                                                               
               END_OF_ROOT = '1'B;                                              
               FEIL_FUNNET = '1'B;                                              
               S1.FNRNR =  0501 ;                                               
              END;                                                              
           END;                                                                 
       END;                                                                     
      ELSE                                                                      
        DO;                                                                     
               END_OF_ROOT = '1'B;                                              
          /* ************************************************** */              
          /*   UIB'S RETUR-CODE ER UAKSEPTABEL                  */              
          /* ************************************************** */              
                                                                                
             MAP_MELDING = 'NOE ER GALT MED DATABASEN PÅ  *REPL*   '            
                      !!'FRIINNTEKTER,:' !! UNSPEC(UIBFCTR) !! ' '              
                                           !! UNSPEC(UIBDLTR);                  
                                 FEIL_FUNNET = '1'B;                            
        END;                                                                    
                                                                                
                                                                                
  END REP_SEGMENT;                                                              
                                                                                
                                                                                
                                                                                
    /* -------------------------------------------------------------- */        
                                                                                
    DATABASE:PROC(STYRING);                                                     
   DCL                                                                          
     STYRING                               CHAR(5),                             
     NULL                                  BUILTIN,                             
     UNSPEC                                BUILTIN;                             
     DCL                                                                        
       W01_PCB_FUNCTION            CHAR (4)       INIT('PCB ');                 
     DCL                                                                        
       W01_TERM_FUNCTION           CHAR (4)       INIT('TERM');                 
                                                                                
     IF STYRING = 'OPEN' THEN                                                   
        DO;                                                                     
           CALL   PLITDLI                        (W02_PARM_CT_3,                
                                                  W01_PCB_FUNCTION,             
                                                  W01_PSB_NAVN,                 
                                                  UIBPTR);                      
                                                                                
           IF DLIUIB.UIBFCTR                    =    '00000000'B THEN           
             DO;                                                                
               ALLOCATE PCB_UIB_PEKER_OMR;                                      
               PCB_UIB_PEKER_OMR.PCB_PEKER      =    UIBPCBAL;                  
               PCB_UIB_PEKER_OMR.UIB_PEKER      =    UIBPTR;                    
             END;                                                               
           ELSE                                                                 
             DO;                                                                
               MAP_MELDING =                                                    
               'NOE ER GALT MED DATABASEN, KODE: ' !!                           
               UNSPEC(UIBFCTR) !! ' ' !! UNSPEC(UIBDLTR);                       
             END;                                                               
        END;                                                                    
     ELSE                                                                       
        DO;                                                                     
           CALL   PLITDLI                        (W02_PARM_CT_1,                
                                                  W01_TERM_FUNCTION);           
           KOM_OMR.PCB_UIB_PEKER = NULL;                                        
        END;                                                                    
                                                                                
 END DATABASE;                                                                  
 /* *************************************************************   */          
 /*  KONTROLL AV REG-DATO MOT VIRK-DATO                             */          
 /* *************************************************************   */          
 KONTROLL_VIRK_DATO:  PROC;                                                     
                                                                                
      DCL W_FRIINNTEKT_DATO_ÅM   PIC '(6)9';                                    
      DCL W_VIRK_DATO_ÅM_MIN     PIC '(6)9';                                    
      DCL W_VIRK_DATO_ÅM_MAX     PIC '(6)9';                                    
      DCL W_VIRK_DATO_ÅM         PIC '(6)9';                                    
                                                                                
       W_VIRK_DATO_ÅM         = STATUS.VIRK_DATO_ÅMD /100;                      
       W_FRIINNTEKT_DATO_ÅM   = S1.FRIINNTEKT_DATO_ÅMD/100;                     
       W_VIRK_DATO_ÅM_MIN     = (STATUS.VIRK_DATO_ÅMD /100) + 100;              
       W_VIRK_DATO_ÅM_MAX     = (STATUS.VIRK_DATO_ÅMD /100) + 500;              
                                                                                
       IF W_VIRK_DATO_ÅM < 200401 THEN                                          
         DO;                                                                    
             FEIL_FUNNET        = '1'B;                                         
             S1.FRIINNTEKT_DATO_ÅMDNR = 642;                                    
         END;                                                                   
   /* SKAL IKKE HA MED DENNE TESTEN LIKEVEL... TRUDE 14.04.05 */                
   /* IF W_FRIINNTEKT_DATO_ÅM   < W_VIRK_DATO_ÅM_MIN  THEN                      
   /*    W_FRIINNTEKT_DATO_ÅM   > W_VIRK_DATO_ÅM_MAX  THEN */                   
   /*    DO;                                                                    
             FEIL_FUNNET        = '1'B;                                         
             S1.FRIINNTEKT_DATO_ÅMDNR = 643;                                    
         END;               TRUDE HIT */                                        
 END  KONTROLL_VIRK_DATO;                                                       
 BERGEN_PEN: PROC(KODE);                                                        
  DCL KODE       CHAR(1);                                                       
                                                                                
     AUS.FNR  = S1.FNR ;                                                        
     AUS.STBREV = KODE ;                                                        
     AUS.TRANSTYPE  = 10 ;                                                      
     TRANS_OPPL_OMR.FØDSNUMMER  = S1.FNR ;                                      
     KJØRINGS_TYPE= 'O' ;                                                       
     BLANKETTYPE = STYREKODE;                                                   
     TRANSKODE = 'R201'               ;                                         
                                                                                
     EXEC CICS LINK PROGRAM('R0012002') COMMAREA(KOM_OMR);                      
     IF FEIL_MELD_NR > 0        THEN                                            
       DO;                                                                      
         IF FEIL_MELD_NR > 650      &                                           
            FEIL_MELD_NR < 660       THEN                                       
           DO;                                                                  
              FEIL_FUNNET        = '1'B;                                        
               W_MELD      =   FEIL_MELD_NR !!                                  
                        ' FEIL TILGANG KONTROLL      ; PROG: ' !!               
                               PROGRAM_ID  ;                                    
          END;                                                                  
         ELSE                                                                   
            DO;                                                                 
               FEIL_FUNNET        = '1'B;                                       
               W_MELD      = FEIL_MELD_NR !!                                    
                           ' FEIL I BEREGNING AV PENSJON; PROG: ' !!            
                             PROGRAM_ID  ;                                      
            END;                                                                
        END;                                                                    
                                                                                
                                                                                
 END BERGEN_PEN;                                                                
    /* -------------------------------------------------------------- */        
                                                                                
    %INCLUDE R0019901; /* F_GYLDE_DATO                                */        
    %INCLUDE R0019904; /* FØDSELSNUMMERKONTROLL                       */        
    %INCLUDE R0019910; /* NUMERISK KONTROLL                           */        
 /* %INCLUDE R0019911; ** DATO KONTROLL                               */        
    %INCLUDE R0019985; /* KONV-DMÅ-HÅMD                               */        
    %INCLUDE R0019984; /* KONV-MÅ-HÅMD                               */         
    %INCLUDE R0019989; /* KONV-HÅMD-Må                               */         
    %INCLUDE R0019990; /* KONV-HÅMD-DMÅ                               */        
 /* %INCLUDE R0019912     KONVERTERING CHAR ==> PIC                   */        
    %INCLUDE R0019944; /* SKRIV_FEIL                                  */        
 /* %INCLUDE R0019945;  * ATK_TILLGANG_KONTROLL                       */        
    %INCLUDE R0019999; /* ACF2 KONTROLL                               */        
    /* -------------------------------------------------------------- */        
 END R001230;                                                                   
