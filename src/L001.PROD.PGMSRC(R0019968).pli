 /*   SIST ENDRET PÅ PROD   2008.05.31 11.11.16 AV   SPA2990          */        
 /*       SIST ENDRET 07/05-98 14.13.29 AV   JDA7339                  */        
 /*       SIST ENDRET 05/05-98 08.48.37 AV   JDA7339                  */        
 /*       SIST ENDRET 05/06-92 12.13.00 AV   DYBVIK                   */        
 /* ***************************************************************** */        
 /* IDENTIFIKASJON                                                    */        
 /*     R00199   - RULL_FORSI                                         */        
 /*                FUNKSJON I PLI.                                    */        
 /*     PROGRAMMERER: TRUDE APRIL 1992                                */        
 /* HENSIKT                                                           */        
 /*     SE FORKLARING NEDENFOR.                                       */        
 /* PROGRAMTILKNYTNING                                                */        
 /*     INCLUDE MODUL.                                                */        
 /* ***************************************************************** */        
 /* ***************************************************************** */        
 /* JUSTERING AV FORSI FRA EN DATO TIL EN ANNEN.                      */        
 /*        MODULEN BLIR KALLT OPP MED FRADATO, TILDATO OG             */        
 /*           FORVENTET INNTEKT.                                      */        
 /*        RETURNERER INNTEKT                                         */        
 /*                                                                   */        
 /* ***************************************************************** */        
 /*                                                                   */        
                                                                                
   F_RULL_FORSI: PROC(FRA_DATO,TIL_DATO,INNTEKT)                                
                    RETURNS(DEC FIXED(5));                                      
   %INCLUDE P0019925;          /***    G - BELØPSTABELL     ***/                
                                                                                
      DCL I                FIXED BIN (15),                                      
          FRA_DATO         FIXED DEC (9) UNAL,            /*2000*/              
          TIL_DATO         FIXED DEC (9) UNAL,            /*2000*/              
          INNTEKT          FIXED DEC (5) UNAL,                                  
          FIXED_DEC6       FIXED DEC (6) INIT(0);         /*2000*/              
      DCL FRA_DATO_ÅM      FIXED DEC (6) UNAL;            /*2000*/              
      DCL TIL_DATO_ÅM      FIXED DEC (6) UNAL;            /*2000*/              
                                                                                
       FRA_DATO_ÅM    = FRA_DATO / 100;                /*2000*/                 
       TIL_DATO_ÅM    = TIL_DATO / 100;                /*2000*/                 
                                                                                
       DO I = 1 TO 65 UNTIL(FIXED_DEC6 >= FRA_DATO_ÅM);   /*2000*/              
                                                                                
          FIXED_DEC6 = PERIODE_START_ÅMD(I)/100;          /*2000*/              
       END;                                                                     
                                                                                
       IF FRA_DATO > TIL_DATO THEN                                              
          DO;                                                                   
             IF FIXED_DEC6 ^= FRA_DATO_ÅM THEN           /*2000*/               
                DO;                                                             
                   I = I - 1;                                                   
                   FIXED_DEC6 = PERIODE_START_ÅMD(I)/100;/*2000*/               
                END;                                                            
                                                                                
                                                                                
             DO WHILE (FIXED_DEC6 > TIL_DATO_ÅM);        /*2000*/               
                                                                                
               INNTEKT = INNTEKT * GRUNNBELØP(I - 1)                            
                                      / GRUNNBELØP(I    );                      
                                                                                
                IF GRUNNBELØP(I - 1) ^= GRUNNBELØP(I) THEN                      
                   INNTEKT = INNTEKT + 1;                                       
                I = I - 1;                                                      
                FIXED_DEC6 = PERIODE_START_ÅMD(I)/100;    /*2000*/              
                                                                                
             END;                                                               
                                                                                
          END;                                                                  
                                                                                
       ELSE IF FRA_DATO < TIL_DATO THEN                                         
          DO;                                                                   
             IF FIXED_DEC6 = FRA_DATO_ÅM THEN             /*2000*/              
                DO;                                                             
                   I = I + 1;                                                   
                   FIXED_DEC6 = PERIODE_START_ÅMD(I)/100; /*2000*/              
                END;                                                            
                                                                                
             DO WHILE (FIXED_DEC6 <= TIL_DATO_ÅM);         /*2000*/             
                                                                                
                INNTEKT = INNTEKT * GRUNNBELØP(I )                              
                                      / GRUNNBELØP(I - 1);                      
                I = I + 1;                                                      
                FIXED_DEC6 = PERIODE_START_ÅMD(I)/100;     /*2000*/             
                                                                                
             END;                                                               
                                                                                
          END;                                                                  
  RETURN(INNTEKT);                                                              
    END F_RULL_FORSI;                                                           
