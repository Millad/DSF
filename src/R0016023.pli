 /*   SIST ENDRET PÅ PROD   2000.02.21 10.32.09 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   1999.11.25 10.58.25 AV   JDA7339          */        
 /*       SIST ENDRET 04/05-98 13.14.21 AV   JDA7339                  */        
 /*       SIST ENDRET 12/03-98 10.33.55 AV   SLA9300                  */        
 /*       SIST ENDRET 28/07-97 13.07.43 AV   HLA7339                  */        
 /*       SIST ENDRET 04/04-97 10.39.33 AV   JDA7339                  */        
 /*       SIST ENDRET 10/06-96 13.49.50 AV   TSB0310                  */        
 /*       SIST ENDRET 04/11-93 14.24.46 AV   DYBVIK                   */        
 /*       SIST ENDRET 28/05-93 08.28.12 AV   DYBVIK                   */        
 /*       SIST ENDRET 17/07-92 13.47.19 AV   DYBVIK                   */        
 /*       SIST ENDRET 23/03-92 10.26.28 AV   DYBVIK                   */        
 /*       SIST ENDRET 19/02-92 14.59.32 AV   DYBVIK                   */        
 /*       SIST ENDRET 17/12-91 08.52.09 AV   TRUDE                    */        
 /*       SIST ENDRET 13/12-91 16.16.40 AV   HERMAN                   */        
 /*       SIST ENDRET 26/04-91 11.43.04 AV   DYBVIK                   */        
 /*       SIST ENDRET 25/03-91 15.01.09 AV   HERMAN                   */        
 /*       SIST ENDRET 14/03-91 08.02.16 AV   DYBVIK                   */        
 /*       SIST ENDRET 13/03-91 09.15.45 AV   DYBVIK                   */        
 /*       SIST ENDRET 03/01-91 09.15.45 AV   DYBVIK                   */        
 /*       SIST ENDRET 14/12-88 13.22.20 AV   DYBVIK                   */        
 /*       SIST ENDRET 28/10-88 08.33.06 AV   DYBVIK                   */        
 /*       SIST ENDRET 18/08-87 10.01.00 AV   DYBVIK                   */        
 /*       SIST ENDRET 23/06-87 12.00.31 AV   LUNDEBY                  */        
 /*       SIST ENDRET 16/06-87 13.40.48 AV   DYBVIK                   */        
 /*       SIST ENDRET 05/11-85 14.05.29 AV   LUNDEBY                  */        
 /*       SIST ENDRET 21/12-84 15.27.28 AV   TRY                      */        
 /*       SIST ENDRET 06/12-84 17.56.46 AV   BREIVIK                  */        
 /*       SIST ENDRET 19/11-84 16.59.47 AV   BREIVIK                  */        
 /*       SIST ENDRET 06/11-84 15.07.34 AV   BREIVIK                  */        
 /*********************************************************************/        
 /*                                                                   */        
 /* IDENTIFIKASJON:                                                   */        
 /*    R0016023                                                       */        
 /*                                                                   */        
 /*    PROGRAMMERER: P. BREIVIK, NOV 1982.                            */        
 /*                                                                   */        
 /* HENSIKT:                                                          */        
 /*    OPPDATERER ARBEIDS-OMRÅDET FOR STAT-TRANS-BASEN.               */        
 /*    ARBEIDS-OMRÅDET OPPDATERES BARE VED FORSKJELL MELLOM           */        
 /*    NEST-SISTE OG SISTE STATUS (B01/B02)                           */        
 /*    PROSEDYREN GJELDER BARE BARN (D.V.S. PERSON (3:14) )           */        
 /*                                                                   */        
 /* NB!!  NB!!:                                                       */        
 /*    VED UTREGNING AV FAKTORER AV G ): SUM-YTELSE * 12 / G , MÅ     */        
 /*                                      --------------------         */        
 /*    FØLGENDE PASSES PÅ: HVIS SUM-YTELSE ER ET REGNESTYKKE MED MER  */        
 /*    ENN 3 FELT, MÅ UTREGNINGEN DELES OPP OG HJELPEFELT BRUKES      */        
 /*    FORDI PL1'S ARBEIDSFELTER IKKE BLIR STORE NOK TIL SÅ MANGE     */        
 /*    REGNEOPERASJONER MED FELT DEFINERT SOM ANGJELDENDE ER.  BØ9104 */        
 /*                                                                   */        
 /* PROGRAMTILKNYTNING:                                               */        
 /*    UNDERPROGRAM TIL R0016001                                      */        
 /*                                                                   */        
 /* BRUK:                                                             */        
 /*    INKLUDERES UNDER KOMPILERING AV R0016001                       */        
 /*                                                                   */        
 /* ENDRET                                                            */        
 /*    NYTT FELT I UPPER : BUPÅR_RED  SVERRE LUNDEBY, DATA-UTVIKLING  */        
 /*                                                                   */        
 /* ENDRET : 10.06.96 AV S.LUNDEBY, DATA-UTVIKLING                    */        
 /*          VED UFT < 9200 HENTES BUP FRA POREKKE                    */        
 /*                                                                   */        
 /* ENDRET : 02.04.97 AV SVEIN B. OPSAHL                              */        
 /*          LAGT INN KALL TIL U3_OPPDAT                              */        
 /*                                                                   */        
 /* ENDRET :   .  .97 AV SVEIN B. OPSAHL (IKKE IMP.)                  */        
 /*          PENSJONS-SEGMENT SKRIVES OGSÅ HVIS BELØP I B01 OG B02    */        
 /*          ER 0                                                     */        
 /* ENDRET : UKE 47 1999 AV  SVERRE LUNDEBY, DATA-UTVIKLING AS       */         
 /*          SETTER RIKTIG KOMMNR. UT I FRA TKNR. BARE 0301 BLE TATT  */        
 /*          HENSYN TIL.                                              */        
 /* ENDRET : UKE 05 2000 AV  SVERRE LUNDEBY, DATA-UTVIKLING AS       */         
 /*          SAK 0013-2000 DAG I VIRKDATO > 00                        */        
 /* ENDRET: 01.04.2004 AV SEA2970    LAGT INN TEST FOR B02_TT_EGEN    */        
 /*********************************************************************/        
                                                                                
 OPPDAT_BARN: PROC OPTIONS (REENTRANT);                                         
                                                                                
 /*-------------------------------------------------------------------*/        
 /*                                                                   */        
 /*                        B A R N                                    */        
 /*                                                                   */        
 /* START OPPDATERING AV ARBEID-OMRÅDET 'STAT_TRANS' FOR BARN.        */        
 /* DET FINNES EN BOOLSK VARIABEL (BRYTER) FOR HVERT SEGMENT          */        
 /* SOM EVENTUELT SKAL DANNES. ETTER BEHANDLING AV HVERT BARN         */        
 /* (OPPDATERING AV ARBEIDS-OMRÅDET 'STAT_TRANS') KALLES EN           */        
 /* EGEN PROSEDYRE FOR SKRIVING AV DE AVMERKEDE SEGMENTENE.           */        
 /*                                                                   */        
 /*-------------------------------------------------------------------*/        
                                                                                
    DCL EBARN                BIT (1);                                           
                                                                                
 DO I=3 TO 14;                                                                  
    IF B02.PERSON(I).FNR > 0 THEN                                               
    DO;                                                                         
                                                                                
 /*-------------------------------------------------------------------*/        
 /*    INITIELT SKAL INGEN SEGMENTER GENERERES.                       */        
 /*    BRYTERNE NULL-STILLES OG ARBEIDS-OMRÅDET BLANKES.              */        
 /*-------------------------------------------------------------------*/        
                                                                                
       DIV_PARAM_OMR.POS_I_B01 = I;                                             
       SEGMENT_TABELL = '';                                                     
       TR0ROT         = '';                                                     
       REGTPKT        = '';                                                     
       PTYPE          = '';                                                     
       SIVILST        = '';                                                     
       APOPPL         = '';                                                     
       UPPER          = '';                                                     
       PENSJON        = '';                                                     
       KT             = '';                                                     
       VT             = '';                                                     
       ET             = '';                                                     
       BT             = '';                                                     
       TILKN          = '';                                                     
       ETTERL         = '';                                                     
       DIAGNOSE       = '';                                                     
       SPESOPPL       = '';                                                     
       SAVE_B01_B02   = 0;                                                      
       ALLOCATE X02;                                                            
       X02.PERSON(I)  = '';                                                     
                                                                                
 /* *-*-* NB!! REGTPKT.FILLER INNEHOLDER TRANSKODE + TRANSTYPE I TEST */        
 /* *-*-*                                                             */        
 /*BØ9204-TEST:                                                                 
          REGTPKT.FILLER             = TRANS_OPPL_OMR.TRANSKODE !!              
                                       TRANS_OPPL_OMR.TRANSTYPE ;               
   BØ9204-TEST-SLUTT                                                            
 */                                                                             
 /*-------------------------------------------------------------------*/        
 /*    TEST STATUS_KODE_HIST I B01/B02.                               */        
 /*    SAVE B01/B02 SÅ LENGE STATISTIKK-BEREGNING PÅGÅR.              */        
 /*-------------------------------------------------------------------*/        
                                                                                
       IF B02.PERSON(I).STATUS.DØDSDATO_ÅMD  = 0 THEN                           
       DO;                                                                      
                                                                                
         IF B02.PERSON(I).STATUS.STATUS_KODE_HIST = 'O' !                       
            B02.PERSON(I).STATUS.STATUS_KODE_HIST = 'X' THEN                    
           DO;                                                                  
             /* OPPHØR AV YTELSE */                                             
             ALLOCATE X01;                                                      
             SAVE_B01_B02 = 2;                                                  
             X01.PERSON(I) = B01.PERSON(I);                                     
             X02.PERSON(I) = B02.PERSON(I);                                     
             B01.PERSON(I) = B02.PERSON(I);                                     
             B02.PERSON(I) = '';                                                
             B02.PERSON(I).RF0PERSN.FNR = B01.PERSON(I).RF0PERSN.FNR;           
             B02.PERSON(I).RF0PERSN.TKNR = B01.PERSON(I).RF0PERSN.TKNR;         
             B02.PERSON(I).STATUS.VIRK_DATO_ÅMD =                               
                                    B01.PERSON(I).STATUS.VIRK_DATO_ÅMD;         
             B02.STATUS_KODE_HIST(I) =                                          
                                    B01.STATUS_KODE_HIST(I);                    
             B01.STATUS_KODE_HIST(I) = ' ';                                     
             IF B01.PERSON(I).STATUS.PENSJONSTYPE1 = 'N'  !                     
                B01.PERSON(I).STATUS.PENSJONSTYPE1 = 'B'  THEN                  
                SEGMENT_TABELL.PENSJON  = TRUE;                                 
           END;                                                                 
         ELSE                                                                   
           IF B01.PERSON(I).STATUS.STATUS_KODE_HIST = 'O' !                     
              B01.PERSON(I).STATUS.STATUS_KODE_HIST = 'X' THEN                  
             DO;                                                                
               IF ((B01.PERSON(I).STATUS.PENSJONSTYPE1 = 'U' &                  
                    B01.PERSON(I).STATUS.PENSJONSTYPE3 = 'A') !                 
                   (B01.PERSON(1).STATUS.PENSJONSTYPE1 = 'U' &                  
                    B01.PERSON(1).STATUS.PENSJONSTYPE3 = 'A') !                 
                   (B01.PERSON(2).STATUS.PENSJONSTYPE1 = 'U' &                  
                    B01.PERSON(2).STATUS.PENSJONSTYPE3 = 'A')) THEN             
                   /* OPPHØR AV ATTFØRING           */                          
                   ;                                                            
               ELSE                                                             
                 DO;                                                            
                   /* TILGANG ETTER OPPHØR AV YTELSE */                         
                   ALLOCATE X01;                                                
                   SAVE_B01_B02 = 1;                                            
                   X01.PERSON(I) = B01.PERSON(I);                               
                   B01.PERSON(I) = '';                                          
                 END;                                                           
             END;                                                               
       END;                                                                     
                                                                                
 /*-------------------------------------------------------------------*/        
 /*    PTYPE-SEGMENT                                                  */        
 /*-------------------------------------------------------------------*/        
 /* I HOVED-BASEN BRUKES PENSJONTYPE1, 2 OG 3.                        */        
 /* I STATISTIKK BRUKES BARE PT1 OG PT2.                              */        
 /* DET MÅ DERFOR FORETAS EN OMREGNING AV PENSJONSTYPEN I B01/B02     */        
 /*                                                                   */        
 /* FRA SEPT. 93 BENYTTES PT3.    SL FRA DATA-UTVIKLING               */        
 /* Q OG R I PT1 UTGÅR                                                */        
 /*-------------------------------------------------------------------*/        
                                                                                
       B01_STAT_PT1 = B01.PERSON(I).STATUS.PENSJONSTYPE1;                       
       B02_STAT_PT1 = B02.PERSON(I).STATUS.PENSJONSTYPE1;                       
                                                                                
       ENDRET = (B01_STAT_PT1 ^= B02_STAT_PT1          ) !                      
                (B01.PERSON(I).STATUS.PENSJONSTYPE2 ^=                          
                 B02.PERSON(I).STATUS.PENSJONSTYPE2    ) !                      
                (B01.PERSON(I).STATUS.PENSJONSTYPE3 ^=                          
                 B02.PERSON(I).STATUS.PENSJONSTYPE3    );                       
                                                                                
       IF ENDRET THEN                                                           
       DO;                                                                      
          SEGMENT_TABELL.PTYPE = TRUE;                                          
          PTYPE.PT1 = B02_STAT_PT1;                                             
          PTYPE.PT2 = B02.PERSON(I).STATUS.PENSJONSTYPE2;                       
          PTYPE.PT3 = B02.PERSON(I).STATUS.PENSJONSTYPE3;                       
                                                                                
          IF (PTYPE.PT1  = 'L' &                                                
              PTYPE.PT2 ^= 'V' ) !     /* SÆRBARN FOM. 1.5.91 BØ9104 */         
              PTYPE.PT1  = 'D'   !                                              
              PTYPE.PT1  = ' '   THEN                                           
              DO;                                                               
                 PTYPE.PT2 = ' ';                                               
                 PTYPE.PT3 = ' ';                                               
              END;                                                              
       END;                                                                     
                                                                                
    IF B01.PERSON(I).STATUS.PENSJONSTYPE1    = 'L' &                            
       B01.PERSON(I).STATUS.PENSJONSTYPE2    = ' ' &                            
       B02.PERSON(I).STATUS.STATUS_KODE_HIST = 'O' THEN                         
       DO;                                                                      
          SEGMENT_TABELL.PTYPE = TRUE;                                          
          /* SÅ LENGE OPPHØRSLOGIKKEN LENGRE OPPE I PGM. ALLTID HAR   */        
          /* SPARING AV B02 I X02 V/STATUS_KODE_HIST ^= ' ', KAN DETTE*/        
          /* FELTET BLANKES HER (EN NOE TVILSOM FRAMGANGSMÅTE). BØ9012*/        
          B02.PERSON(I).STATUS.STATUS_KODE_HIST = ' ';                          
       END;                                                                     
                                                                                
 /*-------------------------------------------------------------------*/        
 /*                                                                   */        
 /*    SIVILST_SEGMENT                                                */        
 /*                                                                   */        
 /* ------------------------------------------------------------------*/        
                                                                                
       IF B02.PERSON (I).STATUS.SIVILSTAND                   ^= ' ' THEN        
          DO;                                                                   
             ENDRET =    (B01.PERSON(I).STATUS.SIVILSTAND    ^=                 
                          B02.PERSON(I).STATUS.SIVILSTAND)             ;        
                                                                                
             IF ^ENDRET & B02.PERSON(I).STATUS.SIVILSTAND     = 'G' THEN        
                                                                                
             ENDRET =    (B01.PERSON(I).STATUS.PENSJONSTYPE2 ^=                 
                          B02.PERSON(I).STATUS.PENSJONSTYPE2)          ;        
                                                                                
             IF ENDRET THEN                                                     
                DO;                                                             
                   SEGMENT_TABELL.SIVILST                     =  TRUE  ;        
                   SIVILST.SIVILSTAND                         =                 
                   F_KONV_SIVILSTAND (B02.PERSON(I).STATUS.SIVILSTAND) ;        
                END;                                                            
          END;                                                                  
                                                                                
 /*-------------------------------------------------------------------*/        
 /*                                                                   */        
 /*    APOPPL-SEGMENT FOREKOMMER IKKE FOR BARN                       */         
 /*                                                                   */        
 /*-------------------------------------------------------------------*/        
                                                                                
    /* NULLSTILLER HJELPEVARIABLE TIL REGTPKT-SEGMENT */                        
                                                                                
       B01_PÅ  = 0;                                                             
       B02_PÅ  = 0;                                                             
       B01_SPT = 0.0;                                                           
       B02_SPT = 0.0;                                                           
       B01_OPT = 0.0;                                                           
       B02_OPT = 0.0;                                                           
       B01_YPT = 0.0;                                                           
       B02_YPT = 0.0;                                                           
 /*BØ9201:*/                                                                    
       B01_PÅ_E91  = 0;                                                         
       B02_PÅ_E91  = 0;                                                         
       B01_SPT_GML = 0.0;                                                       
       B02_SPT_GML = 0.0;                                                       
                                                                                
                                                                                
 /*-------------------------------------------------------------------*/        
 /*       BELØPS-SEGMENTER                                            */        
 /*-------------------------------------------------------------------*/        
                                                                                
       TEST_B02_PT1 = B02.PERSON(I).STATUS.PENSJONSTYPE1;                       
                                                                                
       IF TEST_B02_PT1 = ' ' THEN                                               
          TEST_B02_PT1 = B01.PERSON(I).STATUS.PENSJONSTYPE1;                    
                                                                                
       SELECT(TEST_B02_PT1);                                                    
                                                                                
          WHEN ('D')                                                            
          DO;                                                                   
             IF B01_STAT_PT1  ^= ' '  !                                         
                B01_STAT_PT1  ^= 'L'  THEN                                      
                SEGMENT_TABELL.PENSJON  = TRUE;                                 
             IF B01.PERSON(I).STATUS.DØD_AV_YRKESSKADE = 'J' THEN               
                B01_YPT  = B01.PERSON(I).YRKEPENS.YPT;                          
             IF B02.PERSON(I).STATUS.DØD_AV_YRKESSKADE = 'J' THEN               
                B02_YPT  = B02.PERSON(I).YRKEPENS.YPT;                          
          END;                                                                  
                                                                                
          /*---------------------------------------------------------*/         
          /* HAR VÆRT FEIL VED HENTING AV VERDIER ETTER PENSJONSTYPE.*/         
          /* ANTAR AT ALT DETTE KAN SLÅS I HOP - DVS. LEGGE SAMMEN   */         
          /* BELØPENE I ETTEBARN, UFØRPENS OG YRKEPENS.  BØ9012.     */         
          /*---------------------------------------------------------*/         
          WHEN ('E','N','B')                                                    
          DO;                                                                   
                                                                                
             /* FORBEREDELSE TIL REGTPKT-SEGMENT -                   */         
             /* HAR TATT MED TESTER PÅ YP - KOMMER VEL ENGANG. BØ9012*/         
                                                                                
             IF B02.PERSON(I).STATUS.PENSJONSTYPE3 = 'U' THEN                   
               DO;                                                              
 /*BØ9201-FRA*/                                                                 
                 IF B01.PERSON(I).UFØRPENS.PÅ > 0 THEN                          
                    B01_PÅ  = B01.PERSON(I).UFØRPENS.PÅ;                        
                 ELSE                                                           
                    B01_PÅ  = B01.PERSON(I).YRKEPENS.PÅ;                        
                 IF B02.PERSON(I).UFØRPENS.PÅ > 0 THEN                          
                    B02_PÅ  = B02.PERSON(I).UFØRPENS.PÅ;                        
                 ELSE                                                           
                    B02_PÅ  = B02.PERSON(I).YRKEPENS.PÅ;                        
                                                                                
                 IF B01.PERSON(I).UFØRPENS.SPT > 0 THEN                         
                    B01_SPT = B01.PERSON(I).UFØRPENS.SPT;                       
                 ELSE                                                           
                    B01_SPT = B01.PERSON(I).YRKEPENS.SPT;                       
                 IF B02.PERSON(I).UFØRPENS.SPT > 0 THEN                         
                    B02_SPT = B02.PERSON(I).UFØRPENS.SPT;                       
                 ELSE                                                           
                    B02_SPT = B02.PERSON(I).YRKEPENS.SPT;                       
 /*BØ9201-TIL*/                                                                 
                                                                                
                 B01_OPT = B01.PERSON(I).UFØRPENS.OPT;                          
                 B02_OPT = B02.PERSON(I).UFØRPENS.OPT;                          
                                                                                
                 B01_YPT = B02.PERSON(I).YRKEPENS.YPT;                          
                 B02_YPT = B02.PERSON(I).YRKEPENS.YPT;                          
               END;                                                             
                                                                                
 /*-------------------------------------------------------------------*/        
 /*                                                                   */        
 /*          PENSJON-SEGMENT                                          */        
 /*                                                                   */        
 /*-------------------------------------------------------------------*/        
                                                                                
             ENDRET = (B01.PERSON(I).UFØRPENS.GP +                              
                       B01.PERSON(I).YRKEPENS.GP +                              
                       B01.PERSON(I).ETTEBARN.GP  ^=                            
                       B02.PERSON(I).UFØRPENS.GP +                              
                       B02.PERSON(I).YRKEPENS.GP +                              
                       B02.PERSON(I).ETTEBARN.GP) !                             
                      (B01.PERSON(I).UFØRPENS.ST +                              
                       B01.PERSON(I).ETTEBARN.ST ^=                             
                       B02.PERSON(I).UFØRPENS.ST +                              
                       B02.PERSON(I).ETTEBARN.ST) !                             
                      (B01.PERSON(I).UFØRPENS.TP +                              
                       B01.PERSON(I).YRKEPENS.TP +                              
                       B01.PERSON(I).ETTEBARN.TP ^=                             
                       B02.PERSON(I).UFØRPENS.TP +                              
                       B02.PERSON(I).YRKEPENS.TP +                              
                       B02.PERSON(I).ETTEBARN.TP) !                             
                      (B01.PERSON(I).STATUS.GT_TILLEGG_LOV92 ^=                 
                       B02.PERSON(I).STATUS.GT_TILLEGG_LOV92 ) ;                
                                                                                
 /* SOB0000 FRA */                                                              
 /*                                                                 */          
 /*          IF ^ENDRET THEN                                        */          
 /*            ENDRET = (B01.PERSON(I).UFØRPENS.GP             = 0 &*/          
 /*                      B01.PERSON(I).YRKEPENS.GP             = 0 &*/          
 /*                      B01.PERSON(I).ETTEBARN.GP             = 0 &*/          
 /*                      B01.PERSON(I).UFØRPENS.ST             = 0 &*/          
 /*                      B01.PERSON(I).ETTEBARN.ST             = 0 &*/          
 /*                      B01.PERSON(I).UFØRPENS.TP             = 0 &*/          
 /*                      B01.PERSON(I).YRKEPENS.TP             = 0 &*/          
 /*                      B01.PERSON(I).ETTEBARN.TP             = 0 &*/          
 /*                      B01.PERSON(I).STATUS.GT_TILLEGG_LOV92 = 0);*/          
 /* SOB0000 TIL */                                                              
                                                                                
             IF ENDRET THEN                                                     
             DO;                                                                
                SEGMENT_TABELL.PENSJON = TRUE;                                  
                                                                                
             /*-----------------------------------*/                            
             /* HER REGNES DET BARE UT VERDIER    */                            
             /* HVIS TRANSTYPE < 50               */                            
             /*-----------------------------------*/                            
                                                                                
                IF TRANS_OPPL_OMR.TRANSTYPE < 50 THEN                           
                DO;                                                             
                                                                                
                   PENSJON.GP_FAK =                                             
                      (B02.PERSON(I).UFØRPENS.GP +                              
                       B02.PERSON(I).YRKEPENS.GP +                              
                       B02.PERSON(I).ETTEBARN.GP) *                             
                                           12 / DIV_PARAM_OMR.G;                
                                                                                
                   PENSJON.ST_FAK =                                             
                      (B02.PERSON(I).UFØRPENS.ST +                              
                       B02.PERSON(I).ETTEBARN.ST) *                             
                                           12 / DIV_PARAM_OMR.G;                
                                                                                
                   /* ---------------------------------------------- */         
                   /* TP-GARANTI 2 JFR. KOMM TIL PT1=A I R0016022    */         
                   /*                                    BØ9201      */         
                   /* ---------------------------------------------- */         
                   W_B02_SUM      =                                             
                      (B02.PERSON(I).STATUS.GT_TILLEGG_LOV92 +                  
                       B02.PERSON(I).UFØRPENS.TP +                              
                       B02.PERSON(I).YRKEPENS.TP +                              
                       B02.PERSON(I).ETTEBARN.TP) ;                             
                                                                                
                   PENSJON.TP_FAK = W_B02_SUM * 12 / DIV_PARAM_OMR.G;           
                                                                                
                   PENSJON.TP_GAR_FAK =                                         
                       B02.PERSON(I).STATUS.GT_TILLEGG_LOV92 *                  
                                           12 / DIV_PARAM_OMR.G;                
                END;                                                            
             END;                                                               
                                                                                
 /*-------------------------------------------------------------------*/        
 /*                                                                   */        
 /*          KT-SEGMENT                                               */        
 /*                                                                   */        
 /*-------------------------------------------------------------------*/        
                                                                                
             ENDRET = (B01.PERSON(I).UFØRPENS.KT +                              
                       B01.PERSON(I).ETTEBARN.KT ^=                             
                       B02.PERSON(I).UFØRPENS.KT +                              
                       B02.PERSON(I).ETTEBARN.KT);                              
             IF ENDRET THEN                                                     
             DO;                                                                
                SEGMENT_TABELL.KT = TRUE;                                       
                KT.KT_KR =                                                      
                   (B02.PERSON(I).UFØRPENS.KT +                                 
                    B02.PERSON(I).ETTEBARN.KT) * 12;                            
             END;                                                               
                                                                                
 /*-------------------------------------------------------------------*/        
 /*                       (BØ9012:                                    */        
 /*          ET-SEGMENT    DET KAN IKKE VÆRE MULIG MED EKTEFELLE-TILL.*/        
 /*                        TIL ET BARN SOM ER TILKNYTTET FORELDRE??)  */        
 /*                        ENDRER IKKE LOGIKKEN FOR LOVENDR. 1.5.91.  */        
 /*-------------------------------------------------------------------*/        
                                                                                
             ENDRET = (B01.PERSON(I).UFØRPENS.ET ^=                             
                       B02.PERSON(I).UFØRPENS.ET);                              
             IF ENDRET THEN                                                     
             DO;                                                                
                SEGMENT_TABELL.ET = TRUE;                                       
                                                                                
             /*------------------------------------*/                           
             /* HER REGNES DET BARE UT VERDI       */                           
             /* HVIS TRANSTYPE < 50                */                           
             /*------------------------------------*/                           
                                                                                
                IF TRANS_OPPL_OMR.TRANSTYPE < 50 THEN                           
                   ET.ET_FAK =                                                  
                      B02.PERSON(I).UFØRPENS.ET                                 
                             * 12 / DIV_PARAM_OMR.G;                            
             END;                                                               
                                                                                
          END;   /* OF WHEN ('E', 'N', 'B') */                                  
 /*BØ9012 - ENDRING SLUTT */                                                    
                                                                                
          OTHERWISE ;                                                           
                                                                                
       END; /* OF SELECT */                                                     
                                                                                
 /*-------------------------------------------------------------------*/        
 /*                                                                   */        
 /*    UPPER-SEGMENT                                                  */        
 /*                                                                   */        
 /*-------------------------------------------------------------------*/        
                                                                                
                                                                                
       ENDRET = (B01.PERSON(I).UFØRPENS.UFG ^=                                  
                 B02.PERSON(I).UFØRPENS.UFG);                                   
                                                                                
    /* I DEN SISTE UTFYLTE UFØRHIST LIGGER GJELDENDE UFT OG BUP       */        
                                                                                
    /* HAR INNTIL 9012 KUN TESTET PÅ X02.UFØRHIST - IKKE B02.UFØRHIST */        
    /* - LOGIKK ENDRET HERFRA ....                       BØ9012       */        
                                                                                
       K = 7;                                  /*SOA9707: 5->7 */               
       DO WHILE (B02.PERSON(I).UFØRHIST(K).UFT_ÅMD = 0) UNTIL (K = 0);          
          K = K - 1;                                                            
       END;                                                                     
                                                                                
       IF K = 0 THEN                                                            
          DO;                                                                   
             K = 7;                             /* SOA9707: 5->7 */             
             DO WHILE                                                           
                  (X02.PERSON(I).UFØRHIST(K).UFT_ÅMD = 0) UNTIL (K = 0);        
                K = K - 1;                                                      
             END;                                                               
          END;                                                                  
                                                                                
       IF K > 0 THEN                                                            
          ENDRET = ENDRET !                                                     
   /*2000*/        (B02.PERSON(I).UFØRHIST(K).UFT_ÅMD < 19920000 &              
                    B02.PERSON(I).UFØRHIST(K).BUP     > 5      ) !              
                                                                                
                   (B01.PERSON(I).UFØRHIST(K).UFT_ÅMD ^=                        
                    B02.PERSON(I).UFØRHIST(K).UFT_ÅMD) !                        
                   (B01.PERSON(I).UFØRHIST(K).BUP ^=                            
                    B02.PERSON(I).UFØRHIST(K).BUP) !                            
                   (B01.PERSON(I).UFØRHIST(K).OPPHØRSDATO_ÅMD ^=                
                    B02.PERSON(I).UFØRHIST(K).OPPHØRSDATO_ÅMD);                 
                                                                                
                                                                                
       IF ENDRET & K > 0 THEN                                                   
          DO;                                                                   
             SEGMENT_TABELL.UPPER = TRUE;                                       
                                                                                
             IF X02.PERSON(I).UFØRHIST(K).OPPHØRSDATO_ÅMD ^= 0 THEN             
                UPPER.UFG = 0;                                                  
             ELSE                                                               
                DO;                                                             
                   UPPER.UFG         = B02.PERSON(I).UFØRPENS.UFG;              
                   UPPER.VILKÅR_8_1B =                                          
                                 B02.PERSON(I).UFØRPENS.VILKÅR_8_1B;            
                END;                                                            
                                                                                
             IF UPPER.UFG > 0 THEN                                              
                DO;                                                             
   /*2000*/        IF B02.PERSON(I).UFØRHIST(K).UFT_ÅMD > 19920000 THEN         
                        UPPER.BUP  = B02.PERSON(I).UFØRHIST(K).BUP;             
   /*2000*/        ELSE UPPER.BUP  = POREKKE.BUP_FPP (I,1992);                  
                   UPPER.UFT       = B02.PERSON(I).UFØRHIST(K).UFT_ÅMD;         
                   UPPER.BUPÅR_RED = B02.PERSON(I).UFØRHIST(K).                 
                                         REDUSERT_ANT_BUP_ÅR;                   
                END;                                                            
             ELSE                                                               
                DO;                                                             
   /*2000*/        IF B02.PERSON(I).UFØRHIST(K).UFT_ÅMD > 19920000 THEN         
                        UPPER.BUP  = X02.PERSON(I).UFØRHIST(K).BUP;             
   /*2000*/        ELSE UPPER.BUP  = POREKKE.BUP_FPP (I,1992);                  
                   UPPER.UFT       = X02.PERSON(I).UFØRHIST(K).UFT_ÅMD;         
                   UPPER.BUPÅR_RED = X02.PERSON(I).UFØRHIST(K).                 
                                         REDUSERT_ANT_BUP_ÅR;                   
                END;                                                            
          END;                                                                  
 /* ... LOGIKK ENDRET TIL. BØ9012 */                                            
                                                                                
 /*-------------------------------------------------------------------*/        
 /*    VT-SEGMENT SKAL IKKE FOREKOMME FOR BARN                        */        
 /*-------------------------------------------------------------------*/        
 /*    BT-SEGMENT - DET KAN IKKE VÆRE MULIG MED BARNETILLEGG, HVERKEN */        
 /*                 FOR BARNEPENSJONIST ELLER FORSØRGET BARN. ENDRER  */        
 /*                 DERFOR IKKE LOGIKKEN PGA. LOVENDR. 1.5.91. BØ9104 */        
 /*-------------------------------------------------------------------*/        
                                                                                
       ENDRET = ((B01.PERSON(I).STATUS.ANTALL_BARN ^=                           
                 B02.PERSON(I).STATUS.ANTALL_BARN) &                            
                (B02.PERSON(I).STATUS.PENSJONSTYPE1 ^= 'B')) !                  
                (B01.PERSON(I).UFØRPENS.BT ^=                                   
                 B02.PERSON(I).UFØRPENS.BT);                                    
       IF ENDRET THEN                                                           
       DO;                                                                      
          SEGMENT_TABELL.BT    = TRUE;                                          
          BT.BT_ANT = B02.PERSON(I).STATUS.ANTALL_BARN;                         
          IF TRANS_OPPL_OMR.TRANSTYPE < 50 THEN                                 
             BT.BT_FAK =                                                        
                 B02.PERSON(I).UFØRPENS.BT * 12 /                               
                 DIV_PARAM_OMR.G;                                               
       END;                                                                     
                                                                                
 /*-------------------------------------------------------------------*/        
 /*                                                                   */        
 /*    TILKN-SEGMENT              FOR BARN                            */        
 /*                                                                   */        
 /*-------------------------------------------------------------------*/        
                                                                                
       ENDRET = FALSE;                                                          
       IF I = 3 THEN                                                            
          EBARN = FALSE;                                                        
       IF B02.PERSON(I).RF0PERSN.FNR > 0 THEN                                   
          IF EBARN THEN                                                         
             ENDRET = TRUE;                                                     
          ELSE                                                                  
          DO J=1 TO 13;     /* J INDEKSERER TILKN-ARRAY I B02      */           
             IF (B02.PERSON(I).TILKN(J).FNR_TILKN ^=                            
                 B01.PERSON(I).TILKN(J).FNR_TILKN) !                            
                (B02.PERSON(I).TILKN(J).TILKNYTNINGSKODE ^=                     
                 B01.PERSON(I).TILKN(J).TILKNYTNINGSKODE) THEN                  
                 ENDRET = TRUE;                                                 
          END;                                                                  
                                                                                
       IF ENDRET THEN                                                           
       DO;                                                                      
          SELECT (B02.PERSON(I).STATUS.PENSJONSTYPE1);                          
             WHEN ('L')                                                         
                                                                                
             /*-----------------*/                                              
             /* FORSØRGET BARN  */                                              
             /*-----------------*/                                              
                                                                                
                IF B02.PERSON(I).TILKN(1).FNR_TILKN > 0 THEN                    
                DO;                                                             
                   TILKN.TKNYTT(2).TILKNYTNINGSKODE = 'B';                      
                   TILKN.TKNYTT(2).FNR_TILKN =                                  
                      B02.PERSON(I).TILKN(1).FNR_TILKN;                         
                   SEGMENT_TABELL.TILKN = TRUE;                                 
                END;                                                            
             WHEN ('N','B')                                                     
                                                                                
             /*-----------------*/                                              
             /* ETTERLATT BARN  */                                              
             /*-----------------*/                                              
                                                                                
                DO;                                                             
                   EBARN = TRUE;                                                
                   IF B02.PERSON(1).STATUS.PENSJONSTYPE1 = 'D' THEN             
                   DO;                                                          
                   /*---------------------*/                                    
                   /* TILKNYTNING TIL MOR */                                    
                   /*---------------------*/                                    
                                                                                
                      TILKN.TKNYTT(3).TILKNYTNINGSKODE =            'C';        
                      TILKN.TKNYTT(3).FNR_TILKN =                               
                                            B02.PERSON(1).RF0PERSN.FNR;         
                      SEGMENT_TABELL.TILKN = TRUE;                              
                   END;                                                         
                                                                                
                   IF B02.PERSON(2).STATUS.PENSJONSTYPE1 = 'D' THEN             
                   DO;                                                          
                   /*---------------------*/                                    
                   /* TILKNYTNING TIL FAR */                                    
                   /*---------------------*/                                    
                                                                                
                      TILKN.TKNYTT(4).TILKNYTNINGSKODE =            'D';        
                      TILKN.TKNYTT(4).FNR_TILKN =                               
                         B02.PERSON(2).RF0PERSN.FNR;                            
                      SEGMENT_TABELL.TILKN = TRUE;                              
                   END;                                                         
                                                                                
                   IF B02.PERSON(3).RF0PERSN.FNR > 0 THEN                       
                   DO;                                                          
                   /*-----------------------------*/                            
                   /* TILKNYTNING TIL YNGSTE BARN */                            
                   /*-----------------------------*/                            
                                                                                
                      TILKN.TKNYTT(5).TILKNYTNINGSKODE =            'E';        
                      TILKN.TKNYTT(5).FNR_TILKN =                               
                         B02.PERSON(3).RF0PERSN.FNR;                            
                      SEGMENT_TABELL.TILKN = TRUE;                              
                   END;                                                         
                                                                                
                END;                                                            
             OTHERWISE ;                                                        
          END; /* OF SELECT */                                                  
       END;                                                                     
                                                                                
 /*-------------------------------------------------------------------*/        
 /*                                                                   */        
 /*    ETTERL-SEGMENT    FOR BARN                                     */        
 /*                                                                   */        
 /*-------------------------------------------------------------------*/        
                                                                                
       IF B02.PERSON(I).STATUS.PENSJONSTYPE1 = 'B' !                            
          B02.PERSON(I).STATUS.PENSJONSTYPE1 = 'N' THEN                         
                                                                                
       DO;                                                                      
                                                                                
       /* REGNER UT AVDØDES TRYDGETID I ANTALL MÅNEDER */                       
       /* - TVILSOM NÅR KUN 1 FORELDER ER DØD?? BØ9201.*/                       
                                                                                
          B01_TT_AVD = (MAX(B01.PERSON(1).STATUS.TT_ANV,                        
                            B01.PERSON(2).STATUS.TT_ANV)) * 12;                 
          B02_TT_AVD = (MAX(B02.PERSON(1).STATUS.TT_ANV,                        
                            B02.PERSON(2).STATUS.TT_ANV)) * 12;                 
                                                                                
          ENDRET = (B01_TT_AVD ^= B02_TT_AVD)                    !              
                   (B01.PERSON(I).ETTEBARN.PÅ_MOR  ^=                           
                    B02.PERSON(I).ETTEBARN.PÅ_MOR)               !              
                   (B01.PERSON(I).ETTEBARN.SPT_MOR ^=                           
                    B02.PERSON(I).ETTEBARN.SPT_MOR)              !              
                   (B01.PERSON(I).ETTEBARN.OPT_MOR ^=                           
                    B02.PERSON(I).ETTEBARN.OPT_MOR)              !              
                   (B01.PERSON(I).ETTEBARN.PÅ_FAR  ^=                           
                    B02.PERSON(I).ETTEBARN.PÅ_FAR)               !              
                   (B01.PERSON(I).ETTEBARN.SPT_FAR ^=                           
                    B02.PERSON(I).ETTEBARN.SPT_FAR)              !              
                   (B01.PERSON(I).ETTEBARN.OPT_FAR ^=                           
                    B02.PERSON(I).ETTEBARN.OPT_FAR)              !              
 /*BØ9103:*/      ((B02.PERSON(I).STATUS.PENSJONSTYPE1  = 'N' !                 
                    B02.PERSON(I).STATUS.PENSJONSTYPE1  = 'B' ) &               
                   (B02.PERSON(I).STATUS.PENSJONSTYPE1 ^=                       
                    B01.PERSON(I).STATUS.PENSJONSTYPE1))         !              
 /*BØ9201:*/       (B01.PERSON(I).ETTEBARN.PÅ_ETTER91_MOR  ^=                   
                    B02.PERSON(I).ETTEBARN.PÅ_ETTER91_MOR)       !              
                   (B01.PERSON(I).ETTEBARN.PÅ_ETTER91_FAR  ^=                   
                    B02.PERSON(I).ETTEBARN.PÅ_ETTER91_FAR)       !              
                   (B01.PERSON(I).ETTEBARN.SPT_1291_MOR    ^=                   
                    B02.PERSON(I).ETTEBARN.SPT_1291_MOR)         !              
                   (B01.PERSON(I).ETTEBARN.SPT_1291_FAR    ^=                   
                    B02.PERSON(I).ETTEBARN.SPT_1291_FAR)         ;              
                                                                                
                 /* 'FORVENTET      ARBEIDSINNTEKT'    IKKE FOR BARN */         
                 /* 'FRADRAG FORVENTET      ERVERVSINNTEKT'  "       */         
                 /* 'TP-KODER'                               "       */         
                                                                                
          IF ENDRET THEN                                                        
          DO;                                                                   
             SEGMENT_TABELL.ETTERL = TRUE;                                      
                                                                                
             ETTERL.TT_AVD = B02_TT_AVD;                                        
             M_FNR = B02.PERSON(1).RF0PERSN.FNR;                                
             M_SPT = B02.PERSON(I).ETTEBARN.SPT_MOR;                            
             M_OPT = B02.PERSON(I).ETTEBARN.OPT_MOR;                            
             M_PÅ  = B02.PERSON(I).ETTEBARN.PÅ_MOR;                             
             F_FNR = B02.PERSON(2).RF0PERSN.FNR;                                
             F_SPT = B02.PERSON(I).ETTEBARN.SPT_FAR;                            
             F_OPT = B02.PERSON(I).ETTEBARN.OPT_FAR;                            
             F_PÅ  = B02.PERSON(I).ETTEBARN.PÅ_FAR;                             
                                                                                
             IF F_MAGIC(M_FNR,M_SPT,M_OPT,M_PÅ) >                               
                F_MAGIC(F_FNR,F_SPT,F_OPT,F_PÅ) THEN                            
             DO;                                                                
                ETTERL.SPT_AVD            = M_SPT;                              
                ETTERL.OPT_AVD            = M_OPT;                              
                ETTERL.PÅ_AVD             = M_PÅ;                               
 /*BØ9201:*/    ETTERL.PÅ_AVD_ETTER91     =                                     
                              B02.PERSON(I).ETTEBARN.PÅ_ETTER91_MOR;            
                ETTERL.SPT_AVD_GML        =                                     
                              B02.PERSON(I).ETTEBARN.SPT_1291_MOR;              
                IF I > 3  &                                                     
                   ETTERL.SPT_AVD_GML  = 0  THEN                                
                   ETTERL.SPT_AVD_GML     =                                     
                              B02.PERSON(3).ETTEBARN.SPT_1291_MOR;              
             END;                                                               
             ELSE                                                               
             DO;                                                                
                ETTERL.SPT_AVD            = F_SPT;                              
                ETTERL.OPT_AVD            = F_OPT;                              
                ETTERL.PÅ_AVD             = F_PÅ;                               
 /*BØ9201*/     ETTERL.PÅ_AVD_ETTER91     =                                     
                              B02.PERSON(I).ETTEBARN.PÅ_ETTER91_FAR;            
                ETTERL.SPT_AVD_GML        =                                     
                              B02.PERSON(I).ETTEBARN.SPT_1291_FAR;              
                IF I > 3  &                                                     
                   ETTERL.SPT_AVD_GML  = 0  THEN                                
                   ETTERL.SPT_AVD_GML     =                                     
                              B02.PERSON(3).ETTEBARN.SPT_1291_FAR;              
             END;                                                               
                                                                                
   /*2000*/  FAR_DØDSDATO = B02.PERSON(2).STATUS.DØDSDATO_ÅMD;                  
   /*2000*/  MOR_DØDSDATO = B02.PERSON(1).STATUS.DØDSDATO_ÅMD;                  
   /*2000*/  EK_DØDSDATO  = 0;                                                  
          END;                                                                  
       END;                                                                     
                                                                                
 /*-------------------------------------------------------------------*/        
 /*                                                                   */        
 /*    SPESOPPL-SEGMENT                                               */        
 /*                                                                   */        
 /*-------------------------------------------------------------------*/        
                                                                                
                                                                                
          ENDRET =                                                              
                   (B01.PERSON(I).SPESOPPL.KONVENSJON          ^=               
                    B02.PERSON(I).SPESOPPL.KONVENSJON)            !             
                   (B01.PERSON(I).SPESOPPL.FRAVIK_FLYKTN       ^=               
                    B02.PERSON(I).SPESOPPL.FRAVIK_FLYKTN)         !             
                   (B01.PERSON(I).SPESOPPL.FRAVIK_BOSATT       ^=               
                    B02.PERSON(I).SPESOPPL.FRAVIK_BOSATT)         !             
                   (B01.PERSON(I).SPESOPPL.FRAVIK_TRYGDET_3ÅR  ^=               
                    B02.PERSON(I).SPESOPPL.FRAVIK_TRYGDET_3ÅR)    !             
                   (B01.PERSON(I).SPESOPPL.FRAVIK_19_3         ^=               
                    B02.PERSON(I).SPESOPPL.FRAVIK_19_3)           !             
                   (B01.PERSON(I).SPESOPPL.FRAVIK_8_1          ^=               
                    B02.PERSON(I).SPESOPPL.FRAVIK_8_1)            !             
                   (B01.PERSON(I).SPESOPPL.FRAVIK_10_1         ^=               
                    B02.PERSON(I).SPESOPPL.FRAVIK_10_1)           !             
  /*BØ9012*/                                                                    
                   (B01.PERSON(I).SPESOPPL.PENSJONSTRYGDET     ^=               
                    B02.PERSON(I).SPESOPPL.PENSJONSTRYGDET)       !             
                   (B01.PERSON(I).SPESOPPL.MINST_20ÅR_BOTID    ^=               
                    B02.PERSON(I).SPESOPPL.MINST_20ÅR_BOTID)      !             
                   (B01.PERSON(I).SPESOPPL.FRAVIK_BOSATT_KONVENSJON ^=          
                    B02.PERSON(I).SPESOPPL.FRAVIK_BOSATT_KONVENSJON)  !         
  /*BØ9207*/                                                                    
                   (B01.PERSON(I).FORSI.ARBEIDSINNTEKT       ^=                 
                    B02.PERSON(I).FORSI.ARBEIDSINNTEKT)         !               
                   (B01.PERSON(I).FORSI.PENSJONSINNTEKT      ^=                 
                    B02.PERSON(I).FORSI.PENSJONSINNTEKT)        ;               
                                                                                
                                                                                
          IF ENDRET &                                                           
             TRANS_OPPL_OMR.TRANSTYPE < 50 THEN                                 
             DO;                                                                
                SEGMENT_TABELL.SPESOPPL = TRUE;                                 
                SPESOPPL.KONVENSJON =                                           
                    B02.PERSON(I).SPESOPPL.KONVENSJON;                          
                SPESOPPL.FRAVIK_FLYKTN =                                        
                    B02.PERSON(I).SPESOPPL.FRAVIK_FLYKTN;                       
                SPESOPPL.FRAVIK_BOSATT =                                        
                    B02.PERSON(I).SPESOPPL.FRAVIK_BOSATT;                       
                SPESOPPL.FRAVIK_TRYGDET_3ÅR =                                   
                    B02.PERSON(I).SPESOPPL.FRAVIK_TRYGDET_3ÅR;                  
                SPESOPPL.FRAVIK_19_3 =                                          
                    B02.PERSON(I).SPESOPPL.FRAVIK_19_3;                         
                SPESOPPL.FRAVIK_8_1 =                                           
                    B02.PERSON(I).SPESOPPL.FRAVIK_8_1;                          
                SPESOPPL.FRAVIK_10_1 =                                          
                    B02.PERSON(I).SPESOPPL.FRAVIK_10_1;                         
                SPESOPPL.PENSJONSTRYGDET     =                                  
                    B02.PERSON(I).SPESOPPL.PENSJONSTRYGDET;                     
                SPESOPPL.MINST_20ÅR_BOTID    =                                  
                    B02.PERSON(I).SPESOPPL.MINST_20ÅR_BOTID;                    
                SPESOPPL.FRAVIK_BOSATT_KONV  =                                  
                    B02.PERSON(I).SPESOPPL.FRAVIK_BOSATT_KONVENSJON;            
  /*BØ9207*/                                                                    
                SPESOPPL.ARB_INNT_FAK        =                                  
                    B02.PERSON(I).FORSI.ARBEIDSINNTEKT * 100                    
                                           / DIV_PARAM_OMR.G ;                  
                SPESOPPL.PENSJONS_FAK        =                                  
                    B02.PERSON(I).FORSI.PENSJONSINNTEKT * 100                   
                                           / DIV_PARAM_OMR.G ;                  
                SPESOPPL.ARB_INNT_FAK_EK     =                                  
                    B02.PERSON(I).FORSI.ARBEIDSINNTEKT_EK * 100                 
                                           / DIV_PARAM_OMR.G ;                  
                SPESOPPL.PENSJONS_FAK_EK     =                                  
                    B02.PERSON(I).FORSI.PENSJONSINNTEKT_EK * 100                
                                           / DIV_PARAM_OMR.G ;                  
             END;                                                               
                                                                                
                                                                                
                                                                                
 /*-------------------------------------------------------------------*/        
 /*                                                                   */        
 /*    REGTPKT-SEGMENT            FOR BARN                            */        
 /*                                                                   */        
 /*-------------------------------------------------------------------*/        
                                                                                
 /*    HVIS NOEN AV DE UNDERLIGGENDE SEGMENTER NÅ ER DANNET,          */        
 /*    SKAL DET DANNES ET REGTPKT-SEGMENT.....                        */        
                                                                                
       ENDRET = SEGMENT_TABELL.PTYPE !                                          
                SEGMENT_TABELL.SIVILST !                                        
                SEGMENT_TABELL.PENSJON !                                        
                SEGMENT_TABELL.UPPER !                                          
                SEGMENT_TABELL.KT !                                             
                SEGMENT_TABELL.ET !                                             
                SEGMENT_TABELL.BT !                                             
                SEGMENT_TABELL.TILKN !                                          
                SEGMENT_TABELL.ETTERL!                                          
                SEGMENT_TABELL.SPESOPPL;                                        
                                                                                
    /* ...ELLER NOEN AV FELTENE I REGTPKT-SEGMENTET ER ENDRET.        */        
                                                                                
       /* REGNER UT EGEN TRYGDETID I ANTALL MÅNEDER  */                         
       /* FRA 1991 TAS NYTT TT-FELT I BRUK.  BØ9012 */                          
                                                                                
       IF B01.PERSON(I).STATUS.TT_16_66  >  0  THEN                             
          B01_TT_EGEN =                                                         
                     B01.PERSON(I).STATUS.TT_16_66        +                     
                     B01.PERSON(I).STATUS.TT_FRAMT        +                     
                     B01.PERSON(I).STATUS.TT_67_TIL_70 * 12;                    
       ELSE                                                                     
          B01_TT_EGEN =                                                         
                     B01.PERSON(I).STATUS.TT_ETTER_1966   +                     
                     B01.PERSON(I).STATUS.TT_FRAMT        +                     
                    (B01.PERSON(I).STATUS.TT_FØR_1967  +                        
                     B01.PERSON(I).STATUS.TT_67_TIL_70) * 12;                   
                                                                                
       IF B02.PERSON(I).STATUS.TT_16_66  >  0  THEN                             
          B02_TT_EGEN =                                                         
                     B02.PERSON(I).STATUS.TT_16_66        +                     
                     B02.PERSON(I).STATUS.TT_FRAMT        +                     
                     B02.PERSON(I).STATUS.TT_67_TIL_70 * 12;                    
       ELSE                                                                     
          B02_TT_EGEN =                                                         
                     B02.PERSON(I).STATUS.TT_ETTER_1966   +                     
                     B02.PERSON(I).STATUS.TT_FRAMT        +                     
                    (B02.PERSON(I).STATUS.TT_FØR_1967  +                        
                     B02.PERSON(I).STATUS.TT_67_TIL_70) * 12;                   
                                                                                
       ENDRET = ENDRET !                                                        
                (B01_TT_EGEN ^= B02_TT_EGEN)          !                         
                (B01.PERSON(I).RF0PERSN.TKNR        ^=                          
                 B02.PERSON(I).RF0PERSN.TKNR)         !                         
                (B01.PERSON(I).SPESOPPL.STATSBORGER ^=                          
                 B02.PERSON(I).SPESOPPL.STATSBORGER)  !                         
                (B01.PERSON(I).SPESOPPL.BOSATT      ^=                          
                 B02.PERSON(I).SPESOPPL.BOSATT)       !                         
                                                                                
                /* KOMNR FINNES IKKE På RF0-BASEN */                            
                                                                                
                (TRANS_OPPL_OMR.BLANKETTYPE = 'UP')   !                         
                (TRANS_OPPL_OMR.BLANKETTYPE = 'U2')   !                         
                                                                                
                (B01.PERSON(I).STATUS.TT_ANV        ^=                          
                 B02.PERSON(I).STATUS.TT_ANV)         !                         
                (B01_PÅ ^= B02_PÅ)                    !                         
                (B01_SPT ^= B02_SPT)                  !                         
                (B01_OPT ^= B02_OPT)                  !                         
                (B01.PERSON(I).STATUS.DØDSDATO_ÅMD  ^=                          
                 B02.PERSON(I).STATUS.DØDSDATO_ÅMD)   !                         
                (B01.PERSON(I).STATUS.DØD_AV_YRKESSKADE ^=                      
                 B02.PERSON(I).STATUS.DØD_AV_YRKESSKADE) !                      
 /*BØ9201*/     (B01_PÅ_E91 ^= B02_PÅ_E91)            !                         
                (B01_SPT_GML ^= B02_SPT_GML) ;                                  
                                                                                
       IF ENDRET THEN                                                           
       DO;                                                                      
          SEGMENT_TABELL.REGTPKT = TRUE;                                        
                                                                                
          SELECT (TRANS_OPPL_OMR.TRANSTYPE);                                    
             WHEN (24,25,69,33,41,26,28,42,29,58,31,27)                         
                REGTPKT.VIRKDATO =   TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD;          
             OTHERWISE                                                          
                DO;                                                             
                   IF B02.PERSON(I).STATUS.VIRK_DATO_ÅMD > 0 THEN               
                      HELP_DATO = B02.PERSON(I).STATUS.VIRK_DATO_ÅMD;           
                   ELSE HELP_DATO = B01.PERSON(I).STATUS.VIRK_DATO_ÅMD;         
                   REGTPKT.VIRKDATO = HELP_DATO;                                
                END;                                                            
          END; /* OF SELECT */                                                  
          /*----------------------------------------------------------*/        
          /*SIKRER AT DAG=00 I VIRKDATO                 SL UKE 5 2000 */        
          /*----------------------------------------------------------*/        
          REGTPKT.VIRKDATO = FLOOR (REGTPKT.VIRKDATO / 100) * 100;              
                                                                                
          /*----------------------------------------------------------*/        
          /* HVIS TRYGDEKONTOR-NUMMER = 0,                            */        
          /* HENTES DETTE FRA ET AV FAMILIE-MEDLEMMENE.               */        
          /*----------------------------------------------------------*/        
                                                                                
          IF B02.PERSON(I).RF0PERSN.TKNR > 0 THEN                               
             REGTPKT.TKNR = B02.PERSON(I).RF0PERSN.TKNR;                        
          ELSE                                                                  
          DO;                                                                   
             DO J=1 TO 14 UNTIL (B02.PERSON(J).RF0PERSN.TKNR > 0);              
             END;                                                               
             IF J <= 14 THEN                                                    
                REGTPKT.TKNR = B02.PERSON(J).RF0PERSN.TKNR;                     
          END;                                                                  
                                                                                
 /*BØ9012*/                                                                     
          IF (I ^= 3  !                                                         
              B02.PERSON(I).STATUS.PENSJONSTYPE1 = 'L' )  &                     
             B02.PERSON(I).SPESOPPL.BOSATT = 0            THEN                  
          DO;                                                                   
            DO J=1 TO 3 UNTIL (B02.PERSON(J).SPESOPPL.BOSATT > 0);              
            END;                                                                
            IF J <= 3  THEN                                                     
               REGTPKT.BOSATT_LAND = B02.PERSON(J).SPESOPPL.BOSATT;             
          END;                                                                  
          ELSE                                                                  
             REGTPKT.BOSATT_LAND = B02.PERSON(I).SPESOPPL.BOSATT;               
                                                                                
          REGTPKT.STATSBORGER = B02.PERSON(I).SPESOPPL.STATSBORGER;             
       /*-------------------------------------------------------------*/        
       /* KOMNR OPPDATERES EGENTLIG FRA SSB, MEN SETTES HER LIK       */        
       /* TKNR I TILFELLET SSB IKKE HAR NOEN VERDI PÅ KOMNR.          */        
       /*-------------------------------------------------------------*/        
       /* IF REGTPKT.STATSBORGER = 0 THEN    (ENDRET - BØ9012)        */        
       /*-------------------------------------------------------------*/        
          IF REGTPKT.BOSATT_LAND = 0 THEN                                       
             DO;                                                                
 /*SL9911 */    IF (REGTPKT.TKNR  > 0300  &  REGTPKT.TKNR <  0400) THEN         
                    REGTPKT.KOMNR = 0301;                                       
                ELSE IF                                                         
 /*SL9911 */       (REGTPKT.TKNR >= 1161  &  REGTPKT.TKNR <= 1165) THEN         
                    REGTPKT.KOMNR = 1103;                                       
                ELSE IF                                                         
 /*SL9911 */       (REGTPKT.TKNR >= 1202  &  REGTPKT.TKNR <= 1209) THEN         
                    REGTPKT.KOMNR = 1201;                                       
                ELSE IF                                                         
 /*SL9911 */       (REGTPKT.TKNR >= 1602  &  REGTPKT.TKNR <= 1607) THEN         
                    REGTPKT.KOMNR = 1601;                                       
                ELSE                                                            
                    REGTPKT.KOMNR = REGTPKT.TKNR;                               
             END;                                                               
                                                                                
       /* DIAG.GS OG DIAG.HSU FINNES PÅ US-TRANS */                             
                                                                                
          IF TRANS_OPPL_OMR.BLANKETTYPE = 'UP' THEN                             
             CALL UP_OPPDAT;                                                    
                                                                                
          IF TRANS_OPPL_OMR.BLANKETTYPE = 'U2' THEN                             
             CALL U2_OPPDAT;                                                    
                                                                                
 /*SOB9704:*/                                                                   
          IF TRANS_OPPL_OMR.BLANKETTYPE = 'U3' THEN                             
             CALL U3_OPPDAT;                                                    
                                                                                
          IF B02_TT_EGEN > 480   THEN  B02_TT_EGEN = 480; /* SEA2970 */         
          REGTPKT.TT_EGEN = B02_TT_EGEN;                                        
                                                                                
 /*BØ9112 ENDRET:*/                                                             
          REGTPKT.TT_ANV  = B02.PERSON(I).STATUS.TT_ANV * 12;                   
                                                                                
          REGTPKT.PÅ  = B02_PÅ;                                                 
          REGTPKT.SPT = B02_SPT;                                                
          REGTPKT.OPT = B02_OPT;                                                
                                                                                
          REGTPKT.YPT = B02_YPT;                                                
 /*BØ9201:*/                                                                    
          REGTPKT.PÅ_ETTER91 = B02_PÅ_E91 ;                                     
          REGTPKT.SPT_GML    = B02_SPT_GML ;                                    
                                                                                
          REGTPKT.DØD_AV_YRKESSKADE  =                                          
                       B02.PERSON(I).STATUS.DØD_AV_YRKESSKADE;                  
                                                                                
          REGTPKT.UTDANNING =                                                   
             B02.PERSON(I).UFØRPENS.UTDANNING;                                  
                                                                                
          REGTPKT.YRKESM_ATTF =                                                 
             B02.PERSON(I).UFØRPENS.YRKESM_ATTF;                                
                                                                                
                                                                                
          IF K > 0  THEN                                                        
            IF (B02.PERSON(I).UFØRHIST(K).BUP_GARANTI >                         
                        B02.PERSON(I).UFØRHIST(K).BUP)        &                 
              (B02.PERSON(I).UFØRHIST(K).BUP_GAR_KODE = 'B' !                   
               B02.PERSON(I).UFØRHIST(K).BUP_GAR_KODE = 'D' !                   
               B02.PERSON(I).UFØRHIST(K).BUP_GAR_KODE = 'E' ) THEN              
              REGTPKT.BUP_GARANTI = 'J';                                        
                                                                                
          IF B02.PERSON(I).STATUS.DØDSDATO_ÅMD  > 0 THEN                        
             DO;                                                                
   /*2000*/     REGTPKT.DØDSDATO = B02.PERSON(I).STATUS.DØDSDATO_ÅMD;           
   /*2000*/     REGTPKT.REGDATO_DØD  = DATO_2000;                               
                                                                                
                W_TRANSKODE          = TRANS_OPPL_OMR.TRANSKODE;                
                W_FNR                = TRANS_OPPL_OMR.FØDSNUMMER;               
                W_VIRKD              = TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD;        
                W_VIRKD_T            = W_VIRKD;                                 
                                                                                
                TRANS_OPPL_OMR.TRANSKODE     = 'DØD';                           
                TRANS_OPPL_OMR.FØDSNUMMER    = B02.PERSON(I).FNR;               
                TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD = W_VIRKD_T;                   
                                                                                
                EXEC CICS LINK PROGRAM                                          
                                    ('R0016601') COMMAREA(KOM_OMR);             
                TRANS_OPPL_OMR.TRANSKODE     = W_TRANSKODE;                     
                TRANS_OPPL_OMR.FØDSNUMMER    = W_FNR;                           
                TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD = W_VIRKD;                     
             END;                                                               
                                                                                
 /*-------------------------------------------------------------------*/        
 /*                                                                   */        
 /*       HVIS DET TIL NÅ ER DANNET ET SEGMENT PÅ DETTE FNR,          */        
 /*       SKAL DET OGSå DANNES ET ROT-SEGMENT.                        */        
 /*                                                                   */        
 /*-------------------------------------------------------------------*/        
                                                                                
          TR0ROT.FNR = B02.PERSON(I).RF0PERSN.FNR;                              
                                                                                
 /*-------------------------------------------------------------------*/        
 /*       DANNING AV ENDRINGSKODER I PENSJON OG BT.                   */        
 /*       KODENE AVVIKLES OG FJERNES FRA BASENE FRA 1.5.91.  BJØRG    */        
 /*-------------------------------------------------------------------*/        
                                                                                
                                                                                
 /*-------------------------------------------------------------------*/        
 /*                                                                   */        
 /*       SKRIV UT ALLE SEGMENTENE PÅ DETTE FNR TIL STAT-BASEN.       */        
 /*                                                                   */        
 /*-------------------------------------------------------------------*/        
                                                                                
          IF TR0ROT.FNR > 0 THEN CALL SKRIV_STAT_SEGM;                          
                                                                                
       END;                                                                     
                                                                                
    /*-------------------------------------------------------------*/           
    /* EVENTUELL UNSAVE AV B01/B02                                 */           
    /*-------------------------------------------------------------*/           
                                                                                
                                                                                
       IF SAVE_B01_B02 = 2 THEN                                                 
       DO;                                                                      
          B01.PERSON(I) = X01.PERSON(I);                                        
          B02.PERSON(I) = X02.PERSON(I);                                        
          FREE X01;                                                             
          FREE X02;                                                             
       END;                                                                     
       IF SAVE_B01_B02 = 1 THEN                                                 
       DO;                                                                      
          B01.PERSON(I) = X01.PERSON(I);                                        
          FREE X01;                                                             
       END;                                                                     
                                                                                
    END; /* OF FNR > 0 */                                                       
                                                                                
 END; /* OF DO I=3 TO 14    ( FRA 1 TIL 12 BARN ) */                            
                                                                                
 END; /* OPPDAT_BARN */                                                         
