 /*   SIST ENDRET PÅ PROD   2004.03.16 14.01.42 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.03.12 13.47.04 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2003.10.02  9.43.27 AV   SPA2970          */        
 /*   SIST ENDRET PÅ PROD   2003.10.01 10.03.36 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2003.09.30 11.57.54 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2003.09.19 12.55.59 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2003.08.22 13.41.52 AV   SPA2970          */        
 /*   SIST ENDRET PÅ PROD   2003.02.27 12.41.27 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2002.11.26  8.39.38 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2002.04.19 12.44.42 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.11.02 13.31.39 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.04.18 14.22.14 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.07.13 12.52.53 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.07.12 12.00.19 AV   JDA7339          */        
 /*       SIST ENDRET 19/05-99 12.37.25 AV   JDA7339                  */        
 /*       SIST ENDRET 19/05-99 12.36.17 AV   JDA7339                  */        
 /*       SIST ENDRET 28/07-98 10.20.36 AV   JDA7339                  */        
 /* ***************************************************************** */        
 /*IDENTIFIKASJON:                                                    */        
 /*     R0011820 - PROCEDYRE I CICS/PLI                               */        
 /*     PROGRAMMERER: KRISTENSEN, NOVEMBER  1982                      */        
 /*                                                                   */        
 /*HENSIKT:                                                           */        
 /*     MODULEN LESER INN FAMILIEN TIL SØKER - FNR OG FORETAR         */        
 /*     AUTOHENDELSE MED BEREGNING PÅ FAMILIEN SLIK AT DEN ER AJOUR   */        
 /*     MED DAGENS DATO.                                              */        
 /*                                                                   */        
 /*PROGRAMTILKNYTNING:                                                */        
 /*     PROGRAMMET BLIR LINKET FRA R0012001 - STYRING AV BEHANDLETRANS*/        
 /*                                                                   */        
 /*BRUK:                                                              */        
 /*     EXEC CICS LINK PROGRAM('R0011820') COMMAREA(KOM_OMR)          */        
 /*  OBS! DETTE ER EN KOPI AV NEST SISTE VERSJON På PROD              */        
 /*       NY VERSJON LAGT PAA PROD 24.10 2002                         */        
 /* ***************************************************************** */        
 R001AU:                                                                        
   PROC(COMMAREA_PEKER)  OPTIONS(MAIN);                                         
                                                                                
 %PAGE;                                                                         
 %INCLUDE P0011801;  /*  BL.OMR                  */                             
 %PAGE;                                                                         
 %INCLUDE P0019908;  /*  KOM_OMR        (BASED)  */                             
 %PAGE;                                                                         
 %INCLUDE P0019906;  /*  TRANS_OPPL_OMR (BASED)  */                             
 %PAGE;                                                                         
 %INCLUDE P0019910;  /*  STYRINGS_OMR   (BASED)  */                             
 %PAGE;                                                                         
 %INCLUDE P0019014;  /*  7001/7101 COPYMBR.      */                             
 %PAGE;                                                                         
 DCL                                                                            
   1 TRANS_LISTE_OMR UNAL BASED      (TRANS_LISTE_PEKER),                       
     3 TRANS_LISTE_LINJE(5),                                                    
                                                                                
 %INCLUDE P0019911;  /*  TRANS-LISTE UNDERNIVÅ   */                             
 %PAGE;                                                                         
 DCL                                                                            
   1 WORK_TRANS_LISTE UNAL BASED      (WORK_TRANS_LISTE_PEKER),                 
                                                                                
 %INCLUDE P0019911;  /*  TRANS- OMR  UNDERNIVÅ   */                             
 %PAGE;                                                                         
 DCL 1 B00 BASED      (B00_PEKER),                                              
                                                                                
 %INCLUDE P0019921;  /*  B01_OMR        (BASED)  */                             
                                                                                
 %PAGE;                                                                         
 DCL 1 B01 BASED      (B01_PEKER),                                              
                                                                                
 %INCLUDE P0019921;  /*  B01_OMR        (BASED)  */                             
 %PAGE;                                                                         
 DCL 1 B02 BASED      (B02_PEKER),                                              
                                                                                
 %INCLUDE P0019921;  /*  B01_OMR        (BASED)  */                             
 %PAGE;                                                                         
 %INCLUDE P0019912;  /*  DIV_PARAM_OMR  (BASED)  */                             
                                                                                
                                                                                
   DCL                                                                          
      I                          FIXED BIN(15),                                 
      IND1                       FIXED BIN(15),                                 
      IND2                       FIXED BIN(15),                                 
      FATAL_FEIL                 CHAR(1) INIT('S'),                             
      ENTER                      CHAR(1) INIT('A');                             
                                                                                
   DCL     COMMAREA_PEKER PTR;                                                  
 %SKIP;                                                                         
                                                                                
    DCL 1 FEIL_STRUC,                                                           
          2 FEIL_NR          FIXED DEC(5),                                      
          2 FEIL_MELDING     CHAR(78),                                          
          2 KOM_OMR_PEKER    POINTER;                                           
                                                                                
   DCL  DATO    PIC '(8)9',                                      /*Y2K*/        
      1 DAT DEF DATO,                                                           
         2 ÅR   PIC'(4)9',                                       /*Y2K*/        
         2 MN   PIC'99',                                                        
         2 DG   PIC'99';                                                        
                                                                                
   DCL (CSTG,SUBSTR,VERIFY, ADDR) BUILTIN;                                      
                                                                                
  DCL W_FNR11       PIC '(11)9';                                                
  DCL W_FNR13       PIC '(13)9';                                                
                                                                                
  DCL  1 W_FNR_R13 DEF W_FNR13,                                                 
         2 DAG          PIC '(2)9',                                             
         2 MND          PIC '(2)9',                                             
         2 ÅR           PIC '(4)9',                                             
         2 ÅRHUNDRE     PIC '9',                                 /*Y2K*/        
         2 FILLER       PIC '(4)9';                              /*Y2K*/        
                                                                                
  DCL W_ÅMD             PIC '(8)9';                              /*Y2K*/        
  DCL  1 W_ÅMD_D        DEF W_ÅMD,                               /*Y2K*/        
         2 ÅR           PIC'(4)9',                               /*Y2K*/        
         2 MND          PIC'(2)9',                               /*Y2K*/        
         2 DAG          PIC'(2)9';                               /*Y2K*/        
                                                                                
      TERMINERINGS_IND             = ENTER; /***   ENTER     ****/              
                                                                                
      KOM_OMR.TRANS_PEKER      = ADDR(KOM_OMR.TRANS_OMR);                       
      KOM_OMR.TRANS_LISTE_PEKER = ADDR(KOM_OMR.TRANS_LISTE_OMR);                
      KOM_OMR.TRANS_OPPL_PEKER  = ADDR(KOM_OMR.TRANS_OPPL_OMR);                 
    /*ALLOCATE TRANS_LISTE_OMR;  */                                             
      ALLOCATE WORK_TRANS_LISTE;                                                
      ALLOCATE FNRTAB;                                                          
      ALLOCATE B00, B01, B02;                                                   
      WORK_TRANS_LISTE             = '';                                        
      DIV_PARAM_OMR.PROGRAM_ID     = 'R0011820';                                
                                                                                
      B00                          = '';                                        
      B01                          = '';                                        
      B02                          = '';                                        
      FNRTAB                       = '';                                        
      ØNSKET_STATUS                = ' ';                                       
      DATO                         = DATO_2000;                  /*Y2K*/        
                                                                                
      TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD = 19911200;               /*Y2K*/        
                                                                                
      DO I = 1 TO 5;                                                            
         TRANS_LISTE_LINJE(I) = WORK_TRANS_LISTE;                               
      END;                                                                      
                                                                                
 /* ***************************************************************** */        
 /* DEN NYE TRANSEN MERKES 'NY'.                                      */        
 /* ***************************************************************** */        
 %SKIP;                                                                         
         TRANS_LISTE_OMR.TRANS_LISTE_LINJE.FAST_DEL.TRANSKODE(1) =              
                                               TRANS_OPPL_OMR.TRANSKODE;        
         TRANS_LISTE_OMR.TRANS_LISTE_LINJE.FAST_DEL.FØDSNUMMER(1) =             
                                              TRANS_OPPL_OMR.FØDSNUMMER;        
      TRANS_LISTE_OMR.TRANS_LISTE_LINJE.FAST_DEL.VIRKNINGSDATO_ÅMD(1) =         
                               TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD; /*Y2K*/        
         TRANS_LISTE_OMR.TRANS_LISTE_LINJE.FAST_DEL.BLANKETTYPE(1) =            
                                             TRANS_OPPL_OMR.BLANKETTYPE;        
         TRANS_LISTE_OMR.TRANS_LISTE_LINJE.FAST_DEL.TRANSTYPE(1) =              
                                               TRANS_OPPL_OMR.TRANSTYPE;        
         TRANS_LISTE_OMR.TRANS_LISTE_LINJE.FAST_DEL.TRANS_RETURKODE(1) =        
                                         TRANS_OPPL_OMR.TRANS_RETURKODE;        
         TRANS_LISTE_OMR.NY_GML_TRANS_IND(1) = 'N';                             
                                                                                
         TRANS_LISTE_OMR.VARIABEL_DEL(1) = KOM_OMR.TRANS_OMR;                   
 %SKIP;                                                                         
 /* ***************************************************************** */        
 /*      LINKER MED CICS TIL OPPRETT_STATUS_FOR_FAMILIEN              */        
 /* ***************************************************************** */        
         EXEC CICS LINK PROGRAM('R0013101') COMMAREA(KOM_OMR);                  
                                                                                
         /** OPPDATERING FOERGÅR I B02   **/                                    
                                                                                
         B02 = B01 ;                                                            
                                                                                
                                                                                
         IF FEIL_MELD_NR > 0 ! DIV_PARAM_OMR.STATUS_TYPE = 'A' THEN             
            DO;                                                                 
               TERMINERINGS_IND = FATAL_FEIL;                                   
               GOTO AVSLUTNING;                                                 
            END;                                                                
                                                                                
          W_ÅMD_D.ÅR  = DAT.ÅR;                                  /*Y2K*/        
          W_ÅMD_D.MND = DAT.MN + 2;                              /*Y2K*/        
          IF W_ÅMD_D.MND > 12 THEN                               /*Y2K*/        
             DO;                                                 /*Y2K*/        
                W_ÅMD_D.ÅR = W_ÅMD_D.ÅR +  1;                    /*Y2K*/        
                W_ÅMD_D.MND = W_ÅMD_D.MND - 12;                  /*Y2K*/        
             END;                                                /*Y2K*/        
          W_ÅMD_D.DAG = 0;                                       /*Y2K*/        
          NESTE_HENDELSE_DATO_ÅMD = W_ÅMD;                       /*Y2K*/        
                                                                                
          /**  BEREGNER SØKER_IND OG EKTEF_IND  **/                             
                                                                                
          SØKER_IND = 0;                                                        
                                                                                
          DO I = 1 TO 14  WHILE (SØKER_IND = 0);                                
             IF B01.RF0PERSN.FNR(I) = TRANS_OPPL_OMR.FØDSNUMMER THEN            
                SØKER_IND = I;                                                  
          END;                                                                  
                                                                                
          IF SØKER_IND = 0 THEN                                                 
             DO;                                                                
                IF F_KJØNN(TRANS_OPPL_OMR.FØDSNUMMER) = 'K' THEN                
                   SØKER_IND = 1;                                               
                ELSE                                                            
                   SØKER_IND = 2;                                               
             END;                                                               
                                                                                
          IF SØKER_IND = 1 THEN                                                 
             EKTEF_IND = 2;                                                     
          ELSE                                                                  
             IF SØKER_IND = 2 THEN                                              
                EKTEF_IND = 1;                                                  
                                                                                
    W_FNR11 = B02.FNR(SØKER_IND);                                               
                                                                                
    W_FNR13 = KONV_FNR11_FNR13(W_FNR11);                                        
                                                                                
   /*     KALLER BEREGNING MED TRANSTYPE = 27(GRBL.ENDRING). */                 
                                                                                
    IF B02.VIRK_DATO_ÅMD(SØKER_IND) > 19920000 THEN              /*Y2K*/        
       DO;                                                                      
         TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD = DATO;                /*Y2K*/        
      TRANS_LISTE_OMR.TRANS_LISTE_LINJE.FAST_DEL.VIRKNINGSDATO_ÅMD(1) =         
                            TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD;    /*Y2K*/        
       END;                                                                     
                                                                                
          IF AUS.STBREV = 'J' THEN                                              
             DO;                                                                
                TRANS_OPPL_OMR.TRANSTYPE =                                      
                                          AUS.TRANSTYPE;                        
                DIV_PERIODE.INTENDR_ÅR   = 1992;  /*200205*/                    
                STØNADSBREV_ØNSKET_IND   = '1'B;                                
                                                                                
             END;                                                               
          ELSE                                                                  
             TRANS_OPPL_OMR.TRANSTYPE = 27;                                     
                                                                                
        DO IND1 = 1 TO 14 UNTIL (B01.STATUS.VIRK_DATO_ÅMD(IND1) > 0);           
        END;                                                                    
                                                                                
          DO IND2 = 1 TO 14;                                                    
             IF B01.STATUS.VIRK_DATO_ÅMD   (IND2) >  0     &     /*Y2K*/        
                B01.STATUS.STATUS_KODE_HIST(IND2) = ' '    THEN                 
                 DO;                                                            
                   IF B01.ETTEPENS.FORVENTET     (IND2) >  0  &                 
                      B01.GT_TILLEGG_LOV92(IND2)   = 0        THEN              
                    DO;                                                         
                       DCL FORV  FIXED DEC(7) INIT (0);                         
                       DCL FORV2 FIXED DEC(7) INIT (0);                         
                       FORV =                                                   
                          B02.ETTEPENS.FORVENTET (IND2);                        
                       CALL RULL_FORVENTET(                                     
                            B01.STATUS.G_DATO_ÅMD  (IND2),       /*Y2K*/        
                            B01.STATUS.VIRK_DATO_ÅMD(IND2),      /*Y2K*/        
                            B02.ETTEPENS.FORVENTET (IND2));                     
                       FORV2 =                                                  
                          B02.ETTEPENS.FORVENTET (IND2);                        
                    END;                                                        
                                                                                
                   IF  (W_FNR_R13.ÅR              > 1925 &     /*Y2K*/          
                        B02.KONV_P_KODE(IND2)  ^= 'K') !                        
                      (B02.KONV_P_KODE(IND2)  = 'K'   &                         
                       B02.UTTAKSDATO_ÅMD(IND2) > 19920000 ) THEN/*Y2K*/        
                       DO;                                                      
                         IF B01.ALDERSP.FAI    (IND2) >  0  THEN                
                           DO;                                                  
                               DCL W_FAI FIXED DEC(7) INIT (0);                 
                               DCL FAI2 FIXED DEC(7) INIT (0);                  
                               W_FAI=                                           
                               B02.ALDERSP.FAI (IND2);                          
                             CALL RULL_FAI(                                     
                                  B01.STATUS.G_DATO_ÅMD  (IND2), /*Y2K*/        
                                  B01.STATUS.VIRK_DATO_ÅMD(IND2),/*Y2K*/        
                                  B02.ALDERSP.FAI(IND2));                       
                                FAI2 =                                          
                                    B02.ALDERSP.FAI(IND2);                      
                           END;                                                 
                       END;                                                     
                                                                                
                   IF B01.FORSI.ARBEIDSINNTEKT(IND2) >  0 THEN                  
                    DO;                                                         
                       DCL INNT  FIXED DEC(7) INIT (0);                         
                       DCL INNT2 FIXED DEC(7) INIT (0);                         
                       INNT =                                                   
                          B02.FORSI.ARBEIDSINNTEKT(IND2);                       
                       B02.FORSI.ARBEIDSINNTEKT(IND2) =                         
                           F_RULL_FORSI(                                        
                            B01.STATUS.G_DATO_ÅMD  (IND1),       /*Y2K*/        
                            B01.STATUS.VIRK_DATO_ÅMD(IND1),      /*Y2K*/        
                            B02.FORSI.ARBEIDSINNTEKT(IND2));                    
                       INNT2 =                                                  
                          B02.FORSI.ARBEIDSINNTEKT(IND2);                       
                    END;                                                        
                                                                                
                   IF B01.FORSI.PENSJONSINNTEKT(IND2) >  0 THEN                 
                    DO;                                                         
                       DCL PINNT  FIXED DEC(7) INIT (0);                        
                       DCL PINNT2 FIXED DEC(7) INIT (0);                        
                       PINNT =                                                  
                          B02.FORSI.PENSJONSINNTEKT(IND2);                      
                       B02.FORSI.PENSJONSINNTEKT(IND2) =                        
                           F_RULL_FORSI(                                        
                            B01.STATUS.G_DATO_ÅMD  (IND1),       /*Y2K*/        
                            B01.STATUS.VIRK_DATO_ÅMD(IND1),      /*Y2K*/        
                            B02.FORSI.PENSJONSINNTEKT(IND2));                   
                       PINNT2 =                                                 
                          B02.FORSI.PENSJONSINNTEKT(IND2);                      
                    END;                                                        
                                                                                
                   IF B01.FORSI.ARBEIDSINNTEKT_EK(IND2) >  0 THEN               
                    DO;                                                         
                       DCL AINNT  FIXED DEC(7) INIT (0);                        
                       DCL AINNT2 FIXED DEC(7) INIT (0);                        
                       AINNT =                                                  
                          B02.FORSI.ARBEIDSINNTEKT_EK(IND2);                    
                       B02.FORSI.ARBEIDSINNTEKT_EK(IND2) =                      
                           F_RULL_FORSI(                                        
                            B01.STATUS.G_DATO_ÅMD   (IND1),      /*Y2K*/        
                            B01.STATUS.VIRK_DATO_ÅMD(IND1),      /*Y2K*/        
                            B02.FORSI.ARBEIDSINNTEKT_EK(IND2));                 
                       AINNT2 =                                                 
                          B02.FORSI.ARBEIDSINNTEKT_EK(IND2);                    
                    END;                                                        
                                                                                
                   IF B01.FORSI.PENSJONSINNTEKT_EK(IND2) >  0 THEN              
                    DO;                                                         
                       DCL EINNT  FIXED DEC(7) INIT (0);                        
                       DCL EINNT2 FIXED DEC(7) INIT (0);                        
                       EINNT =                                                  
                          B02.FORSI.PENSJONSINNTEKT_EK(IND2);                   
                       B02.FORSI.PENSJONSINNTEKT_EK(IND2) =                     
                           F_RULL_FORSI(                                        
                            B01.STATUS.G_DATO_ÅMD  (IND1),       /*Y2K*/        
                            B01.STATUS.VIRK_DATO_ÅMD(IND1),      /*Y2K*/        
                            B02.FORSI.PENSJONSINNTEKT_EK(IND2));                
                      EINNT2 =                                                  
                          B02.FORSI.PENSJONSINNTEKT_EK(IND2);                   
                    END;                                                        
                END;                                                            
          END;                                                                  
                                                                                
      KJØRINGS_TYPE = 'A';                                                      
                                                                                
          EXEC CICS LINK PROGRAM('R0014001') COMMAREA(KOM_OMR);                 
                                                                                
          IF FEIL_MELD_NR > 0 THEN                                              
             DO;                                                                
                TERMINERINGS_IND = FATAL_FEIL;                                  
                GOTO AVSLUTNING;                                                
             END;                                                               
                                                                                
          IF TRANS_OPPL_OMR.TRANSTYPE          ^= 27 THEN                       
             DO;                                                                
                                                                                
                 EXEC CICS LINK PROGRAM('R0017001')                             
                                    COMMAREA(KOM_OMR);                          
        /* DO LOOP DANNET AV SATISH ANGåENDE BREV DATO I ROOT  */               
                                                                                
                B01.SB_DATO_ÅMD (SØKER_IND) = DATO_2000;                        
                B01.BRUKER_ID (SØKER_IND)   = DIV_PARAM_OMR.BRUKER_ID;          
        /*       ****************************  OPPTIL   */                      
             END;                                                               
                                                                                
          IF SUBSTR(CICS_IND,2) ^= 'A'           THEN                           
             DO;        /*9905*/                                                
                IF B01.G_DATO_ÅMD(IND1) > B01.VIRK_DATO_ÅMD(IND1) THEN          
                   B02 = B01;                                                   
             EXEC CICS LINK PROGRAM('R0014901') COMMAREA(KOM_OMR);              
             END;                                                               
          ELSE                                                                  
             B01 = B02;                                                         
                                                                                
          IF FEIL_MELD_NR > 0 THEN                                              
             DO;                                                                
                TERMINERINGS_IND = FATAL_FEIL;                                  
                GOTO AVSLUTNING;                                                
             END;                                                               
                                                                                
          /*       SKRIV_STATUS_RECS_TIL_BASEN   */                             
                                                                                
                                                                                
          EXEC CICS LINK PROGRAM('R0015401')                                    
                                            COMMAREA(KOM_OMR);                  
                                                                                
          IF FEIL_MELD_NR > 0 THEN                                              
             TERMINERINGS_IND = FATAL_FEIL;                                     
                                                                                
          IF TRANS_OPPL_OMR.TRANSTYPE          ^= 27 THEN                       
             DO;                                                                
     /*******   CALL                                                            
                    JUSTER-SB-ROTSEGMENTSTØNADSBREV-BASEN                       
                          (ST-BREV-NØKKEL-TABELL)  *****/                       
                                                                                
                                                                                
                EXEC CICS LINK PROGRAM('R0017101')                              
                                    COMMAREA(KOM_OMR);                          
                                                                                
             END;                                                               
                                                                                
  AVSLUTNING:                                                                   
     IF TERMINERINGS_IND = FATAL_FEIL THEN                                      
       DO;                                                                      
         IF KJØRINGS_TYPE = 'N'   !                                             
            KJØRINGS_TYPE = 'O'    THEN                                         
            DO;                                                                 
              IF KJØRINGS_TYPE ^= 'O'  THEN                                     
                 FEIL_MELD_NR = 9999;                                           
               EXEC CICS RETURN;                                                
            END;                                                                
                                                                                
         FEIL_MELDING = 'PROGRAM ' !! DIV_PARAM_OMR.PROGRAM_ID !!               
                           ' GÅR I FEIL VED LABEL NR. '        !!               
                           DIV_PARAM_OMR.FEIL_VED_LABEL;                        
                                                                                
         EXEC CICS PURGE MESSAGE;                                               
         EXEC CICS SEND TEXT FROM (FEIL_MELDING) ACCUM ERASE PAGING             
                                                       JUSTIFY(14);             
                                                                                
         IF DIV_PARAM_OMR.STATUS_TYPE = 'A' THEN                                
            DO;                                                                 
               IF  F_KJØNN(TRANS_OPPL_OMR.FØDSNUMMER)  = 'K' THEN               
                  SØKER_IND = 1;                                                
               ELSE                                                             
                  SØKER_IND = 2;                                                
               IF B02.STATUS_KODE_HIST(SØKER_IND) = 'O' THEN                    
                  /* STATUS TYPE = A2 */                                        
                  FEIL_MELDING = 'STATUS TYPE = A, ' !!                         
                                 'PERSONEN MED FØDSELSNR : ' !!                 
                                 TRANS_OPPL_OMR.FØDSNUMMER !!                   
                                 ' ER OPPHØRT I DATABASEN';                     
               ELSE                                                             
                  /* STATUS TYPE = A1 */                                        
                  FEIL_MELDING = 'STATUS TYPE = A, ' !!                         
                                 'PERSONEN MED FØDSELSNR : ' !!                 
                                 TRANS_OPPL_OMR.FØDSNUMMER !!                   
                                 ' FINNES IKKE I DATABASEN';                    
            END;                                                                
         ELSE                                                                   
            DO;                                                                 
               FEIL_STRUC.KOM_OMR_PEKER = COMMAREA_PEKER;                       
               FEIL_STRUC.FEIL_NR       = FEIL_MELD_NR;                         
               EXEC CICS LINK PROGRAM('R0019921') COMMAREA(FEIL_STRUC);         
            END;                                                                
                                                                                
         EXEC CICS SEND TEXT FROM (FEIL_MELDING)  JUSTIFY(18)                   
                                  ACCUM PAGING;                                 
         FEIL_MELDING = 'TRYKK F3..............!!!!                  ' ;        
                                                                                
         EXEC CICS SEND TEXT FROM (FEIL_MELDING)  JUSTIFY(22)                   
                                  ACCUM PAGING;                                 
         EXEC CICS SEND PAGE;                                                   
                                                                                
         /******** NB!   NB! TASKEN BLIR KANSELERT  ********/                   
         EXEC CICS ABEND;                                                       
       END;                                                                     
                                                                                
   TRANS_OPPL_OMR.TRANS_RETURKODE = 'R030';                                     
 /*  FJERNT AV SATISH .......06.06.2003     ********                            
    EXEC CICS SEND MAP('S001014') MAPSET('S001013')                             
                             ERASE MAPONLY ;                                    
  */                                                                            
   EXEC CICS RETURN;                                                            
                                                                                
                                                                                
 %PAGE;                                                                         
 %INCLUDE R0019902;     /**** F_KJØNN         ****/                             
 %PAGE;                                                                         
 %INCLUDE R0019954;     /**** RULL_FORVENTET  ****/                             
 %PAGE;                                                                         
 %INCLUDE R0019967;     /**** RULL_FAI        ****/                             
 %PAGE;                                                                         
 %INCLUDE R0019968;     /**** RULL_FORSI      ****/                             
 %PAGE;                                                                         
 %INCLUDE R0019995;   /* FNR11 -->  FNR13             2000   */                 
                                                                                
                                                                                
 END R001AU;                                                                    
