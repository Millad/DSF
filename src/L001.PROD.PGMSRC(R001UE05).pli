 /*   SIST ENDRET PÅ PROD   2006.04.07  9.56.55 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2006.01.05  8.37.24 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2005.08.30  8.53.54 AV   TSB2970          */        
 /*   SIST ENDRET PÅ PROD   2005.06.08 13.18.02 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2005.04.26 12.53.52 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2004.12.17 14.34.27 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.11.01  8.18.07 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2004.08.06 12.53.49 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.07.13 12.47.42 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2002.04.19 12.14.44 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.09.27  8.42.45 AV   JDA2970          */        
 /*       SIST ENDRET 26/03-99 13.24.47 AV   JDA7339                  */        
 /*       SIST ENDRET 26/02-99 09.12.52 AV   JDA7339                  */        
 /*       SIST ENDRET 22/06-98 12.41.16 AV   JDA7339                  */        
 /*       SIST ENDRET 15/06-98 08.00.13 AV   RFA9991                  */        
 /*IDENTIFIKASJON:                                                    */        
 /*    R001UE05 - UNDERPROG  I PLI                                    */        
 /*    PROGRAMMERER: HERMAN, FEB 94                                   */        
 /*HENSIKT:                                                           */        
 /*    PROSEDYRENE FORETAR KONTROLL AV DATA FOR EN_KAP7,EN_KAP8 OG    */        
 /*    EN_KAP10 OG EN_KAP11 OG EN_SPES FRA ENDRINGSBLANKETT           */        
 /*PROGRAMTILKNYTNING:                                                */        
 /*    PROSEDYRENE INNGÅR SOM EN DEL AV BEHANDLINGEN AV ENDRINGS-     */        
 /*    BLANKETTEN.                                                    */        
 /*    INCLUDES I  R001UE01.                                          */        
 /*BRUK:                                                              */        
 /*    CALL KONTROLL_EN_KAP7 (FEIL_I_EN_KAP7);                        */        
 /*    CALL KONTROLL_EN_KAP8 (FEIL_I_EN_KAP8);                        */        
 /*    CALL KONTROLL_EN_KAP10(FEIL_I_EN_KAP10);                       */        
 /*    CALL KONTROLL_EN_KAP11(FEIL_I_EN_KAP11);                       */        
 /*    CALL KONTROLL_EN_SPES (FEIL_I_EN_SPES );                       */        
 /******************************************************************* */        
 /*DATASETTOPPLYSNINGER:                                              */        
 /* ***********************                                           */        
 /*   STATTAB - STATSBORGERSKAPSTABELL - EXEC CICS READ DATASET       */        
 /*                                      ('STATTAB')........          */        
 /* ***************************************************************** */        
 /*                                                                   */        
 /*   MODULEN KONTROLLERER EN_KAP7 (ENDRING I INNTEKT                 */        
 /*         OG RETURNERER MED FEIL_FUNNET= '1'B HVIS FEIL, '0'B       */        
 /*   HVIS RIKTIG                                                     */        
 /*                                                                   */        
 /* ***************************************************************** */        
 KONTROLL_EN_KAP7:                                                              
   PROC (FEIL_FUNNET);                                                          
   DCL                                                                          
      FEIL_FUNNET BIT (1);                                                      
                                                                                
      IF ENK7.FAI > 0 THEN                                                      
         IF ^ F_NUMERISK((ENK7.FAI))  THEN                                      
            DO;                                                                 
               FEIL_FUNNET = '1'B;                                              
               ENK7.FAINR = 200;                                                
            END;                                                                
                                                                                
 END KONTROLL_EN_KAP7;                                                          
 /* ================================================================= */        
 KONTROLL_EN_KAP8:                                                              
    PROC (FEIL_FUNNET);                                                         
    /* ************************************************************** */        
    /*                                                                */        
    /*   MODULEN KONTROLLERER EN_KAP8(REDUKSJON AV UFØREGRAD) OG      */        
    /*   RETURNERER MED FEIL_FUNNET = '1'B HVIS FEIL, '0'B ELLERS     */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
    DCL                                                                         
        FEIL_FUNNET BIT (1);                                                    
                                                                                
    IF   (VERIFY(ENK8.REAKTIVISERING,'SX ') ^=0)    THEN                        
       DO;                                                                      
          FEIL_FUNNET = '1'B;                                                   
          ENK8.REAKTIVISERINGNR = 429;                                          
       END;                                                                     
                                                                                
    IF ENK8.REAKTIVISERING = 'S'           &                                    
       ENS.VIRK_DATO_ÅMD < 20010900             THEN                            
       DO;                                                                      
          FEIL_FUNNET = '1'B;                                                   
          ENK8.REAKTIVISERINGNR = 625; /*NY KODE*/                              
       END;                                                                     
                                                                                
    /* -------------------------------------------------------------- */        
    /* SKJERMET ATTFØRING TESTES I R0019804                           */        
    /* -------------------------------------------------------------- */        
                                                                                
    IF (VERIFY(ENK8.UNNTAK_VENTETID,'S ') ^=0) THEN                             
       DO;                                                                      
          FEIL_FUNNET = '1'B;                                                   
          ENK8.UNNTAK_VENTETIDNR = 665;                                         
       END;                                                                     
                                                                                
                                                                                
                                                                                
                                                                                
                                                                                
                                                                                
                                                                                
    IF ENK8.REAKTIVISERING  = 'S'         &                                     
       ENK8.UNNTAK_VENTETID = 'S'                   THEN                        
       DO;                                                                      
          FEIL_FUNNET = '1'B;                                                   
          ENK8.REAKTIVISERINGNR = 442;                                          
       END;                                                                     
                                                                                
    IF ENK8.REAKTIVISERING  = 'X'         &                                     
       ENK8.UNNTAK_VENTETID = 'X'                   THEN                        
       DO;                                                                      
          FEIL_FUNNET = '1'B;                                                   
          ENK8.REAKTIVISERINGNR = 442;                                          
       END;                                                                     
                                                                                
    /* -------------------------------------------------------------- */        
    /* UFØREGRAD UFØREPENSJON TESTES FORELØPIG HER                    */        
    /* -------------------------------------------------------------- */        
    IF   ENK8.UFG > 0           &                                               
         ENK8.UFG < 999                   THEN                                  
       DO;                                                                      
          IF   (^ F_NUMERISK(F_FELT_PIC_CHAR3(ENK8.UFG)))  THEN                 
             DO;                                                                
                 FEIL_FUNNET = '1'B;                                            
                 ENK8.UFGNR  = 200;                                             
             END;                                                               
          ELSE                                                                  
          IF   ^(ENK8.UFG = 15 ! ENK8.UFG = 20 ! ENK8.UFG = 25 !                
                 ENK8.UFG = 30 ! ENK8.UFG = 35 ! ENK8.UFG = 40 !                
                 ENK8.UFG = 45 ! ENK8.UFG = 50 ! ENK8.UFG = 55 !                
                 ENK8.UFG = 60 ! ENK8.UFG = 65 ! ENK8.UFG = 70 !                
                 ENK8.UFG = 75 ! ENK8.UFG = 80 ! ENK8.UFG = 85 !                
                 ENK8.UFG = 100 !                                               
                 ENK8.UFG = 90 ! ENK8.UFG = 95 ! ENK8.UFG = 999 )               
                   & ENS.VIRK_DATO_ÅMD < 20010900      THEN                     
              DO;                                                               
                 FEIL_FUNNET = '1'B;                                            
                   /* ----------------------------------------------- */        
                   /* FEILMELD 38: UGYLDIG UFØREGRAD                            
                   /* ----------------------------------------------- */        
                 ENK8.UFGNR = 38;                                               
              END;                                                              
         END;                                                                   
                                                                                
    /* -------------------------------------------------------------- */        
    /* TRE ÅRS FRYSPERIODE VED ARBEIDSFØRSØK                          */        
    /* -------------------------------------------------------------- */        
                                                                                
    IF   (VERIFY(ENK8.FRYSP_5ÅR_ARB,'SX ') ^=0)     THEN                        
       DO;                                                                      
          FEIL_FUNNET = '1'B;                                                   
          ENK8.FRYSP_5ÅR_ARBNR = 429;                                           
       END;                                                                     
                                                                                
    /* -------------------------------------------------------------- */        
    /* FRYSPERIODE VED TIDSBEGRENSET ARBEIDSFORSØK                    */        
    /* -------------------------------------------------------------- */        
                                                                                
    IF   (VERIFY(ENK8.FRYSP_TIDSB,'SX ') ^=0)     THEN                          
       DO;                                                                      
          FEIL_FUNNET = '1'B;                                                   
          ENK8.FRYSP_TIDSBNR = 429;                                             
       END;                                                                     
                                                                                
    /* -------------------------------------------------------------- */        
    /* DET KAN IKKE VÆRE LIKE KODER I FRYS-FELTENE                    */        
    /* -------------------------------------------------------------- */        
    IF   ENK8.FRYSP_TIDSB = ENK8.FRYSP_5ÅR_ARB     &                            
        (ENK8.FRYSP_TIDSB ^= ' ')                      THEN                     
       DO;                                                                      
          FEIL_FUNNET = '1'B;                                                   
          ENK8.FRYSP_5ÅR_ARBNR = 437;                                           
       END;                                                                     
                                                                                
                                                                                
    /* -------------------------------------------------------------- */        
    /* FRYSP_5ÅR_ARB KAN IKKE OPPHØRES                                */        
    /*   (OPPHØR SKJER VED AT UP FÅR MINST GARANTIGRAD) - REGEL                 
       OPPHEVET  IF   ENK8.FRYSP_5ÅR_ARB  = 'X'  &                              
         ENS.VIRK_DATO_ÅMD < 20010000 &                                         
         ENK8.FRYSP_TIDSB   ^= 'S'             THEN                             
       DO;                                                                      
          FEIL_FUNNET = '1'B;                                                   
          ENK8.FRYSP_5ÅR_ARBNR = 443;                                           
       END;                                                                     
      --------------HL 200201------------------------ */                        
    /* ---------------------------------------------------------- */            
    /* 1.26 LØNNSTILSKUDD               200501              §     */            
    /* --------------------------------------------------------   */            
    IF (VERIFY(ENK8.LØNNSTILSKUDD,'SX ') ^=0)   THEN                            
       DO;                                                                      
          FEIL_FUNNET = '1'B;                                                   
          ENK8.LØNNSTILSKUDDNR = 429;                                           
       END;                                                                     
    IF ENK8.LØNNSTILSKUDD = 'S'              THEN                               
       DO;                                                                      
          IF (ENK8.UFG > 0     &                                                
             (ENK8.UFG < 20      )             !                                
              ENK8.UFG > 95           )        !                                
             (MOD(ENK8.UFG,5 ) ^= 0 ) THEN                                      
             DO;                                                                
                IF ENK8.UFG ^= 999         THEN                                 
                DO;                                                             
                   FEIL_FUNNET = '1'B;                                          
                   ENK8.UFGNR  = 685;                                           
                END;                                                            
             END;                                                               
          ELSE                                                                  
     /*   IF ENK8.FRYSP_5ÅR_ARB ^= 'S'     THEN                                 
             DO;                                                                
                FEIL_FUNNET = '1'B;                                             
                ENK8.LØNNSTILSKUDDNR = 674;                                     
             END;                                                               
          ELSE     */                                                           
          IF ENS.VIRK_DATO_ÅMD < 20050100             THEN                      
             DO;                                                                
                FEIL_FUNNET = '1'B;                                             
                ENK8.LØNNSTILSKUDDNR = 676;                                     
             END;                                                               
       END;                                                                     
    /* ---------------------------------------------------------- */            
    /* -------------------------------------------------------------- */        
    /* KONTROLL AV INNTEKT-FØR-UP                                     */        
    /* -------------------------------------------------------------- */        
    IF ENK8.INNTEKT_FØR_UP > 0  THEN                                            
       DO;                                                                      
          IF SUBSTR(ENK8.INNT_DATO_ÅMD,1,4) < 1965 THEN                         
           DO;                                                                  
              FEIL_FUNNET =    '1'B;                                            
              ENK8.INNT_DATO_ÅMDNR= 040;                                        
           END;                                                                 
       END;                                                                     
 END KONTROLL_EN_KAP8;                                                          
                                                                                
 /* ***************************************************************** */        
 /*                                                                   */        
 /*   MODULEN KONTROLLERER EN_KAP10 (DIVERSE ENDRINGER) OG RETURNERER */        
 /*   MED FEIL_FUNNET = '1'B HVIS FEIL, '0'B HVIS RIKTIG              */        
 /*                                                                   */        
 /* ***************************************************************** */        
                                                                                
 KONTROLL_EN_KAP10:                                                             
   PROC (FEIL_FUNNET);                                                          
   DCL                                                                          
      FEIL_FUNNET     BIT (1);                                                  
                                                                                
   DCL                                                                          
      DAGENS_DATO_ÅMD PIC '99999999',                                           
      DAGENS_DATO_ÅM  DEF DAGENS_DATO_ÅMD PIC '999999',                         
      DAGENS_DATO_Å   DEF DAGENS_DATO_ÅMD PIC '9999';                           
                                                                                
 /* ***************************************************************** */        
 /*  HJELPE DCL FOR Å SNU DATO                                        */        
 /* ***************************************************************** */        
                                                                                
   DCL                                                                          
      HJ_FRAML_DATO_ÅMD PIC '99999999',                                         
      FRAML_DATO_ÅM    DEF HJ_FRAML_DATO_ÅMD POS(1) PIC '999999',               
      FRAML_DATO_Å     DEF HJ_FRAML_DATO_ÅMD POS(1) PIC '9999',                 
      FRAML_DATO_M     DEF HJ_FRAML_DATO_ÅMD POS(5) PIC '99';                   
   DCL                                                                          
      HJ_FRAML_DATO_MÅ PIC '999999',                                            
      HJ_FRAML_DATO_M  DEF HJ_FRAML_DATO_MÅ POS(1) PIC '99',                    
      HJ_FRAML_DATO_Å  DEF HJ_FRAML_DATO_MÅ POS(3) PIC '9999';                  
                                                                                
      DAGENS_DATO_ÅMD = DATO_2000;                                              
      IF ENK10.FORVENTET_INNT > 0 THEN                                          
   /* IF ^F_NUMERISK(ENK10.FORVENTET_INNT) THEN */                              
   /* JFA 6.6.2002 */                                                           
      IF ^F_NUMERISK(F_FELT_PIC_CHAR7(ENK10.FORVENTET_INNT)) THEN               
            DO;                                                                 
               FEIL_FUNNET       = '1'B;                                        
               ENK10.FORVENTET_INNTNR = 200;                                    
            END;                                                                
 /* **************************************************************** */         
 /*   FEILMELD 150: FEIL I OVEGANG TIL PENSJON                       */         
 /* **************************************************************** */         
      IF ^(ENK10.O_TIL_PENSJON = 'X' ! ENK10.O_TIL_PENSJON = ' ') THEN          
         DO;                                                                    
            FEIL_FUNNET           = '1'B;                                       
            ENK10.O_TIL_PENSJONNR =150;                                         
         END;                                                                   
                                                                                
    IF VERIFY(ENK10.FRADRAG_2G,'JN ') ^= 0 THEN                                 
       DO;                                                                      
           FEIL_FUNNET = '1'B;                                                  
           ENK10.FRADRAG_2GNR = 341;                                            
       END;                                                                     
                                                                                
    IF ENS.VIRK_DATO_ÅMD < 20010000   THEN                                      
            ENK10.FRADRAG_2G = ' ';                                             
    ELSE                                                                        
    IF ENK10.FRADRAG_2G = 'N'      THEN                                         
       IF ENK10.FORVENTET_INNT = 9999999   THEN                                 
       DO;                                                                      
           FEIL_FUNNET = '1'B;                                                  
           ENK10.FRADRAG_2GNR = 342;                                            
       END;                                                                     
                                                                                
                                                                                
 /* **************************************************************** */         
 /*   FEILMELD 151: FEIL I OPPHØR-EO                                 */         
 /* **************************************************************** */         
                                                                                
      IF ENK10.FRAML_DATO_ÅMD> 0 THEN                                           
         IF ^ F_NUMERISK(F_FELT_PIC_CHAR8(ENK10.FRAML_DATO_ÅMD)) THEN           
            DO;                                                                 
               FEIL_FUNNET           = '1'B;                                    
               ENK10.FRAML_DATO_ÅMDNR= 200;                                     
            END;                                                                
         ELSE                                                                   
            IF ENK10.FRAML_DATO_ÅMD> 0 THEN                                     
                                                                                
 /* **************************************************************** */         
 /*            FUNKSJONEN F_GYLDIG_DATO KONTROLLERER OM EN DATO(ÅR/  */         
 /*            MND) ER GYLDIG. HVIS GYLDIG RETURNERES '1'B, HVIS     */         
 /*            IKKE GYLDIG '0'B.                                     */         
 /* **************************************************************** */         
                                                                                
 /* **************************************************************** */         
 /*            FEILMELD 71: UGYLDIG FRAMLEGGSDATO                    */         
 /* **************************************************************** */         
               IF ^ F_GYLDIG_DATO(ENK10.FRAML_DATO_ÅMD) THEN                    
                  DO;                                                           
                     FEIL_FUNNET           = '1'B;                              
                                                                                
                                                                                
                     ENK10.FRAML_DATO_ÅMDNR= 71;                                
                  END;                                                          
 /* **************************************************************** */         
 /*   FEILMELD 151: FEIL I OPPHØR-EO                                 */         
 /* **************************************************************** */         
      IF (VERIFY (ENK10.OPPHØR_EO,'JN ') ^= 0)  THEN                            
         DO;                                                                    
            FEIL_FUNNET       = '1'B;                                           
            ENK10.OPPHØR_EONR = 151;                                            
         END;                                                                   
      ELSE                                                                      
 /* **************************************************************** */         
 /*      FEILMELD 184: OPPHØR-EO ER UTFYLT,FRAMLEGGSDATO IKKE UTFYLT */         
 /* **************************************************************** */         
         IF ENK10.OPPHØR_EO ^= ' ' & ENK10.FRAML_DATO_ÅMD= 0 THEN               
            DO;                                                                 
               FEIL_FUNNET       = '1'B;                                        
               ENK10.OPPHØR_EONR = 184;                                         
            END;                                                                
         ELSE                                                                   
 /* **************************************************************** */         
 /*         FEILMELD 187: OPPHØR_EO IKKE UTFYLT,FRAML_DATO ER UTFYLT */         
 /* **************************************************************** */         
            IF ENK10.OPPHØR_EO = ' ' & ENK10.FRAML_DATO_ÅMD> 0 THEN             
               DO;                                                              
                  FEIL_FUNNET       = '1'B;                                     
                  ENK10.OPPHØR_EONR = 187;                                      
               END;                                                             
                                                                                
 /* ***************************************************************** */        
 /*   KRYSS KONTROLLER                                                */        
 /* ***************************************************************** */        
                                                                                
      IF ^ FEIL_FUNNET THEN                                                     
         DO;                                                                    
            HJ_FRAML_DATO_ÅMD= ENK10.FRAML_DATO_ÅMD;                            
                                                                                
            IF (ENK10.FRAML_DATO_ÅMD> 0 ! ENK10.OPPHØR_EO ^= ' ' !              
     /*         ENK10.TP_PROSENT    < 999 ! */                                  
                ENK10.O_TIL_PENSJON ^= ' ') &                                   
                ENK10.FORVENTET_INNT = 9999999   THEN                           
                  DO;                                                           
                     FEIL_FUNNET           = '1'B;                              
                     ENK10.FORVENTET_INNTNR     = 88;                           
                  END;                                                          
            IF ENK10.FRAML_DATO_ÅMD> 0 THEN                                     
               IF HJ_FRAML_DATO_ÅMD< DAGENS_DATO_ÅMD THEN                       
                  DO;                                                           
                     FEIL_FUNNET           = '1'B;                              
                     ENK10.FRAML_DATO_ÅMDNR= 72;                                
                  END;                                                          
         END;                                                                   
   END KONTROLL_EN_KAP10;                                                       
                                                                                
 KONTROLL_EN_KAP11:                                                             
   PROC (FEIL_FUNNET);                                                          
   DCL                                                                          
      FEIL_FUNNET BIT (1);                                                      
                                                                                
      IF ENK11.YUG > 0       &                                                  
         ENK11.YUG < 999 THEN                                                   
         DO;                                                                    
            IF ^ F_NUMERISK(F_FELT_PIC_CHAR3(ENK11.YUG)) THEN                   
               DO;                                                              
                  FEIL_FUNNET = '1'B;                                           
                  ENK11.YUGNR = 200;                                            
               END;                                                             
            ELSE /* JFA 6.4.05 R 978 */                                         
            IF (ENK8.FRYSP_5ÅR_ARB  = 'S')        THEN                          
               DO;                                                              
                  IF (ENK11.YUG < 15 ! ENK11.YUG > 100)  THEN                   
                     DO;                                                        
                        FEIL_FUNNET = '1'B;                                     
                        ENK11.YUGNR = 307;                                      
                     END;                                                       
                  ELSE                                                          
                  IF ENK8.UFG < ENK11.YUG    THEN                               
                     DO;                                                        
                        FEIL_FUNNET = '1'B;                                     
                        ENK8.UFGNR  = 317;                                      
                        ENK11.YUGNR = 999;                                      
                     END;                                                       
               END;                                                             
            ELSE                                                                
 /* *********************************************************** */              
 /*            FEILMELD 307: UGYLDIG UFØREGRAD                  */              
 /* *********************************************************** */              
             IF ^(ENK11.YUG = 15 ! ENK11.YUG = 20 !                             
                  ENK11.YUG = 25 ! ENK11.YUG = 30 !                             
                  ENK11.YUG = 35 ! ENK11.YUG = 40 !                             
                  ENK11.YUG = 45 ! ENK11.YUG = 50 !                             
                  ENK11.YUG = 55 ! ENK11.YUG = 60 !                             
                  ENK11.YUG = 65 ! ENK11.YUG = 70 !                             
                  ENK11.YUG = 75 ! ENK11.YUG = 80 !                             
                  ENK11.YUG = 85 ! ENK11.YUG = 90 !                             
                  ENK11.YUG = 95 ! ENK11.YUG = 999)  THEN                       
                                                                                
                  DO;                                                           
                     FEIL_FUNNET = '1'B;                                        
                     ENK11.YUGNR = 307;                                         
                  END;                                                          
             ELSE                                                               
                IF ENK8.UFG > 0  &                                              
                   ENK8.UFG < 999  THEN                                         
                   DO;                                                          
                      IF ENK8.UFG  < 50        &                                
                         ENK8.UFG ^= ENK11.YUG    THEN                          
                         DO;                                                    
                            FEIL_FUNNET = '1'B;                                 
                            ENK8.UFGNR  = 311;                                  
                            ENK11.YUGNR = 999;                                  
                         END;                                                   
                      IF ENK8.UFG < ENK11.YUG    THEN                           
                         DO;                                                    
                            FEIL_FUNNET = '1'B;                                 
                            ENK8.UFGNR  = 317;                                  
                            ENK11.YUGNR = 999;                                  
                         END;                                                   
                                                                                
                   END;                                                         
         END;                                                                   
   END KONTROLL_EN_KAP11;                                                       
   KONTROLL_EN_SPES:                                                            
   PROC (FEIL_FUNNET);                                                          
   DCL                                                                          
      FEIL_FUNNET BIT (1);                                                      
   DCL                                                                          
      KEY_BIT1                   BIT(32) BASED (KEY_PEKER1),                    
      KEY_BIT2                   BIT(32) BASED (KEY_PEKER2),                    
      KEY_PEKER1                 POINTER,                                       
      KEY_PEKER2                 POINTER,                                       
      ST_RECL                    CHAR (35);                                     
    KEY_PEKER1  = ADDR(EN_SPES.STATSBORGER);                                    
    KEY_PEKER2  = ADDR(EN_SPES.BOSTED);                                         
  /*                                                                            
    IF ^ F_NUMERISK(F_FELT_PIC_CHAR3(EN_SPES.STATSBORGER)) THEN                 
       DO;                                                                      
          FEIL_FUNNET = '1'B;                                                   
          EN_SPES.STATSBORGERNR = 200;                                          
       END;                                                                     
  */                                                                            
    IF ^ F_NUMERISK(F_FELT_PIC_CHAR3(EN_SPES.BOSTED)) THEN                      
       DO;                                                                      
          FEIL_FUNNET = '1'B;                                                   
          EN_SPES.BOSTEDNR = 200;                                               
       END;                                                                     
    IF ^ F_NUMERISK(F_FELT_PIC_CHAR7(EN_SPES.TP_NORDISK)) THEN                  
       DO;                                                                      
          FEIL_FUNNET = '1'B;                                                   
          EN_SPES.TP_NORDISKNR = 200;                                           
       END;                                                                     
    IF ^ F_NUMERISK(F_FELT_PIC_CHAR3(EN_SPES.TP_NORDISK_LAND)) THEN             
       DO;                                                                      
          FEIL_FUNNET = '1'B;                                                   
          EN_SPES.TP_NORDISK_LANDNR = 200;                                      
       END;                                                                     
    IF ^FEIL_FUNNET THEN                                                        
      DO;                                                                       
                                                                                
 /* ***************************************************************** */        
 /* FELTKONTROLLER........                                            */        
 /* ***************************************************************** */        
                                                                                
              EXEC CICS HANDLE CONDITION NOTFND(NOTFND1);                       
              EXEC CICS READ DATASET ('STATTAB') RIDFLD(KEY_BIT1)               
                                               INTO(ST_RECL);                   
              GOTO STAT_UT;                                                     
          NOTFND1:                                                              
             IF EN_SPES.STATSBORGER ^= 999     THEN                             
                DO;                                                             
                   FEIL_FUNNET        = '1'B;                                   
                   EN_SPES.STATSBORGERNR = 24;                                  
                END;                                                            
             GOTO STAT_UT;                                                      
          STAT_UT:                                                              
              EXEC CICS HANDLE CONDITION NOTFND(NOTFND2);                       
              EXEC CICS READ DATASET ('STATTAB') RIDFLD(KEY_BIT2)               
                                               INTO(ST_RECL);                   
              GOTO BO_UT;                                                       
          NOTFND2:                                                              
             IF EN_SPES.BOSTED      ^= 999     THEN                             
                DO;                                                             
                   FEIL_FUNNET   = '1'B;                                        
                   EN_SPES.BOSTEDNR = 25;                                       
                END;                                                            
             GOTO BO_UT;                                                        
          BO_UT:                                                                
      END;                                                                      
                                                                                
      IF EN_SPES.TP_NORDISK > 0       &                                         
         EN_SPES.TP_NORDISK < 99999        THEN                                 
         IF EN_SPES.TP_NORDISK_LAND = 0 !                                       
            EN_SPES.TP_NORDISK_LAND = 999  THEN                                 
            DO;                                                                 
               FEIL_FUNNET = '1'B;                                              
               EN_SPES.TP_NORDISKNR = 460;                                      
            END;                                                                
      IF EN_SPES.TP_NORDISK_LAND > 0   &                                        
         EN_SPES.TP_NORDISK_LAND < 999      THEN                                
         IF EN_SPES.TP_NORDISK_LAND < 101     !                                 
            EN_SPES.TP_NORDISK_LAND > 107   THEN                                
            DO;                                                                 
               FEIL_FUNNET = '1'B;                                              
               EN_SPES.TP_NORDISK_LANDNR = 485;                                 
            END;                                                                
         ELSE                                                                   
         IF EN_SPES.TP_NORDISK = 0       !                                      
            EN_SPES.TP_NORDISK = 99999      THEN                                
            DO;                                                                 
               FEIL_FUNNET = '1'B;                                              
               EN_SPES.TP_NORDISKNR = 486;                                      
            END;                                                                
      IF EN_SPES.PENSJON_EØS > 0 THEN                                           
         IF ^ F_NUMERISK(F_FELT_PIC_CHAR7(EN_SPES.PENSJON_EØS)) THEN            
            DO;                                                                 
               FEIL_FUNNET = '1'B;                                              
               EN_SPES.PENSJON_EØSNR = 200;                                     
            END;                                                                
   /*9811 : */                                                                  
      IF ^(EN_SPES.INNT_PRØVET_EØS_PENS = 'J' !                                 
           EN_SPES.INNT_PRØVET_EØS_PENS = 'N' !                                 
           EN_SPES.INNT_PRØVET_EØS_PENS = ' ' )           THEN                  
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            EN_SPES.INNT_PRØVET_EØS_PENSNR = 1541;                              
                                                                                
         END;                                                                   
      IF ^ F_NUMERISK(F_FELT_PIC_CHAR2(EN_SPES.TT_EØS_ANT_ÅR)) THEN             
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            EN_SPES.TT_EØS_ANT_ÅRNR = 200;                                      
         END;                                                                   
      IF ^ F_NUMERISK(F_FELT_PIC_CHAR2(EN_SPES.TT_EØS_ANT_MND)) THEN            
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            EN_SPES.TT_EØS_ANT_MNDNR = 200;                                     
         END;                                                                   
      IF EN_SPES.TT_EØS_ANT_ÅR < 99           &                                 
         EN_SPES.TT_EØS_ANT_ÅR > 40 THEN                                        
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            EN_SPES.TT_EØS_ANT_ÅRNR = 1819;                                     
         END;                                                                   
      IF EN_SPES.TT_EØS_ANT_MND < 99          &                                 
         EN_SPES.TT_EØS_ANT_MND > 11 THEN                                       
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            EN_SPES.TT_EØS_ANT_MNDNR = 1819;                                    
         END;                                                                   
      IF (EN_SPES.INNT_PRØVET_EØS_PENS ^= 'J') &                                
         (EN_SPES.TT_EØS_ANT_ÅR < 99 !                                          
          EN_SPES.TT_EØS_ANT_MND < 99)           THEN                           
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            EN_SPES.TT_EØS_ANT_ÅRNR = 1820;                                     
         END;                                                                   
  /*HIT 9811*/                                                                  
                                                                                
   END KONTROLL_EN_SPES;                                                        
