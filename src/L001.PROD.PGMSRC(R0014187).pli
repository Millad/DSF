 /*   SIST ENDRET PÅ PROD   2000.07.07 13.00.31 AV   JDA7339          */        
 /*       SIST ENDRET 12/03-99 13.45.10 AV   JDA7339                  */        
 /*       SIST ENDRET 12/03-99 13.45.06 AV   JDA7339                  */        
 /*       SIST ENDRET 19/02-99 12.31.35 AV   JDA7339                  */        
 /*       SIST ENDRET 08/01-99 14.25.16 AV   JDA7339                  */        
 /*       SIST ENDRET 14/10-98 13.24.52 AV   TSB7339                  */        
 /*       SIST ENDRET 02/09-98 12.23.37 AV   SPA7339                  */        
 /* ***************************************************************** */        
 /*IDENTIFIKASJON:                                                    */        
 /*    R0014187 - F_TPÅR_M2DES      - INTERN FUNKSJON I PLI           */        
 /*    PROGRAMMERER: MARTIN - JAN 98                                  */        
 /*    OMSKREVET FOR EØS BEREGNING 1097 (MED DESIMALER) MH            */        
 /*    OMSKREVET FOR EØS ÅRSBELØP  1198 (MED DESIMALER) MH            */        
 /*HENSIKT:                                                           */        
 /*    REGNER TP MED POENGÅR FØR 1992 OG ETTER 1991  (45 OG 42 %)     */        
 /*PROGRAMTILKNYTNING:                                                */        
 /*    INKLUDERES I 4370 ETTERLATTE EØS                               */        
 /*BRUK:                                                              */        
 /*    TP=F_TP_AARSBEL_M_2_DES                                        */        
 /*          (                                                        */        
 /*           FÅ,                  FØDSELSÅR 4 SIFFER                 */        
 /*           SPT,                 SLUTTPOENGTALL (INK. OPT) 3,2      */        
 /*           OPT,                 OVERKOMENSASJON                    */        
 /*           PÅ,                  TOTALT ANTALL POENGÅR              */        
 /*           PÅ_ETTER91,          HVORAV ETTER 91 (42% DEKNING)      */        
 /*           UFG,                 UFØREGRAD                          */        
 /*           TP_PROSENT                                              */        
 /*          )                                                        */        
 /* ***************************************************************** */        
 F_TP_AARSBEL_M_2_DES :                                                         
    PROC(FÅ,G,SPT,OPT,PÅ,PÅ_ETTER91,UFG,TP_PROSENT)                             
         RETURNS(DEC FIXED(9,2));                                               
    DCL                                                                         
       /* PARAMETER INN */                                                      
       FÅ                 PIC '9999',                                           
       G                  FIXED DEC(5),                                         
       SPT                FIXED DEC(3,2),                                       
       OPT                FIXED DEC(3,2),                                       
       PÅ                 FIXED DEC(3),                                         
       PÅ_FØR92           FIXED DEC(3),                                         
       PÅ_ETTER91         FIXED DEC(3),                                         
       UFG                FIXED DEC(5,2),                                       
       TP_PROSENT         FIXED DEC(3),                                         
       /* EGNE VARIABLER FOR DENNE PROC */                                      
       OÅ                 FIXED DEC(3) INIT (20),                               
       TP_WORK            FIXED DEC(11,4) INIT (0), /* TOTAL TP  */             
       TP_WORK_OPT        FIXED DEC(11,4) INIT (0), /* TOTAL OPT */             
       TP_WORK_OPT_42     FIXED DEC(11,4) INIT (0),                             
       TP_WORK_OPT_45     FIXED DEC(11,4) INIT (0),                             
       TP_WORK_SPT        FIXED DEC(11,4) INIT (0), /* TOTAL SPT */             
       TP_WORK_SPT_42     FIXED DEC(11,4) INIT (0),                             
       TP_WORK_SPT_45     FIXED DEC(11,4) INIT (0),                             
       G_42_PROSENT       FIXED DEC(11,4) INIT (0),                             
       G_45_PROSENT       FIXED DEC(11,4) INIT (0),                             
       TP_UT              FIXED DEC(9,2) INIT(0),                               
       PGNAVN             CHAR (8) INIT ('R0014187');                           
    /* ============================================================== */        
    IF (B02.FRAVIK_BOSATT_KONVENSJON(BER_SØ_IND) = 'J' !                        
        B02.KONVENSJON(BER_SØ_IND)  = 'J'              !                        
        B02.KONVENSJON_TP(BER_SØ_IND)  = 'J'           ! /*HL*/                 
        PÅ > 2                                         !                        
        (FÅ = 1899 & PÅ = 2                          ) !                        
        (FÅ = 1898 & PÅ = 1                          ) ) THEN                   
       DO;  /* FYLLER VILKÅRA */                                                
          PÅ_FØR92 = PÅ - PÅ_ETTER91;                                           
          G_45_PROSENT = G * 45 /100;                                           
          G_42_PROSENT = G * 42 /100;                                           
          IF (SPT > OPT ) THEN                                                  
             DO; /* SKAL HA 40 DELS */                                          
                TP_WORK_SPT_42 = G_42_PROSENT * PÅ_ETTER91 / 40;                
                TP_WORK_SPT_45 = G_45_PROSENT * PÅ_FØR92   / 40;                
                TP_WORK_SPT    = TP_WORK_SPT_42 + TP_WORK_SPT_45;               
                TP_WORK_SPT    = TP_WORK_SPT * (SPT-OPT);                       
             END; /* SKAL HA 40 DELS */                                         
          IF (FÅ < 1937 &                                                       
              OPT > 0   ) THEN                                                  
             DO;  /* SKAL HA OVERKOMP */                                        
                IF (FÅ > 1917) THEN                                             
                   OÅ = 20 + FÅ - 1917;                                         
                TP_WORK_OPT_42 = G_42_PROSENT * PÅ_ETTER91 / OÅ;                
                TP_WORK_OPT_45 = G_45_PROSENT * PÅ_FØR92   / OÅ;                
                TP_WORK_OPT    = TP_WORK_OPT_42 + TP_WORK_OPT_45;               
                TP_WORK_OPT    = TP_WORK_OPT * OPT;                             
             END; /* SKAL HA OVERKOMP */                                        
          TP_WORK = TP_WORK_SPT + TP_WORK_OPT;                                  
          TP_WORK = TP_WORK * UFG /100;                                         
          TP_WORK = TP_WORK * TP_PROSENT / 100;                                 
          /* IKKE GI KONV 'F' TILLEGGSPENSJON, TRUDE 060192  */                 
          IF (B02.BOSATT(BER_SØ_IND)              = 000 &                       
              B02.TT_16_66(BER_SØ_IND)            = 0   &                       
              B02.TT_ETTER_1966(BER_SØ_IND)       = 0   &                       
              B02.TT_FØR_1967(BER_SØ_IND)         = 0   &                       
              B02.ALDERSP.KONV_P_KODE(BER_SØ_IND) = 'F' &                       
              B02.ALDERSP.KONV_GRAD(BER_SØ_IND)   = 0   ) THEN                  
             TP_UT  = 0 ;                                                       
             /* TRUDE 060192 HIT */                                             
          ELSE                                                                  
             IF (B02.PENSJONSTYPE2(BER_SØ_IND) = 'A') THEN                      
                B02.ALTERNATIV_TP(BER_SØ_IND) = TP_WORK + 0.5;                  
          TP_UT = TP_WORK + 0.005;                                              
       END; /* FYLLER VILKÅRA */                                                
    RETURN(TP_UT);                                                              
 END F_TP_AARSBEL_M_2_DES;                                                      
