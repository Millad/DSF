 /*   SIST ENDRET PÅ PROD   2004.12.17 13.57.41 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.07.14 13.07.49 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2004.01.08 14.02.57 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2003.09.15  8.09.30 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.09.17 11.46.08 AV   HLA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.01.24 10.29.06 AV   HLA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.11.06 12.51.58 AV   HLA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.02.03  9.01.06 AV   JDA7339          */        
 /*       SIST ENDRET 21/09-99 08.19.05 AV   JDA7339                  */        
 /*       SIST ENDRET 21/09-99 08.15.33 AV   JDA7339                  */        
 /*       SIST ENDRET 20/07-98 10.03.20 AV   JDA7339                  */        
 /*       SIST ENDRET 22/06-98 08.58.11 AV   RFA9991                  */        
 /* ***************************************************************** */        
 /*IDENTIFIKASJON:                                                    */        
 /*    R0014121 - BEREGN_ETTERLATT_AP_DEL1 - INTERN PROC I PLI        */        
 /*    PROGRAMMERER: KRISTENSEN, APRIL 1982                           */        
 /*    ENDRET I JULI AV KARIN                                         */        
 /*HENSIKT:                                                           */        
 /*    PROGRAMMET STYRER BEREGNING AV A/E-PENSJON, OG OPPDATERER      */        
 /*    FORELØPIG ST, KT OG BT.                                        */        
 /*PROGRAMTILKNYTNING:                                                */        
 /*    PROGRAMMET BLIR INKLUDERT I R0014101 - (BEREGN_ETTERLATT_AP)   */        
 /*BRUK:                                                              */        
 /*    CALL BEREGN_ETTERLATT_AP_DEL1;                                 */        
 /* ***************************************************************** */        
 BEREGN_ETTERLATT_AP_DEL1:                                                      
  PROC;                                                                         
                                                                                
                                                                                
                                                                                
  DCL                                                                           
     W_SUM_TP           FIXED DEC(5),                                           
     W_ÅFT_MAX          FIXED DEC(5) INIT (0),                                  
     W_KOMPFAKTOR       FIXED DEC(3,1) INIT (1),                                
     W_FORH_INDIKATOR   CHAR(1) INIT ('J');                                     
                                                                                
 DCL                                                                            
     W_BT                    FIXED DEC (5) INIT (0);                            
                                                                                
                                                                                
  CALL BEREGN_TP_ETTERLATT_AP;                                                  
                                                                                
  /*YS VI HAR NÅ FÅTT OPPDATERT TP_EGEN, TP_PT_EGEN_55 OG TP_EK_PT*/            
                                                                                
                                                                                
  IF FEIL_MELD_NR > 0  THEN                                                     
     GO TO RETUR;                                                               
  ELSE                                                                          
     PROGRAM_ID = 'R0014101';                                                   
                                                                                
     /*-------------------------------------------------------------*/          
     /*PENSON UTLAND . TRUDE MAI 1990                               */          
     /*-------------------------------------------------------------*/          
                                                                                
          /* TAR BORT TESTEN PÅ P.RETT.ERSTATTER MED FØLGENDE , TRUDE*/         
  IF B02.BOSATT(BER_SØ_IND)    >  0  &                                          
    (B02.TT_16_66(BER_SØ_IND)  = 0   &            /*NY0191*/                    
     B02.TT_16_66(BER_EK_IND)  = 0 )  THEN        /*NY0191*/                    
     CALL BEREGN_GP_ETTERLATT_AP_UTLAND;     /* R0014182 */                     
  ELSE                                                                          
     CALL BEREGN_GP_ETTERLATT_AP;                                               
    /* R0014132 */                                                              
                                                                                
  IF B02.YRKEPENS.YUG(BER_SØ_IND) > 0 THEN                                      
     CALL BEREGN_ETTERLATT_AP_YP;              /*4129*/                         
  ELSE                                                                          
     DO;  /* IKKE YRKESSKADE ?*/                                                
        IF TP_EGEN > (TP_PT_EGEN_55 + TP_EK_PT) THEN                            
           /* SE PENSJONSBEREGNINGSPERMEN PKT 13.1.3 - EVT. UNG UFØR */         
           /* PÅ GJENLEVENDE SKAL IKKE TAS MED VED 55%'S BEREGNING   */         
           DO;  /* EGEN PENSJON ER HØYERE ENN ETTERLATTEALTERNATIVET */         
                                                                                
               B02.POENGTILLEGG_DATO_ÅMD(BER_SØ_IND) = 0;                       
               B02.POENGTILLEGG_KODE   (BER_SØ_IND) = ' ';                      
               B02.POENGTILLEGG_DATO_ÅMD(BER_EK_IND) = 0;                       
               B02.POENGTILLEGG_KODE   (BER_EK_IND) = ' ';                      
                                                                                
               POREKKE(BER_SØ_IND)     = POREKKE(14);                           
         /* PLASS 14 BRUKES SOM OPPBEVARINGSOMR. DA DENNE PLASSEN     */        
         /* ALLTID ER LEDIG I ALDERSPENSJON.                          */        
               POREKKE_P67(BER_SØ_IND) = W_POREKKE;                             
                                                                                
               B02.ALDERSP.TP(BER_SØ_IND)   = TP_EGEN;                          
               IF TP_EGEN > 0 THEN                                              
               DO;                                                              
                B02.ALDERSP.SPT(BER_SØ_IND)  = POREKKE.SPT(BER_SØ_IND);         
                B02.ALDERSP.OPT(BER_SØ_IND)  = POREKKE.OPT(BER_SØ_IND);         
               END;                                                             
               ELSE                                                             
               DO;                                                              
                B02.ALDERSP.SPT(BER_SØ_IND)  = 0;                               
                B02.ALDERSP.OPT(BER_SØ_IND)  = 0;                               
               END;                                                             
               B02.ALDERSP.PÅ(BER_SØ_IND)   = POREKKE.PÅ (BER_SØ_IND);          
 /*93*/        B02.ALDERSP.PÅ_ETTER91(BER_SØ_IND)                               
 /*93*/                               = POREKKE.PÅ_ETTER91(BER_SØ_IND);         
                                                                                
               B02.VT_SPT(BER_SØ_IND)   = POREKKE_P67.SPT(BER_SØ_IND);          
               B02.VT_OPT(BER_SØ_IND)   = POREKKE_P67.OPT(BER_SØ_IND);          
               B02.VT_PÅ (BER_SØ_IND)   = POREKKE_P67.PÅ (BER_SØ_IND);          
                                                                                
     /* PLASS I+11 BRUKES SOM OPPBEVARINGSOMR FOR EKTEFELLEN        */          
     /* DETTE GJELDER IKKE ALLE : 200309 HL :                       */          
  IF POREKKE.PÅ (BER_EK_IND + 11) > 0                THEN                       
  DO;                                                                           
  B02.ETTEPENS.SPT_AVD(BER_SØ_IND)  = POREKKE.SPT(BER_EK_IND + 11);             
  B02.ETTEPENS.OPT_AVD(BER_SØ_IND)  = POREKKE.OPT(BER_EK_IND + 11);             
  B02.ETTEPENS.PÅ_AVD(BER_SØ_IND)   = POREKKE.PÅ (BER_EK_IND + 11);             
  END;                                                                          
               B02.ETTEPENS.TP_BRUTTO(BER_SØ_IND) = 0;                          
               B02.ETTEPENS.TP_NETTO(BER_SØ_IND)  = 0;                          
            END; /* EGEN PENSJON ER HØYERE ENN ETTERLATTEALTERNATIVET */        
         ELSE                                                                   
            DO;  /* ETTERLATTEALTERNATIVET ER GUNSTIGEST */                     
               B02.ALDERSP.TP(BER_SØ_IND)   = TP_PT_EGEN_55;                    
               IF TP_PT_EGEN_55 > 0       THEN                                  
               DO;                                                              
                B02.ALDERSP.SPT(BER_SØ_IND) = POREKKE.SPT(BER_SØ_IND);          
                B02.ALDERSP.OPT(BER_SØ_IND) = POREKKE.OPT(BER_SØ_IND);          
               END;                                                             
               ELSE                                                             
               DO;                                                              
                B02.ALDERSP.SPT(BER_SØ_IND)  = 0;                               
                B02.ALDERSP.OPT(BER_SØ_IND)  = 0;                               
               END;                                                             
               B02.ALDERSP.PÅ(BER_SØ_IND)   = POREKKE.PÅ (BER_SØ_IND);          
 /*93*/        B02.ALDERSP.PÅ_ETTER91(BER_SØ_IND)                               
 /*93*/                               = POREKKE.PÅ_ETTER91(BER_SØ_IND);         
                                                                                
               B02.VT_SPT(BER_SØ_IND)   = POREKKE_P67.SPT(BER_SØ_IND);          
               B02.VT_OPT(BER_SØ_IND)   = POREKKE_P67.OPT(BER_SØ_IND);          
               B02.VT_PÅ (BER_SØ_IND)   = POREKKE_P67.PÅ (BER_SØ_IND);          
                                                                                
               B02.ETTEPENS.TP_BRUTTO(BER_SØ_IND) = TP_EK_PT;                   
               B02.ETTEPENS.TP_NETTO(BER_SØ_IND)  = TP_EK_PT;                   
                                                                                
            END; /* ETTERLATTEALTERNATIVET ER GUNSTIGEST */                     
     END; /* IKKE YRKESSKADE ?*/                                                
                                                                                
  W_SUM_TP = B02.ALDERSP.TP(BER_SØ_IND)+B02.TP_BRUTTO(BER_SØ_IND);              
                                                                                
                 /* --------------------------------------------------*/        
                 /* PENSJON UTLAND. TRUDE .---------------------------*/        
                 /* --------------------------------------------------*/        
  IF B02.BOSATT(BER_SØ_IND)               >  0  &                               
    (B02.TT_16_66(BER_SØ_IND)  = 0    &                    /*NY0191*/           
     B02.TT_16_66(BER_EK_IND)  = 0 )  THEN                 /*NY0191*/           
     CALL SÆRTILLEGG_UTLAND(W_SUM_TP);                     /*R0014180*/         
  /* B02.ALDERSP.ST(BER_SØ_IND) = F_SÆRTILLEGG_UTLAND_EP(                       
                                           W_SUM_TP);  */   /*4183*/            
  ELSE                                                                          
     B02.ALDERSP.ST(BER_SØ_IND) = F_BEREGN_SÆRTILLEGG(                          
                                         W_SUM_TP,0,'J',                        
                                         (ST_PROSENT_ENSLIG),                   
                                         100,                                   
                                         (B02.TT_ANV(BER_SØ_IND)));             
                                                                                
    /* W_KOMPFAKTOR ER LIK 1 DVS FULLT KOMPENSASJONSTILLEGG */                  
                                                                                
  W_KOMPFAKTOR = 1.0;                                                           
   /*    ENDRET   17.03.88    SATISH                                 */         
   /*    B02.ALDERSP.KT(BER_SØ_IND) = F_BEREGN_KOMPENSASJONSTILLEGG   */        
   /*                                (B02.PENSJONSTYPE2(BER_SØ_IND),  */        
   /*                                 B02.TT_ANV(BER_SØ_IND),         */        
   /*                                 0,                              */        
   /*                                 W_KOMPFAKTOR,                   */        
   /*                                 W_FORH_INDIKATOR);              */        
   /*                                                                 */        
   /*     HITTIL                                                      */        
         B02.ALDERSP.KT(BER_SØ_IND) = F_BEREGN_KOMPENSASJONSTILLEGG             
                                     (B02.PENSJONSTYPE1(BER_SØ_IND),            
                                      B02.PENSJONSTYPE2(BER_SØ_IND),            
                                      B02.TT_ANV(BER_SØ_IND),                   
                                      0,                                        
                                      W_KOMPFAKTOR,                             
                                      W_FORH_INDIKATOR);                        
                                                                                
  /*  JEG HAR LAGT INN TEST HER PÅ OM SØKEREN ER BOSATT I     */                
  /*  UTLANDET.  HVIS JA, SÅ SKAL KT VÆRE LIK 0. ERIK 8.12.86 */                
                                                                                
  IF B02.SPESOPPL.BOSATT(BER_SØ_IND) > 0 THEN                                   
     B02.ALDERSP.KT(BER_SØ_IND) = 0 ;                                           
                                                                                
                                                                                
 /* B02.ALDERSP.BT(BER_SØ_IND) = 0.25 * G * B02.ANTALL_BARN                     
                                (BER_SØ_IND) / 12 + 0.5;  */                    
                                                                                
    CALL BEREGN_BT(W_BT,100);                           /*NY0191*/              
    B02.ALDERSP.BT(BER_SØ_IND) = W_BT;                  /*NY0191*/              
                                                        /*NY0191*/              
                                                                                
                                                                                
 RETUR:                                                                         
                                                                                
 END BEREGN_ETTERLATT_AP_DEL1;                                                  
