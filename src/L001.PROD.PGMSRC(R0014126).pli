 /*   SIST ENDRET PÅ PROD   2004.03.18 11.53.33 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.03.02  9.57.12 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2000.11.07 12.22.34 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.07.07 13.00.12 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.04.17 12.07.13 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.04.12 10.12.29 AV   JDA7339          */        
 /*       SIST ENDRET 21/09-99 08.19.10 AV   JDA7339                  */        
 /*       SIST ENDRET 21/09-99 08.15.35 AV   JDA7339                  */        
 /*       SIST ENDRET 03/03-99 14.13.16 AV   JDA7339                  */        
 /*       SIST ENDRET 03/03-99 14.11.12 AV   JDA7339                  */        
 /*       SIST ENDRET 10/12-98 10.53.31 AV   JDA7339                  */        
 /*       SIST ENDRET 10/12-98 10.51.37 AV   JDA7339                  */        
 /*       SIST ENDRET 01/12-98 16.24.53 AV   TSB7339                  */        
 /*       SIST ENDRET 01/12-98 16.24.22 AV   TSB7339                  */        
 /*       SIST ENDRET 20/07-98 10.03.28 AV   JDA7339                  */        
 /*       SIST ENDRET 22/06-98 08.58.16 AV   RFA9991                  */        
 /* ***************************************************************** */        
 /*IDENTIFIKASJON:                                                    */        
 /*    R0014126 - BEREGN_ETTERLATT_AP_DEL3 - INTERN PROC I PLI.       */        
 /*    PROGRAMMERER: KRISTENSEN, APRIL 1982.                          */        
 /*HENSIKT:                                                           */        
 /*    PROGRAMMET STYRER BEHANDLING AV JUSTERING MED APD OG BEREGNING */        
 /*    AV VENTETILLEGG.                                               */        
 /*    OPPDATERER : ALDERSP, ETTEPENS.TP_NETTO OG SUM_YTELSE          */        
 /*PROGRAMTILKNYTNING:                                                */        
 /*    PROGRAMMET BLIR INKLUDERT I R0014101 - BEREGN_ETTERLATT_AP.    */        
 /*BRUK:                                                              */        
 /*    CALL BEREGN_ETTERLATT_AP_DEL3                                  */        
 /* ***************************************************************** */        
 BEREGN_ETTERLATT_AP_DEL3: PROC;                                                
    DCL                                                                         
      ALT1                 FIXED DEC(7),                                        
      ALT2                 FIXED DEC(7),                                        
      TT_AVDØD             FIXED DEC(3),                                        
      FNR_ÅM                PIC'(6)9',                                          
      FNR_Å                 PIC'(4)9' DEF FNR_ÅM POS(1),                        
      FNR_M                 PIC'(2)9' DEF FNR_ÅM POS(5);                        
                                                                                
    DCL W_VT_TP_BRUTTO     FIXED DEC(5),                                        
        W_VT_TP_EGEN_55    FIXED DEC(5);                                        
                                                                                
    DCL YRKE_TP_BRUTTO     FIXED DEC (7) INIT (0);                              
    DCL YRKE_TP_EGEN_55    FIXED DEC (7) INIT (0);                              
    DCL REST_GRAD          FIXED DEC (3) INIT (0);                              
    DCL REST_GRAD_EK       FIXED DEC (3) INIT (0);                              
    DCL YRKE_VT_TP         FIXED DEC (7) INIT (0);                              
    DCL YRKE_VT_GP         FIXED DEC (7) INIT (0);                              
    DCL GARANTITILLEGG     FIXED DEC (5) INIT (0);                              
    DCL W_HJELP            FIXED DEC (13,4) INIT (0);                           
                                                                                
    /* == START 4126 ================================================ */        
                                                                                
    DIV_PARAM_OMR.GARANTITILLEGG_TP(BER_SØ_IND) = 0;                            
                                                                                
    FNR_Å = W_FNR_R13.ÅR;                                                       
    FNR_M = W_FNR_R13.MND;                                                      
                                                                                
    IF (B02.ALDERSP.APD(BER_SØ_IND) < 100 &                                     
        W_FNR_R13.ÅR                < 1925)   !                                 
       (B02.KONV_P_KODE(BER_SØ_IND)  = 'K' &                                    
        B02.UTTAKSDATO_ÅMD(BER_SØ_IND) < 19920000)  THEN                        
       DO;  /* VELDIG GAMLE REGLER */                                           
          IF (B02.ALDERSP.APD  (BER_SØ_IND)  =  0  &                            
              B02.SIVILSTAND   (BER_SØ_IND) ^= 'S' ) THEN                       
             DO;  /* APD = 0 & SIVILSTAND ^='S' */                              
                /* PLASS 14 BRUKT TIL OPPBEVARINGSOMR. DA DENNE  */             
                /* ALLTID ER LEDIG PLASS FOR ALDERSP.            */             
                POREKKE(BER_SØ_IND)=POREKKE(14);                                
                POREKKE_P67(BER_SØ_IND)     = W_POREKKE;                        
                B02.ALDERSP.SPT(BER_SØ_IND) = POREKKE.SPT (BER_SØ_IND);         
                B02.ALDERSP.OPT(BER_SØ_IND) = POREKKE.OPT (BER_SØ_IND);         
                B02.ALDERSP.PÅ (BER_SØ_IND) = POREKKE.PÅ  (BER_SØ_IND);         
                B02.VT_SPT(BER_SØ_IND) = POREKKE_P67.SPT (BER_SØ_IND);          
                B02.VT_OPT(BER_SØ_IND) = POREKKE_P67.OPT (BER_SØ_IND);          
                B02.VT_PÅ (BER_SØ_IND) = POREKKE_P67.PÅ  (BER_SØ_IND);          
                B02.POENGTILLEGG_DATO_ÅMD(BER_SØ_IND) = 0;                      
                B02.POENGTILLEGG_KODE   (BER_SØ_IND) = ' ';                     
                B02.POENGTILLEGG_DATO_ÅMD(BER_EK_IND) = 0;                      
                B02.POENGTILLEGG_KODE   (BER_EK_IND) = ' ';                     
             END; /* APD = 0 & SIVILSTAND ^='S' */                              
          CALL JUSTER_MED_APD; /* 4137 */                                       
          B02.VT_GP     (BER_SØ_IND) = 0;                                       
          B02.VT_TP     (BER_SØ_IND) = 0;                                       
          B02.VT_SPT    (BER_SØ_IND) = 0;                                       
          B02.VT_OPT    (BER_SØ_IND) = 0;                                       
          B02.VT_PÅ     (BER_SØ_IND) = 0;                                       
          B02.TT_VENT   (BER_SØ_IND) = 0;                                       
          B02.VENTEFAKTOR(BER_SØ_IND) = 0;                                      
       END; /* VELDIG GAMLE REGLER */                                           
    ELSE                                                                        
       DO;    /* APD = 100 */                                                   
          IF (FNR_ÅM <190302) THEN                                              
             /* ----------------------------------------------------- */        
             /* DET ER ET VILKÅR FOR VENTETILLEGG AT EN ER FØDT ETTER */        
             /* JANUAR 1903                                           */        
             /* ----------------------------------------------------- */        
             DO;  /* IKKE RETT TIL VENTETILLEGG */                              
                B02.VT_GP     (BER_SØ_IND) = 0;                                 
                B02.VT_TP     (BER_SØ_IND) = 0;                                 
                B02.VT_SPT    (BER_SØ_IND) = 0;                                 
                B02.VT_OPT    (BER_SØ_IND) = 0;                                 
                B02.VT_PÅ     (BER_SØ_IND) = 0;                                 
                B02.TT_VENT    (BER_SØ_IND) = 0;                                
                B02.VENTEFAKTOR(BER_SØ_IND) = 0;                                
             END; /* IKKE RETT TIL VENTETILLEGG */                              
          ELSE                                                                  
             DO;  /* KAN HA RETT TIL VENTETILLEGG */                            
                CALL BEREGN_VT;     /* OPPDATERER VENTEHISTORIKK */             
                /* VI REGNER UT TT_VENT  */                                     
                TT_AVDØD = B02.TT_FØR_1967 (BER_EK_IND)                         
                         +(B02.TT_ETTER_1966 (BER_EK_IND)                       
                         + B02.TT_FRAMT(BER_EK_IND) + 11) / 12                  
                         + B02.TT_67_TIL_70(BER_EK_IND);                        
                                                                                
                IF TT_AVDØD > 40 THEN                                           
                   TT_AVDØD = 40;                                               
                                                                                
                B02.TT_VENT(BER_SØ_IND) =                                       
                     B02.STATUS.TT_FØR_1967  (BER_SØ_IND)                       
                  + (B02.STATUS.TT_ETTER_1966(BER_SØ_IND) + 11) / 12;           
                                                                                
                IF B02.TT_VENT(BER_SØ_IND) < 40 THEN                            
                   DO;                                                          
                      IF B02.TT_VENT(BER_SØ_IND) < TT_AVDØD THEN                
                         B02.TT_VENT(BER_SØ_IND) = TT_AVDØD;                    
                   END;                                                         
                ELSE                                                            
                   B02.ALDERSP.TT_VENT(BER_SØ_IND) = 40;                        
                                                                                
                IF B02.TT_VENT (BER_SØ_IND)                                     
                                < B02.TT_GARANTI(BER_SØ_IND) THEN               
                B02.TT_VENT (BER_SØ_IND)                                        
                                = B02.TT_GARANTI(BER_SØ_IND);                   
                                                                                
                                                                                
                /*  VT_TP */                                                    
                                                                                
 /*ENDRET 23.9.87 HL - VENTETILLEGG SKAL REGNES AV 7-4-ALTERNATIVET*/           
 /*                    NÅR DETTE ER STØRST PR 67-ÅRS-ALDER         */           
 /*                    FEILRAPPORT 534                             */           
                                                                                
                                                                                
                IF DIV_PARAM_OMR.BEREGNINGS_ALT_AVD  = '3'    THEN              
                                                                                
 /* ************************************************************** */           
 /*  ALT = 3 BETYR AT PENSJON REGNET MED YP-FORDEL ER STØRST       */           
 /* ************************************************************** */           
                                                                                
            DO;  /* BEREGNINGS_ALT_AVD = '3' YP-FORDEL ER STØRST */             
                                                                                
               IF B02.YRKEPENS.PÅ(BER_EK_IND) > 0           THEN                
               YRKE_TP_BRUTTO = B02.YRKEPENS.YUG(BER_EK_IND)                    
                                * G * B02.YRKEPENS.YPT(BER_EK_IND)              
                                * 0.55 / (12 * 100)                             
                   * (0.45 * (B02.YRKEPENS.PÅ(BER_EK_IND) -                     
                      B02.YRKEPENS.PÅ_ETTER91(BER_EK_IND))  +                   
                      0.42 * B02.YRKEPENS.PÅ_ETTER91(BER_EK_IND))               
                   /  B02.YRKEPENS.PÅ(BER_EK_IND) + 0.5;                        
               ELSE                                                             
               YRKE_TP_BRUTTO = B02.YRKEPENS.YUG(BER_EK_IND)                    
                                * G * B02.YRKEPENS.YPT(BER_EK_IND)              
                                * 0.45 * 0.55 / (12 * 100) + 0.5;               
               REST_GRAD_EK = 100 - B02.YRKEPENS.YUG(BER_EK_IND);               
               W_VT_TP_BRUTTO = F_TP92(W_FNR_EK_R13.ÅR,G,                       
                                         (B02.SPT_AVD(BER_SØ_IND)),             
                                         (B02.OPT_AVD(BER_SØ_IND)),             
                                         (B02.PÅ_AVD(BER_SØ_IND)),              
                               (B02.ETTEPENS.PÅ_ETTER91(BER_SØ_IND)),           
                                         (REST_GRAD_EK),55,'J');                
               W_VT_TP_BRUTTO = W_VT_TP_BRUTTO + YRKE_TP_BRUTTO;                
            END; /* BEREGNINGS_ALT_AVD = '3' YP-FORDEL ER STØRST */             
          ELSE                                                                  
            DO;  /* BEREGNINGS_ALT_AVD ^= '3' YS-FORDEL IKKE STØRST */          
            W_VT_TP_BRUTTO = F_TP92(W_FNR_EK_R13.ÅR,G,                          
                                         (B02.SPT_AVD(BER_SØ_IND)),             
                                         (B02.OPT_AVD(BER_SØ_IND)),             
                                         (B02.PÅ_AVD(BER_SØ_IND)),              
                               (B02.ETTEPENS.PÅ_ETTER91(BER_SØ_IND)),           
                                         (100),55,'J');                         
                                                                                
            END; /* BEREGNINGS_ALT_AVD ^= '3' YS-FORDEL IKKE STØRST */          
         IF DIV_PARAM_OMR.BEREGNINGS_ALT_EGEN = '4'    THEN                     
                                                                                
 /* ************************************************************** */           
 /*  ALT = 4 = EGENPENSJON MED Y-FORDELER + TP ETTER AVDØDE ER BEST*/           
 /* ************************************************************** */           
                                                                                
            DO;                                                                 
                                                                                
               IF B02.YRKEPENS.PÅ(BER_SØ_IND) > 0           THEN                
               YRKE_TP_EGEN_55 = B02.YRKEPENS.YUG(BER_SØ_IND)                   
                                * G * B02.YRKEPENS.YPT(BER_SØ_IND)              
                                * 0.55 / (12 * 100)                             
                   * (0.45 * (B02.YRKEPENS.PÅ(BER_SØ_IND) -                     
                      B02.YRKEPENS.PÅ_ETTER91(BER_SØ_IND))  +                   
                      0.42 * B02.YRKEPENS.PÅ_ETTER91(BER_SØ_IND))               
                   /  B02.YRKEPENS.PÅ(BER_SØ_IND) + 0.5;                        
               ELSE                                                             
               YRKE_TP_EGEN_55 =  B02.YRKEPENS.YUG(BER_SØ_IND)                  
                                 * G * B02.YRKEPENS.YPT(BER_SØ_IND)             
                                 * 0.45 * 0.55 / (12 * 100) + 0.5;              
               REST_GRAD = 100 - B02.YRKEPENS.YUG(BER_SØ_IND);                  
        /*     W_VT_TP_EGEN_55 = F_TP92((FNR_R.ÅR),G, Y2KFEIL 0400MH*/          
               W_VT_TP_EGEN_55 = F_TP92((W_FNR_R13.ÅR),G,                       
                                    (B02.VT_SPT(BER_SØ_IND)),                   
                                    (B02.VT_OPT(BER_SØ_IND)),                   
                                    (B02.VT_PÅ(BER_SØ_IND)),                    
                               (B02.ALDERSP.PÅ_ETTER91(BER_SØ_IND)),            
                                    (REST_GRAD),55,'J');                        
               W_VT_TP_EGEN_55 = YRKE_TP_EGEN_55 + W_VT_TP_EGEN_55;             
            END;                                                                
         ELSE                                                                   
                                                                                
            DO;                                                                 
      /*    W_VT_TP_EGEN_55 = F_TP92((FNR_R.ÅR),G, */                           
      /*    DENNE VAR IKKE Y2K FORMATERT!!! MARTIN 130400 */                    
            W_VT_TP_EGEN_55 = F_TP92((W_FNR_R13.ÅR),G,                          
                                          (B02.VT_SPT(BER_SØ_IND)),             
                                          (B02.VT_OPT(BER_SØ_IND)),             
                                          (B02.VT_PÅ(BER_SØ_IND)),              
                               (B02.ALDERSP.PÅ_ETTER91(BER_SØ_IND)),            
                                          (100),55,'J');                        
            END;                                                                
         IF B02.TP_BRUTTO (BER_SØ_IND) = 0 THEN                                 
                                                                                
 /* AT TP_BRUTTO = 0  INNEBÆRER AT 7-4-ALTERNATIVET ER MINST -  */              
 /* DET KAN LIKEVEL VÆRE STØRRE ENN VENTEGRUNNLAGET (P67)       */              
                                                                                
            DO;                                                                 
               IF DIV_PARAM_OMR.BEREGNINGS_ALT_EGEN = '3'   THEN                
                                                                                
 /* ************************************************************** */           
 /*  ALT = 3 BETYR AT PENSJON REGNET MED YP-FORDEL ER STØRST       */           
 /* ************************************************************** */           
                                                                                
                                                                                
                  DO;                                                           
                                                                                
               IF B02.YRKEPENS.PÅ(BER_SØ_IND) > 0           THEN                
               YRKE_VT_TP      = B02.YRKEPENS.YUG(BER_SØ_IND)                   
                                * G * B02.YRKEPENS.YPT(BER_SØ_IND)              
                                / (12 * 100)                                    
                   * (0.45 * (B02.YRKEPENS.PÅ(BER_SØ_IND) -                     
                      B02.YRKEPENS.PÅ_ETTER91(BER_SØ_IND))  +                   
                      0.42 * B02.YRKEPENS.PÅ_ETTER91(BER_SØ_IND))               
                   /  B02.YRKEPENS.PÅ(BER_SØ_IND) + 0.5;                        
               ELSE                                                             
                     YRKE_VT_TP = B02.YRKEPENS.YUG(BER_SØ_IND)                  
                                 * G * B02.YRKEPENS.YPT(BER_SØ_IND)             
                                 * 0.45 / (12 * 100) + 0.5;                     
                                                                                
                     B02.VT_TP(BER_SØ_IND) =                                    
      /*    DENNE VAR IKKE Y2K FORMATERT!!! MARTIN 130400 */                    
      /*                                  F_TP92((FNR_R.ÅR),G, */               
                                          F_TP92((W_FNR_R13.ÅR),G,              
                                             (B02.VT_SPT(BER_SØ_IND)),          
                                             (B02.VT_OPT(BER_SØ_IND)),          
                                             (B02.VT_PÅ(BER_SØ_IND)),           
                               (B02.ALDERSP.PÅ_ETTER91(BER_SØ_IND)),            
                                             (REST_GRAD),100,'J');              
                     B02.VT_TP(BER_SØ_IND) = B02.VT_TP(BER_SØ_IND)              
                                            + YRKE_VT_TP;                       
                  END;                                                          
               ELSE                                                             
                DO;                                                             
                  B02.VT_TP (BER_SØ_IND) =                                      
      /*    DENNE VAR IKKE Y2K FORMATERT!!! MARTIN 130400 */                    
      /*                                  F_TP92((FNR_R.ÅR),G,  */              
                                          F_TP92((W_FNR_R13.ÅR),G,              
                                               (B02.VT_SPT(BER_SØ_IND)),        
                                               (B02.VT_OPT(BER_SØ_IND)),        
                                               (B02.VT_PÅ (BER_SØ_IND)),        
                               (B02.ALDERSP.PÅ_ETTER91(BER_SØ_IND)),            
                                                (100),100,'J');                 
                END;                                                            
               IF B02.VT_TP (BER_SØ_IND) < (W_VT_TP_BRUTTO                      
                             + W_VT_TP_EGEN_55)            THEN                 
                  DO;                                                           
                     B02.VT_TP (BER_SØ_IND) = W_VT_TP_BRUTTO                    
                                           + W_VT_TP_EGEN_55;                   
                     DIV_PERIODE.BEREGNINGS_ALT_EGEN = '5'; /*9812*/            
                  END;                                                          
            END;                                                                
                                                                                
         ELSE                                                                   
                                                                                
            B02.VT_TP (BER_SØ_IND) = W_VT_TP_BRUTTO                             
                                             + W_VT_TP_EGEN_55;                 
                                                                                
         B02.VT_TP (BER_SØ_IND) =                                               
                      B02.VT_TP (BER_SØ_IND) *                                  
                      B02.VENTEFAKTOR (BER_SØ_IND) / 100  + 0.5;                
                                                                                
                                                                                
          /* VT_GP  */                                                          
                                                                                
        IF DIV_PARAM_OMR.BEREGNINGS_ALT_EGEN = '3'   !                          
           DIV_PARAM_OMR.BEREGNINGS_ALT_EGEN = '4'   THEN                       
           DO;  /* YRKESFORDELER ER BERST */                                    
                                                                                
              YRKE_VT_GP =  B02.YRKEPENS.YUG(BER_SØ_IND)  *                     
                            B02.VENTEFAKTOR (BER_SØ_IND)  *                     
                            G / (12 * 100 * 100) + 0.5;                         
                                                                                
              B02.VT_GP(BER_SØ_IND) = REST_GRAD  *                              
                                      G * B02.TT_VENT(BER_SØ_IND) /             
                                      (40 * 12 * 100) + 0.5;                    
              B02.VT_GP(BER_SØ_IND) = B02.VT_GP(BER_SØ_IND) *                   
                            B02.VENTEFAKTOR (BER_SØ_IND) / 100  + 0.5;          
                                                                                
              B02.VT_GP(BER_SØ_IND) = B02.VT_GP(BER_SØ_IND) +                   
                                      YRKE_VT_GP;                               
           END; /* YRKESFORDELER ER BEST */                                     
        ELSE                                                                    
           DO;  /* EVT. YRKESFORDELER LØNNER SEG IKKE */                        
                                                                                
              B02.VT_GP(BER_SØ_IND) =                                           
                                      G * B02.TT_VENT(BER_SØ_IND) /             
                                      (40 * 12) + 0.5;                          
                                                                                
              B02.VT_GP(BER_SØ_IND) =                                           
                            B02.VT_GP(BER_SØ_IND) *                             
                            B02.VENTEFAKTOR (BER_SØ_IND) / 100  + 0.5;          
           END; /* EVT. YRKESFORDELER LØNNER SEG IKKE */                        
                                                                                
        /* VT_ST  */                                                            
              /* 16.10.2000 MARTIN                                              
              IF B02.ALDERSP.ST (BER_SØ_IND) >                                  
                                        B02.VT_TP (BER_SØ_IND) THEN             
                 B02.ALDERSP.ST (BER_SØ_IND) =                                  
                   B02.ALDERSP.ST(BER_SØ_IND) - B02.VT_TP (BER_SØ_IND);         
              ELSE                                                              
                 B02.ALDERSP.ST = 0;                                            
              /* NY VERSJON UNDER - MARTIN */                                   
              IF B02.VT_TP(BER_SØ_IND) = 0 THEN                                 
                 ;                                                              
              ELSE                                                              
                 DO;  /* VI REDUSERER SÆRTILLEGG FOR VENTETILLEGG */            
                    /* REDUKSJON AV BRUTTOBELØPENE */                           
                    IF (B02.ALDERSP.ST(BER_SØ_IND) >                            
                        B02.VT_TP(BER_SØ_IND) ) THEN                            
                       B02.ALDERSP.ST(BER_SØ_IND) =                             
                          B02.ALDERSP.ST(BER_SØ_IND) -                          
                          B02.VT_TP(BER_SØ_IND);                                
                    ELSE                                                        
                       B02.ALDERSP.ST(BER_SØ_IND) = 0;                          
                    /* REDUKSJON AV NETTOBELØPENE */                            
                    IF (B02.AP_ST_NETTO(BER_SØ_IND) >                           
                        B02.VT_TP(BER_SØ_IND) ) THEN                            
                       B02.AP_ST_NETTO(BER_SØ_IND) =                            
                          B02.AP_ST_NETTO(BER_SØ_IND) -                         
                          B02.VT_TP(BER_SØ_IND);                                
                    ELSE                                                        
                       B02.AP_ST_NETTO(BER_SØ_IND) = 0;                         
                 END; /* VI REDUSERER SÆRTILLEGG FOR VENTETILLEGG */            
           END;                                                                 
     END;                                                                       
                                                                                
 /*KOMP.TILLEGGET ENDRET FRA 1.5.91 ( G > 34100 ) */                            
                                                                                
  IF G > 34100 THEN                                                             
     IF B02.ALDERSP.KT(BER_SØ_IND) > B02.ALDERSP.ST(BER_SØ_IND) THEN            
        B02.ALDERSP.KT(BER_SØ_IND) =                                            
        B02.ALDERSP.KT(BER_SØ_IND) - B02.ALDERSP.ST(BER_SØ_IND);                
     ELSE                                                                       
        B02.ALDERSP.KT(BER_SØ_IND) = 0;                                         
                                                                                
                   /*----------------------------------*/                       
                   /*DERSOM IKKE GP, SKAL HELLER IKKE  */                       
                   /*HA VENTETILLEGG TIL GP. TRUDE10.91*/                       
                   /*----------------------------------*/                       
                                                             /*NY92*/           
     IF B02.GT_LOV92(BER_SØ_IND) >  0       !                                   
        B02.GT_LOV92(BER_EK_IND) >  0         THEN                              
        CALL BEREGN_GTLOV92_AE;                                                 
                                                             /*NY92*/           
     IF (W_FNR_R13.ÅR    > 1924  &                          /*NY92*/            
         B02.KONV_P_KODE(BER_SØ_IND) ^= 'K')  !              /*TS0393*/         
        (W_FNR_R13.ÅR    > 1924  &                          /*NY92*/            
         B02.KONV_P_KODE(BER_SØ_IND)  = 'K' &               /*TS0393*/          
         B02.UTTAKSDATO_ÅMD(BER_SØ_IND) > 19920000)  THEN                       
        DO;                                                  /*NY92*/           
          DCL W_KOMP  FIXED DEC (3);                         /*NY92*/           
                                                             /*NY92*/           
          W_KOMP   = B02.ALDERSP.KT(BER_SØ_IND);             /*NY92*/           
          IF B02.VIRK_DATO_ÅMD(BER_SØ_IND) > 19970000    THEN                   
             CALL JUSTER_40PROS(BER_SØ_IND);    /*200010*/                      
          ELSE                                                                  
             CALL JUSTER_FORVENTET(W_KOMP,BER_SØ_IND);       /*4022*/           
                                                                                
          CALL JUSTER_AE_MED_F(W_KOMP,AP_TEI);               /*NY92*/           
                                                             /*NY92*/           
        END;                                                 /*NY92*/           
     ELSE                                                    /*NY92*/           
       DO;                                                   /*NY92*/           
         B02.TP_NETTO (BER_SØ_IND)      = B02.TP_BRUTTO (BER_SØ_IND);           
         B02.ALDERSP.AP_GP_NETTO(BER_SØ_IND) = B02.ALDERSP.GP                   
                                                      (BER_SØ_IND);             
         B02.ALDERSP.AP_TP_NETTO(BER_SØ_IND) = B02.ALDERSP.TP                   
                                                      (BER_SØ_IND);             
         /* NY 161000 MARTIN */                                                 
         B02.ALDERSP.AP_ST_NETTO(BER_SØ_IND) = B02.ALDERSP.ST                   
                                                      (BER_SØ_IND);             
         /* ENDRA 161000 MARTIN                                                 
         IF B02.GT_LOV92(BER_SØ_IND) > (B02.AP_TP_NETTO(BER_SØ_IND) +           
                               B02.TP_NETTO(BER_SØ_IND)             +           
                               B02.ALDERSP.ST(BER_SØ_IND))    THEN              
         SE NEDANFOR */                                                         
         IF B02.GT_LOV92(BER_SØ_IND) > (B02.AP_TP_NETTO(BER_SØ_IND) +           
                               B02.TP_NETTO(BER_SØ_IND)             +           
                               B02.AP_ST_NETTO(BER_SØ_IND))    THEN             
            DO;                                                                 
               IF B02.VENTEFAKTOR(BER_SØ_IND) > 0      &                        
                  B02.TP_NETTO(BER_SØ_IND)    > 0      THEN                     
                  DO;                                                           
                    W_HJELP = (GAR92_AVD - B02.TP_BRUTTO(BER_SØ_IND))           
                         *  B02.VENTEFAKTOR(BER_SØ_IND)/100;                    
                    B02.GT_LOV92(BER_SØ_IND) = W_HJELP + 0.5                    
                                    +  B02.GT_LOV92(BER_SØ_IND);                
                  END;                                                          
               /* ENDRA 161000 MARTIN                                           
               B02.GT_TILLEGG_LOV92(BER_SØ_IND) =                               
                                B02.GT_LOV92(BER_SØ_IND)                        
                             - (B02.AP_TP_NETTO(BER_SØ_IND) +                   
                                B02.ALDERSP.ST(BER_SØ_IND)  +                   
                                B02.TP_NETTO(BER_SØ_IND)); */                   
               B02.GT_TILLEGG_LOV92(BER_SØ_IND) =                               
                                B02.GT_LOV92(BER_SØ_IND)                        
                             - (B02.AP_TP_NETTO(BER_SØ_IND) +                   
                                B02.AP_ST_NETTO(BER_SØ_IND)  +                  
                                B02.TP_NETTO(BER_SØ_IND));                      
            END;                                                                
         ELSE                                                                   
            DO;                                                                 
               B02.GT_LOV92(BER_SØ_IND) = 0;                                    
               B02.GT_TILLEGG_LOV92(BER_SØ_IND) = 0;                            
            END;                                                                
       END;                                                  /*NY92*/           
                                                             /*NY92*/           
     IF B02.GT_TILLEGG_LOV92(BER_SØ_IND) = 0       THEN                         
        DO;                                                                     
           B02.GT_LOV92(BER_SØ_IND)         = 0;                                
           B02.GT_LOV92(BER_EK_IND)         = 0;                                
        END;                                                                    
                                                                                
     IF B02.IKKE_GP(BER_SØ_IND)  = 'J'    THEN                                  
        B02.ALDERSP.VT_GP(BER_SØ_IND)  = 0 ;                                    
                                                                                
                                                                                
                                                                                
     IF B02.GARANTI_TP(BER_SØ_IND) > 0            THEN                          
        IF (G > 34100)                       !                                  
           ((G = 34100) & (TRANSTYPE ^= 27))      THEN                          
        DO;                                                                     
          IF B02.TP_BRUTTO(SØKER_IND) = 0 THEN                                  
             GARANTITILLEGG    = B02.GARANTI_TP(BER_SØ_IND)                     
                              -  (B02.ALDERSP.ST    (BER_SØ_IND) +              
                                  B02.ALDERSP.TP    (BER_SØ_IND) +              
                                  B02.ALDERSP.VT_TP (BER_SØ_IND) ) ;            
          ELSE                                                                  
          DO;                                                                   
             ALT1   = B02.GARANTI_TP(BER_SØ_IND) * 0.55 + 0.5                   
              - B02.ALDERSP.TP(BER_SØ_IND)                                      
              - (B02.ALDERSP.VT_TP(BER_SØ_IND) -                                
                    (B02.TP_BRUTTO (BER_SØ_IND) *                               
                     B02.VENTEFAKTOR(BER_SØ_IND) / 100));                       
             ALT2   = B02.GARANTI_TP(BER_SØ_IND)                                
                      -  (B02.ALDERSP.ST    (BER_SØ_IND) +                      
                          B02.ALDERSP.TP    (BER_SØ_IND) +                      
                          B02.ALDERSP.VT_TP (BER_SØ_IND) +                      
                          B02.TP_BRUTTO     (BER_SØ_IND) ) ;                    
             IF ALT1 > ALT2 THEN                                                
                GARANTITILLEGG = ALT1;                                          
             ELSE                                                               
                GARANTITILLEGG = ALT2;                                          
          END;                                                                  
          IF GARANTITILLEGG > 0        THEN                                     
             DIV_PARAM_OMR.GARANTITILLEGG_TP(BER_SØ_IND) =                      
                           GARANTITILLEGG;                                      
          ELSE                                                                  
            DO;                                                                 
             B02.GARANTI_TP(BER_SØ_IND) = 0;                                    
             GARANTITILLEGG             = 0; /*GARANTITILL. BLIR LAGT */        
            END;                             /*TIL SUM YTELSE,SELV OM*/         
        END;                                /*NEGATIVT TALL. TRUDE0993*/        
                                                                                
     B02.STATUS.SUM_YTELSE(BER_SØ_IND) =                                        
                        B02.ALDERSP.AP_GP_NETTO(BER_SØ_IND) +                   
                        B02.ALDERSP.AP_ST_NETTO(BER_SØ_IND) +                   
           /*161000 MH  B02.ALDERSP.ST(BER_SØ_IND) + */                         
                                 B02.ALDERSP.BT(BER_SØ_IND) +                   
                                 B02.ALDERSP.KT(BER_SØ_IND) +                   
                                 B02.ALDERSP.AP_TP_NETTO(BER_SØ_IND) +          
                                 B02.ALDERSP.VT_GP (BER_SØ_IND) +               
                                 B02.ALDERSP.VT_TP (BER_SØ_IND) +               
                              B02.GT_TILLEGG_LOV92 (BER_SØ_IND) +               
                                 GARANTITILLEGG                 +               
                                 B02.TP_NETTO (BER_SØ_IND);                     
                                                                                
     B02.GP_NETTO (BER_SØ_IND)          = 0;                                    
     B02.GP_BRUTTO (BER_SØ_IND)         = 0;                                    
     B02.ETTEPENS.ST    (BER_SØ_IND)    = 0;                                    
     B02.ETTEPENS.ST_BRUTTO(BER_SØ_IND) = 0; /* JFA 4.2.04 */                   
     B02.ETTEPENS.KT    (BER_SØ_IND)    = 0;                                    
                                                                                
 END BEREGN_ETTERLATT_AP_DEL3;                                                  
