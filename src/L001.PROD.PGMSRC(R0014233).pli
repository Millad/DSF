 /*       SIST ENDRET 04/05-98 12.59.24 AV   JDA7339                  */00000000
 /*       SIST ENDRET 25/02-98 09.47.40 AV   RFA9991                  */00000010
 /* *************************************************************** */  00000020
 /*IDENTIFIKASJON:                                                  */  00000030
 /*    R0014233 - FUNKSJON  I PLI                                   */  00000040
 /*    PROGRAMMERER: OLAV ØYSTEIN HANSEN 28 MAI 1984                */  00000050
 /*HENSIKT:                                                         */  00000060
 /*    BEREGNE BARNETILLEGG                                         */  00000070
 /*PROGRAMTILKNYTNING:                                              */  00000080
 /*    MODULEN INNGÅR I PROGRAMMENE R0014101, 4201, 4401 OG 4501    */  00000090
 /*                                                                 */  00000100
 /*                                                                 */  00000110
 /*BRUK:                                                            */  00000120
 /*    CALL BEREGN_BT(BT,GRAD)                                      */  00000130
 /*                                                                 */  00000140
 /* *************************************************************** */  00000150
                                                                        00000160
    BEREGN_BT:                                                          00000170
                PROC(BT,GRAD);                                          00000180
                                                                        00000190
       DCL BT        FIXED DEC(5),                                      00000200
           GRAD      FIXED DEC(3);                                      00000210
                                                                        00000220
       DCL ANT_REDU  FIXED DEC(5)            INIT (0);                  00000230
       DCL ANT_FULL  FIXED DEC(5)            INIT (0);                  00000240
       DCL ANT_TT_RED  FIXED DEC(5)          INIT (0);                  00000250
       DCL TT_FAKTOR   FIXED DEC(3)          INIT (0);                  00000260
       DCL W_BT      FIXED DEC(9,1);                                    00000270
                                                                        00000280
       DCL I         FIXED BIN(15);                                     00000290
                                                                        00000300
  /*LOVENDRING OM BEHOVSPRØVET FORSØRGINGSTI_ÅMDLLEGG FRA 1.5.91 :*/    00000310
  /* SÅ SNART DET SKAL BEREGNES MED HØYERE GRUNNBELØP ENN     */        00000320
  /* PR 30.4.91, SÅ SKAL ALL BEREGNING SKJE I R0014011        */        00000330
                                                                        00000340
    IF G > 34100 THEN                                                   00000350
       DO;                                                              00000360
          BT = 0;                                                       00000370
          IF B02.FT_SKAL_BEREGNES(BER_SØ_IND) ^= 'J'     THEN           00000380
             B02.FT_SKAL_BEREGNES(BER_SØ_IND) = 'N';                    00000390
          GO TO SLUTT;                                                  00000400
       END;                                                             00000410
                                                                        00000420
    IF B02.PENSJONSTYPE1(BER_EK_IND) = 'G' &                            00000430
       (B02.SIVILSTAND   (BER_EK_IND) = 'G' !                           00000440
        B02.SIVILSTAND   (BER_EK_IND) = 'W' !                           00000450
        B02.SIVILSTAND   (BER_EK_IND) = 'V'  ) & /*9710 HL*/ /*2000-2*/ 00000460
       B02.PENSJONSTYPE2(BER_EK_IND) = 'M'     THEN                     00000470
                                                                        00000480
                                                                        00000490
 /*090496 HL */                                                         00000500
                                                                        00000510
       DO I = 3 TO 14;                                                  00000520
                                                                        00000530
          IF B02.FNR(I)               >  0  &                           00000540
             B02.STATUS_KODE_HIST(I) ^= 'O'      THEN                   00000550
             IF B02.PENSJONSTYPE2(I)     = 'R'   THEN                   00000560
                ANT_REDU = ANT_REDU + 1;                                00000570
             ELSE                                                       00000580
             DO J = 1 TO 13 WHILE (B02.FNR_TILKN(BER_SØ_IND,J) > 0);    00000590
                IF B02.FNR_TILKN(BER_SØ_IND,J) = B02.FNR(I)   THEN      00000600
                DO;                                                     00000610
                   IF B02.FT_FØR_91(BER_SØ_IND,J) = 'R'    THEN         00000620
                      ANT_REDU = ANT_REDU + 1;                          00000630
                   J = 13;                                              00000640
                END;                                                    00000650
             END;                                                       00000660
       END;                                                             00000670
       IF ANT_REDU > 3 THEN                                             00000680
          ANT_REDU = 3;                                                 00000690
                                                                        00000700
  /* HIT HL*/                                                           00000710
                                                                        00000720
 /*BARNETILLEGG SOM TILSTÅS MED VIRKNING ETTER 1.1.91 SKAL */           00000730
 /*REDUSERES DERSOM TT ER REDUSERT - HL */                              00000740
                                                                        00000750
    TT_FAKTOR = B02.TT_ANV(BER_SØ_IND);                                 00000760
    IF TT_FAKTOR     < 40                    THEN                       00000770
                                                                        00000780
       DO I = 3 TO 14;                                                  00000790
                                                                        00000800
          IF B02.FNR(I)               >  0  &                           00000810
             B02.STATUS_KODE_HIST(I) ^= 'O' &                           00000820
             B02.PENSJONSRETT_FØR_91(I) ^= 'J'        THEN              00000830
                                                                        00000840
             ANT_TT_RED = ANT_TT_RED + 1;                               00000850
                                                                        00000860
       END;                                                             00000870
                                                                        00000880
    IF ANT_TT_RED > 0                            THEN                   00000890
       IF ANT_REDU > 0                         THEN                     00000900
          IF ANT_TT_RED > ANT_REDU               THEN                   00000910
                                                                        00000920
             W_BT = ((ANT_REDU * G * 0.125 * TT_FAKTOR/40)   +          00000930
                     ((ANT_TT_RED - ANT_REDU) * G * 0.25 *              00000940
                          TT_FAKTOR/40)                      +          00000950
                     ((B02.ANTALL_BARN(BER_SØ_IND) - ANT_TT_RED)        00000960
                          * G * 0.25)) * GRAD / 100;                    00000970
                                                                        00000980
          ELSE                                                          00000990
                                                                        00001000
             W_BT = ((ANT_TT_RED * G * 0.125 * TT_FAKTOR/40)   +        00001010
                     ((ANT_REDU - ANT_TT_RED) * G * 0.125)      +       00001020
                     ((B02.ANTALL_BARN(BER_SØ_IND) - ANT_REDU)          00001030
                          * G * 0.25)) * GRAD / 100;                    00001040
                                                                        00001050
       ELSE                                                             00001060
                                                                        00001070
          W_BT = ((ANT_TT_RED * G * 0.25 * TT_FAKTOR/40)   +            00001080
                  ((B02.ANTALL_BARN(BER_SØ_IND) - ANT_TT_RED)           00001090
                             * G * 0.25)) * GRAD / 100;                 00001100
    ELSE                                                                00001110
       W_BT = ((ANT_REDU * G * 0.125)              +                    00001120
               ((B02.ANTALL_BARN(BER_SØ_IND) - ANT_REDU)                00001130
                       * G * 0.25)) * GRAD / 100;                       00001140
                                                                        00001150
    BT   = W_BT / 12 + 0.5;                                             00001160
                                                                        00001170
 SLUTT :                                                                00001180
                                                                        00001190
 END BEREGN_BT;                                                         00001200
