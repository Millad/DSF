 /*   SIST ENDRET PÅ PROD   2000.03.20 13.23.08 AV   JDA7339          */        
 /*       SIST ENDRET 22/06-98 12.24.44 AV   JDA7339                  */        
 /*       SIST ENDRET 11/06-98 11.02.38 AV   RFA9991                  */        
 /* ***************************************************************** */        
 /*IDENTIFIKASJON:                                                    */        
 /*    R0011440 - PROSEDYRE I PLI  -  SUBRUTINE UNDER 1420            */        
 /*    PROGRAMMERER: GEIR, FEBRUAR 1982                               */        
 /*HENSIKT:                                                           */        
 /*    HER ORDNES SKILSMISSE OG OPPHØR AV SAMBOERSKAP                 */        
 /*    RUTINEN KALLES OPP AV ENDRINGSRUTINEN SOM BEHANDLER            */        
 /*    ENDRINGSBLANKETTEN.  FOR HVER FAMILIEPERSON SOM GÅR            */        
 /*    FEILFRI GJENNOM KONTROLLENE BLIR DET OPPRETTET NY STATUS       */        
 /*    FOR PERSONEN, OG SØKERENS STATUS OPPDATERES.                   */        
 /*    TRANSTYPE SETTES I TRANSAKSJONEN.                              */        
 /*PROGRAMTILKNYTNING:                                                */        
 /*    KALLES OPP AV PROGRAM R0011420 / UE20                          */        
 /*    HAR CALL PÅ               9936  OPPHØR_KOBLING_SKILTE          */        
 /*BRUK:                                                              */        
 /*    CALL MELDING_OM_SKILSMISSE                                     */        
 /*                                                                   */        
 /*ENDRET 29.10.85 AV HERMAN : PERSONER SOM LEVER ADSKILT SKAL HA     */        
 /*                            TRANSTYPE = 52  (IKKE 14)              */        
 /* ***************************************************************** */        
 MELDING_OM_SKILSMISSE:                                                         
   PROC;                                                                        
 %SKIP;                                                                         
 /* ***************************************************************** */        
 /* PROGRAMMET BEGYNNER HER                                           */        
 /* ***************************************************************** */        
 %SKIP;                                                                         
 /* ***************************************************************** */        
 /* DERSOM EKTEFELLEN HAR PENSJON                                     */        
 /* ***************************************************************** */        
    DCL JA           BIT(1)  INIT('1'B);                                        
    DCL I            FIXED BIN (15);                                            
                                                                                
    FAMILIE_SPLITTET_IND = JA;                                                  
    IF B01.PENSJONSTYPE2(SØKER_IND) = 'S'          THEN                         
                                                                                
          TRANSTYPE = 14;                                                       
 /* ***************************************************************** */        
 /* DERSOM GRUNNPENSJONEN HAR VÆRT REDUSERT                           */        
 /* ***************************************************************** */        
                                                                                
    ELSE                                                                        
    IF B01.PENSJON_FØR_9802(SØKER_IND) ^= 'J'    THEN                           
       DO;                                                                      
          IF B01.EK_INNT_OVER_2G (SØKER_IND)  = 'J'  !                          
            ((B01.SIVILSTAND      (SØKER_IND)  = 'V')  &                        
             (B01.PENSJONSTYPE2   (EKTEF_IND)  = 'P' !                          
              B01.PENSJONSTYPE2   (EKTEF_IND)  = 'N' )) THEN                    
                                                                                
            TRANSTYPE = 14;                                                     
                                                                                
       END;                                                                     
                                                                                
    ELSE                                                                        
    IF B01.PENSJON_FØR_9802(EKTEF_IND) ^= 'J'    THEN                           
       DO;                                                                      
          IF B01.EK_INNT_OVER_2G (EKTEF_IND)  = 'J'  !                          
            ((B01.SIVILSTAND      (EKTEF_IND)  = 'V')  &                        
             (B01.PENSJONSTYPE2   (SØKER_IND)  = 'P' !                          
              B01.PENSJONSTYPE2   (SØKER_IND)  = 'N' )) THEN                    
                                                                                
            TRANSTYPE = 14;                                                     
                                                                                
       END;                                                                     
                                                                                
 /* ***************************************************************** */        
 /* ELLER OM DET ER EKTEFELLETILLEGG                                  */        
 /* ***************************************************************** */        
    ELSE                                                                        
       IF B01.PENSJONSTYPE2(SØKER_IND) = 'F'          THEN                      
                                                                                
             TRANSTYPE = 15;                                                    
                                                                                
 /* FJERNET 22.10.84 HL -                                            */         
 /*          B01.VIRK_DATO_ÅM =                                      */         
 /*                           F_SNU_DATO(E1S.VIRK_DATO_MÅ) + 0001;   */         
                                                                                
       ELSE                                                                     
                                                                                
             TRANSTYPE = 52;                                                    
                                                                                
    CALL OPPHØR_KOBLING_SKILTE (SØKER_IND,EKTEF_IND);    /*9936*/               
                                                                                
 /* SKAL DET FORETAS NY BEREGNING FORDI BARNETILLEGGET IKKE LENGER */           
 /* SKAL VÆRE REDUSERT ?                                           */           
                                                                                
    IF B01.PENSJONSTYPE2(SØKER_IND) = 'M' !                                     
       B01.PENSJONSTYPE2(EKTEF_IND) = 'M' THEN                                  
       DO;                                                                      
          DO I = 3 TO 14 WHILE                                                  
             (B02.FNR(I) > 0);                                                  
             IF B02.PENSJONSTYPE2(I) = 'R' THEN                                 
                DO;                                                             
                   TRANSTYPE = 47;                                              
                                                                                
       /* EVENTUELLE FORSØRGEDE BARN SKAL IKKE LENGER HA REDUSERT */            
       /* BARNETILLEGG.                                           */            
                                                                                
                   B02.PENSJONSTYPE2(I) = ' ';                                  
                END;                                                            
          END;                                                                  
       END;                                                                     
 RETUR:                                                                         
   END MELDING_OM_SKILSMISSE;                                                   
