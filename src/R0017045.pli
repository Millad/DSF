 /*   SIST ENDRET PÅ PROD   2008.05.31 11.11.10 AV   SPA2990          */        
 /*   SIST ENDRET PÅ PROD   2002.06.21  9.11.30 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2000.07.07 13.01.18 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.05.23 13.09.14 AV   JDA7339          */        
 /*       SIST ENDRET 04/05-98 13.15.51 AV   JDA7339                  */        
 /*       SIST ENDRET 11/03-98 12.06.14 AV   JDA7339                  */        
 /*       SIST ENDRET 04/03-83 12.22.31 AV   DYBVIK                   */        
 /*       SIST ENDRET 21/01-83 14.27.40 AV   OLSEN                    */        
 /* **************************************************************** */         
 /*IDENTIFIKASJON:                                                   */         
 /*    R0017045 - PROSEDYRE I PLI                                    */         
 /*    PROGRAMMERER: JAN, JANUAR 1983                                */         
 /*HENSIKT:                                                          */         
 /*    RUTINEN SETTER INN KODER FOR TEKSTTYPE V I TEKSTTABELLEN FOR  */         
 /*    STØNADSBREV.                                                  */         
 /*PROGRAMTILKNYTNING:                                               */         
 /*    KALLES OPP AV PROGRAM R0017001                                */         
 /*    INCLUDES I R0017001;                                          */         
 /*BRUK:                                                             */         
 /*   CALL PLASSER_TEKSTTYPE_V_KODE(IND)                             */         
 /*   OBS   KALLES BARE OPP NÅR TRANSTYPEN = 27  (SATSENDRING)       */         
 /*ENDRET 16.6.86 HL - FEILRAPP 348. DET SKAL TESTES På ST-PROSENT   */         
 /*                   OG IKKE PÅ BELØPENE.                           */         
 /* **************************************************************** */         
 PLASSER_TEKSTTYPE_V_KODE:PROC(IND);                                            
   DCL                                                                          
      IND                     FIXED BIN (15),                                   
      INDEX                   FIXED BIN (15);                                   
   DCL                                                                          
      W_HELP_ÅMD              PIC '99999999',                                   
      W_HELP_M  DEF W_HELP_ÅMD POS (5) PIC '99',                                
      W_HELP_Å  DEF W_HELP_ÅMD POS (1) PIC '9999',                              
      W_VIRK_ÅMD              PIC '99999999',                                   
      W_VIRK_Å  DEF W_VIRK_ÅMD POS (1) PIC '9999',                              
      W_VIRK_M  DEF W_VIRK_ÅMD POS (5) PIC '99',                                
      W_VIRK_ÅMD_FIX          FIXED DEC (9);                                    
   DCL HUNDREDEL              FIXED DEC (7);                                    
   DCL HUNDREDEL_TS           FIXED DEC (7);  /*TEST*/                          
                                                                                
    /* ============================================================== */        
    /* PROGRAMMET STARTER HER                                         */        
    /* ============================================================== */        
    TEKSTTYPE_V1_KODE = '    ';                                                 
                                                                                
    W_HELP_ÅMD                = TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD;               
    W_VIRK_Å                  = W_HELP_Å;                                       
    W_VIRK_M                  = W_HELP_M;                                       
    W_VIRK_ÅMD_FIX            = W_VIRK_ÅMD;                                     
                                                                                
 /* ***************************************************** */                    
 /*                                                       */                    
 /* ENDRING VED NY LOV 1.1.92                             */                    
 /*                                                       */                    
 /* ***************************************************** */                    
                                                                                
   IF TRANS_OPPL_OMR.TRANSTYPE = 27         &                                   
      TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD = 19920100 THEN                          
      IF (B01.YRKEPENS.TP (IND)                                                 
        + B01.UFØRPENS.TP (IND)                                                 
        + B01.ETTEPENS.TP_NETTO(IND)                                            
        + B01.ALDERSP.AP_TP_NETTO(IND)) ^=                                      
         (B02.YRKEPENS.TP (IND)                                                 
        + B02.UFØRPENS.TP (IND)                                                 
        + B02.ETTEPENS.TP_NETTO(IND)                                            
        + B02.ALDERSP.AP_TP_NETTO(IND)) THEN                                    
         DO;                                                                    
           /* TP ER OMREGNET PGA NYE REGLER FOR BEREGNING AV          */        
           /* PENSJONSPOENG                                           */        
           TEKSTTYPE_V1_KODE = 'V116';                                          
           GOTO L999;                                                           
         END;                                                                   
      ELSE                                                                      
      IF (B01.YRKEPENS.TP (IND)                                                 
        + B01.UFØRPENS.TP (IND)                                                 
        + B01.ETTEPENS.TP_NETTO(IND)                                            
        + B01.ALDERSP.AP_TP_NETTO(IND)) =                                       
         (B02.YRKEPENS.TP (IND)                                                 
        + B02.UFØRPENS.TP (IND)                                                 
        + B02.ETTEPENS.TP_NETTO(IND)                                            
        + B02.ALDERSP.AP_TP_NETTO(IND)) THEN                                    
         DO;                                                                    
           /* FRA 1.MAI 1992 ER DERES TP OMREGNET FORDI DEN           */        
           /* GARANTERTE MINSTEPENSJONEN ER FORHØYET                  */        
           TEKSTTYPE_V1_KODE = 'V199';                                          
           GOTO L999;                                                           
         END;                                                                   
                                                                                
   DO I = 1 TO 65;                                                              
      HUNDREDEL = G_TAB_PERIODE(I).PERIODE_START_ÅMD/100;                       
      HUNDREDEL_TS = W_VIRK_ÅMD_FIX/100;     /*Y2K*/                            
   /* IF HUNDREDEL = W_VIRK_ÅMD_FIX             THEN   */                       
      IF HUNDREDEL = HUNDREDEL_TS               THEN                            
         DO;                                                                    
            INDEX = I;                                                          
            I     = 99;                                                         
         END;                                                                   
   END; /* DO I = 1 TO 60 */                                                    
                                                                                
                                                                                
   IF B02.PENSJONSTYPE2(IND) = 'O' THEN                                         
      DO;  /* HAR OVERGANGSSTØNAD */                                            
                                                                                
         IF (G_TAB_PERIODE.ST_PROSENT_E (INDEX) =                               
             G_TAB_PERIODE.ST_PROSENT_E (INDEX - 1))     &                      
                                                                                
            (G_TAB_PERIODE.ST_PROSENT_S (INDEX) =                               
             G_TAB_PERIODE.ST_PROSENT_S (INDEX - 1))     THEN                   
                                                                                
         /*SÆRTILLEGGET ER IKKE ENDRET*/                                        
                                                                                
                TEKSTTYPE_V1_KODE = 'V082';                                     
                                                                                
         ELSE                                                                   
                                                                                
            IF G_TAB_PERIODE.GRUNNBELØP (INDEX)      =                          
               G_TAB_PERIODE.GRUNNBELØP (INDEX - 1)      THEN                   
                                                                                
                TEKSTTYPE_V1_KODE = 'V084';                                     
                                                                                
          ELSE                                                                  
                                                                                
                TEKSTTYPE_V1_KODE = 'V086';                                     
                                                                                
      END; /* HAR OVERGANGSSTØNAD */                                            
                                                                                
   ELSE                                                                         
      DO;  /* HAR PENSJON - IKKE OVERGANGSSTØNAD */                             
         IF ((G_TAB_PERIODE.ST_PROSENT_E (INDEX) =                              
              G_TAB_PERIODE.ST_PROSENT_E (INDEX - 1))  &                        
             (G_TAB_PERIODE.ST_PROSENT_S (INDEX) =                              
              G_TAB_PERIODE.ST_PROSENT_S (INDEX - 1))) &                        
              TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD ^= 19950100  THEN                
                                                                                
 /* JFA 4.4  (B02.ALDERSP.ST(IND) = 0 &                !                        
        2000  B02.UFØRPENS.ST(IND) = 0 &                                        
              B02.ETTEPENS.ST(IND) = 0 &                                        
              B02.ETTEPENS.ST_BRUTTO(IND) = 0 &                                 
              B02.ETTEBARN.ST(IND) = 0 &                                        
              B02.YRKEPENS.ST(IND) = 0 ) THEN                     */            
                /* SÆRTILLEGGET ER IKKE ENDRET - ELLER UTEN ST        */        
                /* GRUNNBELØPET ER ENDRET TIL - MH 310100             */        
                                                                                
                TEKSTTYPE_V1_KODE = 'V081';                                     
                                                                                
         ELSE                                                                   
            IF G_TAB_PERIODE.GRUNNBELØP (INDEX)      =                          
               G_TAB_PERIODE.GRUNNBELØP (INDEX - 1)      THEN                   
                TEKSTTYPE_V1_KODE = 'V083';                                     
          ELSE                                                                  
             DO;                                                                
                IF TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD = 19980500  &               
                   B02.ANTALL_BARN(IND) > 0   THEN  /* KONS RF */               
                   TEKSTTYPE_V1_KODE = 'V126';                                  
                ELSE                                                            
 /* ***************************************************************** */        
 /* TEKST V189 SKRIVES FRA MAI 02 (BT-SATS ØKES FRA 30% TIL 40 % (JD) */        
 /* ***************************************************************** */        
                IF TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD = 20020500  &               
                 ((B02.UFØRPENS.BT(IND)  +                                      
                   B02.YRKEPENS.BT(IND)  +                                      
                   B02.ALDERSP.BT (IND)  +                                      
                   B02.FORSI.BT_SÆRBARN(IND)) > 0) THEN                         
                   TEKSTTYPE_V1_KODE = 'V189';                                  
                ELSE                                                            
                   TEKSTTYPE_V1_KODE = 'V085'; /*ENDRING I G OG ST*/            
             END;                                                               
      END; /* HAR PENSJON - IKKE OVERGANGSSTØNAD */                             
                                                                                
                                                                                
 /* ***************************************************************** */        
 /* HVIS PTYPE3 = 'A' ENDRES TEKSTKODE TIL Å GJELDE ATTFØRING         */        
 /* ***************************************************************** */        
                                                                                
  IF B02.STATUS.PENSJONSTYPE3(IND) = 'A' THEN                                   
     DO;                                                                        
        SUBSTR(TEKSTTYPE_V1_KODE,2,1) = '6';                                    
     END;                                                                       
 L999:                                                                          
                                                                                
   END PLASSER_TEKSTTYPE_V_KODE;                                                
