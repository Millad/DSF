 /*   SIST ENDRET PÅ PROD   2003.09.25 14.00.50 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2003.06.18 14.35.28 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2002.01.07 12.38.11 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2000.11.07 12.22.46 AV   JDA7339          */        
 /*       SIST ENDRET 20/07-98 10.03.53 AV   JDA7339                  */        
 /*       SIST ENDRET 22/06-98 08.58.35 AV   RFA9991                  */        
 /* ***************************************************************** */        
 /*IDENTIFIKASJON:                                                    */        
 /*    R0014243 - PROSEDYRE I PLI.                                    */        
 /*    PROGRAMMERER: TRUDE, SEPT 1991                                 */        
 /*HENSIKT:                                                           */        
 /*    RUTINEN OPPDATERER AP_GP_NETTO,AP_TP_NETTO,                    */        
 /*                       SÆRTILLEGG OG KOMPTILLEGG                   */        
 /*                       GT_TILLEGG_LOV92                            */        
 /*       KAN NULLSTILLE  GT_LOV92                                    */        
 /*    ALDERSP.GP/TP/ST ER NETTO PENSJONSBELØP                        */        
 /*PROGRAMTILKNYTNING:                                                */        
 /*    PROGRAMMET BLIR INKLUDERT I                                    */        
 /*BRUK:                                                              */        
 /*    CALL AP_JUSTER_MED_FORVENTET_INNTEKT;                          */        
 /* ***************************************************************** */        
 JUSTER_AE_MED_F: PROC(KT,AP_TEI);                                              
    DCL KT  DEC FIXED (3),                                                      
        AP_TEI DEC FIXED (7);                                                   
    DCL                                                                         
       I             FIXED BIN(15)    INIT (0),                                 
       W_BRUTTO_AP   DEC FIXED(7)     INIT (0),                                 
       W_BRUTTO_GP   DEC FIXED(5)     INIT (0),                                 
       W_BRUTTO_TP   DEC FIXED(5)     INIT (0),                                 
       W_BRUTTO_TP_EP   DEC FIXED(7)     INIT (0),                              
       W_BRUTTO_ST   DEC FIXED(5)     INIT (0),                                 
       W_BRUTTO_GT   DEC FIXED(7)     INIT (0),                                 
       W_SUM_TP      DEC FIXED(7)     INIT (0),                                 
       FRADRAG_F     DEC FIXED(7)     INIT (0),                                 
       FRIBELØP      DEC FIXED(7)     INIT (0),                                 
       G_HALVE       DEC FIXED(7)     INIT (0),                                 
       W_NETTO_AP    DEC FIXED(7)     INIT (0),                                 
       W_KT_FRADRAG  DEC FIXED(3)     INIT (0),                                 
       W_ST_FRADRAG  DEC FIXED(5)     INIT (0),                                 
       REST_FRADRAG  DEC FIXED(5)     INIT (0),                                 
       W_GP_FRADRAG  DEC FIXED(5)     INIT (0),                                 
       W_TEI         DEC FIXED(7)     INIT (0),                                 
       EP_SUM_YTELSE DEC FIXED(7)     INIT (0),                                 
       W_ETTER92     DEC FIXED(7)     INIT (0),                                 
       W_KT          DEC FIXED(3)     INIT (0);                                 
                                                                                
                                                                                
    DCL ROUND        BUILTIN;                                                   
    /* ============================================================== */        
    W_KT    = KT;                                                               
    W_TEI   = AP_TEI;                                                           
    FRADRAG_F = (B02.FAI(BER_SØ_IND) * 100);                                    
    G_HALVE   = G / 2;                                                          
  /*FøLGENDE FELT FLYTTES INN I SUM-YTELSE JD 01.03.2002*/                      
    W_SUM_YTELSE = B02.AP_GP_NETTO(BER_SØ_IND)                                  
                 + B02.AP_TP_NETTO(BER_SØ_IND)                                  
                 + B02.AP_ST_NETTO(BER_SØ_IND)                                  
                 + B02.ALDERSP.KT (BER_SØ_IND)                                  
                 + B02.GT_TILLEGG_LOV92(BER_SØ_IND);                            
 /* LOVENDRING FRA 1.1.2002  : */                                               
                                                                                
   IF TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD > 20020000   &                           
      B02.PENSJONSTYPE1(BER_SØ_IND)    =   'A'   THEN                           
      FRIBELØP = 2 * G;                                                         
   ELSE                                                                         
      FRIBELØP = G;                                                             
                                                                                
   IF FRADRAG_F  ^> FRIBELØP  THEN                                              
    DO;  /* SKAL IKKE REDUSERES */                                              
      B02.AP_GP_NETTO(BER_SØ_IND)   = B02.ALDERSP.GP(BER_SØ_IND);               
      B02.AP_TP_NETTO(BER_SØ_IND)   = B02.ALDERSP.TP(BER_SØ_IND);               
      /* NYTT FELT 131000 MARTIN */                                             
      B02.AP_ST_NETTO(BER_SØ_IND)   = B02.ALDERSP.ST(BER_SØ_IND);               
      B02.TP_NETTO(BER_SØ_IND)      = B02.TP_BRUTTO (BER_SØ_IND);               
      B02.ALDERSP.KT(BER_SØ_IND)    = W_KT;                                     
      IF B02.GT_LOV92(BER_SØ_IND)   > 0          THEN                           
         DO;                                                                    
            B02.GT_TILLEGG_LOV92(BER_SØ_IND) = B02.GT_LOV92(BER_SØ_IND)         
                                       -  B02.AP_TP_NETTO(BER_SØ_IND)           
                                       -  B02.TP_NETTO(BER_SØ_IND)              
                                       -  B02.ALDERSP.ST(BER_SØ_IND);           
                                                                                
            IF B02.GT_TILLEGG_LOV92(BER_SØ_IND) < 0    THEN                     
               DO;                                                              
                  B02.GT_TILLEGG_LOV92(BER_SØ_IND) = 0;                         
                  B02.GT_LOV92(BER_SØ_IND)         = 0;                         
               END;                                                             
         END;                                                                   
      ELSE                                                                      
         B02.GT_TILLEGG_LOV92(BER_SØ_IND) = 0;                                  
    END; /* SKAL IKKE REDUSERES */                                              
  ELSE                                                                          
    DO;  /* SKAL REDUSERES */                                                   
       FRADRAG_F = FRADRAG_F - FRIBELØP;                                        
       FRADRAG_F = ( FRADRAG_F * 0.4) / 12;                                     
   /******* DIV_PARAM_OMR.FULLT_SÆRTILLEGG  = W_ST;??******/                    
   /******* DIV_PARAM_OMR.FULLT_SÆRTILLEGG MÅ BRUKES 03.93 TRUDE*/              
                                                                                
       W_BRUTTO_GP    =  B02.ALDERSP.GP(BER_SØ_IND);                            
       W_BRUTTO_TP    =  B02.ALDERSP.TP(BER_SØ_IND);                            
       W_BRUTTO_TP_EP    =  B02.TP_BRUTTO(BER_SØ_IND);                          
     /*W_BRUTTO_ST    =  DIV_PARAM_OMR.FULLT_SÆRTILLEGG(BER_SØ_IND); */         
       W_BRUTTO_ST    =  B02.ALDERSP.ST(BER_SØ_IND);                            
       W_ETTER92     =  W_BRUTTO_TP + W_BRUTTO_ST + W_BRUTTO_TP_EP;             
                                                                                
       IF B02.GT_LOV92(BER_SØ_IND)  > W_ETTER92    THEN                         
          W_BRUTTO_GT  = B02.GT_LOV92(BER_SØ_IND) - W_ETTER92;                  
                                                                                
       W_BRUTTO_AP    = W_BRUTTO_GP + W_BRUTTO_TP + W_BRUTTO_ST +               
                        B02.TP_BRUTTO(BER_SØ_IND) + W_KT                        
                        + W_BRUTTO_GT;                                          
                                                                                
       IF FRADRAG_F > W_BRUTTO_AP  THEN                                         
          DO;  /* FRADRAGET ER STØRRE ENN BRUTTO - NULLSTILL */                 
             B02.AP_GP_NETTO           (BER_SØ_IND)    = 0;                     
             B02.AP_TP_NETTO           (BER_SØ_IND)    = 0;                     
             B02.TP_NETTO              (BER_SØ_IND)    = 0;                     
             /* NYTT FELT 131000 MARTIN                                         
             B02.ALDERSP.ST            (BER_SØ_IND)    = 0; */                  
             B02.AP_ST_NETTO           (BER_SØ_IND)    = 0;                     
             B02.ALDERSP.KT            (BER_SØ_IND)    = 0;                     
             B02.GT_TILLEGG_LOV92      (BER_SØ_IND)    = 0;                     
          END; /* FRADRAGET ER STØRRE ENN BRUTTO - NULLSTILL */                 
       ELSE                                                                     
           DO;  /* FRADRAGET ER MINDRE ENN BRUTTO - REDUSER */                  
                                                                                
                W_NETTO_AP            = W_BRUTTO_AP - FRADRAG_F;                
                                                                                
                AP_GP               = ROUND(                                    
                                   B02.ALDERSP.GP(BER_SØ_IND) *                 
                                   W_NETTO_AP / W_BRUTTO_AP , 0);               
                REST_FRADRAG =                                                  
                         FRADRAG_F - B02.ALDERSP.GP(BER_SØ_IND)                 
                                   + AP_GP;                                     
                                                                                
                IF  B02.TP_BRUTTO(BER_SØ_IND)  > 0 THEN                         
                  DO;  /* REDUSER TP */                                         
                     EP_TP              = ROUND(                                
                                        B02.TP_BRUTTO(BER_SØ_IND) *             
                                        W_NETTO_AP / W_BRUTTO_AP , 0);          
                     REST_FRADRAG = REST_FRADRAG -                              
                                    B02.TP_BRUTTO(BER_SØ_IND) +                 
                                    EP_TP;                                      
                  END; /* REDUSER TP */                                         
                                                                                
                IF  W_BRUTTO_GT  > 0 THEN                                       
                  DO;                                                           
                     AP_GT             = ROUND(                                 
                                        W_BRUTTO_GT *                           
                                        W_NETTO_AP / W_BRUTTO_AP , 0);          
                     REST_FRADRAG = REST_FRADRAG -                              
                                    W_BRUTTO_GT +                               
                                    AP_GT;                                      
                  END;                                                          
                                                                                
                IF  W_BRUTTO_TP  > 0 THEN                                       
                  DO;                                                           
                    IF G > 35500   THEN                                         
                      DO;                                                       
                        IF B02.ALDERSP.ST(BER_SØ_IND)  = 0 THEN                 
                           AP_TP  = W_BRUTTO_TP - REST_FRADRAG;                 
                        ELSE                                                    
                          DO;                                                   
                            AP_TP             = ROUND(                          
                                                W_BRUTTO_TP *                   
                                         W_NETTO_AP / W_BRUTTO_AP , 0);         
                           REST_FRADRAG = REST_FRADRAG -                        
                                          W_BRUTTO_TP +                         
                                          AP_TP;                                
                          END;                                                  
                      END;                                                      
                    ELSE                                                        
                      DO;                                                       
                        AP_TP          = ROUND(                                 
                                         W_BRUTTO_TP *                          
                                         W_NETTO_AP / W_BRUTTO_AP , 0);         
                        REST_FRADRAG = REST_FRADRAG -                           
                                       W_BRUTTO_TP +                            
                                       AP_TP;                                   
                      END;                                                      
                  END;                                                          
                                                                                
                IF  W_BRUTTO_ST  > 0 THEN                                       
                  DO;                                                           
                    IF W_KT = 0 THEN                                            
                      AP_ST       = W_BRUTTO_ST - REST_FRADRAG;                 
                    ELSE                                                        
                      DO;                                                       
                        AP_ST          = ROUND(                                 
                                         W_BRUTTO_ST *                          
                                         W_NETTO_AP / W_BRUTTO_AP , 0);         
                        REST_FRADRAG = REST_FRADRAG -                           
                                       W_BRUTTO_ST +                            
                                       AP_ST;                                   
                      END;                                                      
                  END;                                                          
                                                                                
                IF W_KT > REST_FRADRAG             THEN                         
                   AP_KT = W_KT - REST_FRADRAG;                                 
                ELSE                                                            
                   AP_KT = 0;                                                   
           END; /* FRADRAGET ER MINDRE ENN BRUTTO - REDUSER */                  
      EP_SUM_YTELSE = AP_GP + AP_TP + EP_TP + AP_ST + AP_KT + AP_GT;            
      IF W_SUM_YTELSE < EP_SUM_YTELSE THEN                                      
        DO;                                                                     
          IF EP_SUM_YTELSE  < 10 THEN  /*OK IFL. P.JESPERSEN 271191*/           
             DO;  /* REDUSERT YTELSE < 10 KR MND, SETTES TIL 0 */               
                B02.AP_GP_NETTO(BER_SØ_IND) = 0;                                
                B02.AP_TP_NETTO(BER_SØ_IND) = 0;                                
                B02.TP_NETTO(BER_SØ_IND) = 0;                                   
                /* NYTT FELT 131000 MARTIN                                      
                B02.ALDERSP.ST(BER_SØ_IND) = 0; */                              
                B02.AP_ST_NETTO(BER_SØ_IND) = 0;                                
                B02.ALDERSP.KT(BER_SØ_IND) = 0;                                 
                B02.GT_TILLEGG_LOV92(BER_SØ_IND) = 0;                           
             END; /* REDUSERT YTELSE < 10 KR MND, SETTES TIL 0 */               
          ELSE                                                                  
             DO;  /* REDUSERT YTELSE >= 10 KR MND, UTBETALES */                 
                B02.AP_GP_NETTO(BER_SØ_IND) = AP_GP;                            
                B02.AP_TP_NETTO(BER_SØ_IND) = AP_TP;                            
                B02.TP_NETTO(BER_SØ_IND) = EP_TP;                               
                /* NYTT FELT 131000 MARTIN                                      
                B02.ALDERSP.ST(BER_SØ_IND) = AP_ST; */                          
                B02.AP_ST_NETTO(BER_SØ_IND) = AP_ST;                            
                B02.ALDERSP.KT(BER_SØ_IND) = AP_KT;                             
                B02.GT_TILLEGG_LOV92(BER_SØ_IND) = AP_GT;                       
             END; /* REDUSERT YTELSE >= 10 KR MND, UTBETALES */                 
           IF (DIV_PARAM_OMR.BEREGNINGS_ALT_EGEN = '2' !                        
               DIV_PARAM_OMR.BEREGNINGS_ALT_EGEN = '4' ) THEN                   
              /* NY TEST SE REMEDY 228 */                                       
              DIV_PARAM_OMR.AP_KODE_AEY(BER_SØ_IND) = 'E';                      
        END;                                                                    
                                                                                
    END;                                                                        
                                                                                
    B02.UTTAKS_APD(BER_SØ_IND)   = 000; /* 999 ØNSKE FRA NORTRYGD*/             
    B02.APD(BER_SØ_IND)          = 000; /* 999 ØNSKE FRA NORTRYGD*/             
                                                                                
    /* TRUDE 210792 : */                                                        
    IF B02.ALDERSP.UTTAKSDATO_ÅMD(BER_SØ_IND) = 0 THEN                          
       B02.ALDERSP.UTTAKSDATO_ÅMD(BER_SØ_IND) =                                 
                                B02.VIRK_DATO_ÅMD(BER_SØ_IND);                  
                                                                                
 RETUR:                                                                         
 END JUSTER_AE_MED_F;                                                           
