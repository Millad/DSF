 /*   SIST ENDRET PÅ PROD   2005.12.05 13.50.05 AV   SPA2970          */        
 /*   SIST ENDRET PÅ TEST   2005.11.30 13.11.07 AV   SPA2970          */        
 /*   SIST ENDRET PÅ PROD   2005.09.26 13.50.10 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2005.09.26 13.31.21 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2005.05.23 13.52.47 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2004.07.16 12.51.59 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.03.12 13.47.17 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2003.02.27 12.41.56 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2002.11.21 13.33.43 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2002.09.25 10.52.21 AV   JDA2970          */        
 /*       SIST ENDRET 10/05-99 10.39.37 AV   JDA7339                  */        
 /*       SIST ENDRET 10/05-99 10.37.13 AV   JDA7339                  */        
                                                                                
  /* **************************************************************** */        
  /*IDENTIFIKASJON:                                                   */        
  /*    R0015601 - HOVEDPROGRAM I PLI                                 */        
  /*                                                                  */        
  /*    PROGRAMMERER: S. LUNDEBY , SFK , DESEMBER 1982                */        
  /*                                                                  */        
  /*HENSIKT:                                                          */        
  /*    SJEKKER ROT-SEGMENTET OM PERSONEN ER SPERRET                  */        
  /*    OG SPERRER DET MED :                                          */        
  /*                                                                  */        
  /*    ROT_SEGM. EKSISTERTE      :      J                            */        
  /*    ROT_SEGM. EKSISTERTE IKKE :      D     (FOR DUMMY-SEGM)       */        
  /*                                                                  */        
  /*PROGRAMTILKNYTNING:                                               */        
  /*                                                                  */        
  /*BRUKER                                                            */        
  /*                                                                  */        
  /* **************************************************************** */        
                                                                                
  R001560:                                                                      
    PROC            (COMMAREA_PEKER)                                            
           OPTIONS  (MAIN);                                                     
                                                                                
    %PAGE;                                                                      
    %INCLUDE P0019906;                              /* TRANS_OPPL_OMR */        
                                                                                
    %PAGE;                                                                      
    %INCLUDE P0019908;                              /* KOM_OMR        */        
                                                                                
    %PAGE;                                                                      
    %INCLUDE P0019910;                              /* STYRINGS_OMR   */        
                                                                                
    %PAGE;                                                                      
    %INCLUDE P0019912;                              /* DIV_PARAM_OMR  */        
                                                                                
    %PAGE;                                                                      
       DCL 1 B01              BASED (B01_PEKER),                                
    %INCLUDE P0019921;                              /* B01            */        
                                                                                
    %PAGE;                                                                      
       DCL 1 B02              BASED (B02_PEKER),                                
    %INCLUDE P0019921;                              /* B02            */        
                                                                                
    %PAGE;                                                                      
    /* ************************************************************** */        
    /*                                                                */        
    /*   INNEBYGDE PLI-FUNKSJONER                                     */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
       DCL                                                                      
         ( ADDR , CSTG , LOW , STG , SUBSTR )  BUILTIN;                         
       DCL                                                                      
         PLITDLI ENTRY;                                                         
                                                                                
    /* ************************************************************** */        
    /*                                                                */        
    /*   ARBEIDSFELTER SOM BRUKES AV UIB (USER INTERFACE BLOCK)       */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
    %INCLUDE DLIUIB;                                                            
                                                                                
       DCL                                                                      
           UIB_RC_OK                 BIT  (8)             INIT ( 0 );           
       DCL                                                                      
           COMMAREA_PEKER            POINTER;                                   
       DCL                                                                      
           W01_HJELPE_PTR            POINTER;                                   
                                                                                
    /* ************************************************************** */        
    /*                                                                */        
    /*   OPPTELINGS- OG LOOP-PARAMETRE M.M                            */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
       DCL 1  W01_OMR                   UNALIGNED,                              
              2   I                     FIXED  BIN  (15)     INIT ( 0 ),        
              2   W02_ANT_GANGER_I_WAIT FIXED  BIN  (15)     INIT ( 0 ),        
              2   B                            CHAR (1)      INIT (' '),        
              2   SAVE_FUNKSJONSKODE           CHAR (1)      INIT (' ');        
                                                                                
                                                                                
    /* ************************************************************** */        
    /*                                                                */        
    /*   DLI CALL-PARAMETRE + ANDRE KODER .                           */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
       DCL 1  W01_DLI_PARAM,                                                    
              2   W02_GHU                 CHAR (4)   INIT ('GHU '),             
              2   W02_ISRT                CHAR (4)   INIT ('ISRT'),             
              2   W02_REPL                CHAR (4)   INIT ('REPL'),             
              2   W02_PARM_CT_3     FIXED BIN  (31)  INIT (3),                  
              2   W02_PARM_CT_4     FIXED BIN  (31)  INIT (4),                  
              2   W02_OUT2                CHAR (20)  INIT ((20)' '),            
              2   W02_SPERREBEHANDLING    BIT  (1)   INIT ('0'B),               
              2   W02_PERSONEN_ER_SPERRET BIT  (1)   INIT ('1'B);               
                                                                                
       DCL    W01_IO                 CHAR (100)      INIT ( (100) ' ');         
       DCL    W01_HJELPE_IO          CHAR (100) BASED (W01_HJELPE_PTR);         
                                                                                
    /* ************************************************************** */        
    /*                                                                */        
    /*   DL1 SSA-OMRÅDER FOR ROT                                      */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
       DCL    SSA_UQUAL                         CHAR (9)  INIT ((9)' ');        
                                                                                
       DCL 1  SSA1_RF0PERSN          UNALIGNED,                                 
              2    HDEL                         CHAR (17) INIT                  
                   ('RF0PERSN(FNR     '),                                       
              2    REL_OP                       CHAR (2)  INIT (' ='),          
              2    PKEY               FIXED     DEC  (11) INIT ( 0  ),          
              2    HP                           CHAR (1)  INIT (')' );          
                                                                                
    /* ************************************************************** */        
    /*                                                                */        
    /*   PCB-PEKER-OMR    OG    PCB-OMR                               */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
    %PAGE;                                                                      
    %INCLUDE P0012002;                         /* PCB_UIB_PEKER_OMR   */        
                                                                                
    %PAGE;                                                                      
       DCL 1  RF1              BASED (RF1_PCB_PEKER),                           
    %INCLUDE P0012003;                         /* PCB                 */        
                                                                                
                                                                                
                                                                                
                                                                                
                                                                                
    /* ************************************************************** */        
    /*                                                                */        
    /*      SLUTT PÅ DEKLARASJONENE , EKSEKVERINGEN STARTER .         */        
    /*     ---------------------------------------------------        */        
    /*                                                                */        
    /*                                                                */        
    /*      INITIERING AV PARAMETRE TIL DIV_PARAM_OMR.                */        
    /*                                                                */        
    /*      SETTER UIBPTR = UIB_PEKER   SOM OVERFØRES ETTER           */        
    /*      INITIERINGS-KALL   I     R0012001 .                       */        
    /*                                                                */        
    /* ************************************************************** */        
    ATK_KOM_PTR                          =  ADDR(KOM_OMR.ATK_COM_OMR);          
    DB_STATUS_KODE                       =   '  '         ;                     
    IF FEIL_MELD_NR    =   0  THEN                                              
         PROGRAM_ID         =   'R0015601'   ;                                  
         UIBPTR             =    UIB_PEKER   ;                                  
                                                                                
    /* ********************************************************** */            
    /*        SJEKKER OM SPERRE-BEHANDLING SKAL UTFØRES           */            
    /* ********************************************************** */            
                                                                                
    CALL P010_SPERREBEHANDLING;                                                 
                                                                                
                                                                                
    /* ********************************************************** */            
    /*        SJEKKER ROT-SEGM FINNES FRA FØR.                    */            
    /* ********************************************************** */            
                                                                                
    SSA1_RF0PERSN.PKEY                      =    SEARCH_FNR  ;                  
    CALL P020_LES_ROT_GHU;                                                      
                                                                                
    IF   DLIUIB.UIBFCTR                    ^=    UIB_RC_OK         THEN         
      DO;                                                                       
         /* ********************************************** */                   
         /* UIB'S RETURCODE ER UAKSEPTABEL                 */                   
         /* ********************************************** */                   
  L100:                                                                         
         FEIL_MELD_NR                 =   500;                                  
         FEIL_VED_LABEL               =  '100';                                 
         GO TO L999;                                                            
      END;                                                                      
                                                                                
                                                                                
    DO   WHILE ( W02_PERSONEN_ER_SPERRET );                                     
     SELECT    ( RF1.STATUS_KODE);                                              
      WHEN     ('  ')                                                           
        /* ****************************************************** */            
        /*                                                        */            
        /*   PERSONEN FINNES I DB. PERSON-OPPLYSNINGENE           */            
        /*   LEGGES I B01 .                                       */            
        /*                                                        */            
        /*   UNDERSØKER OM HAN ER SPERRET :                       */            
        /*   1. GANG FORUTSETTES PERSONEN SPERRET .               */            
        /*                                                        */            
        /*   HVIS IKKE :                                          */            
        /*   -----------                                          */            
        /*     SPERRE-KODEN SETTES LIK  J  FOR Å VISE AT PERSONEN */            
        /*     ER UNDER BEHANDLING OG ROT-SEGM. LEGGES UT IGJEN   */            
        /*     MED SPERRE-KODEN .                                 */            
        /*                                                        */            
        /*   HVIS :                                               */            
        /*   -----------                                          */            
        /*     GÅR PROGRAMMET I WAIT OG PRØVER PÅ NYTT ETTER EN   */            
        /*     GITT PERIODE .                                     */            
        /*                                                        */            
        /* ****************************************************** */            
                                                                                
        DO;                                                                     
          /* ************************************************** */              
          /* NULLSTILLER SPERRE-KODEN I DO-LØKKEN.              */              
          /* ************************************************** */              
                                                                                
           W02_PERSONEN_ER_SPERRET   =   '0'B ;                                 
                                                                                
           SUBSTR            (ADDR (B01.RF0PERSN (POS_I_B01) )                  
           -> W01_HJELPE_IO,1,STG (B01.RF0PERSN (POS_I_B01)))                   
                              =    W01_IO;                                      
                                                                                
          /* ************************************************** */              
          /* KONTROLL AV ACF2 FOR TILLGANGE TIL TKNR            */              
          /*                                                    */              
          /* ************************************************** */              
           IF ACF2_AUTO            = 'B' !                                      
              ACF2_F_KODE          = 'J' ! /* FAMILIA KODE ER SET OK */         
              FUNKSJONSKODE        = 'D' !                                      
              FUNKSJONSKODE        = 'S' ! /* SATSVIS KJøRING      */           
              FUNKSJONSKODE        = 'A'    THEN    /* ADMIST     */            
              FEIL_MELD_NR    =  0  ;                                           
          ELSE                                                                  
              DO;                                                               
                 CALL  ATK_KONTROLL ;                                           
  L105:                                                                         
                 IF FEIL_MELD_NR    >  0  THEN                                  
                    DO;                                                         
                                                                                
                       FEIL_VED_LABEL               =  '105';                   
                       GO TO L999;                                              
                    END;                                                        
              END;                                                              
          /* ************************************************** */              
          /* HVIS IKKE FORESØRSEL :                             */              
          /*      SJEKKER OM PERSONEN ER SPERRET                */              
          /* ************************************************** */              
                                                                                
           IF W02_SPERREBEHANDLING             THEN                             
                                                                                
            IF  B01.RF0PERSN.SPERRE (POS_I_B01)    ^= ' '    THEN               
                W02_PERSONEN_ER_SPERRET             = '1'B      ;               
            ELSE                                                                
              DO;                                                               
                 B01.RF0PERSN.SPERRE    (POS_I_B01) = ' '       ;               
                 W01_HJELPE_PTR                     =                           
                     ADDR (B01.RF0PERSN (POS_I_B01))            ;               
              END;                                                              
                                                                                
                                                                                
          /* ************************************************** */              
          /*                                                    */              
          /* HVIS PERSON ER SPERRET GÅR PROGRAMMET I WAIT       */              
          /* OG PRØVER PÅ NYTT ETTER   X  SEK.                  */              
          /*                                                    */              
          /* ETTER 5 FORSØK TERMINERER PROGRAMMET OG GÅR UT MED */              
          /* MELDING OM AT PERSONEN ER UNDER BEHANDLING AV EN   */              
          /* ANNEN TRANS. OPERATØREN BØR VENTE EN STUND OG      */              
          /* FORSØKE PÅ NYTT .                                  */              
          /*                                                    */              
          /* ************************************************** */              
                                                                                
          IF   W02_PERSONEN_ER_SPERRET                 THEN                     
                                                                                
            IF W02_ANT_GANGER_I_WAIT  <   5            THEN                     
              DO;                                                               
                W02_OUT2        = ' CICS DELAY  5      ';                       
                CALL P020_LES_ROT_GHU;                                          
                W02_ANT_GANGER_I_WAIT  =   W02_ANT_GANGER_I_WAIT + 1;           
              END;                                                              
            ELSE                                                                
              DO;                                                               
                /* ********************************************** */            
                /* PERSONEN BEHANDLES AV EN ANNEN TRANS           */            
                /* VIDERE BEHANDLING AVBRYTES .                   */            
                /* ********************************************** */            
  L110:                                                                         
                FEIL_MELD_NR                 =   499;                           
                FEIL_VED_LABEL               =  '110';                          
                GO TO L999;                                                     
              END;                                                              
          ELSE                                                                  
            DO;                                                                 
              /* ************************************************ */            
              /*                                                  */            
              /* PERSONEN  ER IKKE UNDER BEHANDLING .             */            
              /*                                                  */            
              /* HVIS IKKE FORESPØRSEL :                          */            
              /* -----------------------                          */            
              /*     SPERRE-KODEN ER SATT OG RFOPERSN OPPDATERES  */            
              /*     MED DEN                                      */            
              /*                                                  */            
              /* ************************************************ */            
                                                                                
              IF W02_SPERREBEHANDLING                           THEN            
                DO;                                                             
                  W01_IO                =     W01_HJELPE_IO;                    
                  CALL PLITDLI               (W02_PARM_CT_3,                    
                                              W02_REPL,                         
                                              RF1,                              
                                              W01_IO);                          
                                                                                
                  IF  RF1.STATUS_KODE  ^=     '  '                              
                    ! UIBFCTR          ^=     UIB_RC_OK    THEN                 
                    DO;                                                         
                      /* *************************************** */             
                      /*  FEIL VED REPLACE AV ROT-SEGM .         */             
                      /* *************************************** */             
  L120:                                                                         
                      FEIL_MELD_NR        =  500;                               
                      FEIL_VED_LABEL      = '120';                              
                      DB_STATUS_KODE      =  RF1.STATUS_KODE;                   
                      GO TO L999;                                               
                    END;                                                        
                END;                                                            
            END;      /*  ELSE                                    */            
        END;          /*  WHEN  ( '  ' )                          */            
                                                                                
      WHEN ('GE')                                                               
        /* ****************************************************** */            
        /*      PERSONEN FINNES IKKE I DB.                        */            
        /*                                                        */            
        /* HVIS IKKE FORESPØRSEL ELLER FJERNING :                 */            
        /* --------------------------------------                 */            
        /*                                                        */            
        /*     OPPRETTER ET FORELØPIG ROTSEGMENT MED SPERRE       */            
        /*     SPERRE-KODEN    =  D    FOR DUMMY-SEGMENT          */            
        /*     ---------------------                              */            
        /* ****************************************************** */            
        DO;                                                                     
          IF FUNKSJONSKODE        ^= 'F'       &                                
             FUNKSJONSKODE        ^= 'E'       &                                
             EIBTRNID             ^= 'RF05'    THEN                             
             DO;                                                                
                                                                                
              B01.RF0PERSN(POS_I_B01)   =  '';                                  
              B01.RF0PERSN.SPERRE (POS_I_B01)   =  ' ';                         
              B01.RF0PERSN.FNR    (POS_I_B01)   =   SEARCH_FNR;                 
              B01.RF0PERSN.TKNR   (POS_I_B01)   =   BO_TKNR;                    
              B01.RF0PERSN.PERSN_KODE(POS_I_B01) = ' ';                         
              W01_HJELPE_PTR        =  ADDR (B01.RF0PERSN (POS_I_B01));         
                                                                                
                                                                                
              W01_IO                =       W01_HJELPE_IO;                      
              SSA_UQUAL             =      'RF0PERSN ';                         
              CALL PLITDLI                 (W02_PARM_CT_4,                      
                                            W02_ISRT,                           
                                            RF1,                                
                                            W01_IO,                             
                                            SSA_UQUAL);                         
                                                                                
              IF   RF1.STATUS_KODE ^=       '  '                                
                !  UIBFCTR         ^=       UIB_RC_OK        THEN               
                DO;                                                             
                   /* *********************************************** */        
                   /*  FEIL VED INSERT AV DUMMY - SEGM .              */        
                   /* *********************************************** */        
  L130:                                                                         
                   DB_STATUS_KODE  =  RF1.STATUS_KODE;                          
                   FEIL_MELD_NR    =  500;                                      
                   FEIL_VED_LABEL  = '130';                                     
                   GO TO L999;                                                  
                END;                                                            
            END;      /*    IF  FUNKSJONSKODE  ^= 'F'         */                
                                                                                
          /* ************************************************ */                
          /*   SETTER SPERRE-KODEN = FALSE  (DO-LØKKEN)       */                
          /*   NULLSTILLER RF0PERSN                           */                
          /* ************************************************ */                
                                                                                
          W02_PERSONEN_ER_SPERRET             =   '0'B ;                        
                                                                                
          B01.RF0PERSN.FNR    (POS_I_B01)     =    0   ;                        
          B01.RF0PERSN.TKNR   (POS_I_B01)     =    BO_TKNR  ;                   
          B01.RF0PERSN.SPERRE (POS_I_B01)     =   ' '  ;                        
        END;                                                                    
                                                                                
      OTHERWISE                                                                 
        DO;                                                                     
          /* ************************************************ */                
          /*    UGYLDIG STATUSKODE FRA DLI                    */                
          /* ************************************************ */                
  L140:                                                                         
          FEIL_MELD_NR    =  500;                                               
          FEIL_VED_LABEL  = '140';                                              
          DB_STATUS_KODE  =  RF1.STATUS_KODE;                                   
          GO TO L999;                                                           
        END;                                                                    
     END;     /*   SELECT : STATUS-KODE ETTER KALL MOT ROT  */                  
                                                                                
    END;      /*   DO WHILE PERSON_ER_SPERRET               */                  
                                                                                
                                                                                
                                                                                
    %PAGE;                                                                      
    /* ************************************************************** */        
    /*                                                                */        
    /* PROCEDURE FOR SJEKKE OM SPERRE-BEHANDLINGNE SKAL UTFØRES       */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
    P010_SPERREBEHANDLING:                                                      
      PROC;                                                                     
                                                                                
         W02_SPERREBEHANDLING            =  '1'B;                               
                                                                                
         SELECT   ( FUNKSJONSKODE );                                            
                                                                                
            WHEN  ( 'F' )                                                       
                                                                                
               /************************************************** */           
               /*   FORESPØRSEL                                    */           
               /************************************************** */           
                                                                                
               W02_SPERREBEHANDLING  =  '0'B;                                   
                                                                                
            WHEN  ( 'E' )                                                       
                                                                                
               /************************************************** */           
               /* ENDRING . SJEKKER OM PERSONEN ER LEST INN FØR .  */           
               /************************************************** */           
                                                                                
               DO I = 1 TO 14  WHILE  ( W02_SPERREBEHANDLING );                 
                                                                                
                  IF SEARCH_FNR               =  B02.FNR (I)  THEN              
                     W02_SPERREBEHANDLING     =  '0'B      ;                    
               END;                                                             
                                                                                
            OTHERWISE ;                                                         
                                                                                
         END;   /*   SELECT  (FUNKSJONSKODE)                     */             
                                                                                
    END P010_SPERREBEHANDLING;                                                  
                                                                                
                                                                                
                                                                                
                                                                                
    %PAGE;                                                                      
    /* ************************************************************** */        
    /*                                                                */        
    /* PROCEDURE FOR LESE ROT-SEGM              ( GHU   )             */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
    P020_LES_ROT_GHU:                                                           
      PROC;                                                                     
                                                                                
      CALL PLITDLI                              (W02_PARM_CT_4,                 
                                                 W02_GHU,                       
                                                 RF1,                           
                                                 W01_IO,                        
                                                 SSA1_RF0PERSN);                
    END P020_LES_ROT_GHU;                                                       
                                                                                
                                                                                
  L999:                                                                         
                                                                                
    EXEC CICS RETURN;                                                           
                                                                                
  /* ******************************************************* */                 
  /* SKRIVER AV :  SATISH PATHAK                             */                 
  /* DATO       :  16.11.2002                                */                 
  /* PROGRAM KONTROLLER FNR OG TKNR MOT ATK TILLGANG        */                  
  /* ******************************************************* */                 
  ATK_KONTROLL: PROC;                                                           
                                                                                
      IF B01.RF0PERSN.PERSN_KODE (POS_I_B01) = 'I' THEN                         
         B01.RF0PERSN.PERSN_KODE (POS_I_B01) = ' ';                             
      IF B01.RF0PERSN.PERSN_KODE (POS_I_B01) = 'S' THEN                         
         ACF2_F_KODE     = ' '  ;                                               
      IF DIV_PARAM_OMR.FEIL_MELD_NR = 0          THEN                           
         DO;                                                                    
            IF ACF2_F_KODE     = ' ' &                                          
               FEIL_MELD_NR    =  0  THEN                                       
               DO;                                                              
                  BO_TKNR  = B01.RF0PERSN.TKNR (POS_I_B01);                     
                                                                                
                  IF BO_TKNR  >  0 THEN                                         
                     CALL  KONTROLL_ACF2;                                       
                                                                                
                     IF FEIL_MELD_NR       = 0  &                               
                        B01.RF0PERSN.PERSN_KODE (POS_I_B01) =                   
                                                  'S'       THEN                
                        DO;                                                     
                           SAVE_FUNKSJONSKODE        = FUNKSJONSKODE ;          
                           FUNKSJONSKODE        = 'M';                          
                           CALL       KONTROLL_ACF2;                            
                           FUNKSJONSKODE        = SAVE_FUNKSJONSKODE ;          
                        END;                                                    
              END;                                                              
                                                                                
            IF FEIL_MELD_NR       = 0  THEN                                     
               ACF2_F_KODE = 'J';       /* FAMILIA KODE ER SET OK */            
            ELSE                                                                
               ACF2_F_KODE = ' ';       /* FAMILIA KODE ER FJERNT */            
        END;                                                                    
  END ATK_KONTROLL;                                                             
  %INCLUDE R0019999;               /* KONTROLL ACF2           */                
  END    R001560;                                                               
