 /*   SIST ENDRET PÅ PROD   2008.11.05 16.23.39 AV   SPA2990          */        
 /*   SIST ENDRET PÅ PROD   2006.04.24  8.53.54 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2006.04.18 13.16.05 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2006.03.20 13.56.21 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2006.03.20 13.55.47 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2005.06.08 13.18.38 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2005.02.22 11.18.04 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2004.12.17 14.03.17 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.12.16 11.31.23 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2004.12.02 13.26.12 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.12.02 13.19.15 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2004.11.29 14.39.44 AV   TSB2970          */        
 /*   SIST ENDRET PÅ TEST   2004.11.29 14.39.20 AV   TSB2970          */        
 /*   SIST ENDRET PÅ QASS   2004.11.24 11.55.19 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.11.24 11.53.14 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2004.11.15 12.41.29 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.11.15 12.38.23 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2004.11.10 14.11.10 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.11.10 14.10.54 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2004.10.19 10.23.51 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.10.19  9.56.50 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2004.10.01  8.11.36 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.10.01  8.05.53 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2004.09.24 12.02.35 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.09.24 12.00.03 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2004.09.17  9.40.22 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.09.17  9.39.52 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2004.09.16 11.35.22 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.09.16 11.33.29 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2004.09.13  9.27.40 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.09.13  9.26.25 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2004.09.07 12.12.51 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2004.08.18 11.08.45 AV   SPA2970          */        
 /*   SIST ENDRET PÅ QASS   2004.08.06 13.01.27 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.07.13 14.25.13 AV   SPA2970          */        
 /* ----------------------------------------------------------------- */        
 /* IDENTIFIKASJON:                                                   */        
 /*     R0014428 - UNDERPROGRAM I PL1                                 */        
 /*     PROGRAMMERER: MARTIN HELLE, MARS 2004                         */        
 /* ----------------------------------------------------------------- */        
 /* HENSIKT:                                                          */        
 /*     RUTINEN BENYTTES FOR BEREGNING OG OPPDATERING AV              */        
 /*     LOVLIG INNTEKT OG INNTEKTSKODER                               */        
 /* ----------------------------------------------------------------- */        
 /* VED ENDRING:                                                      */        
 /*     R0014401 & R0014501 MÅ KOMPILERES!  OG 9F01                   */        
 /* ----------------------------------------------------------------- */        
 /* ----------------------------------------------------------------- */        
 OPPDATER_INNTEKTSGRENSE:PROC(IND);                                             
    /* -------------------------------------------------------------- */        
                              /* FELT SOM BLIR OVERFØRT */                      
   DCL  IND                        FIXED BIN(15);                               
                                                                                
                                                                                
       /* BUILIN SOM BENYTTES I DETTE PROGRAM */                                
   DCL ( HBOUND,                                                                
         MAX,                                                                   
         MOD,                                                                   
         ROUND   )                          BUILTIN;                            
                                                                                
       /* FELT SOM BLIR SLÅTT OPP - FRA B02 */                                  
   DCL                                                                          
       BUP_GARANTI_KODE           CHAR      (1)    INIT(' '),                   
       OMREGNINGSDATO_ÅMD         PIC '(8)9'       INIT(0),                     
       OMREGNINGSDATO_ÅM          PIC '(6)9'       INIT(0),                     
       REAKTIVISERING             BIT(1) ALIGNED   INIT('0'B),                  
       UNNTAK_12_12               BIT(1) ALIGNED   INIT('0'B),                  
       UFØRETIDSPUNKT             DEC FIXED (9)    INIT(0),                     
       UFØRETIDSPUNKT_ÅM          DEC FIXED (9)    INIT(0),                     
       UFØREGRAD                  DEC FIXED (3)    INIT(0),                     
       INNT_FØR_UP                DEC FIXED (5)    INIT(0),                     
       INNT_DATO_ÅMD              DEC FIXED (9)    INIT(0),                     
       INNT_DATO_ÅM               DEC FIXED (9)    INIT(0),                     
       YRKEGRAD                   DEC FIXED (3)    INIT(0),                     
       VIRK_DATO_ÅMD              DEC FIXED (9)    INIT(0),                     
       YRKESKODE                  FIXED DEC (3)    INIT(0),                     
       /* FELT SOM BLIR HENTA FRA G_TAB_RE  */                                  
       G_NÅ                       DEC FIXED (5)    INIT(0),                     
       G_NÅ_HALVE                 DEC FIXED (5)    INIT(0),                     
       G_PÅ_UFT                   DEC FIXED (5)    INIT(0),                     
       /* FELT SOM BLIR BRUKT INTERNT */                                        
       FRIINNTEKT                 BIT(1) ALIGNED   INIT('0'B),                  
       NYE_REGLER                 BIT(1) ALIGNED   INIT('0'B),                  
       FRIINNTEKT_FOM_ÅM          PIC '(6)9'         INIT(0),                   
       FØRSTE_VIRK_FOM            DEC FIXED (9)      INIT(0),                   
       RESTARBEIDSEVNE            DEC FIXED (3,2)    INIT(0),                   
       GARANTI_INNTEKT            DEC FIXED (5)      INIT(0),                   
       W_INNTEKTSGRENSE           DEC FIXED (9)      INIT(0),                   
       TOLERANSEBELØP             DEC FIXED (7,2)    INIT(0),                   
       HJELPE_FELT                DEC FIXED (13,11)  INIT(0),                   
       SUM_UP                     DEC FIXED (9)      INIT(0),                   
       W_OIFU1                    DEC FIXED (13)   INIT(0),                     
       W_OIFU2                    DEC FIXED (15,7) INIT(0),                     
       OIFU                       DEC FIXED (9)      INIT(0),                   
       W_OIEU                     DEC FIXED (15,7)  INIT(0),                    
       OIEU                       DEC FIXED (13)   INIT(0),                     
       MAKS_LOVLIG                DEC FIXED (9) INIT(9999900),                  
       TAK                        DEC FIXED (9)    INIT(0);                     
 /* %INCLUDE P0019925;      OVERFØRT TIL  HOVED PROGRAM G_TAB_RE */             
    /* ============================================================== */        
    W_OIFU            = 0;                                                      
    W_OIEU            = 0;                                                      
    IF (DIV_PARAM_OMR.CICS_IND = 'PA' !                                         
        DIV_PARAM_OMR.CICS_IND = 'RA' ) THEN                                    
       /* AUTOKJØRING */                                                        
       OMREGNINGSDATO_ÅMD = DIV_PARAM_OMR.TRANS_VIRKDATO_ÅMD;                   
    ELSE                                                                        
       OMREGNINGSDATO_ÅMD =                                                     
          MAX                                                                   
             (TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD,                                 
              B02.VIRK_DATO_ÅMD(IND),                                           
              DIV_PARAM_OMR.DATO_2000);                                         
    IF (MOD(OMREGNINGSDATO_ÅND,100)=0) THEN                                     
       OMREGNINGSDATO_ÅMD = OMREGNINGSDATO_ÅMD + 1;                             
                                                                                
    OMREGNINGSDATO_ÅM = OMREGNINGSDATO_ÅMD/100;                                 
                                                                                
    FRIINNTEKT_FOM_ÅM = B02.STATUS.FRIINNTEKT_DATO_ÅMD(IND)/100;                
    UFØRETIDSPUNKT    = B02.UFØRHIST.UFT_ÅMD(IND,1);                            
    UFØREGRAD         = B02.UFØRPENS.UFG(IND);                                  
    YRKEGRAD          = B02.YRKEPENS.YUG(IND);                                  
    INNT_FØR_UP       = B02.UFØRPENS.INNTEKT_FØR_UP(IND);                       
    INNT_DATO_ÅMD     = B02.UFØRPENS.INNT_DATO_ÅMD(IND);                        
    UFØRETIDSPUNKT_ÅM = UFØRETIDSPUNKT/100;                                     
    INNT_DATO_ÅM      = INNT_DATO_ÅMD /100;                                     
    FØRSTE_VIRK_FOM   = B02.UFØRHIST.VIRK_UFØRHIST_ÅMD(IND,1);                  
    IF (MOD(INNT_DATO_ÅMD,100)=0) THEN                                          
       INNT_DATO_ÅMD = INNT_DATO_ÅMD + 1;                                       
    YRKESKODE        = B02.STATUS.YRKES_KODE(IND);                              
    BUP_GARANTI_KODE = B02.UFØRHIST.BUP_GAR_KODE(IND,1);                        
    SUM_UP           = B02.UFØRPENS.GP(IND)                                     
                     + B02.UFØRPENS.TP(IND)                                     
                     + B02.UFØRPENS.ST(IND)                                     
                     + B02.YRKEPENS.GP(IND)                                     
                     + B02.YRKEPENS.TP(IND)                                     
                     + B02.YRKEPENS.ST(IND);                                    
    SUM_UP           = SUM_UP * 12;                                             
                                                                                
    IF (B02.UFØRPENS.REAKTIVISERING(IND) = 'J') THEN                            
       DO;                                                                      
          REAKTIVISERING = '1'B;                                                
          FRIINNTEKT     = '1'B;                                                
       END;                                                                     
    ELSE                                                                        
    IF (FRIINNTEKT_FOM_ÅM <= OMREGNINGSDATO_ÅM &                                
        FRIINNTEKT_FOM_ÅM > 0                  ) THEN                           
       FRIINNTEKT = '1'B;                                                       
                                                                                
    IF (B02.UFØRPENS.UNNTAK_VENTETID(IND)   = 'J') THEN                         
       UNNTAK_12_12 = '1'B;                                                     
                                                                                
    /* -------------------------------------------------------------- */        
    /* TD 047 ENDRET 13.11.2004                                       */        
    /*        VI GA TIDLIGERE ALLE MED UNNTAK VENTETID NYE REGLER     */        
    /*        DETTE VAR IKKE MENINGEN                                 */        
    /* -------------------------------------------------------------- */        
 /* IF (REAKTIVISERING                                !                         
        FRIINNTEKT                                    !                         
        FØRSTE_VIRK_FOM                    > 20039999 !                         
        B02.FRIINNTEKT_FRA_HIST_ÅMD(IND,1) > 20039999 ) THEN */                 
    IF (FØRSTE_VIRK_FOM > 20039999) THEN                                        
       NYE_REGLER = '1'B;                                                       
                                                                                
    /* ------------------------------------------------------------- */         
    /* FØRST BLANKER VI INNTEKTSKODENE - VI REGNER ALLTID PÅ NYTT    */         
    /* ------------------------------------------------------------- */         
    B02.INNTEKTSKODE1(IND) = ' ';                                               
    B02.INNTEKTSKODE2(IND) = ' ';                                               
    /* ------------------------------------------------------------- */         
    /* FØRST SETTER VI INNTEKTSKODE1                                 */         
    /* ------------------------------------------------------------- */         
    IF (B02.LØNNSTILSKUDD(IND) = 'J') THEN                                      
       DO;                                                                      
          B02.INNTEKTSKODE1(IND) = 'A';                                         
          B02.INNTEKTSKODE2(IND) = '0';                                         
       END;                                                                     
    ELSE                                                                        
    IF (YRKESKODE = 11 !                                                        
        YRKESKODE = 12 ) THEN                                                   
       DO;                                                                      
          B02.INNTEKTSKODE1(IND) = '2';                                         
          B02.INNTEKTSKODE2(IND) = '0';                                         
       END;                                                                     
    ELSE                                                                        
    IF (YRKESKODE = 18) THEN                                                    
       DO;                                                                      
          B02.INNTEKTSKODE1(IND) = '9';                                         
          B02.INNTEKTSKODE2(IND) = '0';                                         
       END;                                                                     
    ELSE                                                                        
    /* -------------------------------------------------------------- */        
    /* TD 044 ENDRET 10.11.2004                                       */        
    /*        ALLE MED NULL INNTEKTSNIVÅ FØR UFØRHET SKAL UAVHENGIG   */        
    /*        FØRSTE VIRKNINGSDATO HAR KODE 30                        */        
    /* TD 045 ENDRET 10.11.2004                                       */        
    /*        UNNTAK AV UNGE UFØRE SKAL HA GARANTI - OG KODE SOM      */        
    /*        NORMALT                                                 */        
    /* -------------------------------------------------------------- */        
    /* GAMMEL VERSJON                                                           
    IF (UFØRETIDSPUNKT_ÅM ^= INNT_DATO_ÅM !                                     
        INNT_DATO_ÅM      = 0             ) !                                   
       (FØRSTE_VIRK_FOM   > 20040000      &                                     
        INNT_FØR_UP       = 0             ) THEN                                
       DO;                                                                      
          B02.INNTEKTSKODE1(IND) = '3';                                         
          B02.INNTEKTSKODE2(IND) = '0';                                         
       END;                                                                     
    /* NY VERSJON 10.11.2004 */                                                 
    IF (UFØRETIDSPUNKT_ÅM ^= INNT_DATO_ÅM !                                     
        INNT_DATO_ÅM      = 0             !                                     
        INNT_FØR_UP       = 0             ) &                                   
       (BUP_GARANTI_KODE ^= 'B' &                                               
        BUP_GARANTI_KODE ^= 'E'           ) THEN                                
       DO;                                                                      
          B02.INNTEKTSKODE1(IND) = '3';                                         
          B02.INNTEKTSKODE2(IND) = '0';                                         
       END;                                                                     
    ELSE                                                                        
    IF (UFØREGRAD > 49  &                                                       
        UFØREGRAD < 100 ) THEN                                                  
       DO;  /* UFØREGRAD 50 - 99% */                                            
          IF (REAKTIVISERING) THEN                                              
             B02.INNTEKTSKODE1(IND) = '5';                                      
          ELSE                                                                  
             B02.INNTEKTSKODE1(IND) = '1';                                      
       END; /* UFØREGRAD 50 - 99% */                                            
    ELSE                                                                        
    IF (UFØREGRAD < 50) THEN                                                    
       DO;  /* GRAD UNDER 50 */                                                 
          IF (REAKTIVISERING) THEN                                              
             DO;                                                                
                B02.INNTEKTSKODE1(IND) = '8';                                   
                B02.INNTEKTSKODE2(IND) = '0';                                   
             END;                                                               
          ELSE                                                                  
          IF (YRKEGRAD > 29) THEN                                               
             B02.INNTEKTSKODE1(IND) = '0';                                      
          ELSE                                                                  
             DO;                                                                
                B02.INNTEKTSKODE1(IND) = '7';                                   
                /* -------------------------------------------------- */        
                /* TD 043 ENDRET 10.11.2004 GRADERTE MED FØRSTE VIRK  */        
                /*        FØR 2004 KAN IKKE BEREGNES                  */        
                /* -------------------------------------------------- */        
                IF (B02.VIRK_UFØRHIST_ÅMD(IND,1) < 20039999) THEN               
                   B02.INNTEKTSKODE2(IND) = '0';                                
             END;                                                               
       END; /* GRAD UNDER 50 */                                                 
    ELSE                                                                        
    IF (UFØREGRAD = 100) THEN                                                   
       B02.INNTEKTSKODE1(IND) = '4';                                            
                                                                                
    IF (B02.INNTEKTSKODE1(IND) ^= '2' &                                         
        B02.INNTEKTSKODE1(IND) ^= '5' &                                         
        B02.INNTEKTSKODE1(IND) ^= '8' &                                         
        B02.INNTEKTSKODE1(IND) ^= '9' ) THEN                                    
       /* TD 114 VI TAR IKKE HENSYN TIL UNNTAK FOR I1 LIK 2 ELLER 9 */          
       DO;  /* REAKTIVISERING VINNER OVER UNNTAK */                             
          IF (UNNTAK_12_12) THEN                                                
             /* REMEDY 1941 */                                                  
             IF (B02.INNTEKTSKODE1(IND) = '3' ) THEN                            
                IF (BUP_GARANTI_KODE = 'B' !                                    
                    BUP_GARANTI_KODE = 'E' ) THEN                               
                   B02.INNTEKTSKODE1(IND) = '6';                                
                ELSE;                                                           
             ELSE                                                               
                B02.INNTEKTSKODE1(IND) = '6';                                   
       END; /* REAKTIVISERING VINNER OVER UNNTAK */                             
                                                                                
    IF  B02.INNTEKTSKODE1(IND)  ='6' !                                          
        B02.INNTEKTSKODE2(IND) ^='0' !                                          
        /* REMEDY 1941 */                                                       
        (B02.INNTEKTSKODE1(IND) ='3' &                                          
         (BUP_GARANTI_KODE = 'B' !                                              
          BUP_GARANTI_KODE = 'E' )   ) THEN                                     
       DO;  /* DET ER AKTUELT Å BEREGNE INNTEKTSGRENSE */                       
                                                                                
                                                                                
          /* -------------------------------------------------------- */        
          /* FØRST FINNER VI AKTUELLE G VERDIER                       */        
          /* -------------------------------------------------------- */        
          IF (INNT_DATO_ÅM  < 196700) THEN                                      
             /* ----------------------------------------------------- */        
             /* IFLG. BREV UFØRHETSKONTORET 08.09.97:                 */        
             /* "DERSOM UFØRETIDSPUNKTET ER FØR 1967, SKAL DET FØRSTE */        
             /*  FASTSATTE GRUNNBELØPET BENYTTES I OPPJUSTERINGEN, DVS*/        
             /*  KR 5.400, SOM BLE FASTSATT FRA 0167."                */        
             /* ----------------------------------------------------- */        
             G_PÅ_UFT = 5400;                                                   
                                                                                
          DO I = 1 TO HBOUND(G_TAB_PERIODE);                                    
             IF (INNT_DATO_ÅMD >= PERIODE_START_ÅMD(I) &                        
                 INNT_DATO_ÅMD <= PERIODE_SLUTT_ÅMD(I) ) THEN                   
                DO;                                                             
                   G_PÅ_UFT    = G_TAB_PERIODE.GRUNNBELØP(I);                   
                   IF G_NÅ > 0 THEN                                             
                      I=HBOUND(G_TAB_PERIODE);                                  
                END;                                                            
             IF (OMREGNINGSDATO_ÅMD >= PERIODE_START_ÅMD(I) &                   
                 OMREGNINGSDATO_ÅMD <= PERIODE_SLUTT_ÅMD(I) ) THEN              
                DO;                                                             
                   G_NÅ       = G_TAB_PERIODE.GRUNNBELØP(I);                    
                   G_NÅ_HALVE = G_TAB_PERIODE.GRUNNBELØP_HALVE(I);              
                   IF G_PÅ_UFT > 0 THEN                                         
                      I=HBOUND(G_TAB_PERIODE);                                  
                END;                                                            
          END; /* DO I = 1 TO HBOUND G_TAB_PERIODE */                           
                                                                                
          /* -------------------------------------------------------- */        
          /* VI TAR HENSYN TIL UNGE UFØRE                             */        
          /* -------------------------------------------------------- */        
          IF (BUP_GARANTI_KODE = 'B' !                                          
              BUP_GARANTI_KODE = 'E' ) THEN                                     
             DO;                                                                
               /* ----------------------------------------------- */            
               /* GARANTI INNTEKT FØR UFØRHET, SE FORSKRIFT TIL   */            
               /* PARAGRAF 12-7, PARAGRAF 3 FJERDE LEDD.          */            
               /* ----------------------------------------------- */            
               IF INNT_DATO_ÅMD < 20080500   !                                  
                  BUP_GARANTI_KODE = 'E'        THEN                            
                  GARANTI_INNTEKT = ( G_PÅ_UFT * 4.3 / 100 + 0.5);              
               ELSE                                                             
                  GARANTI_INNTEKT = ( G_PÅ_UFT * 4.5 / 100 + 0.5);              
               IF GARANTI_INNTEKT > INNT_FØR_UP THEN                            
                  INNT_FØR_UP = GARANTI_INNTEKT;                                
             END;                                                               
                                                                                
          /* -------------------------------------------------------- */        
          W_OIFU1 = INNT_FØR_UP * 100 * G_NÅ;                                   
          W_OIFU2 = ROUND((W_OIFU1/G_PÅ_UFT),0);                                
          OIFU    = W_OIFU2;                                                    
                                                                                
          IF (UFØREGRAD = 100) THEN                                             
             RESTARBEIDSEVNE = 0;                                               
          ELSE                                                                  
             RESTARBEIDSEVNE = (100 - UFØREGRAD)/100;                           
          W_OIEU = ROUND((RESTARBEIDSEVNE * OIFU),0);                           
          OIEU   = W_OIEU;                                                      
                                                                                
          TAK  = MAX(0,                                                         
                     OIFU - SUM_UP);                                            
                                                                                
          W_INNTEKTSGRENSE = OIEU;                                              
                                                                                
          IF (NYE_REGLER) THEN                                                  
             B02.INNTEKTSKODE2(IND) = '1';                                      
          ELSE                                                                  
             B02.INNTEKTSKODE2(IND) = '5';                                      
                                                                                
          IF (FRIINNTEKT) THEN                                                  
             DO;                                                                
                W_INNTEKTSGRENSE = W_INNTEKTSGRENSE + G_NÅ;                     
                /* TD 51, KORRIGERT REAKTIV ALLTID I2=2 */                      
                IF (NYE_REGLER    !                                             
                    REAKTIVISERING) THEN                                        
                   B02.INNTEKTSKODE2(IND) = '2';                                
                ELSE                                                            
                   B02.INNTEKTSKODE2(IND) = '6';                                
             END;                                                               
                                                                                
          IF (REAKTIVISERING) THEN                                              
             DO;  /* REAKTIVISERING - LEGG TIL TOLERANSEBELØP */                
                /* -------------------------------------------------- */        
                /* IFLG TELEFON M/KARI SANNESS 070804 SKAL VI IKKE    */        
                /* TA HENSYN TIL TAK NÅR VI BEREGNER LOVLIG INNTEKT   */        
                /* I TILFELLER MED REAKTIVISERING                     */        
                /* -------------------------------------------------- */        
                TOLERANSEBELØP   = ROUND((G_NÅ * 0.1),0);                       
                W_INNTEKTSGRENSE = W_INNTEKTSGRENSE + TOLERANSEBELØP;           
             END; /* REAKTIVISERING - LEGG TIL TOLERANSEBELØP */                
          ELSE                                                                  
          IF (W_INNTEKTSGRENSE > TAK  &                                         
              NYE_REGLER              ) THEN                                    
             DO;                                                                
                /* TD 13, KRAVSPESIFIKASJON PKT. 2.1.4 */                       
                W_INNTEKTSGRENSE = MAX(TAK,OIEU);                               
                IF (B02.INNTEKTSKODE2(IND) = '1') THEN                          
                   B02.INNTEKTSKODE2(IND) = '3';                                
                ELSE                                                            
                   B02.INNTEKTSKODE2(IND) = '4';                                
             END;                                                               
                                                                                
          IF (^FRIINNTEKT                   ) &                                 
             ((W_INNTEKTSGRENSE+G_NÅ) > TAK ) &                                 
             (NYE_REGLER                    ) &                                 
             (B02.INNTEKTSKODE2(IND)  = '1' ) THEN                              
             B02.INNTEKTSKODE2(IND) = '3';                                      
       END; /* DET ER AKTUELT Å BEREGNE INNTEKTSGRENSE */                       
    ELSE                                                                        
       /* DET ER IKKE AKTUELT Å BEREGNE INNTEKTSGRENSE */                       
       W_INNTEKTSGRENSE = 0;                                                    
                                                                                
    IF W_INNTEKTSGRENSE > MAKS_LOVLIG                  THEN                     
       B02.LOVLIG_INNTEKT(IND) = MAKS_LOVLIG;                                   
    ELSE                                                                        
       B02.LOVLIG_INNTEKT(IND) = W_INNTEKTSGRENSE;                              
 END OPPDATER_INNTEKTSGRENSE;                                                   
