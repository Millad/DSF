 /*       SIST ENDRET 14/10-98 13.24.52 AV   TSB7339                  */00000000
 /*       SIST ENDRET 02/09-98 12.23.37 AV   SPA7339                  */00000010
 /* ***************************************************************** */00000020
 /*IDENTIFIKASJON:                                                    */00000030
 /*    R0014186 - F_TP92_DES        - INTERN FUNKSJON I PLI           */00000040
 /*    PROGRAMMERER: MARTIN - JAN 98                                  */00000050
 /*    OMSKREVET FOR EØS BEREGNING 1097 (MED DESIMALER) MH            */00000060
 /*HENSIKT:                                                           */00000070
 /*    REGNER TP MED POENGÅR FØR 1992 OG ETTER 1991  (45 OG 42 %)     */00000080
 /*PROGRAMTILKNYTNING:                                                */00000090
 /*                                                                   */00000100
 /*BRUK:                                                              */00000110
 /*    TP=F_TP92_DES(FÅ,SPT,OPT,PÅ,PÅ_ETTER91,UFG,TP_PROSENT)         */00000120
 /* ***************************************************************** */00000130
 F_TP92_DES :                                                           00000140
    PROC(FÅ,G,SPT,OPT,PÅ,PÅ_ETTER91,UFG,TP_PROSENT)                     00000150
         RETURNS(DEC FIXED(7,2));                                       00000160
    DCL                                                                 00000170
       FÅ                 PIC '9999',                                   00000180
       G                  FIXED DEC(5),                                 00000190
       SPT                FIXED DEC(3,2),                               00000200
       OPT                FIXED DEC(3,2),                               00000210
       PÅ                 FIXED DEC(3),                                 00000220
       PÅ_FØR92           FIXED DEC(3),                                 00000230
       PÅ_ETTER91         FIXED DEC(3),                                 00000240
       UFG                FIXED DEC(5,2),                               00000250
       TP_PROSENT         FIXED DEC(3),                                 00000260
       OÅ                 FIXED DEC(3) INIT (0),                        00000270
       TP_WORK            FIXED DEC(11,4) INIT (0),                     00000280
       TP_WORK_2          FIXED DEC(11,4) INIT (0),                     00000290
       TP2_WORK           FIXED DEC(11,4) INIT (0),                     00000300
       TP_WORK_92         FIXED DEC(11,4) INIT (0),                     00000310
       GAMMEL             FIXED DEC(11,4) INIT (0),                     00000320
       NY                 FIXED DEC(11,4) INIT (0),                     00000330
       TP_UT              FIXED DEC(7,2) INIT(0),                       00000340
       PGNAVN             CHAR (8) INIT ('R0014186');                   00000350
   /* =============================================================== */00000360
   IF PÅ_ETTER91 = 0 THEN                                               00000370
      CALL GAMLE_REGLER;                                                00000380
   ELSE                                                                 00000390
      DO;   /* NYE REGLER FRA 1992 */                                   00000400
         PÅ_FØR92 = PÅ - PÅ_ETTER91;                                    00000410
         GAMMEL = G * 45 /100;                                          00000420
         NY     = G * 42 /100;                                          00000430
         IF B02.FRAVIK_BOSATT_KONVENSJON(BER_SØ_IND) = 'J'      !       00000440
            B02.KONVENSJON(BER_SØ_IND)  = 'J' !   /* PENSJON UTLAND  */ 00000450
            PÅ > 2               !                                      00000460
            (FÅ = 1899 & PÅ = 2)   !                                    00000470
            (FÅ = 1898 & PÅ = 1)         THEN                           00000480
            DO;  /* FYLLER VILKÅRA */                                   00000490
               IF FÅ > 1936 THEN                                        00000500
                  DO; /* KAN IKKE FÅ OVERKOMPENSASJON */                00000510
                     TP_WORK = GAMMEL * PÅ_FØR92 / 40;                  00000520
                     TP_WORK_92 = NY * PÅ_ETTER91 / 40;                 00000530
                     TP_WORK = (TP_WORK + TP_WORK_92) * SPT;            00000540
                     TP_WORK = TP_WORK * UFG /100;                      00000550
                     TP_WORK = TP_WORK * TP_PROSENT / 100;              00000560
                     IF B02.PENSJONSTYPE2(BER_SØ_IND) = 'A' THEN        00000570
                        B02.ALTERNATIV_TP(BER_SØ_IND) = TP_WORK + 0.5;  00000580
                     TP_WORK = TP_WORK / 12;                            00000590
                  END; /* KAN IKKE FÅ OVERKOMPENSASJON */               00000600
               ELSE                                                     00000610
                  DO;  /* KAN FÅ OVERKOMP */                            00000620
                     IF FÅ > 1917 THEN                                  00000630
                        OÅ = 20 + FÅ - 1917;                            00000640
                     ELSE                                               00000650
                        OÅ = 20;                                        00000660
                     IF SPT > OPT THEN                                  00000670
                        DO;  /* SPT > OPT */                            00000680
                           TP_WORK = GAMMEL * PÅ_FØR92 / OÅ;            00000690
                           TP_WORK_92 = NY * PÅ_ETTER91 / OÅ;           00000700
                           TP_WORK = (TP_WORK + TP_WORK_92) * OPT;      00000710
                           TP2_WORK = GAMMEL * PÅ_FØR92 / 40;           00000720
                           TP_WORK_92 = NY * PÅ_ETTER91 / 40;           00000730
                           TP2_WORK                                     00000740
                              = (TP2_WORK + TP_WORK_92) * (SPT - OPT);  00000750
                           TP_WORK = (TP_WORK + TP2_WORK) * UFG /100;   00000760
                           TP_WORK = TP_WORK * TP_PROSENT / 100;        00000770
                           IF B02.PENSJONSTYPE2(BER_SØ_IND) = 'A' THEN  00000780
                              B02.ALTERNATIV_TP(BER_SØ_IND)             00000790
                                 = TP_WORK + 0.5;                       00000800
                           TP_WORK = TP_WORK / 12;                      00000810
                        END; /* SPT > OPT */                            00000820
                     ELSE                                               00000830
                        DO;  /* SPT = OPT */                            00000840
                           TP_WORK = GAMMEL * PÅ_FØR92 / OÅ;            00000850
                           TP_WORK_92 = NY * PÅ_ETTER91 / OÅ;           00000860
                           TP_WORK = (TP_WORK + TP_WORK_92) * SPT;      00000870
                           TP_WORK = TP_WORK * UFG /100;                00000880
                           TP_WORK = TP_WORK * TP_PROSENT / 100;        00000890
                           IF B02.PENSJONSTYPE2(BER_SØ_IND) = 'A' THEN  00000900
                           B02.ALTERNATIV_TP(BER_SØ_IND)                00000910
                              = TP_WORK + 0.5;                          00000920
                           TP_WORK = TP_WORK / 12;                      00000930
                        END; /* SPT = OPT */                            00000940
                  END; /* KAN FÅ OVERKOMP */                            00000950
            END; /* FYLLER VILKÅRA */                                   00000960
         ELSE  /* FYLLER IKKJE VILKÅRA */                               00000970
            TP_WORK = 0;                                                00000980
         TP_UT = TP_WORK + 0.005;                                       00000990
     END; /* NYE REGLER FRA 1992 */                                     00001000
 /* ----------------------------------------------------------------- */00001010
 GAMLE_REGLER: PROC;                                                    00001020
    IF B02.FRAVIK_BOSATT_KONVENSJON(BER_SØ_IND) = 'J'      !            00001030
       B02.KONVENSJON(BER_SØ_IND)  = 'J'  !     /* PENSJON UTLAND  */   00001040
       PÅ > 2               !                                           00001050
       (FÅ = 1899 & PÅ = 2)   !                                         00001060
       (FÅ = 1898 & PÅ = 1)         THEN                                00001070
       DO;  /* FYLLER VILKÅRA */                                        00001080
          IF FÅ > 1936 THEN                                             00001090
             DO;  /* KAN IKKE FÅ OVERKOMPENSASJON */                    00001100
                TP_WORK = G * 45 /100;                                  00001110
                TP_WORK = TP_WORK * SPT;                                00001120
                TP_WORK = TP_WORK * PÅ / 40;                            00001130
                TP_WORK = TP_WORK * UFG /100;                           00001140
                TP_WORK = TP_WORK * TP_PROSENT / 100;                   00001150
                IF B02.PENSJONSTYPE2(BER_SØ_IND) = 'A' THEN             00001160
                   B02.ALTERNATIV_TP(BER_SØ_IND) = TP_WORK + 0.5;       00001170
                TP_WORK = TP_WORK / 12;                                 00001180
             END; /* KAN IKKE FÅ OVERKOMPENSASJON */                    00001190
          ELSE                                                          00001200
             DO;  /* KAN FÅ OVERKOMPENSASJON */                         00001210
                IF FÅ > 1917 THEN                                       00001220
                   OÅ = 20 + FÅ - 1917;                                 00001230
                ELSE                                                    00001240
                   OÅ = 20;                                             00001250
                IF SPT > OPT THEN                                       00001260
                   DO;  /* SPT > OPT */                                 00001270
                      TP_WORK = G * 45 /100;                            00001280
                      TP_WORK = TP_WORK * OPT;                          00001290
                      TP_WORK = TP_WORK * PÅ / OÅ;                      00001300
                      TP_WORK_2 = G * 45 / 100;                         00001310
                      TP_WORK_2 = TP_WORK_2 * (SPT - OPT);              00001320
                      TP_WORK_2 = TP_WORK_2 * PÅ / 40;                  00001330
                      TP_WORK = (TP_WORK + TP_WORK_2) * UFG /100;       00001340
                      TP_WORK = TP_WORK * TP_PROSENT / 100;             00001350
                      IF B02.PENSJONSTYPE2(BER_SØ_IND) = 'A' THEN       00001360
                         B02.ALTERNATIV_TP(BER_SØ_IND) = TP_WORK + 0.5; 00001370
                      TP_WORK = TP_WORK / 12;                           00001380
                   END; /* SPT > OPT */                                 00001390
                ELSE                                                    00001400
                   DO;  /* SPT = OPT */                                 00001410
                      TP_WORK = G * 45 / 100;                           00001420
                      TP_WORK = TP_WORK * SPT;                          00001430
                      TP_WORK = TP_WORK * PÅ / OÅ;                      00001440
                      TP_WORK = TP_WORK * UFG /100;                     00001450
                      TP_WORK = TP_WORK * TP_PROSENT / 100;             00001460
                      IF B02.PENSJONSTYPE2(BER_SØ_IND) = 'A' THEN       00001470
                         B02.ALTERNATIV_TP(BER_SØ_IND) = TP_WORK + 0.5; 00001480
                      TP_WORK = TP_WORK / 12;                           00001490
                   END; /* SPT = OPT */                                 00001500
             END; /* KAN FÅ OVERKOMPENSASJON */                         00001510
          TP_UT = TP_WORK + 0.005;                                      00001520
       END; /* FYLLER VILKÅRA */                                        00001530
    ELSE /* FYLLER IKKE VILKÅRA */                                      00001540
       TP_WORK = 0;                                                     00001550
 END GAMLE_REGLER;                                                      00001560
 /* ----------------------------------------------------------------- */00001570
 SLUTT :                                                                00001580
    /* IKKE GI KONV 'F' TILLEGGSPENSJON, TRUDE 060192  */               00001590
    IF B02.BOSATT(BER_SØ_IND)  = 000  &                                 00001600
       B02.TT_16_66(BER_SØ_IND) = 0     &                               00001610
       B02.TT_ETTER_1966(BER_SØ_IND) = 0 &                              00001620
       B02.TT_FØR_1967(BER_SØ_IND)   = 0 &                              00001630
       B02.ALDERSP.KONV_P_KODE(BER_SØ_IND)  = 'F'     &                 00001640
       B02.ALDERSP.KONV_GRAD(BER_SØ_IND)  = 0     THEN                  00001650
       TP_UT  = 0 ;                                                     00001660
       /* TRUDE 060192 HIT */                                           00001670
    RETURN(TP_UT);                                                      00001680
 END F_TP92_DES;                                                        00001690
