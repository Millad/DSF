 /*   SIST ENDRET PÅ PROD   2005.10.27  9.27.30 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2005.10.20 14.51.30 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2004.05.14  8.20.37 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.04.29  8.37.00 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2003.06.11 10.14.48 AV   JDA2970          */        
 /*       SIST ENDRET 22/06-98 10.22.26 AV   JDA7339                  */        
 /*       SIST ENDRET 18/06-98 15.49.45 AV   RFA9991                  */        
 /* *************************************************************** */          
 /*IDENTIFIKASJON:                                                  */          
 /*    R0014326 - PROSEDYRE I PLI                                   */          
 /*HENSIKT:                                                         */          
 /*                                                                 */          
 /*  1. FINNE BESTE YRKEPENS.YPT FOR AVDØD.                         */          
 /*                                                                 */          
 /*  2. OPPDATERE YRKEPENS.YPT OG YRKEPENS YPT_KODE (V), DERSOM     */          
 /*       SPT GIR HØYEST POENG.                                     */          
 /*       SETTE DIV_PARAM_OMR.BEREGNINGS_ALT_AVD = 3, DERSOM DET    */          
 /*       LØNNER SEG Å BRUKE Y-FORDELENE VED BEREGNING AV EP.       */          
 /*                                                                 */          
 /*  3. BEREGNE OG OPPDATERE ETTEPENS : GP_BRUTTO                   */          
 /*                                     TP_BRUTTO                   */          
 /*                                                                 */          
 /*                                                                 */          
 /*PROGRAMTILKNYTNING:                                              */          
 /*    INCLUDES I R0014301                                          */          
 /*BRUK:                                                            */          
 /*    CALL BEREGN_ETTERLATT_YP                                     */          
 /*                                                                 */          
 /* *************************************************************** */          
 BEREGN_ETTERLATT_YP :                                                          
   PROC;                                                                        
                                                                                
  DCL      W_TP_PROSENT      FIXED DEC    (3);                                  
                                                                                
  DCL    1 KAP11,                                                               
          2  GP_BRUTTO         FIXED DEC (5),                                   
          2  TP_BRUTTO         FIXED DEC (5);                                   
                                                                                
  DCL    1 VANLIG,                                                              
          2  GP_BRUTTO         FIXED DEC (5),                                   
          2  TP_BRUTTO         FIXED DEC (5);                                   
                                                                                
  DCL        TP_WORK           FIXED DEC (13,4);                                
  DCL        FAKTOR1           FIXED DEC (13,4);                                
  DCL        FAKTOR2           FIXED DEC (13,4);                                
                                                                                
    IF (B02.YRKEPENS.YUG(BER_EK_IND) = 100 )      &                             
       (B01.YRKEPENS.YUG(BER_EK_IND) < 100 )      &  /*26.1.90 HL*/             
       (B02.YRKEPENS.YPT(BER_EK_IND) < B02.SPT_AVD(BER_SØ_IND)) THEN            
                                                                                
       DO;                                                                      
                                                                                
          B02.YRKEPENS.YPT(BER_EK_IND) = B02.SPT_AVD(BER_SØ_IND);               
                                                                                
          B02.YPT_KODE(BER_EK_IND)     = 'V'; /*VANLIGE REGLER    */            
                                              /*GIR BEST SPT      */            
       END;                                                                     
                                                                                
    IF B02.SIVILSTAND(BER_SØ_IND)   = 'E'   !                                   
       B02.SIVILSTAND(BER_SØ_IND)   = 'U'   THEN                                
           W_TP_PROSENT             = 55;                                       
    ELSE                                                                        
           W_TP_PROSENT             = B02.TP_PROSENT(BER_SØ_IND);               
                                                                                
                                                                                
 /* *************************************************************** */          
 /* REGN GP MED KAP11-FORDELER                                      */          
 /* *************************************************************** */          
                                                                                
    /* NYTT FELT 200107 : */                                                    
        IF B02.PENSJON_FØR_9802(BER_SØ_IND) ^= 'J'   &                          
           B02.RED_GP_3_2_5(BER_SØ_IND)      = 'J'   THEN                       
           DO;                                                                  
              KAP11.GP_BRUTTO = G*B02.YRKEPENS.YUG(BER_EK_IND)/(100*12)         
                                * GP_PROSENT + 0.5;                             
              DIV_PARAM_OMR.REDUSERT_GP_2G(BER_SØ_IND) = 'X';                   
              B02.GP_REDUKSJON_KODE       (BER_SØ_IND) = 'X';                   
           END;                                                                 
        ELSE                                                                    
    KAP11.GP_BRUTTO = G*B02.YRKEPENS.YUG(BER_EK_IND)/(100*12) + 0.5;            
                                                                                
 /* *************************************************************** */          
 /* REGN TP MED KAP11-FORDELER                                      */          
 /* *************************************************************** */          
                                                                                
                                                                                
  TP_WORK  = G * B02.YRKEPENS.YPT(BER_EK_IND);                                  
  TP_WORK  = TP_WORK * B02.YRKEPENS.YUG(BER_EK_IND) /100;                       
  TP_WORK  = TP_WORK * W_TP_PROSENT/100 ;                                       
  TP_WORK  = TP_WORK / 12;                                                      
  IF B02.YRKEPENS.PÅ_ETTER91(BER_EK_IND) = 0    THEN                            
     KAP11.TP_BRUTTO = TP_WORK * 0.45 + 0.5;                                    
                                                                                
  ELSE                                                                          
     DO;                                                                        
        FAKTOR1 = (B02.YRKEPENS.PÅ(BER_EK_IND)                                  
                   - B02.YRKEPENS.PÅ_ETTER91(BER_EK_IND) )                      
                   / B02.YRKEPENS.PÅ(BER_EK_IND) * 0.45;                        
        FAKTOR2 = B02.YRKEPENS.PÅ_ETTER91(BER_EK_IND)                           
                  / B02.YRKEPENS.PÅ(BER_EK_IND) * 0.42;                         
        KAP11.TP_BRUTTO = TP_WORK * (FAKTOR1 + FAKTOR2 + 0.00005) + 0.5;        
     END;                                                                       
                                                                                
                                                                                
     DO;                                                                        
                                                                                
 /* *************************************************************** */          
 /* REGN PENSJON   ETTER VANLIGE REGLER, MEN YUG-PROSENT            */          
 /* *************************************************************** */          
                                                                                
        VANLIG.GP_BRUTTO = B02.GP_BRUTTO(BER_SØ_IND)   *                        
                         B02.YRKEPENS.YUG(BER_EK_IND) /100 + 0.5;               
                                                                                
        VANLIG.TP_BRUTTO = B02.TP_BRUTTO(BER_SØ_IND)   *                        
                         B02.YRKEPENS.YUG(BER_EK_IND) /100 + 0.5;               
                                                                                
        IF (KAP11.GP_BRUTTO + KAP11.TP_BRUTTO) >                                
           (VANLIG.GP_BRUTTO + VANLIG.TP_BRUTTO)      THEN                      
                                                                                
           DO;                                                                  
                                                                                
 /* *************************************************************** */          
 /* BYTT UT VANLIG BEREGNING MED KAP11-BEREGNING                    */          
 /* *************************************************************** */          
                                                                                
              DIV_PARAM_OMR.BEREGNINGS_ALT_AVD = '3';                           
                                                                                
              B02.GP_BRUTTO(BER_SØ_IND) = B02.GP_BRUTTO(BER_SØ_IND) +           
                                  KAP11.GP_BRUTTO - VANLIG.GP_BRUTTO;           
                                                                                
              B02.TP_BRUTTO(BER_SØ_IND) = B02.TP_BRUTTO(BER_SØ_IND) +           
                                  KAP11.TP_BRUTTO - VANLIG.TP_BRUTTO;           
      /*      IF (B02.YRKEPENS.YUG(BER_EK_IND) = 100) THEN                      
                 B02.TT_ANV(BER_EK_IND) = 40;          */                       
           END;                                                                 
     END;                                                                       
                                                                                
                                                                                
                                                                                
                                                                                
     RETUR:                                                                     
                                                                                
   END BEREGN_ETTERLATT_YP;                                                     
                                                                                
