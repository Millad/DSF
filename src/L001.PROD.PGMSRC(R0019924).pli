 /*   SIST ENDRET PÅ PROD   2002.11.28  9.07.17 AV   JDA2970          */        
 /*       SIST ENDRET 04/05-98 13.27.29 AV   JDA7339                  */        
 /* *************************************************************** */          
 /*IDENTIFIKASJON:                                                  */          
 /*    R0019924 - PROSEDYRE I PLI                                   */          
 /*    PROGRAMMERER: FARVIK/HANSEN OKT -82.                         */          
 /*HENSIKT:                                                         */          
 /*    KOBLE TO PERSONER.                                           */          
 /*BRUK:                                                            */          
 /*    CALL KOBLE_TO_PERSONER(INDEKS1,INDEKS2)                      */          
 /*                                                                 */          
 /* *************************************************************** */          
 %SKIP(1);                                                                      
 KOBLE_TO_PERSONER:                                                             
   PROC(IND1,IND2) OPTIONS(REENTRANT);                                          
 %SKIP;                                                                         
   DCL                                                                          
      IND1    FIXED BIN (15),                                                   
      IND2    FIXED BIN (15),                                                   
      IND     FIXED BIN (15),                                                   
      I       FIXED BIN (15);                                                   
 /* *************************************************************** */          
 /* FINN FØRSTE LEDIGE TILKNYTNINGSSEGMENT FOR FØRSTE PERSON.       */          
 /* *************************************************************** */          
                                                                                
      DO I = 1 TO 13 UNTIL (B02.FNR_TILKN(IND1,I) = 0 !                         
                            B02.FNR_TILKN(IND1,I) = B02.FNR(IND2));             
      END;                                                                      
      B02.FNR_TILKN(IND1,I)                 = B02.FNR(IND2);                    
      IF B02.PENSJONSTYPE1(IND1) = 'D' THEN                                     
         IF B02.PENSJONSTYPE1(IND2) = 'N' !                                     
            B02.PENSJONSTYPE1(IND2) = 'B' THEN                                  
            B02.TILKNYTNINGSKODE(IND1,I) = B02.PENSJONSTYPE1(IND2);             
         ELSE                                                                   
            B02.TILKNYTNINGSKODE(IND1,I) = 'E';                                 
      ELSE                                                                      
      IF B02.PENSJONSTYPE1(IND2) = 'D' THEN  /* AB  ENDRET 061299 */            
         B02.TILKNYTNINGSKODE(IND1,I) = 'D'; /*  AB  ENDRET 061299 */           
      ELSE                                                                      
      IF (B02.PENSJONSTYPE1(IND2)  = 'N' !                                      
          B02.PENSJONSTYPE1(IND2)  = 'B') &                                     
          B02.PENSJONSTYPE2(IND2) ^= 'P' THEN                                   
         B02.TILKNYTNINGSKODE(IND1,I) = B02.PENSJONSTYPE2(IND2);                
      ELSE                                                                      
      IF (B02.PENSJONSTYPE2(IND2)  = 'V' !                                      
          B02.PENSJONSTYPE2(IND2)  = 'W')          THEN                         
         B02.TILKNYTNINGSKODE(IND1,I) = B02.PENSJONSTYPE2(IND2);                
      ELSE                                                                      
         DO;                                                                    
            B02.TILKNYTNINGSKODE(IND1,I) = B02.PENSJONSTYPE1(IND2);             
            IF B02.VIRK_DATO_ÅMD(IND1) > 19910400         &                     
  /*HL 0794 */                                                                  
               B02.PENSJONSRETT_FØR_91(IND2) ^= ' '       THEN                  
               CALL SETT_FT_FØR_91(IND1,I,IND2);                                
         END;                                                                   
                                                                                
      DO I = 1 TO 13 UNTIL (B02.FNR_TILKN(IND2,I) = 0 !                         
                            B02.FNR_TILKN(IND2,I) = B02.FNR(IND1));             
      END;                                                                      
      B02.FNR_TILKN(IND2,I)                 = B02.FNR(IND1);                    
      IF B02.PENSJONSTYPE1(IND2) = 'D' THEN                                     
         IF B02.PENSJONSTYPE1(IND1) = 'N' !                                     
            B02.PENSJONSTYPE1(IND1) = 'B' THEN                                  
            B02.TILKNYTNINGSKODE(IND2,I) = B02.PENSJONSTYPE1(IND1);             
         ELSE                                                                   
            B02.TILKNYTNINGSKODE(IND2,I) = 'E';                                 
      ELSE                                                                      
         IF (B02.PENSJONSTYPE1(IND1)  = 'N' !                                   
             B02.PENSJONSTYPE1(IND1)  = 'B') &                                  
             B02.PENSJONSTYPE2(IND1) ^= 'P' THEN                                
            B02.TILKNYTNINGSKODE(IND2,I) = B02.PENSJONSTYPE2(IND1);             
         ELSE                                                                   
         IF (B02.PENSJONSTYPE2(IND1)  = 'V' !                                   
             B02.PENSJONSTYPE2(IND1)  = 'W')     THEN                           
            B02.TILKNYTNINGSKODE(IND2,I) = B02.PENSJONSTYPE2(IND1);             
                                                                                
         ELSE                                                                   
            DO;                                                                 
               B02.TILKNYTNINGSKODE(IND2,I) = B02.PENSJONSTYPE1(IND1);          
               IF B02.VIRK_DATO_ÅMD(IND1) > 19910400         &                  
  /*HL 0794 */                                                                  
                  B02.PENSJONSRETT_FØR_91(IND1) ^= ' '       THEN               
                  CALL SETT_FT_FØR_91(IND2,I,IND1);                             
            END;                                                                
                                                                                
 SETT_FT_FØR_91 :                                                               
    PROC(HOVED_IND,TILKN_IND,FORSØRGET_IND);                                    
                                                                                
   DCL HOVED_IND         FIXED BIN (15);                                        
   DCL TILKN_IND         FIXED BIN (15);                                        
   DCL FORSØRGET_IND     FIXED BIN (15);                                        
   DCL EK_IND            FIXED BIN (15);                                        
                                                                                
   EK_IND = 3 - HOVED_IND;                                                      
                                                                                
   IF B02.PENSJONSTYPE2(FORSØRGET_IND) = 'R'      &                             
      B02.PENSJONSTYPE2(EK_IND)        = 'M'      THEN                          
      DO;                                                                       
         B02.TILKNYTNINGSKODE(HOVED_IND,TILKN_IND) = 'W';                       
         B02.FT_FØR_91(HOVED_IND,TILKN_IND) = 'R';                              
      END;                                                                      
   ELSE                                                                         
   IF B02.PENSJONSTYPE1(FORSØRGET_IND) = 'L'      THEN                          
      DO;                                                                       
         B02.TILKNYTNINGSKODE(HOVED_IND,TILKN_IND) = 'W';                       
         B02.FT_FØR_91(HOVED_IND,TILKN_IND) = 'L';                              
      END;                                                                      
                                                                                
 END SETT_FT_FØR_91;                                                            
                                                                                
   END KOBLE_TO_PERSONER;                                                       
