 /*   SIST ENDRET PÅ PROD   2003.02.27 12.40.37 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2002.11.21 13.32.34 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.10.22 13.05.41 AV   JDA2970          */        
 /*       SIST ENDRET 23/06-98 08.37.53 AV   JDA7339                  */        
 /*       SIST ENDRET 20/05-98 14.18.14 AV   HLA7339                  */        
 /* ***************************************************************** */        
 /*PROGRAM-IDENT:      R0011201                                       */        
 /*                                                                   */        
 /* ***************************************************************** */        
 R00112:                                                                        
   PROC(COMMAREA_PEKER) OPTIONS(MAIN);                                          
 %PAGE;                                                                         
 %INCLUDE S00112;    /*  FT-MAPSETTET   */                                      
 %PAGE;                                                                         
 %INCLUDE P0011201;  /*  TRANSOMR FOR FORSØRGINGSTILL.EKTEF/BARN */             
 %PAGE;                                                                         
 %INCLUDE P0019906;  /*  TRANS-OPPL-OMRÅDE (BASED)      */                      
 %PAGE;                                                                         
 %INCLUDE P0019908;  /*  KOM-OMRÅDE (BASED)             */                      
 %PAGE;                                                                         
 %INCLUDE P0019910;  /*  STYRINGS-OMRÅDE (BASED)        */                      
 %PAGE;                                                                         
 %INCLUDE P0019912;  /*  DIV-PARAM-OMRÅDE (BASED)       */                      
 %PAGE;                                                                         
 %INCLUDE DFHBMSCA;                                                             
 %PAGE;                                                                         
   DCL                                                                          
      (BMSMAPBR,COMMAREA_PEKER) PTR;                                            
   DCL                                                                          
      (ADDR,UNSPEC,CSTG,ONCODE,VERIFY,SUBSTR) BUILTIN;                          
   DCL                                                                          
      FEIL_FUNNET  BIT (1),                                                     
      ONKODE PIC'9999',                                                         
      CURSOR_POS FIXED BIN(15) INIT(-1),                                        
      ONK DEF ONKODE CHAR(4),                                                   
      FEILKODE       CHAR(4),                                                   
      DSNAVN         CHAR(8),                                                   
      ANT_FEIL_SKREVET FIXED DEC (3);                                           
 %SKIP(1);                                                                      
 %PAGE;                                                                         
      ON ERROR SNAP BEGIN       ;                                               
         ON ERROR SYSTEM        ;                                               
         ONKODE=ONCODE          ;                                               
         FEILKODE = ONK         ;                                               
         DSNAVN   = EIBDS       ;                                               
         GO TO FEILBEH          ;                                               
      END;                                                                      
                                                                                
 FEILKODE   = 'FEIL'    ;                                                       
 DSNAVN     = '        ';                                                       
                                                                                
 EXEC CICS HANDLE CONDITION ERROR(FEILBEH);                                     
                                                                                
 EXEC CICS HANDLE AID PF1(PF1) PF2(PF2);                                        
                                                                                
 KOM_OMR.PEKER_LISTE.DIV_PARAM_PEKER  = ADDR(KOM_OMR.DIV_PARAM_OMR);            
                                                                                
 IF FRA_CICS THEN                                                               
  DO;                                                                           
    KOM_OMR.PEKER_LISTE.TRANS_OPPL_PEKER = ADDR(KOM_OMR.TRANS_OPPL_OMR);        
    KOM_OMR.PEKER_LISTE.TRANS_PEKER      = ADDR(KOM_OMR.TRANS_OMR);             
    KOM_OMR.PEKER_LISTE.STYRINGS_PEKER   = ADDR(KOM_OMR.STYRINGS_OMR);          
    ATK_KOM_PTR           = ADDR  (KOM_OMR.ATK_COM_OMR  ) ;                     
  END;                                                                          
                                                                                
 IF HENT_FRAM_MAP  THEN                                                         
   DO;                                                                          
     EXEC CICS SEND MAP('S001121') MAPSET('S001123') MAPONLY                    
                                                   SET (BMSMAPBR);              
     ALLOCATE S001121O;                                                         
     S001121O.CICS_INFOO= CICS_NAVN;                                            
   END;                                                                         
                                                                                
 RECEIVE_MAP:                                                                   
                                                                                
                                                                                
 IF ^HENT_FRAM_MAP THEN                                                         
    DO;                                                                         
       EXEC CICS RECEIVE MAP('S001121')                                         
                                    MAPSET('S001123') SET(BMSMAPBR);            
       CALL BLANK_FT_MELDNR;                                                    
       FEIL_MELD_NR = 0;                                                        
    END;                                                                        
 IF FUNKSJONSKODEL > 0 THEN                                                     
    DO;                                                                         
       FUNKSJONSKODE = FUNKSJONSKODEI;                                          
       EXEC CICS XCTL PROGRAM('R0010301') COMMAREA(KOM_OMR);                    
    END;                                                                        
                                                                                
 IF STYRE_KODEL > 0 THEN                                                        
    DO;                                                                         
       STYREKODE = STYRE_KODEI;                                                 
       TRANSKODE = 'R041';                                                      
       EXEC CICS XCTL PROGRAM('R0010401') COMMAREA(KOM_OMR);                    
    END;                                                                        
                                                                                
                                                                                
 IF STYREKODE ^= 'FT'  &  FRA_CICS THEN                                         
    TRANSKODE = 'R041';                                                         
                                                                                
 ELSE                                                                           
  DO;                                                                           
   TRANS_RETURKODE = TRANSKODE;                                                 
   ANT_FEIL_SKREVET = 0;                                                        
   FEIL_FUNNET = '0'B;                                                          
                                                                                
   SELECT (TRANSKODE);                                                          
    WHEN('R120')                                                                
       DO;                                                                      
          CALL BLANK_FT_SØKER;                                                  
          CALL BLANK_FT_MELDNR;                                                 
          CALL BLANK_FT_BARN;                                                   
          CALL OVERFØR_FT_SØKER;                                                
          CALL KONTROLL_FT_SØKER(FEIL_FUNNET);                                  
       END;                                                                     
    WHEN('R121')                                                                
       DO;                                                                      
         CALL BLANK_FT_MELDNR;                                                  
         IF FUNKSJONSKODE ^= 'V' THEN                                           
            CALL BLANK_FT_BARN;                                                 
         CALL OVERFØR_FT_SØKER;                                                 
         CALL KONTROLL_FT_SØKER(FEIL_FUNNET);                                   
       END;                                                                     
    WHEN('R122')                                                                
       DO;                                                                      
          CALL BLANK_FT_MELDNR;                                                 
          CALL OVERFØR_FT_SØKER;                                                
          CALL OVERFØR_FT_BARN;                                                 
          CALL KONTROLL_FT_SØKER(FEIL_FUNNET);                                  
          IF F_FELT_PIC_CHAR2(FTS.BT_ANT) = '00' THEN                           
             CALL BLANK_FT_BARN;                                                
          ELSE                                                                  
             CALL KONTROLL_FT_BARN(FEIL_FUNNET);                                
       END;                                                                     
    OTHERWISE;                                                                  
       /* INGEN UTGANG DA CICS IKKE GODTAR UGYLDIGE TRANSAKSJONSKODER */        
   END;  /*   SELECT   */                                                       
                                                                                
    IF F_FELT_PIC_CHAR2(FTS.BT_ANT) ^= '00' & ^FEIL_FUNNET THEN                 
       TRANSKODE = 'R122';                                                      
    IF TRANSKODE = 'R122' & F_FELT_PIC_CHAR2(FTS.BT_ANT) = '00' THEN            
       TRANSKODE = 'R121';                                                      
                                                                                
                                                                                
 IF FEIL_FUNNET ! HENT_FRAM_MAP ! FRA_UTEN_DIALOG !                             
    TRANS_RETURKODE ^= TRANSKODE THEN                                           
    DO;                                                                         
       IF TRANSKODE = 'R120' THEN                                               
          TRANSKODE = 'R121';                                                   
       CALL BLANK_FT_MAP;                                                       
       CALL OVERFØR_FT_SØKER_MAP;                                               
       SELECT (TRANSKODE);                                                      
          WHEN ('R121')                                                         
             DO;                                                                
                CALL FJERN_TEKST_BARN;                                          
                CALL BLOKKERE_BARN;                                             
             END;                                                               
          WHEN ('R122')                                                         
             DO;                                                                
                CALL TEKST_BARN;                                                
                CALL OVERFØR_FT_BARN_MAP;                                       
             END;                                                               
          OTHERWISE;                                                            
       END;                                                                     
                                                                                
      IF HENT_FRAM_MAP THEN                                                     
        EXEC CICS SEND MAP('S001121') MAPSET('S001123') ERASE CURSOR;           
      ELSE                                                                      
        EXEC CICS SEND MAP('S001121') MAPSET('S001123') ERASEAUP                
                                                     DATAONLY CURSOR;           
      HENT_FRAM_MAP = '0'B;                                                     
      FEIL_MELD_NR  = 666;                                                      
                                                                                
      IF  FRA_CICS THEN                                                         
        EXEC CICS RETURN TRANSID(TRANSKODE) COMMAREA(KOM_OMR);                  
      ELSE                                                                      
        GOTO RECEIVE_MAP;                                                       
    END;                                                                        
                                                                                
   ELSE                                                                         
                                                                                
    DO;                                                                         
                                                                                
      IF TRANSKODE = 'R120' THEN                                                
        TRANS_RETURKODE = 'R121';                                               
      ELSE                                                                      
        TRANS_RETURKODE = TRANSKODE;                                            
      IF  FRA_CICS  THEN                                                        
        DO;                                                                     
          BLANKETTYPE = STYREKODE;                                              
          TRANSKODE = 'R042';                                                   
        END;                                                                    
    END;                                                                        
  END;                                                                          
                                                                                
  CALL UTGANG;                                                                  
                                                                                
  UTGANG: PROC;                                                                 
    IF  FRA_CICS THEN                                                           
      EXEC CICS XCTL PROGRAM('R0010401') COMMAREA(KOM_OMR);                     
    ELSE                                                                        
      EXEC CICS RETURN;                                                         
  END UTGANG;                                                                   
                                                                                
  FEILBEH:                                                                      
                                                                                
     EXEC CICS HANDLE CONDITION ERROR(ABEND);                                   
     IF FEIL_MELD_NR = 0 THEN /* AB 01.08.16 */                                 
        PROGRAM_ID = 'R0011201';                                                
                                                                                
     S001121O.MELDING2O =                                                       
              'F E I L  H A R  O P P S T Å T T ! ! !.';                         
                                                                                
     S001121O.MELDING3O =                                                       
              'EIBRCODE : ' !! UNSPEC(EIBRCODE);                                
                                                                                
     S001121O.MELDING4O =                                                       
          'EIBFN    : ' !! UNSPEC(EIBFN) !! '. FEILKODE : ' !! FEILKODE         
                                         !! '. DATASETT : ' !! DSNAVN;          
                                                                                
     S001121O.MELDING5O =                                                       
                   'PROGRAMNAVN :  ' !! PROGRAM_ID    !!                        
                   '.INGEN OPPDATERING ER BLITT UTFØRT.';                       
                                                                                
     S001121O.MELDING6O =                                                       
                   'TA UTSKRIFT!!!. TRYKK DERETTER "ENTER".';                   
                                                                                
                                                                                
     EXEC CICS SEND MAP('S001121')                                              
                                                 MAPSET('S001123');             
                                                                                
     EXEC CICS RECEIVE MAP('S001121')                                           
                                    MAPSET('S001123') SET(BMSMAPBR);            
                                                                                
                                                                                
     EXEC CICS SYNCPOINT ROLLBACK;                                              
                                                                                
                                                                                
     TERMINERINGS_IND = 'F';                                                    
                                                                                
     TRANSKODE  = 'R041';                                                       
                                                                                
     CALL UTGANG;                                                               
                                                                                
  ABEND:                                                                        
     EXEC CICS ABEND ABCODE(FEIL);                                              
                                                                                
  PF1:                                                                          
                                                                                
 /* LEGGER UNDERKJENT TRANS UT PÅ VENTEDATABASE HVIS GYLDIG     */              
 /* FNR OG VIRKNINGSDATO,ELLERS FORKASTES TRANSAKSJONEN.        */              
                                                                                
     IF ^(FTS.FNRNR = 1          !                                              
          FTS.FNRNR = 200        !                                              
          FTS.VIRK_DATO_ÅMDNR = 9 !              /*2000*/                       
          FTS.VIRK_DATO_ÅMDNR = 200) THEN        /*2000*/                       
        DO;                                                                     
                                                                                
           IF FØDSNUMMER    = 0   THEN                                          
                                                                                
 /* SKRIVER MELDING OM AT TRANSAKSJONEN ER FORKASTET.           */              
                                                                                
              DO;                                                               
                 FEIL_MELD_NR  = 0;                                             
                 S001121O.MELDING6O =                                           
                         'REGISTERINGEN ER IKKE AVSLUTTET MED "ENTER"'!!        
                         '.TRANSAKSJONEN ER FORKASTET.';                        
                                                                                
              END;                                                              
           ELSE                                                                 
              DO;                                                               
                                                                                
                 SEARCH_FNR  = TRANS_OPPL_OMR.FØDSNUMMER;                       
                 IF FRA_CICS THEN                                               
                    BLANKETTYPE = STYREKODE;                                    
                                                                                
                                                                                
                 IF FEIL_MELD_NR = 666 &      /*BLANKETTKONTROLLFEIL*/          
                    ^FRA_MED_DIALOG     THEN  /*IKKE BASEKONTROLLFEIL*/         
                    FUNKSJONSKODE = 'B';                                        
                                                                                
                 IF F_FELT_PIC_CHAR2(FTS.BT_ANT) ^= '00'  THEN                  
                    DO;                                                         
                       CALL BLANK_FT_MELDNR;                                    
                       CALL OVERFØR_FT_BARN_MAP;                                
                    END;                                                        
                                                                                
                 FEIL_MELD_NR  = 0;                                             
                                                                                
                 EXEC CICS LINK PROGRAM('R0010490') COMMAREA(KOM_OMR);          
                                                                                
                 IF FEIL_MELD_NR > 0 THEN                                       
                    DO;                                                         
                       DSNAVN   = 'R0010490';                                   
                       FEILKODE = DB_STATUS_KODE;                               
                       GO TO FEILBEH;                                           
                    END;                                                        
                                                                                
 /* SKRIVER MELDING OM AT TRANSAKSJONEN ER LAGT UT PÅ VENTEBASE.*/              
                                                                                
                                                                                
                 S001121O.MELDING6O =                                           
                         'TRANSAKSJONEN ER LAGT UT PÅ VENTEREGISTER.';          
              END;                                                              
                                                                                
                                                                                
           EXEC CICS SEND MAP('S001121') MAPSET('S001123')                      
                                                           DATAONLY;            
     /* DUMMY SEND-KALL.                              */                        
                                                                                
           EXEC CICS SEND MAP('S001121')                                        
                                                  MAPSET('S001123');            
           EXEC CICS DELAY INTERVAL(005);                                       
                                                                                
           TERMINERINGS_IND = 'F';                                              
                                                                                
           TRANSKODE  = 'R041';                                                 
                                                                                
           CALL UTGANG;                                                         
                                                                                
        END;                                                                    
     ELSE                                                                       
        DO;                                                                     
           TERMINERINGS_IND = 'F';                                              
                                                                                
           TRANSKODE = 'R041';                                                  
                                                                                
           S001121O.MELDING6O =                                                 
                                                                                
   'NB** TRANSAKSJONEN ER FORKASTET P.G.A. UGYLDIG FNR EL. VDATO **NB';         
                                                                                
           EXEC CICS SEND MAP('S001121') MAPSET('S001123')                      
                                                           DATAONLY;            
     /* DUMMY SEND-KALL.                            */                          
                                                                                
           EXEC CICS SEND MAP('S001121')                                        
                                    MAPSET('S001123');                          
                                                                                
           EXEC CICS DELAY INTERVAL(005);                                       
           CALL UTGANG;                                                         
        END;                                                                    
                                                                                
                                                                                
  PF2:                                                                          
     TERMINERINGS_IND   = 'F';                                                  
                                                                                
     S001121O.MELDING6O =                                                       
      'NBNBNBNBNB***    TRANSAKSJONEN ER FORKASTET.    ***NBNBNBNBNB';          
                                                                                
     EXEC CICS SEND MAP('S001121') MAPSET('S001123')                            
                                                      DATAONLY ;                
     /* DUMMY SEND-KALL.                                     */                 
                                                                                
     EXEC CICS SEND MAP('S001121')                                              
                                    MAPSET('S001123');                          
     EXEC CICS DELAY INTERVAL(005);                                             
                                                                                
                                                                                
     TRANSKODE = 'R041';                                                        
                                                                                
     CALL UTGANG;                                                               
 %PAGE;                                                                         
      %INCLUDE R0011202;               /* BLANK  ARBEIDSOMRÅDE */               
 %PAGE;                                                                         
      %INCLUDE R0011203;               /* KONTROLL AV SØKERDELEN */             
 %PAGE;                                                                         
      %INCLUDE R0011204;               /* KONTROLL AV BARNEDATA */              
 %PAGE;                                                                         
      %INCLUDE R0011206;               /* ARBEIDSOMRÅDE -> MAP */               
 %PAGE;                                                                         
      %INCLUDE R0011207;               /* FORTEKTSTER OG ATTRIBUTTER            
                                       BARN OG SPES.DEL */                      
 %PAGE;                                                                         
      %INCLUDE R0011209;               /* MAP -> ARBEIDSOMRÅDE */               
 %PAGE;                                                                         
      %INCLUDE R0011210;               /* FLYTTING AV BARNEDATA */              
 %PAGE;                                                                         
      %INCLUDE R0010508;               /* BEREGN TT AF */                       
 %PAGE;                                                                         
      %INCLUDE R0010608;               /* BEREGN TT UF */                       
 %PAGE;                                                                         
      %INCLUDE R0019901;               /*  TESTER OM DATO ER GYLDIG */          
 %PAGE;                                                                         
      %INCLUDE R0019902;               /*  FINNE KJØNN     */                   
 %PAGE;                                                                         
      %INCLUDE R0019903;               /* EKTEFELLES ALDER GYLDIG */            
 %PAGE;                                                                         
      %INCLUDE R0019904;               /* FØDSELSNUMMERKONTROLL */              
 %PAGE;                                                                         
      %INCLUDE R0019905;               /* BEREGNER ALDER */                     
 %PAGE;                                                                         
      %INCLUDE R0019908;               /* DAGENS DATO PLUSS 1 */                
 %PAGE;                                                                         
      %INCLUDE R0019909;               /* KONTROLL TRYGDETID */                 
 %PAGE;                                                                         
      %INCLUDE R0019910;               /* NUMERISK KONTROLL */                  
 %PAGE;                                                                         
      %INCLUDE R0019912;  /* KONVERTERING CHAR ==> PIC / PIC ==> CHAR */        
 %PAGE;                                                                         
      %INCLUDE R0019944;               /* SKRIV_FEIL      */                    
      %INCLUDE R0019959;               /* F_2MDR          */                    
      %INCLUDE R0019983;               /* KONV_ÅMD_HÅMD   */                    
      %INCLUDE R0019984;               /* KONV_MÅ_HÅMD   */                     
      %INCLUDE R0019988;               /* KONV_HÅMD_ÅMD  */                     
      %INCLUDE R0019989;               /* KONV_HÅMD_MÅ   */                     
      %INCLUDE R0019995;               /* KONV_FNR11_FNR13(FNR)*/               
      %INCLUDE R0019945;               /* ATK_TILLGANG_KONTROLL   */            
      %INCLUDE R0019999;               /* KONTROLL_ACF2           */            
   END R00112;                                                                  
