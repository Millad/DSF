 /*   SIST ENDRET PÅ PROD   2005.01.13  8.42.29 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2005.01.10 13.11.23 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2004.12.17 15.39.59 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.07.13 13.19.15 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2004.03.10 13.42.33 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2004.03.02 11.38.04 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.02.11 11.26.29 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.08.07 11.54.57 AV   HLA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.02.02  7.44.47 AV   HLA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.04.06  8.17.21 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.01.14 12.42.47 AV   JDA7339          */        
 /*       SIST ENDRET 11/10-99 12.42.47 AV   JDA7339                  */        
 /*       SIST ENDRET 11/10-99 12.24.23 AV   JDA7339                  */        
 /*       SIST ENDRET 11/10-99 12.18.44 AV   JDA7339                  */        
 /*       SIST ENDRET 29/07-99 10.31.33 AV   JDA7339                  */        
 /*       SIST ENDRET 07/07-99 12.30.01 AV   JDA7339                  */        
 /*       SIST ENDRET 19/02-99 12.31.58 AV   JDA7339                  */        
 /*       SIST ENDRET 08/01-99 14.25.50 AV   JDA7339                  */        
 /*       SIST ENDRET 08/09-98 13.29.20 AV   JDA7339                  */        
 /*       SIST ENDRET 12/10-95 14.13.57 AV   JDA0310                  */        
 /* ***************************************************************** */        
 /*                R 0 0 1 U 8 2 0                                    */        
 /* ***************************************************************** */        
 /*  IDENTIFIKASJON:                                                  */        
 /*  ---------------                                                  */        
 /*  PROGRAMIDENT : U820 - KONTROLL EETRANS MOT STATUS FOR FAMILIEN   */        
 /*  PROGRAMSPRÅK : PLI/CICS                                          */        
 /*  PROGRAMMERER : HERMAN                                            */        
 /*  PROGRAMMET BLE LAGET I DES 1994                                  */        
 /*  ****************************                                     */        
 /*  HENSIKT:                                                         */        
 /*      KONTROLLER EETRANS MOT STATUS FOR FAMILIEN                   */        
 /*  PROGRAMTILKNYTNING:                                              */        
 /*      KALLES OPP AV 3520                                           */        
 /*  BRUK:                                                            */        
 /*      EXEC CICS XCTL PROGRAM ('R001U820') COMMAREA (KOM_OMR)       */        
 /*                                                                   */        
 /*  *****************************                                    */        
 /*  ENDRING                                                          */        
 /* ***************************************************************** */        
 R01U820: PROC (COMMAREA_PEKER) OPTIONS (MAIN);                                 
    %INCLUDE P001U801;                                                          
    %INCLUDE P0019908;  /* KOM OMRÅDE (BASED)                         */        
    %INCLUDE P0019906;  /* TRANS_OPPL_OMRÅDE (BASED)                  */        
    %INCLUDE P0019910;                                                          
    %INCLUDE P0019912;                                                          
    %INCLUDE P0019924; /* GRUNNBELØP-VEIET GJENNOMSNITT */                      
    DCL 1 B01 BASED(B01_PEKER), %INCLUDE P0019921;                              
    DCL 1 B02 BASED(B02_PEKER), %INCLUDE P0019921;                              
    DCL COMMAREA_PEKER PTR;                                                     
                                                                                
  DCL POENGÅR                     FIXED DEC (5) INIT (0);                       
  DCL REGÅR                       FIXED DEC (5) INIT (0);                       
  DCL SISTE                       FIXED DEC (5);                                
  DCL SISTE_IND                      FIXED BIN(15);                             
  DCL W_ÅR_MÅNED                     PIC '(6)9';                                
  DCL W_ÅR DEF W_ÅR_MÅNED POS(1)     PIC '9999';                                
  DCL W_MÅNED DEF W_ÅR_MÅNED POS(5)  PIC   '99';                                
  DCL MND_TIL                     FIXED DEC (5);                                
  DCL MND_FRA                     FIXED DEC (5);                                
  DCL TT_NY                       FIXED DEC (5);                                
  DCL W_TT_FRAMT     FIXED DEC(5) INIT (0),                                     
      TT_SØKER       FIXED DEC(5) INIT (0);                                     
                                                                                
    DCL                                                                         
        ADDR                BUILTIN,                                            
        AVDØD_IND           FIXED BIN (15) INIT (0),                            
        CSTG                BUILTIN,                                            
        VERIFY              BUILTIN,                                            
        HBOUND              BUILTIN,                                            
        FNR13               PIC '(13)9'    INIT (0),                            
        FNR_M13        DEF  FNR13         POS (3) PIC '(2)9',                   
        FNR_ÅR13       DEF  FNR13         POS (5) PIC '(4)9',                   
        FNR_EK13            PIC '(13)9'    INIT (0),                            
        FNR_EK_M13     DEF  FNR_EK13      POS(3)  PIC '(2)9',                   
        FNR_EK_ÅR13    DEF  FNR_EK13      POS(5)  PIC '(4)9',                   
        I                   FIXED BIN (15) INIT (0),                            
        INNT_ÅR             FIXED BIN (15) INIT (0),                            
        J                   FIXED BIN (15) INIT (0),                            
        STATUS_TYPE_SØKER   CHAR(1)        INIT (''),                           
        SEKSTISJUÅRSDATO_EK           PIC '( 6)9' INIT (0),                     
        SEKSTISJUÅRSDATO_SØKER        PIC '( 6)9' INIT (0),                     
        CAL_FELT                      PIC '( 8)9' INIT (0),                     
        TILKNYTTET_SW       CHAR(1)        INIT (''),                           
        W_ALDER_ÅM          PIC '( 5)9'    INIT (0),                            
        W_ALDER_ÅM_EK       PIC '( 5)9'    INIT (0),                            
        W_VIRKDATO_MND DEF W_VIRKDATO_ÅMD POS (5) PIC '(2)9',                   
        W_DØDSDATO_ÅM  DEF W_DØDSTP_ÅMD   POS (1) PIC '(6)9',                   
        W_VIRKDATO_ÅMD      PIC '( 8)9'    INIT (0),                            
        W_VIRKDATO_ÅM  DEF W_VIRKDATO_ÅMD POS (1) PIC '(6)9',                   
        W_VIRKDATO_ÅR  DEF W_VIRKDATO_ÅMD POS (1) PIC '(4)9',                   
        W_DØDSTP_MND   DEF W_DØDSTP_ÅMD   POS (5) PIC '(2)9',                   
        W_DØDSTP_ÅM         FIXED DEC ( 7) INIT (0),                            
        W_DØDSTP_ÅMD        PIC '( 8)9'    INIT (0),                            
        W_DØDSTP_ÅR    DEF W_DØDSTP_ÅMD   POS (1) PIC '(4)9',                   
        1 ALDER_67_SØKER   DEF SEKSTISJUÅRSDATO_SØKER,                          
          2 ÅR           PIC '9999',                                            
          2 MND          PIC '99',                                              
        1 ALDER_67_EK      DEF SEKSTISJUÅRSDATO_EK,                             
          2 ÅR           PIC '9999',                                            
          2 MND          PIC '99',                                              
          W_STI_ÅMD                       PIC '(8)9' INIT ('0'),                
          TELLER FIXED BIN(15) INIT (0),                                        
          W_STI_ÅR   POS(1) DEF W_STI_ÅMD PIC '(4)9',                           
          W_STI_M    POS(5) DEF W_STI_ÅMD PIC '(2)9',                           
          W_STI_DAG  POS(7) DEF W_STI_ÅMD PIC '(2)9',                           
       /* TT_SØKER        FIXED DEC (5), */                                     
          TT_AVDØD        FIXED DEC (5) INIT (0),                               
          TT_RED         CHAR (1) INIT (' '),                                   
          ÅR_IND         FIXED BIN (15) INIT (0);                               
                                                                                
 /* ***************************************************************** */        
 /* HER BEGYNNER PROGRAMMET KON_EP                                    */        
 /* ENDRET 11/1-85 AV KARIN : SAVING AV SØKERS STATUS_TYPE            */        
 /* ***************************************************************** */        
                                                                                
    PROGRAM_ID      = 'R001U820';                                               
    I               = 0;                                                        
    TRANSTYPE       = ' 3';                                                     
    STATUS_TYPE_SØKER = STATUS_TYPE;                                            
                                                                                
    FNR13    = KONV_FNR11_FNR13((EES.FNR));                                     
    FNR_EK13 = KONV_FNR11_FNR13((EES.FNR_EK));                                  
                                                                                
    SEKSTISJUÅRSDATO_SØKER = FNR_ÅR13 * 100 + 6700 + FNR_M13;                   
    SEKSTISJUÅRSDATO_EK    = FNR_EK_ÅR13 * 100 + 6700 + FNR_EK_M13;             
                                                                                
    W_STI_ÅMD = EES.DØDSDATO_ÅMD_EK;                                            
    /* ************************************************************** */        
    /* AVDØD EKTEFELLES INDEX FINNES.                                 */        
    /* ************************************************************** */        
    IF SØKER_IND = 1 THEN                                                       
       AVDØD_IND = 2;                                                           
    ELSE                                                                        
       AVDØD_IND = 1;                                                           
                                                                                
    /* AFP: SØKER ER AFP-PENSJONIST. FOR Å FÅ EP, MÅ VI HA          */          
    /*      OPPHØRSBLANKETT FØRST.BLANKETT AVVISES.TRUDE 080289     */          
    IF B01.STATUS_KODE_HIST (SØKER_IND)  = ' '  &                               
       B01.PENSJONSTYPE1(SØKER_IND)  = 'K'  THEN                                
       DO;                                                                      
          FEIL_MELD_NR  = 1735;                                                 
          GO TO L999;                                                           
       END;                                                                     
                                                                                
    /* ************************************************************** */        
    /* AVDØDE ER OPPGITT PÅ BLANKETTEN - FNR ULIK 11111111111         */        
    /* ************************************************************** */        
    IF EES.FNR_EK ^= (11)'1' THEN                                               
       DO;                                                                      
          /* ******************************************************** */        
          /* DERSOM AVDØD IKKE ER LEST INN I B01.                     */        
          /* ******************************************************** */        
          IF B01.FNR(AVDØD_IND) = 0 THEN                                        
             DO;                                                                
                DØD = '1'B;                                                     
                SEARCH_FNR  = EES.FNR_EK;                                       
                POS_I_B01   = AVDØD_IND;                                        
                EXEC CICS LINK PROGRAM ('R0019928')                             
                               COMMAREA (KOM_OMR);                              
                IF FEIL_MELD_NR > 0 THEN                                        
                   GO   TO L999;                                                
                ELSE                                                            
                   PROGRAM_ID = 'R001U820';                                     
                DØD = '0'B;                                                     
                /* ************************************************** */        
                /* KONTROLLERER OM AVDØD ER KJENT OG ER DØD I BASEN.  */        
                /* ************************************************** */        
                IF B01.FNR(AVDØD_IND) = 0 !                                     
                   (B01.FNR(AVDØD_IND) > 0 &                                    
                   B01.DØDSDATO_ÅMD(AVDØD_IND) = 0) THEN                        
                   DO;                                                          
  L100:                                                                         
                      FEIL_VED_LABEL = '100';                                   
                      FEIL_MELD_NR   = 1710;                                    
                      GO TO L999;                                               
                   END;                                                         
                /* ************************************************** */        
                IF ((B01.BEREGN_FOLKETRYGD(AVDØD_IND) ^= ' ' !                  
                     B01.PRO_RATA_BEREGNING(AVDØD_IND) ^= ' ') &                
                    (EES.NORSK_BOSATT_EK = 'J'               ) ) THEN           
                   DO;                                                          
  L101:                                                                         
                      FEIL_VED_LABEL = '101';                                   
                      FEIL_MELD_NR   = 1743;                                    
                      GO TO L999;                                               
                   END;                                                         
                ELSE                                                            
 /* ***************************************************************** */        
 /*  EKTEFELLEN ER DØD OG SØKEREN HAR IKKE PENSJONSSTATUS.            */        
 /* ***************************************************************** */        
                IF B01.PENSJONSTYPE1 (SØKER_IND) = ' ' !                        
                   B01.STATUS_KODE_HIST (SØKER_IND) ^= ' ' THEN                 
 /* ***************************************************************** */        
 /*  SØKEREN ER I BASEN MED INNTEKTER OG HISTORIKK.                   */        
 /* ***************************************************************** */        
                   DO;                                                          
                      B02.TKNR  (SØKER_IND) =  EES.TKNR;                        
                      B02.NAVN  (SØKER_IND) =  EES.NAVN;                        
                      B02.SPRÅK (SØKER_IND) =  EES.SPRÅK;                       
                      IF B01.FNR(SØKER_IND) > 0 THEN                            
                         CALL                                                   
                         FLYTT_B01_TIL_B02(SØKER_IND);                          
                      ELSE                                                      
                                                                                
 /* ***************************************************************** */        
 /*  SØKEREN ER IKKE I BASEN.                                         */        
 /*  ENDRING 11/1-85 AV KARIN: RUTINEN OPPRETT_STATUS_EP_SØKER        */        
 /* ***************************************************************** */        
                                                                                
                         B02.FNR   (SØKER_IND) =  EES.FNR;                      
                      B02.PERSON(AVDØD_IND) = B01.PERSON(AVDØD_IND);            
                      CALL                                                      
                         OPPRETT_STATUS_EE_SØKER; /* R001U821 */                
                         IF FEIL_MELD_NR > 0  THEN                              
                            GO TO L999;                                         
                                                                                
                      CALL                                                      
                         KOBLE_TO_PERSONER(SØKER_IND,AVDØD_IND);                
                   END;                                                         
                ELSE                                                            
                   DO;                                                          
                                                                                
 /* ***************************************************************** */        
 /*  SØKEREN HAR PENSJONSSTATUS.                                      */        
 /* ***************************************************************** */        
                                                                                
                      IF B01.TKNR(SØKER_IND) > 0  THEN                          
                         IF B01.TKNR(SØKER_IND) ^= EES.TKNR THEN                
                            B02.TKNR(SØKER_IND) = EES.TKNR;                     
  /*                        DO                                                  
  L110: FJERNET 180701 MARTIN                                                   
                               FEIL_VED_LABEL = '110'                           
                               FEIL_MELD_NR   = 1702                            
                               GO TO L999                                       
                            END  */                                             
                      SELECT (B01.STATUS.SIVILSTAND(SØKER_IND));                
                                                                                
 /* ***************************************************************** */        
 /*  SØKEREN ER UGIFT.                                                */        
 /*  SIVILSTAND 'U' PÅ BLANKETTEN ER TILLATT VED YRKESSKADE.    */              
 /*  SØKEREN HAR DA VÆRT SAMBOER MED AVDØDE.                    */              
 /* ***************************************************************** */        
                                                                                
                         WHEN('U')                                              
                            DO;                                                 
                               IF EES.SIVILSTAND ^= 'U' THEN                    
                                  DO;                                           
  L120:                                                                         
                                     FEIL_VED_LABEL = '120';                    
                                     FEIL_MELD_NR = 1407;                       
                                     GO TO L999;                                
                                  END;                                          
                               ELSE                                             
                                  DO;                                           
                                     CALL                                       
                                     FLYTT_B01_TIL_B02(SØKER_IND);              
                                     B02.PERSON(AVDØD_IND) =                    
                                           B01.PERSON(AVDØD_IND);               
                                     CALL                                       
                                     AJOURFØR_B02_MED_EE_TRANS;                 
                                     IF FEIL_MELD_NR > 0   THEN                 
                                        GO TO L999;                             
                                     CALL                                       
                                     KOBLE_TO_PERSONER                          
                                     (SØKER_IND,AVDØD_IND);                     
                                  END;                                          
                            END;                                                
                                                                                
 /* ***************************************************************** */        
 /*  SØKEREN ER SKILT.                                                */        
 /* ***************************************************************** */        
                                                                                
                        WHEN('S')                                               
                           DO;                                                  
                              IF EES.SIVILSTAND ='E' THEN                       
                                 DO;                                            
  L130:                                                                         
                                    FEIL_VED_LABEL = '130';                     
                                    FEIL_MELD_NR   = 1409;                      
                                    GO TO L999;                                 
                                 END;                                           
                              ELSE                                              
                                 DO;                                            
                                    CALL                                        
                                    FLYTT_B01_TIL_B02(SØKER_IND);               
                                    B02.PERSON(AVDØD_IND) =                     
                                          B01.PERSON(AVDØD_IND);                
                                    CALL                                        
                                    AJOURFØR_B02_MED_EE_TRANS;                  
                                    IF FEIL_MELD_NR > 0  THEN                   
                                       GO TO L999;                              
                                    CALL                                        
                                    KOBLE_TO_PERSONER                           
                                    (SØKER_IND,AVDØD_IND);                      
                                 END;                                           
                           END;                                                 
                                                                                
 /* ***************************************************************** */        
 /*  SØKEREN MÅ VÆRE ETTERLATT                                        */        
 /* ***************************************************************** */        
                                                                                
                        WHEN('E')                                               
                           DO;                                                  
                              IF EES.SIVILSTAND ='S' THEN                       
                                 DO;                                            
      L140:                                                                     
                                    FEIL_VED_LABEL = '140';                     
                                    FEIL_MELD_NR   = 1410;                      
                                    GO TO L999;                                 
                                 END;                                           
                              ELSE                                              
                                 DO;                                            
                                    CALL                                        
                                    FLYTT_B01_TIL_B02(SØKER_IND);               
                                    B02.PERSON(AVDØD_IND) =                     
                                              B01.PERSON(AVDØD_IND);            
                                    CALL                                        
                                     AJOURFØR_B02_MED_EE_TRANS;                 
                                     IF FEIL_MELD_NR > 0  THEN                  
                                        GO TO L999;                             
                                    CALL                                        
                                     KOBLE_TO_PERSONER                          
                                           (SØKER_IND,AVDØD_IND);               
                                 END;                                           
                           END;                                                 
                        OTHERWISE                                               
                           DO;                                                  
  L145:                                                                         
                              FEIL_VED_LABEL = '145';                           
                              FEIL_MELD_NR   = 1412;                            
                              GO TO L999;                                       
                           END;                                                 
                      END; /* END SELECT */                                     
                   END;    /* END SØKER HAR PENSJONSSTATUS */                   
          END;             /* END AVDØD IKKE I B01 */                           
       ELSE                                                                     
                                                                                
 /* ***************************************************************** */        
 /*  AVDØD ER I B01-D.V.S KNYTTET TIL SØKEREN I BASEN.                */        
 /*  SØKER OG AVDØDE BEHØVER IKKE Å VÆRE TILKNYTTET HVERANDRE,        */        
 /*  HVIS SØKER FYLLER 67/70 ÅR MELLOM TRANSDATO OG DAGENS-DATO KAN   */        
 /*  BEGGE LIGGE I B01 UTEN Å VÆRE TILKNYTTET                         */        
 /* ***************************************************************** */        
                                                                                
          DO;                                                                   
             IF B01.TKNR(SØKER_IND) > 0 THEN                                    
                IF B01.TKNR(SØKER_IND) ^= EES.TKNR THEN                         
                   DO;                                                          
  L150:                                                                         
                      FEIL_VED_LABEL = '150';                                   
                      FEIL_MELD_NR   = 1702;                                    
                      GO TO L999;                                               
                   END;                                                         
                SELECT (B01.STATUS.SIVILSTAND(SØKER_IND));                      
                                                                                
 /* ***************************************************************** */        
 /*  SØKEREN ER GIFT.                                                 */        
 /* ***************************************************************** */        
                                                                                
                   WHEN('G','A')                                                
                                                                                
 /* ***************************************************************** */        
 /*  EKTEFELLEN ER IKKE REGISTRERT SOM DØD I BASEN.                   */        
 /* ***************************************************************** */        
                                                                                
                      DO;                                                       
                        IF B01.FNR(AVDØD_IND) = EES.FNR_EK THEN                 
                           DO;                                                  
  L160:                                                                         
                              FEIL_VED_LABEL = '160';                           
                              FEIL_MELD_NR   = 1710;                            
                              GO TO L999;                                       
                           END;                                                 
                        ELSE                                                    
                                                                                
 /* ***************************************************************** */        
 /*  SØKEREN ER GIFT MED EN ANNEN.                                    */        
 /* ***************************************************************** */        
                                                                                
                           DO;                                                  
  L170:                                                                         
                              FEIL_VED_LABEL = '170';                           
                              FEIL_MELD_NR   = 1408;                            
                              GO TO L999;                                       
                           END;                                                 
                      END;                                                      
                                                                                
 /* ***************************************************************** */        
 /*  SØKEREN ER ETTERLATT MED/UTEN TILKNYTTING TIL AVDØDE             */        
 /* ***************************************************************** */        
                                                                                
                   OTHERWISE                                                    
                      DO;                                                       
                        IF B01.FNR(AVDØD_IND) = EES.FNR_EK THEN                 
                                                                                
 /* ***************************************************************** */        
 /*  DET ER DEN SAMME AVDØD.                                          */        
 /* ***************************************************************** */        
                                                                                
                           DO;                                                  
                              IF EES.SIVILSTAND ^=                              
                                  B01.STATUS.SIVILSTAND(SØKER_IND) THEN         
                                 DO;                                            
  L180:                                                                         
                                    FEIL_VED_LABEL = '180';                     
                                    FEIL_MELD_NR   = 1410;                      
                                    GO TO L999;                                 
                                 END;                                           
                              ELSE                                              
                                 DO;                                            
                                    CALL                                        
                                      FLYTT_B01_TIL_B02(SØKER_IND);             
                                    B02.PERSON(AVDØD_IND) =                     
                                            B01.PERSON(AVDØD_IND);              
                                    CALL                                        
                                     AJOURFØR_B02_MED_EE_TRANS;                 
                                    IF FEIL_MELD_NR > 0  THEN                   
                                       GO TO L999;                              
                                 /* ENDRET AV KARIN 19/1-84      */             
                                 /* TESTE OM TILKNYTTING MANGLER */             
                                    TILKNYTTET_SW = 'N';                        
                                    DO I = 1 TO HBOUND(B02.TILKN,2)             
                                     WHILE                                      
                                     (B01.FNR_TILKN(AVDØD_IND,I) > 0);          
                                     IF B01.FNR_TILKN(AVDØD_IND,I) =            
                                       B01.FNR(SØKER_IND) THEN                  
                                       TILKNYTTET_SW = 'J';                     
                                    END;                                        
                                    IF TILKNYTTET_SW = 'N' THEN                 
                                      CALL KOBLE_TO_PERSONER                    
                                          (SØKER_IND,AVDØD_IND);                
                                 /* ENDRINGEN SLUTTER HER, KS 19/1-84 */        
                                 END;                                           
                           END;                                                 
                                                                                
 /* ***************************************************************** */        
 /*  DET ER IKKE DEN SAMME AVDØD.                                     */        
 /* ***************************************************************** */        
                                                                                
                        ELSE                                                    
                           DO;                                                  
  L190:                                                                         
                              FEIL_VED_LABEL = '190';                           
                              FEIL_MELD_NR   = 1408;                            
                              GO TO L999;                                       
                           END;                                                 
                      END;                                                      
                END;  /* END SELECT     */                                      
          END;        /*AVDØD ER I B01 */                                       
    END;              /*AVDØD ER OPPGITT PÅ BLANKETTEN */                       
 ELSE                                                                           
    DO;                                                                         
 /* ***************************************************************** */        
 /*  AVDØD EKTEFELLE ER UOPPGITT  - ENDRET AV KARIN 21/2-84           */        
 /* ***************************************************************** */        
 /* ***************************************************************** */        
 /* DERSOM AVDØD ER LEST INN I B01. (DETTE ER FEIL)                   */        
 /* ***************************************************************** */        
                                                                                
       IF B01.FNR(AVDØD_IND) > 0 THEN                                           
          DO;                                                                   
  L200:                                                                         
              FEIL_VED_LABEL = '200';                                           
              FEIL_MELD_NR   = 1715;                                            
              GO TO L999;                                                       
             /* FEIL - UOPPGITT EKTEFELLE På BLANKETTEN */                      
          END;                                                                  
                                                                                
                                                                                
                                                                                
 /* ***************************************************************** */        
 /*  HAR SØKER PENSJONSSTATUS, INNTEKTER , HISTORIKK?                 */        
 /* ***************************************************************** */        
                                                                                
       IF B01.PENSJONSTYPE1 (SØKER_IND) = ' '   !                               
          B01.STATUS_KODE_HIST (SØKER_IND) ^= ' '  THEN                         
                                                                                
 /* ***************************************************************** */        
 /*  SØKEREN ER I BASEN MED INNTEKTER OG HISTORIKK.                   */        
 /* ***************************************************************** */        
                                                                                
          DO;                                                                   
             B02.TKNR  (SØKER_IND) =  EES.TKNR;                                 
             B02.NAVN  (SØKER_IND) =  EES.NAVN;                                 
             B02.SPRÅK (SØKER_IND) =  EES.SPRÅK;                                
             IF B01.FNR(SØKER_IND) > 0 THEN                                     
                CALL                                                            
                 FLYTT_B01_TIL_B02(SØKER_IND);                                  
             ELSE                                                               
                                                                                
 /* ***************************************************************** */        
 /*  SØKEREN ER IKKE I BASEN.                                         */        
 /* ***************************************************************** */        
                                                                                
                 B02.FNR   (SØKER_IND) =  EES.FNR;                              
                                                                                
              CALL                                                              
                OPPRETT_STATUS_EE_SØKER;                                        
              IF FEIL_MELD_NR > 0         THEN                                  
                 GO TO L999;                                                    
          END;                                                                  
       ELSE                                                                     
          DO;                                                                   
                                                                                
 /* ***************************************************************** */        
 /*  SØKEREN HAR PENSJONSSTATUS.                                      */        
 /* ***************************************************************** */        
                                                                                
             IF B01.TKNR(SØKER_IND) > 0  THEN                                   
                IF B01.TKNR(SØKER_IND) ^= EES.TKNR THEN                         
                   DO;                                                          
             L210:                                                              
                      FEIL_VED_LABEL = '210';                                   
                      FEIL_MELD_NR   = 1702;                                    
                      GO TO L999;                                               
                   END;                                                         
                                                                                
             SELECT (B01.STATUS.SIVILSTAND(SØKER_IND));                         
                                                                                
 /* ***************************************************************** */        
 /*  SØKEREN ER UGIFT.                                                */        
 /*YS      FØDSELSNUMMER AVDØD ER IKKE OPPGITT.  YRKESSKADE OG SIV=U  */        
 /*YS      SKAL IKKE FOREKOMME.                                       */        
 /* ***************************************************************** */        
                                                                                
                WHEN('U')                                                       
                   DO;                                                          
              L220:                                                             
                      FEIL_VED_LABEL = '220';                                   
                      FEIL_MELD_NR = 1407;                                      
                      GO TO L999;                                               
                   END;                                                         
                                                                                
 /* ***************************************************************** */        
 /*  SØKEREN ER SKILT.                                                */        
 /*YS      FØDSELSNUMMER AVDØD ER IKKE OPPGITT.  YRKESSKADE OG SIV=U  */        
 /*YS      SKAL IKKE FOREKOMME.                                       */        
 /* ***************************************************************** */        
                                                                                
                WHEN('S')                                                       
                   DO;                                                          
                      IF EES.SIVILSTAND ='E' THEN                               
                         DO;                                                    
                L230:                                                           
                            FEIL_VED_LABEL = '230';                             
                            FEIL_MELD_NR   = 1409;                              
                            GO TO L999;                                         
                          END;                                                  
                       ELSE                                                     
                          DO;                                                   
                             CALL                                               
                              FLYTT_B01_TIL_B02(SØKER_IND);                     
                             CALL                                               
                              AJOURFØR_B02_MED_EE_TRANS;                        
                             IF FEIL_MELD_NR > 0    THEN                        
                                GO TO L999;                                     
                          END;                                                  
                   END;                                                         
                                                                                
 /* ***************************************************************** */        
 /*  SØKEREN MÅ VÆRE ETTERLATT                                        */        
 /*YS      FØDSELSNUMMER AVDØD ER IKKE OPPGITT.  YRKESSKADE OG SIV=U  */        
 /*YS      SKAL IKKE FOREKOMME.                                       */        
 /* ***************************************************************** */        
                                                                                
                WHEN('E')                                                       
                   DO;                                                          
                      IF EES.SIVILSTAND ='S' THEN                               
                         DO;                                                    
          L240:                                                                 
                            FEIL_VED_LABEL = '240';                             
                            FEIL_MELD_NR   = 1410;                              
                            GO TO L999;                                         
                         END;                                                   
                      ELSE                                                      
                         DO;                                                    
                            CALL                                                
                              FLYTT_B01_TIL_B02(SØKER_IND);                     
                            CALL                                                
                                 AJOURFØR_B02_MED_EE_TRANS;                     
                            IF FEIL_MELD_NR > 0    THEN                         
                               GO TO L999;                                      
                         END;                                                   
                   END;                                                         
                OTHERWISE                                                       
                   DO;                                                          
           L245:                                                                
                      FEIL_VED_LABEL = '245';                                   
                      FEIL_MELD_NR   = 1412;                                    
                      GO TO L999;                                               
                   END;                                                         
             END;        /* END SELECT */                                       
          END;           /* END SØKER HAR PENSJONSSTATUS */                     
    END;                 /* END AVDØD ER UOPPGITT        */                     
                                                                                
  /* JFA 7.3.2001 SKILTE SOM BLIR ETTERLATT SKAL BLI ETTERLATT */               
  IF B02.STATUS.SIVILSTAND(SØKER_IND) = 'S' THEN                                
     B02.STATUS.SIVILSTAND(SØKER_IND) = 'E';                                    
                                                                                
                                                                                
  L999:                                                                         
                                                                                
  EXEC CICS PURGE MESSAGE;                                                      
  EXEC CICS RETURN;                                                             
    /* -------------------------------------------------------------- */        
    %INCLUDE R001N824;    /* F_BEREGN_TT_FRAMT_UTLAND              */           
    %INCLUDE R001U821;    /* OPPRETT_STATUS_EE_SØKER               */           
    %INCLUDE R001U822;    /* AJOURFØR_B02_MED_EE_TRANS             */           
    %INCLUDE R001U823;    /* KONTROLL_AJOURFØR_AVDØD_YP_EE         */           
    %INCLUDE R001U825;    /* AJOURFØR_B02_MED_EE_EØS               */           
    %INCLUDE R0019901;    /* F_GYLDIG_DATO                         */           
    %INCLUDE R0019902;    /* F_KJøNN BRUKES I 9949                 */           
    %INCLUDE R0019905;    /* F_ALDER                               */           
    %INCLUDE R0019926;    /* FLYTT_B01_TIL_B02                     */           
    %INCLUDE R0019924;    /* KOBLE_TO_PERSONER                     */           
    %INCLUDE R0019949;    /* BEREGNER TT_FRAMT                     */           
    %INCLUDE R0019965;    /* F_DATO_ÅM_PLUSS1                      */           
    %INCLUDE R0019995;    /* KONV_FNR11_FNR13                      */           
    /* -------------------------------------------------------------- */        
 END R01U820;                                                                   
