 /*   SIST ENDRET PÅ PROD   2004.06.18 12.24.17 AV   SPA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.05.03 12.32.13 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2003.06.26  8.33.59 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2003.03.14 10.49.38 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2003.03.14 10.36.58 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2002.06.05  9.52.36 AV   HLA2970          */        
 /*   SIST ENDRET PÅ PROD   2002.04.19 12.28.02 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.03.05 13.38.23 AV   JDA7339          */        
 /*       SIST ENDRET 30/03-99 08.42.14 AV   JDA7339                  */        
 /*       SIST ENDRET 30/03-99 08.41.05 AV   JDA7339                  */        
 /*       SIST ENDRET 24/02-99 14.23.16 AV   JDA7339                  */        
 /*       SIST ENDRET 24/02-99 14.22.46 AV   JDA7339                  */        
 /*       SIST ENDRET 20/07-98 10.03.56 AV   JDA7339                  */        
 /*       SIST ENDRET 17/06-98 09.46.10 AV   RFA9991                  */        
 /* ***************************************************************** */        
 /*IDENTIFIKASJON:                                                    */        
 /*    R0014521 - SUBRUTINE I PLI                                     */        
 /*    PROGRAMMERER: PER F. BERGESTAD, JUNI 1982                      */        
 /*HENSIKT:                                                           */        
 /*    RUTINEN BEREGNER GP ETTERLATT-UP                               */        
 /*ENDRIGER:                                                          */        
 /*    ARVING AV TRYGDETID ETTER AVDØDE.    10.9.84   OLAV            */        
 /*    ******                                                         */        
 /*    GARANTI FOR Å FÅ TRYGDETID FRAM TIL 70 ÅR FOR  PERSONER SOM    */        
 /*    HAR HATT DENNE RETTIGHETEN SOM UFØR. 18/9-84   KARIN           */        
 /* ***************************************************************** */        
  BEREGN_GP_ETTERLATT_UP:                                                       
    PROC;                                                                       
                                                                                
    DCL TT_EGEN_GML    FIXED DEC(3) INIT (0);                                   
    DCL LENGSTE_TT     FIXED DEC(3) INIT (0);      /*9902*/                     
 /* DCL GP_FAKTOR      FIXED DEC(3,2) INIT (1.00);  */                          
    DCL GP_FAKTOR      FIXED DEC(5,3) INIT (1.000);    /*200402*/               
    DCL W_FNR_EK11     PIC '(11)9',                                             
        W_FNR_EK13     PIC '(13)9';                                             
    DCL 1 W_FNR_DEF_EK13 DEF W_FNR_EK13,                                        
         2 DAG           PIC '99',                                              
         2 MND           PIC '99',                                              
         2 ÅR            PIC '9999',                                            
         2 ÅRHUNDRE      PIC '9',                                               
         2 FILLER        PIC '(4)9';                                            
    IF (B02.PENSJON_FØR_9802(BER_SØ_IND) ^= 'J'  &                              
        B02.RED_GP_3_2_5(BER_SØ_IND)      = 'J'   )  THEN                       
        DO;                                                                     
            GP_FAKTOR = GP_PROSENT;                                             
            DIV_PARAM_OMR.REDUSERT_GP_2G(BER_SØ_IND) = 'X';                     
            B02.GP_REDUKSJON_KODE(BER_SØ_IND)        = 'X';                     
        END;                                                                    
    ELSE                                                                        
           B02.GP_REDUKSJON_KODE(BER_SØ_IND)        = ' ';                      
 /*                                                                   */        
 /* AVDØDES TRYGDETID REGNES PÅ NYTT HER, SELVOM DET IKKE ALLTID      */        
 /* ER NØDVENDIG                                                      */        
 /*                                                                   */        
    W_FNR_EK11 = B02.FNR(BER_EK_IND);                                           
    W_FNR_EK13 = KONV_FNR11_FNR13(W_FNR_EK11);                                  
    B02.STATUS.TT_67_TIL_70(BER_EK_IND) = 0;                                    
 /* IF W_DØDSDATO_Å - FNR13_EK_R.ÅR_EK > 66 THEN */                             
    IF W_DØDSDATO_Å - W_FNR_DEF_EK13.ÅR > 66 THEN                               
 /*    (AVDØDES ALDER PÅ DØDSTIDSPUNKTET...)                          */        
       CALL REGN_TT_67_TIL_70_DØD;                                              
                                                                                
    TT_ANV_UP     =  B02.TT_FØR_1967(BER_SØ_IND) +                              
                    (B02.TT_ETTER_1966(BER_SØ_IND) +                            
                     B02.TT_FRAMT(BER_SØ_IND) + 11) / 12;                       
                                                                                
                                                                                
    IF TT_ANV_UP  > 39 THEN                                                     
       TT_ANV_UP  = 40;                                                         
                                                                                
                                                                                
    ELSE                                                                        
                                                                                
    IF B02.TT_16_66(BER_SØ_IND) > 0 THEN                  /*NY0191*/            
        TT_ANV_UP = (B02.TT_16_66(BER_SØ_IND) +                                 
                     B02.TT_FRAMT(BER_SØ_IND) + 6) / 12;                        
                                                                                
    ELSE                                                                        
       DO;                                                                      
          TT_EGEN_GML = B01.STATUS.TT_FØR_1967(BER_SØ_IND) +                    
             (B01.TT_ETTER_1966(BER_SØ_IND) +                                   
              B01.TT_FRAMT(BER_SØ_IND) + 11) / 12;                              
          IF TT_ANV_UP < TT_EGEN_GML THEN                                       
             DO;                                                                
                B02.TT_FRAMT(BER_SØ_IND) = B02.TT_FRAMT(BER_SØ_IND) +           
               (TT_EGEN_GML - TT_ANV_UP)*12;                                    
                TT_ANV_UP     =  B02.TT_FØR_1967(BER_SØ_IND) +                  
                                (B02.TT_ETTER_1966(BER_SØ_IND) +                
                                 B02.TT_FRAMT(BER_SØ_IND) + 11) / 12;           
             END;                                                               
       END;                                                                     
                                                                                
    IF B02.TT_GARANTI (BER_SØ_IND) >                                            
       TT_ANV_UP                                  THEN                          
       TT_ANV_UP     = B02.TT_GARANTI(BER_SØ_IND);                              
                                                                                
 /*NESTE TO TESTER ER FLYTTET HIT 9902 HL */                                    
                                                                                
    IF W_BEREGNING_KONVENSJON  = 'J'    THEN                                    
       TT_ANV_UP               = W_KONVENSJON_ÅR;                               
                                                                                
  /*IF B02.TT_PÅ(BER_SØ_IND)   = 'J'    THEN                                    
       TT_ANV_UP               = B02.UFØRPENS.PÅ(BER_SØ_IND); */                
                                                                                
    IF B02.UFØRPENS.PÅ(BER_SØ_IND) > TT_ANV_UP   THEN                           
       TT_ANV_UP               = B02.UFØRPENS.PÅ(BER_SØ_IND);                   
                                                                                
 /*HIT 9902*/                                                                   
                                                                                
    IF TT_ANV_UP   > 39 THEN            /* TRUDE,15.2.90          */            
       TT_ANV_UP   = 40;                /* DENNE TESTEN VAR GLEMT */            
                                                                                
    IF B02.FRAVIK_FLYKTN(BER_SØ_IND)  = 'J'  THEN    /*9903*/                   
       TT_ANV_UP   = 40;                                                        
                                                                                
                                                                                
    TT_ANV_AVD    =  B02.STATUS.TT_FØR_1967(BER_EK_IND) +                       
                     B02.STATUS.TT_67_TIL_70(BER_EK_IND) +                      
                    (B02.TT_ETTER_1966(BER_EK_IND) +                            
                     B02.TT_FRAMT(BER_EK_IND) + 11) / 12;                       
                                                                                
                                                                                
    IF B02.TT_16_66(BER_EK_IND) > 0 THEN                  /*NY0191*/            
        TT_ANV_AVD =   (B02.TT_16_66(BER_EK_IND)                                
                     +  B02.TT_FRAMT(BER_EK_IND) + 6) / 12                      
                     +  B02.STATUS.TT_67_TIL_70(BER_EK_IND);                    
                                                                                
    IF B02.TT_GARANTI (BER_EK_IND) > TT_ANV_AVD  THEN /* 1.3.2001 JFA */        
       TT_ANV_AVD    = B02.TT_GARANTI(BER_EK_IND);                              
                                                                                
    IF B02.TT_PÅ(BER_EK_IND)   = 'J'    THEN                                    
       TT_ANV_AVD              = B02.ETTEPENS.PÅ_AVD(BER_SØ_IND);               
                                                                                
    IF B02.FRAVIK_FLYKTN(BER_EK_IND)  = 'J'  THEN    /*TRUDE 0991*/             
       TT_ANV_AVD   = 40;                                                       
                                                                                
    IF POREKKE.PÅ(BER_EK_IND)   > TT_ANV_AVD     THEN /*9902*/                  
       TT_ANV_AVD  = POREKKE.PÅ(BER_EK_IND);                                    
                                                                                
    IF TT_ANV_AVD  > 39 THEN                                                    
       TT_ANV_AVD  = 40;                                                        
                                                                                
    IF TT_ANV_AVD  > TT_ANV_UP                    THEN                          
       LENGSTE_TT  = TT_ANV_AVD;          /*9902*/                              
    ELSE                                                                        
       LENGSTE_TT  = TT_ANV_UP;          /*9902*/                               
                                                                                
                                                                                
    IF B02.DØDSDATO_ÅMD(BER_EK_IND)  < 19910000        !   /*9902*/             
      (B02.PENSJONSRETT_FØR_91 (BER_SØ_IND)  = 'J' &                            
       B02.PENSJONSRETT_FØR_91 (BER_EK_IND)  = 'J'   )     THEN                 
                                                                                
       TT_ANV_UP   =   LENGSTE_TT;                                              
                                                                                
                                                                                
    B02.TT_ANV(BER_SØ_IND) = TT_ANV_UP;                                         
    B02.TT_ANV(BER_EK_IND) = TT_ANV_AVD;                                        
                                                                                
    IF B02.YRKEPENS.YUG(BER_SØ_IND) > 0 THEN                                    
                                                                                
       DO;                                                                      
          GP_UP      = G * UFGRAD * TT_ANV_UP * GP_FAKTOR /                     
                       (100 * 40 * 12) + 0.5;                                   
                                                                                
          GP_YP      = G * B02.YRKEPENS.YUG(BER_SØ_IND)*GP_FAKTOR /             
                       (12 * 100 ) + 0.5;                                       
                                                                                
          GP_EGEN    = GP_UP + GP_YP;                                           
                                                                                
       END;                                                                     
                                                                                
    ELSE                                                                        
      DO;                                                                       
        GP_EGEN = G * B02.UFØRPENS.UFG(BER_SØ_IND) * GP_FAKTOR *                
                       TT_ANV_UP / (100 * 40 * 12) + 0.5;                       
                                                                                
        GP_UP = GP_EGEN;                                                        
      END;                                                                      
                                                                                
    IF B02.DØDSDATO_ÅMD(BER_EK_IND)  < 19930000     !  /*9902*/                 
      (B02.PENSJONSRETT_FØR_91 (BER_SØ_IND)  = 'J' &                            
       B02.PENSJONSRETT_FØR_91 (BER_EK_IND)  = 'J'   )     THEN                 
                                                                                
       TT_ANV_EP   =   LENGSTE_TT;                                              
    ELSE                                                                        
       TT_ANV_EP   =   TT_ANV_AVD;                                              
                                                                                
    IF B02.YRKEPENS.YUG(BER_EK_IND) > 0 THEN                                    
                                                                                
       GP_EP = G * TT_ANV_EP * GP_FAKTOR *                                      
                   (100 - B02.YRKEPENS.YUG(BER_EK_IND))                         
                   / (40 * 12 * 100)                  +                         
                   G * B02.YRKEPENS.YUG(BER_EK_IND) * GP_FAKTOR                 
                   / (12 * 100) + 0.5;                                          
                                                                                
    ELSE                                                                        
                                                                                
       GP_EP = G * TT_ANV_EP * GP_FAKTOR / (40 * 12) + 0.5;                     
                                                                                
                                                                                
  END BEREGN_GP_ETTERLATT_UP;                                                   
