 /*   SIST ENDRET PÅ PROD   2004.07.16 12.37.06 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.03.12 13.30.28 AV   JDA2970          */        
 /*********************************************************************/        
 /*                                                                   */        
 /* IDENTIFIKASJON:                                                   */        
 /*    R0018033   ET PROGR. :                                         */        
 /*    SPORBARHET               --------------------------            */        
 /*    PROGRAMMER :  SATISH PATHAK                                    */        
 /*    SKRIVET    :  26.11.2003                                       */        
 /*    PROGRAM KONTROLL PROGRAM    FOR                                */        
 /*    SKRIVING AV  FNR_SEGM INFORMAJON REVS DATA BASE D001DR0        */        
 /*********************************************************************/        
                                                                                
 OPPDATE_DR_FNR: PROC(FRA_FNR ,TIL_FNR , KODE);                                 
                                                                                
 DCL FRA_FNR                    PIC '(11)9'   ;                                 
 DCL TIL_FNR                    PIC '(11)9'   ;                                 
 DCL KODE                       CHAR( 2)     ;                                  
 /*                                                                             
 DCL PER_PEKER             POINTER;                                             
 DCL 1 RF0PERSN          BASED(PER_PEKER),                                      
     %INCLUDE P0019927;                                                         
 */                                                                             
 DCL 1 PERSN     BASED (IO_PEKER),                                              
      %INCLUDE P0018011;                                                        
    /* -------------------------------------------------------------- */        
    /* INNTEKT INFO                                                   */        
    /* -------------------------------------------------------------- */        
  DCL  1 FNR_INFO  BASED (IO_PEKER),                                            
      %INCLUDE P0018014;                                                        
                                                                                
 /*-------------------------------------------------------------------*/        
 /*     SSA                                                           */        
 /*-------------------------------------------------------------------*/        
  DCL 1 SSA1_ROOT               STATIC,                                         
        2    HDEL                   CHAR (17)    INIT                           
                               ('DR0PERSN(FNR     '),                           
        2    REL_OP                CHAR (2)     INIT (' =')     ,               
        2    PKEY              FIXED     DEC  (11)    INIT ( 0  ),              
        2    HP                    CHAR (1)     INIT (')' )     ;               
                                                                                
                                                                                
   DCL     SSA_UQUAL                        CHAR (9) INIT ((9)' ');             
 /*-------------------------------------------------------------------*/        
 /* PCB-OMRÅDER                                                       */        
 /*-------------------------------------------------------------------*/        
    DCL 1  DR1     BASED    (DR1_PCB_PEKER),                                    
    %INCLUDE P0012003;                                 /* PCB-OMRÅDE  */        
    DCL IO_AREA              CHAR        (100) INIT (''  ) ;                    
    DCL IO_PEKER             POINTER;                                           
                                                                                
    DCL  DATE_TID              PIC '(15)9';                                     
    DCL  DATO                  PIC '(15)9';                                     
    DCL  DATO_REV              PIC '(15)9';                                     
    DCL  01 DATE_CH               CHAR(15);                                     
                                                                                
 /*-------------------------------------------------------------------*/        
 /*                                                                   */        
 /*      S T A R T   P R O G R A M   R0019X30                         */        
 /*                                                                   */        
 /*-------------------------------------------------------------------*/        
       IO_PEKER = ADDR(IO_AREA);                                                
                                                                                
                                                                                
       DATE_CH = DATETIME;                                                      
       DATO      = SUBSTR(DATE_CH,1,15 );                                       
       DATO_REV  = 999999999999999 - DATO    ;                                  
                                                                                
       CALL KONTROLL_ROT;                                                       
       IF FEIL_MELD_NR > 0  THEN GO TO LF99;                                    
       CALL KONTROLL_FNR ;                                                      
 /* ************************************************************ */             
 /* FINNES ROT ELLER SKRIVE NY ROT                              */              
 /* ************************************************************ */             
                                                                                
 KONTROLL_ROT: PROC;                                                            
                                                                                
  SSA1_ROOT.PKEY  = FRA_FNR      ;                                              
  CALL LES_ROT;                                                                 
  IF DR1.STATUS_KODE = '  '  THEN; /* ROT         FUNNET */                     
  ELSE                                                                          
  IF DR1.STATUS_KODE = 'GE'  THEN   /* ROT         IKKE FUNNET  */              
     DO;                                                                        
        PERSN.FNR   = RF0PERSN_FRA.FNR;                                         
        PERSN.NAVN  = RF0PERSN_FRA.NAVN;                                        
        PERSN.TKNR  = RF0PERSN_FRA.TKNR;                                        
        PERSN.PERSN_KODE = RF0PERSN_FRA.PERSN_KODE;                             
                                                                                
                                                                                
        SSA_UQUAL     =  'DR0PERSN';                                            
         CALL ISRT_ROT;                                                         
     END;                                                                       
  ELSE                                                                          
      DO;                                                                       
 L065:                                                                          
       FEIL_MELD_NR = 500;                                                      
       FEIL_VED_LABEL = 'L065';                                                 
       DB_STATUS_KODE  = DR1.STATUS_KODE;                                       
       GO TO LF99;                                                              
     END;                                                                       
 END KONTROLL_ROT;                                                              
 /* ********************************************************** */               
 KONTROLL_FNR : PROC;                                                           
                                                                                
  SSA1_ROOT.PKEY  = FRA_FNR ;                                                   
  FNR_INFO.FN_DATOTID = DATO_REV;                                               
  FNR_INFO.FRA_FNR    = FRA_FNR     ;                                           
  FNR_INFO.TIL_FNR    = TIL_FNR ;                                               
  FNR_INFO.F_KODE1      = SUBSTR(KODE,1,1)    ;                                 
  FNR_INFO.F_KODE2      = SUBSTR(KODE,2,1)    ;                                 
  FNR_INFO.BRUKER_ID = DIV_PARAM_OMR.BRUKER_ID;                                 
  SSA_UQUAL     =  'FNEDATE ';                                                  
                                                                                
  CALL ISRT_BARN_AV_ROOT;                                                       
                                                                                
 END KONTROLL_FNR;                                                              
 /* ********************************************************** */               
 /* ********************************************************** */               
 LES_ROT: PROC;                                                                 
                                                                                
    CALL PLITDLI                                (W02_PARM_CT_4    ,             
                                                 W02_GU           ,             
                                                 DR1          ,                 
                                                 PERSN        ,                 
                                                 SSA1_ROOT);                    
                                                                                
 END LES_ROT;                                                                   
    /*----------------------------------------------------------------*/        
    /* SKRIVE ROT                                                     */        
    /*----------------------------------------------------------------*/        
 ISRT_ROT: PROC;                                                                
    CALL PLITDLI                                (W02_PARM_CT_4    ,             
                                                 W02_ISRT         ,             
                                                 DR1          ,                 
                                                 PERSN        ,                 
                                                 SSA_UQUAL    );                
                                                                                
                                                                                
                                                                                
    IF DR1.STATUS_KODE        ^=         '  '    THEN                           
      DO;                                                                       
 L067:                                                                          
       FEIL_MELD_NR = 500;                                                      
       FEIL_VED_LABEL = 'LA67';                                                 
       DB_STATUS_KODE  = DR1.STATUS_KODE;                                       
       GO TO LF99;                                                              
     END;                                                                       
                                                                                
 END ISRT_ROT;                                                                  
                                                                                
 /*----------------------------------------------------------------*/           
 /* SKRIVE BARN AV ROT                                             */           
 /*----------------------------------------------------------------*/           
 ISRT_BARN_AV_ROOT:  PROC;                                                      
                                                                                
    CALL PLITDLI                                (W02_PARM_CT_5    ,             
                                                 W02_ISRT         ,             
                                                 DR1          ,                 
                                                 FNR_INFO     ,                 
                                                 SSA1_ROOT,                     
                                                 SSA_UQUAL);                    
                                                                                
                                                                                
    IF DR1.STATUS_KODE        ^=         '  '    THEN                           
      DO;                                                                       
 L068:                                                                          
       FEIL_MELD_NR = 500;                                                      
       FEIL_VED_LABEL = 'LA68';                                                 
       DB_STATUS_KODE  = DR1.STATUS_KODE;                                       
       GO TO LF99;                                                              
     END;                                                                       
                                                                                
 END ISRT_BARN_AV_ROOT;                                                         
 LF99:                                                                          
 END OPPDATE_DR_FNR;                                                            
