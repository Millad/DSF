 /*   SIST ENDRET PÅ PROD   2003.12.05  8.38.02 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2003.12.05  8.26.20 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2000.02.21 10.33.35 AV   JDA7339          */        
 /*       SIST ENDRET 04/05-98 14.31.04 AV   JDA7339                  */        
 /*       SIST ENDRET 12/03-98 10.36.24 AV   SLA9300                  */        
 /*       SIST ENDRET 19/02-92 14.59.42 AV   DYBVIK                   */        
 /*       SIST ENDRET 13/12-91 13.12.52 AV   DYBVIK                   */        
 /*       SIST ENDRET 14/04-89 15.01.13 AV   DYBVIK                   */        
 /*       SIST ENDRET 14/12-88 14.33.38 AV   DYBVIK                   */        
 /*       SIST ENDRET 14/03-86 14.36.31 AV   FARVIK                   */        
 /*       SIST ENDRET 21/02-86 09.53.48 AV   FARVIK                   */        
 /*       SIST ENDRET 29/11-85 14.01.26 AV   FARVIK                   */        
 /*       SIST ENDRET 28/06-84 09.37.07 AV   BREIVIK                  */        
 /*       SIST ENDRET 07/02-84 12.28.16 AV   BREIVIK                  */        
 /*       SIST ENDRET 03/11-83 12.26.19 AV   FARVIK                   */        
 /*       SIST ENDRET 03/12-82 13.56.53 AV   BREIVIK                  */        
 /*-------------------------------------------------------------------*/        
 /* IDENTIFIKASJON:                                                   */        
 /*    R0016501                                                       */        
 /*                                                                   */        
 /*    PROGRAMMERER: P. BREIVIK, DES 1982.                            */        
 /*                                                                   */        
 /* HENSIKT:                                                          */        
 /*    GENERERING AV SLETTE-SEGMENTER FOR GS/HSU.                     */        
 /*                                                                   */        
 /* PROGRAMTILKNYTNING:                                               */        
 /*                                                                   */        
 /* BRUK:                                                             */        
 /* ENDRET : UKE 05 2000 AV  SVERRE LUNDEBY, DATA-UTVIKLING AS       */         
 /*          SAK 0013-2000 DAG I VIRKDATO > 00                        */        
 /*********************************************************************/        
                                                                                
 R001650: PROC (COMMAREA_PEKER) OPTIONS (MAIN);                                 
 DCL (COMMAREA_PEKER)        POINTER;                                           
 %PAGE;                                                                         
 %INCLUDE P0019906; /* TRANS_OPPL_OMR */                                        
 %SKIP(4);                                                                      
 %INCLUDE P0019908; /* KOM_OMR */                                               
 %SKIP(4);                                                                      
 %INCLUDE P0019912; /* DIV_PARAM_OMR */                                         
 %PAGE;                                                                         
 %INCLUDE P0011701; /* TRANS_OMR FOR OPPHØRSBLANKETT NR 2 */                    
 %PAGE;                                                                         
 %INCLUDE P0016001; /* BOOLSK SEGMENT-TABELL */                                 
                                                                                
 %PAGE;                                                                         
 DCL 1 TR0ROT,                                                                  
 %INCLUDE P0016201; /* ARBEIDSOMRÅDE TR0ROT               */                    
 DCL 1 REGTPKT,                                                                 
 %INCLUDE P0016202; /* ARBEIDSOMRÅDE REGTPKT              */                    
                                                                                
 %PAGE;                                                                         
 DCL 1 GS,                                                                      
 %INCLUDE P0016212; /* ARBEIDSOMRÅDE GS                   */                    
 DCL 1 HSU,                                                                     
 %INCLUDE P0016213; /* ARBEIDSOMRÅDE HSU                  */                    
 %PAGE;                                                                         
 /*-------------------------------------------------------------------*/        
 /*   DL/1 PARAMETRE                                                  */        
 /*-------------------------------------------------------------------*/        
 %INCLUDE DLIUIB;                                                               
 DCL PLITDLI ENTRY;                                                             
                                                                                
 DCL UIB_RC_OK               BIT (8) INIT(0);    /* KONSTANT */                 
 DCL 1 TR0_PCB_PEKER         BASED (UIBPCBAL),                                  
       2 TR0_PCB_PTR         POINTER;                                           
                                                                                
 DCL INSERT                  CHAR (4) INIT('ISRT');                             
 DCL FIRE                    FIXED BIN (31) INIT(4);                            
 DCL FEM                     FIXED BIN (31) INIT(5);                            
 DCL SEKS                    FIXED BIN (31) INIT(6);                            
 /*-------------------------------------------------------------------*/        
 /*   DEKLARASJON AV SSA                                              */        
 /*-------------------------------------------------------------------*/        
 DCL SSA_UNQUAL              CHAR (9);                                          
                                                                                
 DCL 1 SSA_TR0ROT UNALIGNED,                                                    
       2 HOVEDDEL            CHAR (17) INIT('TR0ROT  (FNR     '),               
       2 REL_OP              CHAR (2)  INIT(' ='),                              
       2 FNR_KEY             FIXED DEC (11),                                    
       2 END_MRK             CHAR (1)  INIT(')');                               
                                                                                
 DCL 1 SSA_REGTPKT UNALIGNED,                                                   
       2 HOVEDDEL            CHAR (17) INIT('REGTPKT (DATOKL  '),               
       2 REL_OP              CHAR (2)  INIT(' ='),                              
       2 DATO_KEY            FIXED DEC (09),                   /*2000*/         
       2 KLOKKE_KEY          FIXED DEC (09),                   /*2000*/         
       2 END_MRK             CHAR (1)  INIT(')');                               
 /*-------------------------------------------------------------------*/        
 /*   PCB-OMRÅDE                                                      */        
 /*-------------------------------------------------------------------*/        
 %INCLUDE P0012002;                                                             
                                                                                
 DCL 1 TR0_PCB               BASED (TR1_PCB_PEKER),                             
 %INCLUDE P0012003; /* PCB-OMRÅDE */                                            
 %PAGE;                                                                         
 /*-------------------------------------------------------------------*/        
 /*   HJELPEVARIABLE                                                  */        
 /*-------------------------------------------------------------------*/        
 DCL TRUE                    BIT(1) INIT('1'B);                                 
 DCL FALSE                   BIT(1) INIT('0'B);                                 
 DCL (ENDRET,TP_ENDRET,                                                         
      FUNNET,FEIL)           BIT(1);                                            
 DCL HELP_FNR                PIC '(11)9';                                       
 DCL HELP_DATO               PIC '(8)9';                                        
 DCL PBR_TEXT                CHAR (50);                                         
 /*  2000                                                                       
 DCL DATO_KLOKKE             PIC '(17)9',                                       
     DATO_ÅMD                PIC '(8)9' DEF DATO_KLOKKE,                        
     DATO                    PIC '(8)9' DEF DATO_KLOKKE,                        
     KLOKKE                  PIC '(9)9' DEF DATO_KLOKKE POS(9);                 
     */                                                                         
 DCL DATO                    PIC '(8)9',                       /*2000*/         
     KLOKKE_CH               CHAR (9)  ,                       /*2000*/         
     KLOKKE                  PIC '(9)9' DEF KLOKKE_CH POS(1);  /*2000*/         
 DCL IO_PEK                  POINTER;                                           
 DCL IO_AREA                 CHAR (150);                                        
 DCL IO_HELP_AREA            CHAR (150) BASED (IO_PEK);                         
 DCL SATS                    PIC '9';                                           
 DCL I                       FIXED BIN (15);                                    
 DCL (TIME,SUBSTR,CSTG,ADDR,MIN,MAX,LENGTH,UNSPEC,                              
      CHAR,VERIFY,HBOUND,FLOOR)    BUILTIN;                                     
                                                                                
 %PAGE;                                                                         
 NOP: PROC;                                                                     
                                                                                
    /* IMPLEMENTASJON AV 'NO OPERATION'                               */        
                                                                                
 END; /* OF NOP */                                                              
                                                                                
 %PAGE;                                                                         
 SKRIV_STAT_SEGM: PROC;                                                         
                                                                                
 /*-------------------------------------------------------------------*/        
 /*                                                                   */        
 /* PROSEDYREN LESER SEGMENT-TABELL OG SKRIVER UT DE                  */        
 /* SEGMENTENE TIL TRANS-STAT-BASEN SOM HAR TILHØRENDE                */        
 /* ELEMENTET I SEGMENT-TABELL SATT TIL 'TRUE' (DVS '1'B).            */        
 /* PROSEDYREN HENTER INFORMASJONEN FRA ARBEIDS-OMRÅDE                */        
 /* TIL TRANS-STAT-BASEN.                                             */        
 /*                                                                   */        
 /*-------------------------------------------------------------------*/        
 /* SETTING AV REGDATO'ER */                                                    
 /*---------------------- */                                                    
    DATO            = DATO_2000;                             /*2000*/           
    KLOKKE_CH       = TIME;                                  /*2000*/           
    GS.REGDATO      = DATO_2000;                             /*2000*/           
    HSU.REGDATO     = DATO_2000;                             /*2000*/           
 /* BEHANDLING AV ROT-SEGMENT */                                                
                                                                                
    HELP_FNR  = TR0ROT.FNR;                                                     
                                                                                
  /*PBR_TEXT = 'DANN ROTSEGMENT.  ID = ' !! HELP_FNR;                           
    EXEC CICS SEND TEXT FROM (PBR_TEXT) ACCUM;*/                                
                                                                                
  IF ^(F_NUMERISK(F_FELT_PIC_CHAR11(HELP_FNR)) &                                
         F_GYLDIG_FNR(HELP_FNR)) THEN                                           
    IF  ((F_NUMERISK(F_FELT_PIC_CHAR11(FØDSNUMMER)) &                           
         F_GYLDIG_FNR(FØDSNUMMER))) THEN                                        
          TR0ROT.FNR = FØDSNUMMER;                                              
    ELSE                                                                        
       DO;                                                                      
          PBR_TEXT = 'TIDSPUNKT    :  ' !! DATO !! ' ' !! KLOKKE;               
          EXEC CICS SEND TEXT FROM (PBR_TEXT) JUSTIFY (1) ACCUM PAGING          
                                                           ERASE;               
          PBR_TEXT = 'AKTUELT FNR  :  ' !! CHAR(HELP_FNR);                      
          EXEC CICS SEND TEXT FROM (PBR_TEXT) JUSTIFY (2) ACCUM PAGING;         
          PBR_TEXT = 'TRANSKODE    :  ' !! CHAR(TRANSKODE);                     
          EXEC CICS SEND TEXT FROM (PBR_TEXT) JUSTIFY (3) ACCUM PAGING;         
          PBR_TEXT = 'FØDSNUMMER   :  ' !! CHAR(FØDSNUMMER);                    
          EXEC CICS SEND TEXT FROM (PBR_TEXT) JUSTIFY (4) ACCUM PAGING;         
          PBR_TEXT = 'VIRKNINGSDATO_ÅMD: ' !! CHAR(VIRKNINGSDATO_ÅMD);          
          EXEC CICS SEND TEXT FROM (PBR_TEXT) JUSTIFY (5) ACCUM PAGING;         
          PBR_TEXT = 'BLANKETTYPE  :  ' !! CHAR(BLANKETTYPE);                   
          EXEC CICS SEND TEXT FROM (PBR_TEXT) JUSTIFY (6) ACCUM PAGING;         
          PBR_TEXT = 'TRANSTYPE    :  ' !! CHAR(TRANSTYPE);                     
          EXEC CICS SEND TEXT FROM (PBR_TEXT) JUSTIFY (7) ACCUM PAGING;         
          PBR_TEXT = 'REG-DATO-ÅMD :  ' !! CHAR(REG_DATO_ÅMD);                  
          EXEC CICS SEND TEXT FROM (PBR_TEXT) JUSTIFY (8) ACCUM PAGING;         
          PBR_TEXT = 'STYRINGSPEKER:  ' !! UNSPEC(STYRINGS_PEKER);              
          EXEC CICS SEND TEXT FROM (PBR_TEXT) JUSTIFY (8) ACCUM PAGING;         
          PBR_TEXT = 'TRANSOPPLPEKER:  ' !! UNSPEC(TRANS_OPPL_PEKER);           
          EXEC CICS SEND TEXT FROM (PBR_TEXT) JUSTIFY (9) ACCUM PAGING;         
          PBR_TEXT = 'TRANSPEKER    :  ' !! UNSPEC(TRANS_PEKER);                
          EXEC CICS SEND TEXT FROM (PBR_TEXT) JUSTIFY (10) ACCUM PAGING;        
          PBR_TEXT = 'TRANSLISTEPEKER:  ' !! UNSPEC(TRANS_LISTE_PEKER);         
          EXEC CICS SEND TEXT FROM (PBR_TEXT) JUSTIFY (11) ACCUM PAGING;        
          PBR_TEXT = 'DIV-PARAM-PEKER:  ' !! UNSPEC(DIV_PARAM_PEKER);           
          EXEC CICS SEND TEXT FROM (PBR_TEXT) JUSTIFY (12) ACCUM PAGING;        
          PBR_TEXT = 'B00-PEKER      :  ' !! UNSPEC(B00_PEKER);                 
          EXEC CICS SEND TEXT FROM (PBR_TEXT) JUSTIFY (13) ACCUM PAGING;        
          PBR_TEXT = 'B01-PEKER      :  ' !! UNSPEC(B01_PEKER);                 
          EXEC CICS SEND TEXT FROM (PBR_TEXT) JUSTIFY (14) ACCUM PAGING;        
          PBR_TEXT = 'B02-PEKER      :  ' !! UNSPEC(B02_PEKER);                 
          EXEC CICS SEND TEXT FROM (PBR_TEXT) JUSTIFY (15) ACCUM PAGING;        
          PBR_TEXT = 'TRANSAKSJONEN ER KANSELERT.';                             
          EXEC CICS SEND TEXT FROM (PBR_TEXT) JUSTIFY (22) ACCUM PAGING;        
          PBR_TEXT = 'TA UTSKRIFT OG KONTAKT MED SYSTEMANSVARLIG.';             
          EXEC CICS SEND TEXT FROM (PBR_TEXT) JUSTIFY (23) ACCUM PAGING;        
          EXEC CICS SEND PAGE;                                                  
          EXEC CICS ABEND ABCODE(TRY);                                          
       END;                                                                     
                                                                                
    SSA_UNQUAL = 'TR0ROT  ';                                                    
    CALL PLITDLI (FIRE,                                                         
                  INSERT,                                                       
                  TR0_PCB,                                                      
                  TR0ROT,                                                       
                  SSA_UNQUAL);                                                  
    IF DLIUIB.UIBRCODE.UIBFCTR ^= UIB_RC_OK !                                   
       ^(TR0_PCB.STATUS_KODE = '  ' ! TR0_PCB.STATUS_KODE = 'II') THEN          
    DO;                                                                         
 L100:                                                                          
     /*--------------------------------------*/                                 
     /* FEIL VED INSERT-KALLET TIL TR0-BASEN */                                 
     /* SEGMENTET FANTES IKKE FRA FØR OG     */                                 
     /* LOT SEG HELLER IKKE LEGGE INN        */                                 
     /*--------------------------------------*/                                 
                                                                                
        DIV_PARAM_OMR.FEIL_MELD_NR   = 500;                                     
        DIV_PARAM_OMR.FEIL_VED_LABEL = 'L100';                                  
        GO TO UTGANG;                                                           
    END;                                                                        
                                                                                
                                                                                
 /*-------------------------------*/                                            
 /* BEHANDLING AV REGTPKT-SEGMENT */                                            
 /*-------------------------------*/                                            
                                                                                
    SSA_TR0ROT.FNR_KEY = TR0ROT.FNR;                                            
    IF SEGMENT_TABELL.REGTPKT THEN                                              
    DO;                                                                         
       SSA_REGTPKT.DATO_KEY   = REGTPKT.DATO ;                 /*2000*/         
       SSA_REGTPKT.KLOKKE_KEY = REGTPKT.KLOKKE;                /*2000*/         
       PBR_TEXT = 'DANN REGTPKT-SEGMENT.ID=' !! DATO !! ' ' !! KLOKKE;          
       EXEC CICS SEND TEXT FROM (PBR_TEXT) ACCUM;                               
                                                                                
       IO_PEK = ADDR(REGTPKT);                                                  
       IO_AREA = IO_HELP_AREA;                                                  
       SSA_UNQUAL = 'REGTPKT ';                                                 
       CALL PLITDLI (FEM,                                                       
                     INSERT,                                                    
                     TR0_PCB,                                                   
                     IO_AREA,                                                   
                     SSA_TR0ROT,                                                
                     SSA_UNQUAL);                                               
       IF DLIUIB.UIBRCODE.UIBFCTR ^= UIB_RC_OK !                                
          TR0_PCB.STATUS_KODE ^= '  ' THEN                                      
       DO;                                                                      
 L110:                                                                          
        /*------------------------------------------------*/                    
        /* FEIL VED INSERT-KALLET TIL TR0-BASEN           */                    
        /* UNIKT REGTPKT-SEGMENTET LOT SEG IKKE LEGGE INN */                    
        /*------------------------------------------------*/                    
                                                                                
           DIV_PARAM_OMR.FEIL_MELD_NR   = 500;                                  
           DIV_PARAM_OMR.FEIL_VED_LABEL = 'L110';                               
           GO TO UTGANG;                                                        
        END;                                                                    
     END;                                                                       
                                                                                
 /*--------------------------*/                                                 
 /* BEHANDLING AV GS-SEGMENT */                                                 
 /*--------------------------*/                                                 
                                                                                
    SSA_REGTPKT.DATO_KEY   = REGTPKT.DATO  ;                   /*2000*/         
    SSA_REGTPKT.KLOKKE_KEY = REGTPKT.KLOKKE;                   /*2000*/         
    IF SEGMENT_TABELL.GS THEN                                                   
    DO;                                                                         
       PBR_TEXT = 'DANN GS-SEGMENT';                                            
       EXEC CICS SEND TEXT FROM (PBR_TEXT) ACCUM;                               
                                                                                
       IO_PEK = ADDR(GS);                                                       
       IO_AREA = IO_HELP_AREA;                                                  
       SSA_UNQUAL = 'GS      ';                                                 
       CALL PLITDLI (SEKS,                                                      
                     INSERT,                                                    
                     TR0_PCB,                                                   
                     IO_AREA,                                                   
                     SSA_TR0ROT,                                                
                     SSA_REGTPKT,                                               
                     SSA_UNQUAL);                                               
       IF DLIUIB.UIBRCODE.UIBFCTR ^= UIB_RC_OK !                                
          TR0_PCB.STATUS_KODE ^= '  ' THEN                                      
       DO;                                                                      
 L120:                                                                          
           /*-------------------------------------------*/                      
           /* FEIL VED INSERT-KALLET TIL TR0-BASEN      */                      
           /* UNIKT GS-SEGMENTET LOT SEG IKKE LEGGE INN */                      
           /*-------------------------------------------*/                      
                                                                                
           DIV_PARAM_OMR.FEIL_MELD_NR   = 500;                                  
           DIV_PARAM_OMR.FEIL_VED_LABEL = 'L120';                               
           GO TO UTGANG;                                                        
        END;                                                                    
     END;                                                                       
                                                                                
 /*---------------------------*/                                                
 /* BEHANDLING AV HSU-SEGMENT */                                                
 /*---------------------------*/                                                
                                                                                
    IF SEGMENT_TABELL.HSU THEN                                                  
    DO;                                                                         
       PBR_TEXT = 'DANN HSU-SEGMENT';                                           
       EXEC CICS SEND TEXT FROM (PBR_TEXT) ACCUM;                               
                                                                                
       IO_PEK = ADDR(HSU);                                                      
       IO_AREA = IO_HELP_AREA;                                                  
       SSA_UNQUAL = 'HSU     ';                                                 
       CALL PLITDLI (SEKS,                                                      
                     INSERT,                                                    
                     TR0_PCB,                                                   
                     IO_AREA,                                                   
                     SSA_TR0ROT,                                                
                     SSA_REGTPKT,                                               
                     SSA_UNQUAL);                                               
       IF DLIUIB.UIBRCODE.UIBFCTR ^= UIB_RC_OK !                                
          TR0_PCB.STATUS_KODE ^= '  ' THEN                                      
       DO;                                                                      
 L130:                                                                          
        /*--------------------------------------------*/                        
        /* FEIL VED INSERT-KALLET TIL TR0-BASEN       */                        
        /* UNIKT HSU-SEGMENTET LOT SEG IKKE LEGGE INN */                        
        /*--------------------------------------------*/                        
                                                                                
           DIV_PARAM_OMR.FEIL_MELD_NR   = 500;                                  
           DIV_PARAM_OMR.FEIL_VED_LABEL = 'L130';                               
           GO TO UTGANG;                                                        
        END;                                                                    
     END;                                                                       
                                                                                
 END SKRIV_STAT_SEGM;                                                           
                                                                                
 %PAGE;                                                                         
 /*-------------------------------------------------------------------*/        
 /* START PÅ PROGRAM LAG_STATISTIKK_FJERN_TRANS_FOR_STØNADER          */        
 /*-------------------------------------------------------------------*/        
   IF FEIL_MELD_NR = 0 THEN                                                     
     DIV_PARAM_OMR.PROGRAM_ID = 'R0016501';                                     
    UIBPTR = UIB_PEKER;                                                         
    FEIL = TRUE;                                                                
 /*-------------------------------------------------------------------*/        
 /* CLEAR SEGMENT-TABELLEN OG ARBEIDSFELTENE FOR BASE-SEGMENTENE.     */        
 /*-------------------------------------------------------------------*/        
    SEGMENT_TABELL    = '';                                                     
    TR0ROT            = '';                                                     
    REGTPKT           = '';                                                     
    GS                = '';                                                     
    HSU               = '';                                                     
                                                                                
    TR0ROT.FNR = TRANS_OMR.O2.FNR;                                              
                                                                                
    SEGMENT_TABELL.REGTPKT      = TRUE;                                         
    REGTPKT.VIRKDATO            = TRANS_OMR.O2.VIRK_DATO_ÅMD;                   
    /*----------------------------------------------------------*/              
    /*SIKRER AT DAG=00 I VIRKDATO                 SL UKE 5 2000 */              
    /*----------------------------------------------------------*/              
    REGTPKT.VIRKDATO = FLOOR (REGTPKT.VIRKDATO / 100) * 100;                    
    REGTPKT.TKNR     = TRANS_OMR.O2.TKNR;                                       
                                                                                
    IF TRANS_OMR.O2.OPPHØR_KODE = 'C' THEN                                      
    DO;                                                                         
       SEGMENT_TABELL.GS = TRUE;                                                
   /*  GS.GS_SATS               = '0'       BØ9201*/                            
    END;                                                                        
                                                                                
    IF TRANS_OMR.O2.OPPHØR_KODE = 'D' THEN                                      
    DO;                                                                         
       SEGMENT_TABELL.HSU = TRUE;                                               
   /*  HSU.HSU_SATS               = '0';    BØ9201*/                            
    END;                                                                        
                                                                                
    CALL SKRIV_STAT_SEGM;                                                       
                                                                                
    FEIL = FALSE;                                                               
                                                                                
 UTGANG:                                                                        
 /*-------------------------------------------------------------------*/        
 /*                                                                   */        
 /* PROGRAMKONTROLLEN ENDER HER NÅR ALL OPPDATERING ER FORETATT       */        
 /* ELLER NÅR DET ER DETEKTERT EN FEIL I OPPDATERING AV ET SEGMENT.   */        
 /* VARIABELEN 'FEIL' AVGJØR FEIL-STATUS.                             */        
 /*                                                                   */        
 /*-------------------------------------------------------------------*/        
                                                                                
    IF FEIL THEN                                                                
    DO;                                                                         
       PBR_TEXT = 'DL/I-RETUR-KODE = ' !!                                       
                  CHAR(DLIUIB.UIBRCODE.UIBFCTR) !!                              
                  '   STATUS-KODE = ' !! TR0_PCB.STATUS_KODE;                   
       EXEC CICS SEND TEXT FROM (PBR_TEXT) ACCUM;                               
       PBR_TEXT = 'SEGMENT-NIVÅ = ' !! TR0_PCB.SEGM_NIVAA;                      
    END;                                                                        
    ELSE                                                                        
       PBR_TEXT = 'NORMAL AVSLUTNING PÅ PROGRAM R0016501';                      
    EXEC CICS SEND TEXT FROM (PBR_TEXT) ACCUM;                                  
                                                                                
    EXEC CICS PURGE MESSAGE;                                                    
    EXEC CICS RETURN;                                                           
                                                                                
                                                                                
  %INCLUDE R0019904 ;    /* FNR-KONTROLL  */                                    
  %INCLUDE R0019910 ;    /* NUM-KONTROLL  */                                    
  %INCLUDE R0019912 ;    /* KONV FRA NUM TIL CHAR OG OMVENDT */                 
                                                                                
 END; /* OF PROGRAM R0016501 */                                                 
