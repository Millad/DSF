 /*   SIST ENDRET PÅ PROD   2007.12.21 12.20.39 AV   JDA2990          */        
 /*   SIST ENDRET PÅ PROD   2004.12.17 15.30.25 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.07.13 13.21.04 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.08.29 12.10.01 AV   HLA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.06.01  8.36.26 AV   HLA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.11.07 12.25.20 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.08.08 12.01.46 AV   HLA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.04.17 12.52.08 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.04.12 10.15.08 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.03.22  9.41.21 AV   JDA7339          */        
 /*       SIST ENDRET 18/03-99 09.18.22 AV   JDA7339                  */        
 /*       SIST ENDRET 18/03-99 09.17.59 AV   JDA7339                  */        
 /*       SIST ENDRET 10/12-98 10.15.37 AV   JDA7339                  */        
 /*       SIST ENDRET 10/12-98 10.14.37 AV   JDA7339                  */        
 /*       SIST ENDRET 04/05-98 14.49.00 AV   JDA7339                  */        
 /*       SIST ENDRET 25/02-98 09.58.07 AV   RFA9991                  */        
 /* **************************************************************** */         
 /*IDENTIFIKASJON :                                                  */         
 /*    R0014201 - HOVEDPROGRAM I PLI                                 */         
 /*    PROGRAMMERER:              R.FARVIK,APRIL -82.                */         
 /*HENSIKT:                                                          */         
 /*    BEREGNE ALDERSPENSJON.                                        */         
 /*                                                                  */         
 /*PROGRAMTILKNYTNING:                                               */         
 /*    KALLES OPP AV PROGRAM R0013520                                */         
 /*BRUK:                                                             */         
 /*    EXEC CICS XCTL PROGRAM ('R0014201') COMMAREA (KOM_OMR)        */         
 /*                                                                  */         
 /* **************************************************************** */         
 R001421: PROC (COMMAREA_PEKER) OPTIONS (MAIN);                                 
    %INCLUDE P0019906;  /*TRANS_OPPL_OMR BASED(TRANS_OPPL_PEKER)*/              
    %INCLUDE P0019908;  /*KOM_OMR        BASED(COMMAREA_PEKER)  */              
    %INCLUDE P0019910;  /*STYRINGS_OMR   BASED(STYRINGS_PEKER)  */              
    %INCLUDE P0019912;  /*DIV_PARAM_OMR  BASED(DIV_PARAM_PEKER) */              
    %INCLUDE P0019924;  /*GV_TAB_RE      BASED(GV_PEKER)        */              
    %INCLUDE P0014002;  /*W_POREKKE*/                                           
    %INCLUDE P0014003;  /*TAB*/                                                 
    %INCLUDE P0014009;  /*POTALL_OPPL    BASED(PO_PEKER)        */              
    %INCLUDE P0019980; /* DEFINERING AV FNR11/13 SØKER & EKTEFELLE */           
                                                                                
   DCL 1 B01 BASED(B01_PEKER), %INCLUDE P0019921;                               
   DCL 1 B02 BASED(B02_PEKER), %INCLUDE P0019921;                               
   DCL                                                                          
      (ADDR,CSTG,ROUND,SUBSTR,SUM,VERIFY,HBOUND) BUILTIN;                       
   DCL                                                                          
      J                                   FIXED BIN (15) INIT (0);              
   DCL                                                                          
      HJ_DATO                             PIC      '(8)9';    /*2000*/          
   DCL                                                                          
      VIRK_LOV92_ÅM                       PIC      '(6)9';    /*2000*/          
                                                                                
   DCL HJ_DATO_HHÅÅ  DEF HJ_DATO   POS (1) PIC      '(4)9'; /*2000*/            
                                                                                
   DCL                                                                          
      HJ_DATO_ÅR    DEF HJ_DATO   POS (3) PIC      '(2)9';    /*2000*/          
   DCL                                                                          
      APD_ØNSKET                          FIXED DEC(3),                         
      MAX_ÅR                              PIC      '(2)9',                      
      ALDER_ÅM                            PIC      '(5)9';                      
   DCL                                                                          
      ALDER_ÅM_IDAG                      PIC      '(5)9';                       
   DCL                                                                          
      ÅR_ALDER_IDAG DEF ALDER_ÅM_IDAG POS (1) PIC '(3)9';                       
   DCL    W_FNR13           PIC'(13)9';        /*2000*/                         
   DCL                                                                          
    1 FNR_R DEF W_FNR,                                                          
      2 DAG                   PIC '(2)9',                                       
      2 MND                   PIC '(2)9',                                       
      2 ÅR                    PIC '(2)9',                                       
      2 ÅRHUNDRE              PIC '9',                                          
      2 FILLER                PIC '(4)9';                                       
   DCL                                                                          
    1 FNR_ÅMD,                                                                  
      2 FNR_Å                 PIC '(2)9',                                       
      2 FNR_M                 PIC '(2)9';                                       
   DCL W_FNR_R  LIKE FNR_R;  /*NY92*/                                           
    DCL                                                                         
       W_FNREK13        PIC'(13)9';        /*2000*/                             
    DCL                                                                         
       1 FNR_EK_R13     DEF W_FNREK13,        /*9903*/                          
         2 DAG          PIC'(2)9',                                              
         2 MND          PIC'(2)9',                                              
         2 ÅR_EK        PIC'(4)9',                                              
         2 ÅRHUNDRE     PIC'9',                                                 
         2 FILLER       PIC'(4)9';                                              
   DCL  VIRKDATO_ÅMD          PIC '(8)9';                      /*2000*/         
   DCL  VIRKDATO_ÅR DEF VIRKDATO_ÅMD POS (1) PIC '(4)9';       /*2000*/         
                                                                                
   DCL W_SUM_YTELSE           FIXED DEC(7);   /*NY92*/                          
   DCL AP_TEI                 FIXED DEC(7);   /*NY92*/                          
                                                                                
   DCL                                                                          
    1 FNR_ÅMD_NUM DEF FNR_ÅMD POS (1) PIC '(4)9';                               
                                                                                
                                                                                
   DCL ST_BEREGNET(2)  CHAR (1) INIT (' ',' ');                                 
                                                                                
   DCL                                                                          
    1 DATO                    PIC '(8)9',      /*2000*/                         
    1 HJ_ÅMD                  PIC '(8)9';    /*2000*/                           
   DCL                                                                          
    1 DAGENS_DATO DEF DATO POS(1),                                              
      2 HH                    PIC '(2)9',       /*2000*/                        
      2 ÅR                    PIC '(2)9',                                       
      2 MND                   PIC '(2)9',                                       
      2 DAG                   PIC '(2)9';                                       
   DCL                                                                          
    1 COMMAREA_PEKER          POINTER;                                          
                                                                                
     /* ============================================================= */        
     COMMAREA_PEKER  = ADDR(KOM_OMR);                                           
     ST_BEREGNET(1)  = ' ';                                                     
     ST_BEREGNET(2)  = ' ';                                                     
                                                                                
     W_POREKKE = '';                                                            
     B02.AFP_TILLEGG(BER_SØ_IND)  = 0;       /* TRUDE    1292 */                
     B02.AFP_TILLEGG_NETTO(BER_SØ_IND)  = 0; /* MARTIN   0800 */                
     B02.PENSJONSTYPE3(BER_SØ_IND) = ' ';    /* TRUDE  190994 */                
     B02.PÅ_EØS_FAKTISK(BER_SØ_IND) = 0;     /*          9909 */                
     DATO     = DATO_2000;                    /*2000*/                          
     W_FNR    = B02.FNR(BER_SØ_IND);                                            
     W_FNR_EK = B02.FNR(BER_EK_IND);                                            
     W_FNR_13 = KONV_FNR11_FNR13(W_FNR);                                        
     HJ_ÅMD   = DATO_2000;                /*2000*/                              
                                                                                
     IF B02.UTTAKSDATO_ÅMD(BER_SØ_IND) < 19980200   &                           
        B02.UTTAKSDATO_ÅMD(BER_SØ_IND) > 0        THEN /*200108*/               
       DO;                                                                      
         IF B02.UTTAKSDATO_ÅMD(BER_SØ_IND) < 19971000   &                       
            W_FNR_R13.ÅR > 1930                         &                       
            B02.KONV_P_KODE   (BER_SØ_IND) = 'O'      THEN;                     
         ELSE                                                                   
            B02.PENSJON_FØR_9802(BER_SØ_IND) = 'J';                             
       END;                                                                     
                                                                                
                                                                                
 /* ***************************************************************** */        
 /* BEREGN DEL1 AV ALDERSPENSJONEN.                                   */        
 /* ***************************************************************** */        
                                                                                
    /* ************************************************************** */        
    /* IKKE SAMMENSTØTENDE BEREGNING NÅR EK HAR 0 APD                 */        
    /* ************************************************************** */        
                                                                                
    IF B02.PENSJONSTYPE1(BER_EK_IND) = 'A'      !                               
 /*AFP*/     (  B02.PENSJONSTYPE1(BER_EK_IND) = 'K'      &                      
 /*AFP*/        B02.PENSJONSTYPE2(BER_EK_IND) ^= 'N'  )  THEN                   
       DO;                                                                      
          IF B02.ALDERSP.APD  (BER_EK_IND) =  0   &                             
             B02.SUM_YTELSE(BER_EK_IND)    =  0    THEN     /*NY92*/            
             DO;                                                                
                IF B02.PENSJONSTYPE2(BER_EK_IND)  = 'S'   &                     
                   B02.KONV_P_KODE  (BER_EK_IND) ^= 'F'   &                     
                   B02.P67_KODE  (BER_EK_IND) ^= '5'   THEN   /*HL*/            
                                                                                
         /*------DETTE LÅ PÅ TEST. VET IKKE HVORFOR. TRUDE -----                
         P67_KODE = 5 DVS KONV ET, ELLER UFG < 100 OG DATO >9200                
                   NULLPENSJON                                                  
                IF B02.PENSJONSTYPE2(BER_EK_IND)  = 'S'   THEN                  
                   IF  B02.KONV_P_KODE(BER_EK_IND) ^= 'F'   &                   
                       B02.P67_KODE (BER_EK_IND)   ^= '5'   THEN                
          ------------------------- */                                          
                   DO;                                                          
                      B02.PENSJONSTYPE2(BER_SØ_IND)  = 'P';                     
                      B02.PENSJONSTYPE2(BER_EK_IND)  = 'P';                     
                   END;                                                         
             END;                                                               
                                                                                
          ELSE                                                                  
                                                                                
             DO;                                                                
                IF (B02.SIVILSTAND   (BER_EK_IND)  = 'G'  !                     
                    B02.SIVILSTAND   (BER_EK_IND)  = 'P'  ! /*2000-2*/          
                    B02.SIVILSTAND   (BER_EK_IND)  = 'W')      &                
                   B02.PENSJONSTYPE2(BER_SØ_IND)  = 'P'  &                      
                   B02.PENSJONSTYPE2(BER_EK_IND)  = 'P'    THEN                 
                                                                                
                   DO;                                                          
                      B02.PENSJONSTYPE2(BER_SØ_IND)  = 'S';                     
                      B02.PENSJONSTYPE2(BER_EK_IND)  = 'S';                     
  /*AFP1093*/         IF B02.PENSJONSTYPE3(BER_EK_IND) = 'S' THEN               
                         B02.PENSJONSTYPE2(BER_SØ_IND)  = 'P';                  
                   END;                                                         
             END;                                                               
       END;                                                                     
                                                                                
    ELSE                                                                        
                                                                                
       IF (B02.PENSJONSTYPE1(BER_EK_IND)  = 'U'   !                             
           B02.PENSJONSTYPE1(BER_EK_IND)  = 'Y')  &                             
           (B02.SIVILSTAND   (BER_EK_IND)  = 'G'  !      /*2000-2*/             
            B02.SIVILSTAND   (BER_EK_IND)  = 'P'  !                             
            B02.SIVILSTAND   (BER_EK_IND)  = 'W')      &                        
          B02.PENSJONSTYPE2(BER_SØ_IND)  = 'P'  &                               
          B02.PENSJONSTYPE2(BER_EK_IND)  = 'P'    THEN                          
                                                                                
          DO;                                                                   
             B02.PENSJONSTYPE2(BER_SØ_IND)  = 'S';                              
             B02.PENSJONSTYPE2(BER_EK_IND)  = 'S';                              
  /*200103*/  IF B02.YRKEPENS.YUG(BER_EK_IND) = 0  &                            
                 B02.UFØRPENS.UFG(BER_EK_IND) = 0   THEN                        
                 DO;                                                            
                    B02.PENSJONSTYPE2(BER_SØ_IND)  = 'P';                       
                    B02.PENSJONSTYPE2(BER_EK_IND)  = 'P';                       
                 END;                                                           
          END;                                                                  
                                                                                
 /* ***************************************************************** */        
 /* BEREGN SØKERS ALDER PÅ VIRKNINGSDATOTIDSPKT.                      */        
 /* ***************************************************************** */        
                                                                                
        ALDER_ÅM  = F_ALDER(W_FNR,                                              
                               TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD);               
        IF (ALDER_ÅM > 6800   !                                                 
            TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD < 20080000)   THEN;                
        ELSE                                                                    
           DO;                                                                  
              B02.FAI(BER_SØ_IND) = 0;                                          
              B02.FAI_DATO_ÅMD(BER_SØ_IND) = 0;                                 
           END;                                                                 
                                                                                
        CALL BEREGN_AP_DEL1;                         /* 4221 */                 
                                                                                
                                                                                
        IF FEIL_MELD_NR > 0  THEN                                               
           GO TO L999;                                                          
        ELSE                                                                    
           PROGRAM_ID = 'R0014201';                                             
                                                                                
                                                                                
 /* ***************************************************************** */        
 /* BEREGN SØKERS ALDER IDAG.                                         */        
 /* ***************************************************************** */        
                                                                                
        HJ_DATO = DATO_2000;                         /*2000*/                   
        ALDER_ÅM_IDAG = F_ALDER(W_FNR,HJ_ÅMD);                                  
                                                                                
 /* ***************************************************************** */        
 /* FØLENDE SELECT REPRESENTERER DEL2 AV PENSJONSBEREGNINGEN.         */        
 /* DEL2 ER NEMLIG IKKE AKTUELL FOR ALLE TRANSTYPER.                  */        
 /* ***************************************************************** */        
                                                                                
 /*LOVENDRING PR. 1.1.91 HL : */                                                
                                                                                
    VIRKDATO_ÅMD = TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD;                            
    IF   W_FNR_R13.ÅR < 1925 !                        /*NY92*/                  
       (B02.KONV_P_KODE(BER_SØ_IND) = 'K' &             /*NY92*/                
        B02.UTTAKSDATO_ÅMD(BER_SØ_IND) < 19920000) THEN  /*2000*/               
      DO;                                                                       
        IF (ALDER_ÅM  < 7000 & VIRKDATO_ÅR < 1991)  !       /*2000*/            
           (ALDER_ÅM  < 7001 & VIRKDATO_ÅR > 1990)    THEN  /*2000*/            
           SELECT (TRANSTYPE);                                                  
                                                                                
 /* ***************************************************************** */        
 /* GRUNNBLANKETT ALDER.                                              */        
 /* ***************************************************************** */        
                                                                                
              WHEN (1)                                                          
                 DO;                                                            
                   IF TRANS_OPPL_OMR.BLANKETTYPE =  'AP'  THEN                  
                      CALL HENT_FRA_AP;                                         
                   IF TRANS_OPPL_OMR.BLANKETTYPE =  'A1'  THEN                  
                      CALL HENT_FRA_A1;                                         
                                                                                
                   CALL BEREGN_APD(APD_ØNSKET);            /* 4122 */           
                                                                                
                 END;                                                           
                                                                                
 /* ***************************************************************** */        
 /* NY FORSØRGINGSTILLEGGS-BLANKETT - MED ARBEIDSINNTEKT          */            
 /* ***************************************************************** */        
                                                                                
              WHEN (6)                                                          
                 DO;                                                            
                   IF B02.VIRK_DATO_ÅMD(BER_SØ_IND) >                           
                                               19910400 THEN /*2000*/           
                       CALL BEREGN_APD(100);        /* 4122 */                  
                 END;                                                           
                                                                                
                                                                                
 /* ***************************************************************** */        
 /* ENDRINGSBLANKETT-ENDRING I GRAD ELLER FAI.                        */        
 /* ***************************************************************** */        
                                                                                
              WHEN (8)                                                          
                 DO;                                                            
                     IF TRANS_OPPL_OMR.BLANKETTYPE  = 'EN'  THEN                
                        CALL BEREGN_APD(100);            /* 4122 */             
                     ELSE                                                       
                       DO;                                                      
                          CALL HENT_FRA_E1;                                     
                          CALL BEREGN_APD(APD_ØNSKET);    /* 4122 */            
                       END;                                                     
                 END;                                                           
                                                                                
 /* ***************************************************************** */        
 /* AUTOMATISK OVERGANG FRA UP/YP TIL AP ETTER FYLTE 67 ÅR.           */        
 /* ***************************************************************** */        
                                                                                
              WHEN (24)                                                         
                 DO;                                                            
                     CALL BEREGN_APD_OVERGANG_FRA_UP;       /* 4123 */          
                                                                                
                     IF B01.PENSJONSTYPE1(BER_SØ_IND) = 'Y' THEN                
                       CALL BEREGN_APD_OVERGANG_FRA_YP;    /* 4127 */           
                                                                                
                 END;                                                           
                        /*TRANSTYPE=33 ETTERLATT FAMILIEPLEIER*/                
              WHEN (33)                                                         
                 DO;                                                            
                                                                                
  /* LOVENDRING PR 1.4.84      *             *         * */                     
                                                                                
                     IF B02.STATUS.VIRK_DATO_ÅMD(BER_SØ_IND) >                  
                                              19840300 THEN    /*2000*/         
                       DO;                                                      
                          APD_ØNSKET = 100;                                     
                          CALL BEREGN_APD(APD_ØNSKET);      /* 4122 */          
                       END;                                                     
                                                                                
  /* HIT    *       *          *         *        *      */                     
                                                                                
                                                                                
                    CALL BEREGN_APD_OVERGANG_FRA_KAP10;   /*R0014124*/          
                 END;                                                           
              OTHERWISE                                                         
                 DO;                                                            
                        /* ENDRING MARS 92.  DERSOM B02.APD > 0     */          
                        /* LEGGES B02.APD INN I ØNSKET_APD.         */          
                        /* VED Å LEGGE 100 INN I APD ØNSKET, FÅR    */          
                        /* IKKE DE VENTETILLEGG SOM BARE ØNSKER Å   */          
                        /* TA UT DELEPENSJON FØR 0484               */          
                        /*TRUDE 180392                              */          
                                                                                
                    IF  B02.ALDERSP.APD(BER_SØ_IND) > 0   &      /*TS*/         
                        B02.STATUS.VIRK_DATO_ÅMD(BER_SØ_IND) <                  
                                               19840400  THEN  /*2000*/         
                       APD_ØNSKET = B02.ALDERSP.APD(BER_SØ_IND); /*TS*/         
                    ELSE                                         /*TS*/         
                       APD_ØNSKET = 100;                                        
                    CALL BEREGN_APD(APD_ØNSKET);      /* 4122 */                
                 END;                                                           
           END;                                                                 
        ELSE                                                                    
           CALL APD_70ÅR;                                 /* 4125 */            
                                                                                
 /* ***************************************************************** */        
 /* DEL3 AV ALDERSPENSJONEN BEREGNES.                                 */        
 /* ***************************************************************** */        
                                                                                
      END;                                                                      
        CALL BEREGN_AP_DEL3;                              /* 4222 */            
 /* */                                                                          
                                                                                
 HENT_FRA_AP: PROC;                                                             
   %INCLUDE P0010501;  /* AP-OMRÅDE                */                           
      APD_ØNSKET = APS.APD;                                                     
 END HENT_FRA_AP;                                                               
                                                                                
 HENT_FRA_A1: PROC;                                                             
   %INCLUDE P001N501;  /* A1-OMRÅDE                */                           
      APD_ØNSKET = A1S.APD;                                                     
 END HENT_FRA_A1;                                                               
                                                                                
 HENT_FRA_E1: PROC;                                                             
   %INCLUDE P0011401;  /* E1-OMRÅDE                */                           
     APD_ØNSKET = E1K7.APD;                                                     
 END HENT_FRA_E1;                                                               
                                                                                
        %INCLUDE R0014021;      /* JUSTER_40PROS               */               
        %INCLUDE R0014022;      /* JUSTER_FORVENTET            */               
        %INCLUDE R0014122;      /* BEREGN_APD                  */               
        %INCLUDE R0014123;      /* BEREGN_APD_OVERGANG_FRA_UP  */               
        %INCLUDE R0014124;      /* BEREGN_APD_OVERGANG_FRA_KAP10 */             
        %INCLUDE R0014125;      /* APD_70ÅR                    */               
        %INCLUDE R0014127;      /* BEREGN_APD_OVERGANG_FRA_YP  */               
        %INCLUDE R0014133;      /* F_BEREGN_SÆRTILLEGG         */               
        %INCLUDE R0014134;      /* F_BEREGN_KOMPTILLEGG        */               
        %INCLUDE R0014135;      /* TIDLIGERE_ARBEIDSINNTEKT    */               
        %INCLUDE R0014136;      /* ÅTTI_PROSENT_REGELEN        */               
        %INCLUDE R0014137;      /* JUSTER_MED_APD              */               
        %INCLUDE R0014138;      /* BEREGN_VT                   */               
   /*   %INCLUDE R0014139  /*JUSTER_MED_FORVENTET_INNTEKT_AP NY92*/             
        %INCLUDE R0014140;   /* F_OVERGANG_FRA_YP               NY92*/          
        %INCLUDE R0014142;      /* F_TILLEGGSPENSJON           */               
        %INCLUDE R0014143;      /* POENG_TILLEGG               */               
        %INCLUDE R0014145;      /* REGN_TT_67_TIL_70           */               
        %INCLUDE R0014146;      /* FAI_GRENSER                 */               
        %INCLUDE R0014147;      /* FRAVIK_80%                  */               
        %INCLUDE R0014148;   /* F_TP92                          NY92*/          
        %INCLUDE R0014151;      /* KLAGJØR_TAB            NY92 */               
        %INCLUDE R0014153;      /* BEREGN_SPT_OPT_PÅ           */               
        %INCLUDE R0014155;      /* FAI_GRENSER_FRAVIK          */               
        %INCLUDE R0014156;      /* BEREGN_SPT_OPT_PÅ_ALT       */               
        %INCLUDE R0014161;      /* LEGG_UFØREHIST_I_TAB    NY92 */              
        %INCLUDE R0014180;      /* SÆRTILLEGG_UTLAND           */               
        %INCLUDE R0014181;      /* F_ET_UTLAND                 */               
        %INCLUDE R0014221;      /* BEREGN_AP_DEL1              */               
        %INCLUDE R0014222;      /* BEREGN_AP_DEL3              */               
        %INCLUDE R0014231;      /* BEREGN TP_AP                */               
        %INCLUDE R0014232;      /* BEREGN GP_AP                */               
        %INCLUDE R0014233;      /* BEREGN BT (BARNETILLEGG)    */               
        %INCLUDE R0014241;      /* BEREGN_AP_OVERGANG_FRA_YP   */               
        %INCLUDE R0014242;      /* POENGTILLEGG_ENDRES_YP      */               
        %INCLUDE R0019905;      /* F_ALDER                     */               
    /*  %INCLUDE R0019988    /* KONV_HÅMD_ÅMD                2000   */          
        %INCLUDE R0019995;   /* FNR11 -->  FNR13             2000   */          
   L999:                                                                        
                                                                                
   END R001421;                                                                 
