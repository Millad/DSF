 /*   SIST ENDRET PÅ PROD   2003.02.27 12.41.32 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2002.11.21 13.33.22 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.10.22 13.07.20 AV   JDA2970          */        
 /*       SIST ENDRET 04/05-98 14.26.36 AV   JDA7339                  */        
 /*       SIST ENDRET 16/03-98 15.33.25 AV   RFA9991                  */        
 /* ***************************************************************** */        
 /*                                                                   */        
 /* ***************************************************************** */        
 /*IDENTIFIKASJON:                                                    */        
 /* **********************                                            */        
 /*  PROGRAM-IDENT : R0011901 KONTROLL_GRUNNBLANKETT_AFP HOVEDPROG    */        
 /*  PROGRAM-SPRÅK : PLI - CICS                                       */        
 /*  PROGRAMMERER  : SATISH PATHAK                                    */        
 /*  PROGRAMMET BLE LAGET : 1988                                      */        
 /*  ENDRINGERSDATO :                                                 */        
 /*  ENDRINGEN GJELDER:                                               */        
 /*  ENDRINGEN BLE UTFØRT AV :                                        */        
 /*                                                                   */        
 /* ***************************************************************** */        
 /*HENSIKT:                                                           */        
 /* **********                                                        */        
 /*  PROGRAMMET KONTROLLERER AFP (AVTALEFESTET FØRTIDSPENSJON)        */        
 /*  BLANKETTEN KONTROLLERES ISOLERT - IKKE MOT STATUS.               */        
 /*  OPERATØREN KAN BRUKE PF-TASTENE :                                */        
 /*    PF1  FØRER TIL OPPBEVARING AV TRANSEN I VENTEREGISTERET.       */        
 /*    PF2  FØRER TIL AT TRANSAKSJONEN FORKASTES -                    */        
 /*    I BEGGE TILFELLER SKRIVES DET UT MELDING PÅ SKJERM, OG         */        
 /*    PROGRAM R0010401 SØRGER FOR Å LEGGE UT NYTT BILDE FOR          */        
 /*    REGISTRERING AV AFP - TRANSKODE R041.                          */        
 /*  NÅR INGEN FEIL MER FINNES SETTES TRANSKODE = R042, OG DET        */        
 /*  RETURNERES TIL R0010401.                                         */        
 /*                                                                   */        
 /* ***************************************************************** */        
 /*PROGRAMTILKNYTTING:                                                */        
 /* ********************                                              */        
 /*  PROGRAMMETS TRANSKODER ER R190, R191, R192, R193, R194.          */        
 /*  PROGRAMMET KALLES FRA R0010401 - REGISTRERING, MED               */        
 /*  TRANSKODE 'R190'.                                                */        
 /*  PROGRAMMET HAR EXEC CICS XCTL TIL :                              */        
 /*             R0010301 - VALG_AV_FUNKSJON                           */        
 /*             R0010401 - REGISTRERING                               */        
 /*  PROGRAMMET HAR EXEC CICS LINK TIL :                              */        
 /*             R0010490 - SKRIV_UT_PÅ_VENTEREG                       */        
 /*  PROGRAMMET INCLUDER :                                            */        
 /*             R0011902  -  BLANK_ARBEIDSOMRÅDE                      */        
 /*             R0011903  -  KONTROLL_SØKER                           */        
 /*             R0011905  -  KONTROLL_SPESIELLE_OPPLYSNINGER          */        
 /*             R0011906  -  FLYTT_FRA_ARBEIDSOMRÅDE_TIL_MAP          */        
 /*             R0011908  -  BEREGN_TT_AFP                            */        
 /*             R0011909  -  FLYTT_FRA_MAP_TIL_ARBEIDSOMRÅDE          */        
 /*             R0019901  -  TEST OM DATO ER GYLDIG                   */        
 /*             R0019902  -  FINNE KJØNN                              */        
 /*             R0019903  -  ER EKTEFELLES ALDER GYLDIG               */        
 /*             R0019904  -  FØDSELSNUMMERKONTROLL                    */        
 /*             R0019905  -  BEREGN_ALDER                             */        
 /*             R0019907  -  TABELLSØKING                             */        
 /*             R0019910  -  NUMERISK KONTROLL                        */        
 /*             R0019911  -  DATO_KONTROLL                            */        
 /*             R0019912  -  KONVERTERING CHAR ==> PIC OG OMV.        */        
 /*             R0019944  -  SKRIV_FEIL                               */        
 /*             R0019964  -  DAGENS DATO + 6 MDR                      */        
 /*  PROGRAMMET BRUKER MAPSET S001193 - AFP                           */        
 /*         MAP S001191   -  REGISTRERING AV AFP-TRANS                */        
 /* ***************************************************************** */        
 /*DATASETTOPPLYSNINGER:                                              */        
 /* ***********************                                           */        
 /*  FØDSELSNR-ENDRINGSREG BLIR LEST I SUBPROGRAM R0011903 VED        */        
 /*  CALL TIL PROGRAM R0019906.                                       */        
 /*                                                                   */        
 /* ***************************************************************** */        
 /*FEILMELDINGER:                                                     */        
 /* *******************                                               */        
 /*                                                                   */        
 /*  SKRIVES UT PÅ SKJERMBILDET                                       */        
 /*                                                                   */        
 /* ***************************************************************** */        
 R00119:                                                                        
   PROC(COMMAREA_PEKER) OPTIONS(MAIN);                                          
 %PAGE;                                                                         
      %INCLUDE S00119;                 /* AFP-MAPSETTET   */                    
 %PAGE;                                                                         
      %INCLUDE P0011901;               /* TRANS-OMR FOR AFP          */         
 %PAGE;                                                                         
      %INCLUDE P0019906;               /* TRANS_OPPL_OMR (BASED) */             
 %PAGE;                                                                         
      %INCLUDE P0019908;               /* KOM_OMRÅDE (BASED) */                 
 %PAGE;                                                                         
      %INCLUDE P0019910;               /* STYRINGS_OMRÅDE (BASED) */            
 %PAGE;                                                                         
      %INCLUDE P0019912;               /*  DIV_OMRÅDE (BASED)      */           
 %PAGE;                                                                         
      %INCLUDE DFHBMSCA;                                                        
 %PAGE;                                                                         
   DCL                                                                          
      (BMSMAPBR,                                                                
      COMMAREA_PEKER) PTR;                                                      
   DCL                                                                          
      (CSTG,                                                                    
      ADDR,                                                                     
      VERIFY,                                                                   
      UNSPEC,                                                                   
      ONCODE,                                                                   
      SUBSTR) BUILTIN;                                                          
   DCL                                                                          
      (FEIL_FUNNET,FEIL19,FEIL20) BIT(1),                                       
      FEIL_I_SØKER                BIT(1),                                       
      FEIL_I_SPES                 BIT(1),                                       
      ONKODE                      PIC'9999',                                    
      CURSOR_POS                  FIXED BIN(15) INIT(-1),                       
      ONK DEF ONKODE              CHAR(4),                                      
      FEILKODE                    CHAR(4),                                      
      DSNAVN                      CHAR(8),                                      
      ANT_FEIL_SKREVET            FIXED DEC (3);                                
 %PAGE;                                                                         
      ON ERROR SNAP                                                             
      BEGIN;                                                                    
                                                                                
         ON ERROR SYSTEM        ;                                               
         ONKODE=ONCODE          ;                                               
         FEILKODE = ONK         ;                                               
         DSNAVN   = EIBDS       ;                                               
         GO TO FEILBEH          ;                                               
      END;                                                                      
                                                                                
                                                                                
 FEILKODE  = 'FEIL';                                                            
 DSNAVN    = '        ';                                                        
                                                                                
 EXEC CICS HANDLE AID PF1(PF1) PF2(PF2);                                        
                                                                                
 EXEC CICS HANDLE CONDITION ERROR(FEILBEH);                                     
                                                                                
                                                                                
 KOM_OMR.PEKER_LISTE.DIV_PARAM_PEKER  = ADDR(KOM_OMR.DIV_PARAM_OMR);            
                                                                                
 IF FRA_CICS THEN                                                               
  DO;                                                                           
    KOM_OMR.PEKER_LISTE.TRANS_OPPL_PEKER = ADDR(KOM_OMR.TRANS_OPPL_OMR);        
    KOM_OMR.PEKER_LISTE.TRANS_PEKER      = ADDR(KOM_OMR.TRANS_OMR);             
    KOM_OMR.PEKER_LISTE.STYRINGS_PEKER   = ADDR(KOM_OMR.STYRINGS_OMR);          
    ATK_KOM_PTR           = ADDR  (KOM_OMR.ATK_COM_OMR  ) ;                     
  END;                                                                          
                                                                                
                                                                                
 IF HENT_FRAM_MAP  THEN                                                         
   DO;                                                                          
     EXEC CICS SEND MAP('S001191') MAPSET('S001193') MAPONLY                    
     SET (BMSMAPBR);                                                            
     ALLOCATE S001191O;                                                         
     S001191O.CICS_INFOO = DIV_PARAM_OMR.CICS_NAVN;                             
   END;                                                                         
                                                                                
 RECEIVE_MAP:                                                                   
                                                                                
                                                                                
 IF ^HENT_FRAM_MAP THEN                                                         
    DO;                                                                         
       EXEC CICS RECEIVE MAP('S001191')                                         
                 MAPSET('S001193') SET(BMSMAPBR);                               
       CALL BLANK_AFP_MELDNR;                            /* R0011902 */         
       CALL MAP_SPESIAL_INFO_PROT;                                              
       FEIL_MELD_NR = 0;                                                        
    END;                                                                        
                                                                                
 IF FUNKSJONSKODEL > 0 THEN                                                     
    DO;                                                                         
       FUNKSJONSKODE = FUNKSJONSKODEI;                                          
       EXEC CICS XCTL PROGRAM('R0010301') COMMAREA(KOM_OMR);                    
    END;                                                                        
                                                                                
 IF STYRE_KODEL > 0 THEN                                                        
    DO;                                                                         
       STYREKODE = STYRE_KODEI;                                                 
       TRANSKODE = 'R041';                                                      
       EXEC CICS XCTL PROGRAM('R0010401') COMMAREA(KOM_OMR);                    
    END;                                                                        
                                                                                
 IF STYREKODE ^= 'AF'  &  FRA_CICS THEN      /*   CHANGE TO */                  
    TRANSKODE = 'R041';                                                         
                                                                                
 ELSE                                                                           
                                                                                
  DO;                                                                           
   ANT_FEIL_SKREVET = 0;                                                        
   FEIL_FUNNET      = '0'B;                                                     
   FEIL_I_SØKER     = '0'B;                                                     
   FEIL_I_SPES      = '0'B;                                                     
   TRANS_RETURKODE  = TRANSKODE;                                                
                                                                                
            SELECT (TRANSKODE);                                                 
               WHEN('R190')                                                     
                  DO;                                                           
                     CALL BLANK_AFP_SØKER;                /* R0011902 */        
                     CALL BLANK_AFP_SPES;                 /* R0011902 */        
                     CALL BLANK_AFP_MELDNR;               /* R0011902 */        
                     CALL OVERFØR_AFP_SØKER;              /* R0011909 */        
                     CALL KONTROLL_AFP_SØKER(FEIL_I_SØKER);/*R0011903*/         
                  END;                                                          
               WHEN('R191')                                                     
                  DO;                                                           
                     CALL BLANK_AFP_MELDNR;               /* R0011902 */        
                     IF FUNKSJONSKODE ^= 'V' THEN                               
                        DO;                                                     
                           CALL BLANK_AFP_SPES;           /* R0011902 */        
                        END;                                                    
                     CALL OVERFØR_AFP_SØKER;              /* R0011909 */        
                     CALL KONTROLL_AFP_SØKER(FEIL_I_SØKER);/*R0011903*/         
                  END;                                                          
               WHEN('R192')                                                     
                  DO;                                                           
                     CALL BLANK_AFP_MELDNR;               /* R0011902 */        
                     CALL BLANK_AFP_SPES;                 /* R0011902 */        
                     CALL OVERFØR_AFP_SØKER;              /* R0011909 */        
                     CALL KONTROLL_AFP_SØKER(FEIL_I_SØKER);/*R0011903*/         
                  END;                                                          
               WHEN('R193' , 'R194' )                                           
                  DO;                                                           
                     CALL BLANK_AFP_MELDNR;               /* R0011902 */        
                     CALL OVERFØR_AFP_SØKER;              /* R0011909 */        
                     CALL OVERFØR_AFP_SPES;               /* R0011909 */        
                     CALL KONTROLL_AFP_SØKER(FEIL_I_SØKER);/*R0011903*/         
                     IF AFP.NORSK_BOSATT = 'J' THEN                             
                        CALL BLANK_AFP_SPES;              /* R0011902 */        
                     ELSE                                                       
                        CALL KONTROLL_AFP_SPES(FEIL_I_SPES);/*R0011905*/        
                                                                                
                  END;                                                          
               OTHERWISE                                                        
                  ;                                                             
 /* INGEN UTGANG DA CICS IKKE GODTAR UGYLDIGE TRANSAKSJONSKODER */              
            END;                                                                
                                                                                
            IF FEIL_I_SØKER = '0'B &                                            
               FEIL_I_SPES  = '0'B THEN                                         
               FEIL_FUNNET = '0'B;                                              
            ELSE                                                                
               FEIL_FUNNET = '1'B;                                              
                                                                                
                                                                                
               IF AFP.NORSK_BOSATT = 'N' & ^FEIL_FUNNET THEN                    
                  TRANSKODE        = 'R193';                                    
                                                                                
            IF ( TRANSKODE = 'R192' !                                           
             TRANSKODE = 'R193' ! TRANSKODE =  'R194') &                        
             AFP.NORSK_BOSATT = 'J' THEN                                        
               TRANSKODE = 'R191';                                              
                                                                                
            ELSE                                                                
            IF (TRANSKODE = 'R193' ! TRANSKODE = 'R194') &                      
                AFP.NORSK_BOSATT ='J'  THEN                                     
              TRANSKODE = 'R192';                                               
                                                                                
            ELSE                                                                
            IF (TRANSKODE = 'R192' ! TRANSKODE = 'R194') &                      
               AFP.NORSK_BOSATT = 'N'   THEN                                    
                TRANSKODE = 'R193';                                             
                                                                                
            ELSE                                                                
            IF (TRANSKODE = 'R192' ! TRANSKODE = 'R193') &                      
                AFP.NORSK_BOSATT ='N'         THEN                              
                TRANSKODE = 'R194';                                             
                                                                                
            IF FEIL_FUNNET ! HENT_FRAM_MAP ! FRA_UTEN_DIALOG !                  
            TRANS_RETURKODE ^= TRANSKODE THEN                                   
                                                                                
               DO;                                                              
                  IF TRANSKODE = 'R190' THEN                                    
                     TRANSKODE = 'R191';                                        
                  CALL BLANK_AFP_MAP;                     /* R0011909 */        
                  CALL OVERFØR_AFPSØKER_MAP;             /* R0011906 */         
                  SELECT (TRANSKODE);                                           
                     WHEN ('R193' , 'R194')                                     
                        DO;                                                     
                           CALL OVERFØR_AFPSPES_MAP;      /* R0011906 */        
                        END;                                                    
                     OTHERWISE                                                  
                        ;                                                       
                  END;                                                          
                                                                                
                IF HENT_FRAM_MAP THEN                                           
                   EXEC CICS SEND MAP('S001191') MAPSET('S001193')              
                                                        ERASE CURSOR;           
                ELSE                                                            
                   EXEC CICS SEND MAP('S001191') MAPSET('S001193')              
                                            DATAONLY ERASEAUP CURSOR;           
                FEIL_MELD_NR  = 666;/* => FEIL_FUNNET ER SATT. */               
                HENT_FRAM_MAP = '0'B;                                           
                                                                                
                IF  FRA_CICS THEN                                               
                    EXEC CICS RETURN TRANSID(TRANSKODE)                         
                                                  COMMAREA(KOM_OMR);            
                ELSE                                                            
                    GO TO RECEIVE_MAP;                                          
               END;                                                             
                                                                                
            ELSE                                                                
              DO;                                                               
                                                                                
                IF TRANSKODE = 'R190' THEN                                      
                  TRANS_RETURKODE = 'R191';                                     
                ELSE                                                            
                  TRANS_RETURKODE = TRANSKODE;                                  
                IF  FRA_CICS  THEN                                              
                  DO;                                                           
                    BLANKETTYPE = STYREKODE;                                    
                    TRANSKODE = 'R042';                                         
                  END;                                                          
              END;                                                              
                                                                                
  END;                                                                          
                                                                                
  CALL UTGANG;                                                                  
                                                                                
  UTGANG: PROC;                                                                 
    IF  FRA_CICS THEN                                                           
      EXEC CICS XCTL PROGRAM('R0010401') COMMAREA(KOM_OMR);                     
    ELSE                                                                        
      EXEC CICS RETURN;                                                         
  END UTGANG;                                                                   
                                                                                
                                                                                
  FEILBEH:                                                                      
                                                                                
     EXEC CICS HANDLE CONDITION ERROR(ABEND);                                   
     IF FEIL_MELD_NR = 0 THEN /* AB 01.08.16 */                                 
        PROGRAM_ID = 'R0011901';                                                
                                                                                
     S001191O.MELDING2O =                                                       
              'F E I L  H A R  O P P S T Å T T ! ! !.';                         
                                                                                
     S001191O.MELDING3O =                                                       
              'EIBRCODE : ' !! UNSPEC(EIBRCODE);                                
                                                                                
     S001191O.MELDING4O =                                                       
       'EIBFN    : ' !! UNSPEC(EIBFN) !! '. FEILKODE : ' !! FEILKODE !!         
                                            '. DATASETT : ' !! DSNAVN;          
                                                                                
     S001191O.MELDING5O =                                                       
                   'PROGRAMNAVN :  ' !! PROGRAM_ID    !!                        
                   '.INGEN OPPDATERING ER BLITT UTFØRT.';                       
                                                                                
     S001191O.MELDING6O =                                                       
                   'TA UTSKRIFT!!!. TRYKK DERETTER "ENTER".';                   
                                                                                
                                                                                
     EXEC CICS SEND MAP('S001191')                                              
                                                 MAPSET('S001193');             
                                                                                
     EXEC CICS RECEIVE MAP('S001191')                                           
                                    MAPSET('S001193') SET(BMSMAPBR);            
                                                                                
     CALL MAP_SPESIAL_INFO_PROT;                                                
                                                                                
     EXEC CICS SYNCPOINT ROLLBACK;                                              
                                                                                
                                                                                
     TERMINERINGS_IND = 'F';                                                    
                                                                                
     TRANSKODE  = 'R041';                                                       
                                                                                
     CALL UTGANG;                                                               
                                                                                
  ABEND:                                                                        
     EXEC CICS ABEND ABCODE(FEILKODE);                                          
                                                                                
  PF1:                                                                          
                                                                                
 /* LEGGER UNDERKJENT TRANS UT PÅ VENTEDATABASE HVIS GYLDIG     */              
 /* FNR OG MIRKNINGSDATO,ELLERS FORKASTES TRANSAKSJONEN.        */              
                                                                                
     IF ^(AFP.FNRNR = 1          !                                              
          AFP.FNRNR = 200        !                                              
          AFP.VIRK_DATO_ÅMDNR = 9 !                                             
          AFP.VIRK_DATO_ÅMDNR = 200) THEN                                       
        DO;                                                                     
                                                                                
           IF FØDSNUMMER    = 0   THEN                                          
                                                                                
 /* SKRIVER MELDING OM AT TRANSAKSJONEN ER FORKASTET.           */              
                                                                                
              DO;                                                               
                 FEIL_MELD_NR  = 0;                                             
                 S001191O.MELDING6O =                                           
                         'REGISTERINGEN ER IKKE AVSLUTTET MED "ENTER"'!!        
                         '.TRANSAKSJONEN ER FORKASTET.';                        
              END;                                                              
           ELSE                                                                 
              DO;                                                               
                                                                                
                 SEARCH_FNR  = TRANS_OPPL_OMR.FØDSNUMMER;                       
                 IF FRA_CICS THEN                                               
                    BLANKETTYPE = STYREKODE;                                    
                                                                                
                                                                                
                 IF FEIL_MELD_NR = 666 &      /*BLANKETTKONTROLLFEIL*/          
                    ^FRA_MED_DIALOG     THEN  /*IKKE BASEKONTROLLFEIL*/         
                    FUNKSJONSKODE = 'B';                                        
                                                                                
                 CALL BLANK_AFP_MELDNR;                  /* R0011902 */         
                                                                                
                                                                                
                 IF AFP.NORSK_BOSATT = 'N' THEN                                 
                    CALL OVERFØR_AFPSPES_MAP;            /* R0011906 */         
                                                                                
                 EXEC CICS LINK PROGRAM('R0010490') COMMAREA(KOM_OMR);          
                                                                                
 /* SKRIVER MELDING OM AT TRANSAKSJONEN ER LAGT UT PÅ VENTEBASE.*/              
                                                                                
                 S001191O.MELDING6O =                                           
                         'TRANSAKSJONEN ER LAGT UT PÅ VENTEREGISTER.';          
              END;                                                              
                                                                                
                                                                                
           EXEC CICS SEND MAP('S001191') MAPSET('S001193')                      
                                                           DATAONLY;            
     /* DUMMY SEND-KALL.                              */                        
                                                                                
           EXEC CICS SEND MAP('S001191')                                        
                                                  MAPSET('S001193');            
           EXEC CICS DELAY INTERVAL(005);                                       
                                                                                
           TERMINERINGS_IND = 'F';                                              
                                                                                
           TRANSKODE  = 'R041';                                                 
                                                                                
           CALL UTGANG;                                                         
                                                                                
        END;                                                                    
     ELSE                                                                       
        DO;                                                                     
           TERMINERINGS_IND = 'F';                                              
                                                                                
           TRANSKODE = 'R041';                                                  
                                                                                
           S001191O.MELDING6O =                                                 
                                                                                
   'NB** TRANSAKSJONEN ER FORKASTET P.G.A. UGYLDIG FNR EL. VDATO **NB';         
                                                                                
           EXEC CICS SEND MAP('S001191') MAPSET('S001193')                      
                                                           DATAONLY;            
     /* DUMMY SEND-KALL.                            */                          
                                                                                
           EXEC CICS SEND MAP('S001191')                                        
                                    MAPSET('S001193');                          
                                                                                
           EXEC CICS DELAY INTERVAL(005);                                       
           CALL UTGANG;                                                         
        END;                                                                    
                                                                                
                                                                                
  PF2:                                                                          
     TERMINERINGS_IND   = 'F';                                                  
                                                                                
     S001191O.MELDING6O =                                                       
                                                                                
      'NBNBNBNBNB***    TRANSAKSJONEN ER FORKASTET.    ***NBNBNBNBNB';          
                                                                                
     EXEC CICS SEND MAP('S001191') MAPSET('S001193')                            
                                                      DATAONLY ;                
     /* DUMMY SEND-KALL.                                     */                 
                                                                                
     EXEC CICS SEND MAP('S001191')                                              
                                    MAPSET('S001193');                          
     EXEC CICS DELAY INTERVAL(005);                                             
                                                                                
                                                                                
     TRANSKODE = 'R041';                                                        
     CALL UTGANG;                                                               
                                                                                
                                                                                
  MAP_SPESIAL_INFO_PROT:                                                        
               PROC;                                                            
     /*----------------------------------------------*/                         
     /* SPESIAL OPPLYSNINGER FELTER ER BLOKET        */                         
     /*----------------------------------------------*/                         
     /*----------------------------------------------*/                         
      S001191O.TT_F67A       =  DFHBMPRO;                                       
      S001191O.TT_E66_ÅA     =  DFHBMPRO;                                       
      S001191O.TT_E66_MA     =  DFHBMPRO;                                       
      S001191O.STATSBORGERA  =  DFHBMPRO;                                       
      S001191O.BOSATTA       =  DFHBMPRO;                                       
      S001191O.FRAVIK_FLYKTA =  DFHBMPRO;                                       
      S001191O.OVERKOMP_UTLA =  DFHBMPRO;                                       
      S001191O.FRAVIK_19_3A  =  DFHBMPRO;                                       
                                                                                
  END MAP_SPESIAL_INFO_PROT;                                                    
 %PAGE;                                                                         
      %INCLUDE R0011902;               /* BLANK  ARBEIDSOMRÅDE      */          
 %PAGE;                                                                         
      %INCLUDE R0011903;               /* KONTROLL AV SØKERDELEN    */          
 %PAGE;                                                                         
      %INCLUDE R0011905;               /* KONTROLL AV SPESIELLE     */          
                                       /* OPPLYSNINGER              */          
 %PAGE;                                                                         
      %INCLUDE R0011906;               /* ARBEIDSOMRÅDE -> MAP      */          
 %PAGE;                                                                         
      %INCLUDE R0011908;               /* BEREGN TT AP              */          
 %PAGE;                                                                         
      %INCLUDE R0011909;               /* MAP -> ARBEIDSOMRÅDE      */          
 %PAGE;                                                                         
      %INCLUDE R0019901;               /* TESTER OM DATO ER GYLDIG  */          
 %PAGE;                                                                         
      %INCLUDE R0019902;               /* FINNE KJØNN               */          
 %PAGE;                                                                         
      %INCLUDE R0019903;               /* EKTEFELLES ALDER GYLDIG   */          
 %PAGE;                                                                         
      %INCLUDE R0019904;               /* FØDSELSNUMMERKONTROLL     */          
 %PAGE;                                                                         
      %INCLUDE R0019905;               /* BEREGNER ALDER            */          
 %PAGE;                                                                         
      %INCLUDE R0019910;               /* NUMERISK KONTROLL         */          
                                                                                
   /* %INCLUDE R0019911;                  DATO KONTROLL  DATO       */          
 %PAGE;                                                                         
      %INCLUDE R0019912;               /* KONVERTERING CHAR ==> PIC */          
                                       /* PIC ==> CHAR              */          
 %PAGE;                                                                         
      %INCLUDE R0019983;              /* KONV_ÅMD_HÅMD  2000       */           
 %PAGE;                                                                         
      %INCLUDE R0019984;              /* KONV_MÅ_HÅMD  2000        */           
 %PAGE;                                                                         
      %INCLUDE R0019987;              /* KONV_HÅMD_ÅM  2000        */           
 %PAGE;                                                                         
      %INCLUDE R0019988;              /* KONV_HÅMD_ÅMD 2000        */           
 %PAGE;                                                                         
      %INCLUDE R0019989;               /* KONV_HÅMD_MÅ  2000        */          
 %PAGE;                                                                         
      %INCLUDE R0019981;               /* KONV_ÅR_HÅR   2000        */          
 %PAGE;                                                                         
      %INCLUDE R0019944;               /* SKRIV_FEIL      */                    
 %PAGE;                                                                         
      %INCLUDE R0019964;               /* DAGENS DATO PLUSS 6 MDR   */          
      %INCLUDE R0019995;               /* KONV-FNR11-FNR13          */          
      %INCLUDE R0019945;               /* ATK_TILLGANG_KONTROLL   */            
      %INCLUDE R0019999;               /* KONTROLL_ACF2           */            
   END R00119;                                                                  
