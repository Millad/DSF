 /*   SIST ENDRET PÅ PROD   2008.04.03  8.06.31 AV   JDA2990          */        
 /*   SIST ENDRET PÅ PROD   2006.08.16  8.11.49 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2006.07.14 11.06.43 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2003.03.25  8.35.25 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.09.13 11.56.41 AV   HLA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.02.07 12.57.36 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.10.16 13.20.59 AV   HLA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.07.26 12.35.53 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   1999.12.15 11.51.42 AV   JDA7339          */        
 /*       SIST ENDRET 06/05-99 13.56.09 AV   JDA7339                  */        
 /*       SIST ENDRET 06/05-99 13.55.19 AV   JDA7339                  */        
 /*       SIST ENDRET 03/03-99 14.13.12 AV   JDA7339                  */        
 /*       SIST ENDRET 03/03-99 14.11.10 AV   JDA7339                  */        
 /*                                                                   */        
 /*IDENTIFIKASJON:                                                    */        
 /*    R0014171 - SUBRUTINE I PLI                                     */        
 /*    PROGRAMMERER: PER F. BERGESTAD, JUNI 1982                      */        
 /*HENSIKT:                                                           */        
 /*    RUTINEN REGNER UT BUP/FPP.                                     */        
 /*BRUK   :                                                           */        
 /*    CALL REGN_BUP_FPP(INNT_START_ÅR,IND)                           */        
 /*ENDRET I FORB MED NY LOV I 1992 - HL                               */        
 /*                                                                   */        
 /*                                                                   */        
 /* ***************************************************************** */        
 REGN_BUP_FPP: PROC(INNT_START_ÅR,INDEKS);                                      
    DCL /* PARAMETER INN */                                                     
       INDEKS                   FIXED BIN(15),                                  
       INNT_START_ÅR            PIC'9999';                                      
    DCL /* BUILTIN'S SOM BENYTTES KUN HER */                                    
       (                                                                        
        HBOUND,                                                                 
        MAX,                                                                    
        MIN,                                                                    
        SUM                                                                     
       )                        BUILTIN;                                        
    DCL /* TELLERE */                                                           
       (                                                                        
       I,    /* OK */                                                           
       J,    /* OK */                                                           
       K,    /* OK */                                                           
       L,                                                                       
       M,    /* OK */                                                           
       N,    /* OK */                                                           
       X     /* OK */                                                           
       )                        FIXED BIN(15)  INIT(0);                         
    DCL /* TABELLER */                                                          
       (                                                                        
        POENG_TAB,                                                              
        T_POENG_TAB,                                                            
        V_POENG_TAB,                                                            
        O_POENG_TAB,                                                            
        X_POENG_TAB,                                                            
        W_POENG_TAB,                                                            
        Z_POENG_TAB                                                             
       )                       (1963:2060) FIXED DEC(5,3),                      
       (                                                                        
        Y_PI_POENG,                                                             
        X_STØRSTE_P,                                                            
        STØRSTE_POENG,                                                          
        W_STØRSTE_P                                                             
       )                       (60)        FIXED DEC (5,3),                     
       (                                                                        
        W_STØRSTE_POENG,                                                        
        OMSÅR_HALVPARTEN,                                                       
        W_OMSÅR_HALVPARTEN                                                      
       )                       (25)        FIXED DEC (5,3),                     
       (                                                                        
        STØRSTE_POENG_ALLE                                                      
       )                       (3)         FIXED DEC (5,3) INIT (0);            
                                                                                
    DCL                                                                         
       TIDLIGSTE_ÅR                 PIC'9999',            /*5.9.85*/            
       SLUTT_ÅR                 PIC'9999'        INIT(0),                       
       ANTALL_ÅR                PIC'999'        INIT(0);                        
                                                                                
    DCL HALVPARTEN               PIC'999'        INIT(0);                       
                                                                                
    DCL                                                                         
       POENG_SISTE_3ÅR          FIXED DEC(5,3) INIT(0),                         
       POENG_HALVPARTEN         FIXED DEC(5,3) INIT(0);                         
    DCL                                                                         
       RUNDING                  FIXED DEC(5,3) INIT(0.005);                     
    DCL RESULTAT                FIXED DEC(3,2) INIT(0);        /*HL*/           
                                                                                
   /*-----DCL AV FELTER FOR BEREGNING AV BUP VED OMSORGSPOENG  */               
                                                                                
  DCL  W1_RESULTAT        FIXED DEC(3,2) INIT (0);                              
  DCL  W_RESULTAT_TRE     FIXED DEC(3,2) INIT (0);                              
  DCL  W_RESULTAT_20      FIXED DEC(3,2) INIT (0);                              
                                                                                
  DCL A                  FIXED DEC (5) INIT (0);                                
  DCL B                  FIXED DEC (5) INIT (0);                                
  DCL T                  FIXED DEC (5) INIT (0);                                
  DCL Z                  FIXED DEC (5) INIT (0);                                
  DCL S                  FIXED DEC (5) INIT (1);                                
  DCL JJ                 FIXED DEC (5) INIT (0);                                
        /* ENDRA FRA 5,3 DA 20*7 = 140 KAN VI FÅ OVERFLOW */                    
    DCL VANLIG        FIXED DEC (7,3) INIT (0);                                 
    DCL PI_BUP        FIXED DEC (5,3) INIT (0);                                 
    DCL PI_BUP_SNITT  FIXED DEC (5,3) INIT (0);                                 
    DCL HJELP1             FIXED DEC (5,3)INIT (0);                             
    DCL SNITT_BLANDET      FIXED DEC (5,3) INIT (0);                            
    DCL TRE_BESTE_OMS      FIXED DEC (5,3) INIT (0);                            
    DCL TRE_OMS_SNITT      FIXED DEC (5,3) INIT (0);                            
    DCL TABELL_SLUTT_ÅR    FIXED DEC (5)   INIT (0);                            
    DCL SLUTT_OMS_TRE      FIXED DEC (5)   INIT (0);                            
    DCL W_TABELL_SLUTT_ÅR  FIXED DEC (5)   INIT (0);                            
    DCL TRE_SISTE_O          FIXED DEC (5) INIT (0);                            
    DCL LAVESTE_OMS_ÅR       FIXED DEC (5) INIT (0);                            
                                                                                
                                                                                
    DCL  W_ANTALL_ÅR           FIXED DEC (3) INIT (0);                          
    DCL  OMS_ÅR_PI             FIXED DEC (5) INIT (0);                          
    DCL  BESTE_HALVPARTEN      FIXED DEC (5,3) INIT (0);                        
                                                                                
   /*-----DCL AV FELTER FOR BEREGNING AV BUP VED OMSORGSPOENG  HIT */           
    /* ============================================================== */        
    IF FEIL_MELD_NR = 0 THEN /* AB 01.08.16 */                                  
        PROGRAM_ID = 'R0014171';                                                
                                                                                
    POENG_TAB(*) = 0;           /*HL 0796*/                                     
    O_POENG_TAB(*) = 0;                                                         
                                                                                
 /* ENDRING 5.9.85 HL - SE FEILRAPPORT 234 -                  */                
                                                                                
    IF INNT_START_ÅR < 1967            THEN                                     
                                                                                
       DO;                                                                      
                                                                                
          TIDLIGSTE_ÅR = INDEKS - 4;                                            
          IF TIDLIGSTE_ÅR > 1967       THEN                                     
             TIDLIGSTE_ÅR = 1967;                                               
                                                                                
          IF INNT_START_ÅR < TIDLIGSTE_ÅR       THEN                            
             INNT_START_ÅR = TIDLIGSTE_ÅR;                                      
                                                                                
       END;                                                                     
                                                                                
  SLUTT_ÅR = INDEKS - 1;                                                        
       IF VIRK_LOV92_ÅM < 199200        THEN                                    
     DO;                                                                        
        DO I = INNT_START_ÅR TO SLUTT_ÅR;                                       
           POENG_TAB(I) = TAB.POENG_ANVENDT(I);                                 
           O_POENG_TAB(I) = TAB.POENG_ANVENDT(I);                               
        END;                                                                    
        CALL UTREGNING;                                                         
        TAB.BUP_FPP(INDEKS) = RESULTAT;                                         
        BUP_FPP92(INDEKS)   = RESULTAT;         /*9807*/                        
     END;                                                                       
  ELSE                                                                          
     IF INDEKS < 1992               THEN                                        
     DO;                                                                        
        DO I = INNT_START_ÅR TO SLUTT_ÅR;                                       
           POENG_TAB(I) = TAB.POENG_ANVENDT(I);                                 
           O_POENG_TAB(I) = TAB.POENG_ANVENDT(I);                               
        END;                                                                    
        CALL UTREGNING;                                                         
        TAB.BUP_FPP(INDEKS) = RESULTAT;                                         
        IF RESULTAT > 5.00          THEN                                        
           DO;                                                                  
              DO I = INNT_START_ÅR TO SLUTT_ÅR;                                 
                 POENG_TAB(I) = TAB.POENG_ANVENDT_LOV92(I);                     
                 O_POENG_TAB(I) = TAB.POENG_ANVENDT_LOV92(I);                   
              END;                                                              
              CALL UTREGNING;                                                   
              IF RESULTAT > 5.00       THEN                                     
                 BUP_FPP92(INDEKS) = RESULTAT;      /*9807*/                    
              ELSE                                                              
                 BUP_FPP92(INDEKS) = 5.00;             /*9807*/                 
           END;                                                                 
        ELSE                                                                    
           BUP_FPP92(INDEKS) = RESULTAT;            /*9807*/                    
     END;                                                                       
  ELSE                                                                          
     DO;                                                                        
        DO I = INNT_START_ÅR TO SLUTT_ÅR;                                       
   /*      IF I < UNG_UFØR_START_ÅR      THEN                                   
              TAB.POENG_ANVENDT_LOV92(I) =                                      
              TAB.INNT_POENG_LOV92(I) + 0.005;      9807*/                      
           POENG_TAB(I) = TAB.POENG_ANVENDT_LOV92(I);                           
           O_POENG_TAB(I) = TAB.POENG_ANVENDT_LOV92(I);                         
        END;                                                                    
        CALL UTREGNING;                                                         
        TAB.BUP_FPP(INDEKS) = RESULTAT;                                         
        BUP_FPP92(INDEKS)   = RESULTAT;              /*9807*/                   
     END;                                                                       
 IF TAB.UFØR_PERIODE (INDEKS) ^= 'D'          THEN                              
    BUP92_UFØR = BUP_FPP92(INDEKS);                   /*9807*/                  
                                                                                
 /* ----------------------------------------------------------------- */        
 /* INTERNE PROC STARTER HER :                                        */        
    /* -------------------------------------------------------------- */        
    UTREGNING : PROC;                                                           
       DCL INT_KODE  CHAR (1) INIT(' ');                                        
                                                                                
       ANTALL_ÅR  = SLUTT_ÅR - INNT_START_ÅR + 1;                               
   /*FJERNET BEGRENSNING PÅ 20 ÅR - HL 200803*/                                 
       HALVPARTEN = ((ANTALL_ÅR / 2) + 0.5);                                    
       IF ANTALL_ÅR > 6 THEN                                                    
          /* REGNER UT GJ.SNITTSPOENGET FOR DE 3 SISTE ÅRENE */                 
          POENG_SISTE_3ÅR = (POENG_TAB(SLUTT_ÅR - 2) +                          
                             POENG_TAB(SLUTT_ÅR - 1) +                          
                             POENG_TAB(SLUTT_ÅR)) / 3 + RUNDING;                
       STØRSTE_POENG = 0;                                                       
       DO I = 1 TO HALVPARTEN;                                                  
          /* PLUKKER UT HALVPARTEN AV DE BESTE POENGENE */                      
          DO J = INNT_START_ÅR TO SLUTT_ÅR;                                     
             IF POENG_TAB(J) > STØRSTE_POENG(I) THEN                            
                DO;                                                             
                   STØRSTE_POENG(I) = POENG_TAB(J);                             
                   K = J;                                                       
                END;                                                            
          END;                                                                  
          IF K > 0 THEN    /*0007 - HL*/                                        
             POENG_TAB(K) = 0;                                                  
       END;                                                                     
                                                                                
       IF HALVPARTEN > 0      THEN          /*9905*/                            
          POENG_HALVPARTEN = SUM(STØRSTE_POENG) / HALVPARTEN + RUNDING;         
                                                                                
       RESULTAT = MAX(POENG_SISTE_3ÅR,POENG_HALVPARTEN);                        
                                                                                
       /* ER DET OMSORGPOENG FOR SMÅ BARN ? */                                  
       /* REGLENE GJELDER ALLE TYPER OMSORGSPOENG - IKKE BARE FOR BARN*/        
       /* PROGRAMMET OMARBEIDET I NOV 97 HL*/                                   
       DO M = SLUTT_ÅR TO 1992 BY -1;                                           
          IF (VERIFY(B01.PI_KODE(IND,M),'JKLM') = 0) THEN                       
             DO;   /*KODE M ER OMSORG OG FORELØPIG INNT. */                     
                TAB.OMSORG_KODE (M) = 'J';                                      
                INT_KODE = 'J';                                                 
             END;                                                               
       END;                                                                     
       IF INT_KODE = 'J' THEN                                                   
          CALL REGN_OMS;                                                        
       IF W1_RESULTAT > RESULTAT  THEN                                          
          RESULTAT = W1_RESULTAT;                                               
    END UTREGNING;                                                              
    /* -------------------------------------------------------------- */        
    REGN_OMS : PROC;                                                            
       /* BEREGNING AV BUP MED OMSORGSPOENG */                                  
      /* VI MÅ UNDERSØKE OM FOREGÅENDE ÅR ER OMSORGSÅR        */                
      /* DERSOM DETTE ER SANT,MÅ VI GÅ LENGER TILBAKE         */                
      /* ELLERS ER SLUTT ÅR SATT. BEHØVER IKKE ????           */                
      /* PGA TEST TIDLIGERE. OMSÅR I TRE SISTE ÅR  ????       */                
      DO I = 1 TO HBOUND(Y_PI_POENG,1);                                         
          Y_PI_POENG(I)  = 0;                                                   
          W_STØRSTE_P(I) = 0;                                                   
      END;                                                                      
      DO I = INNT_START_ÅR TO SLUTT_ÅR;                                         
         IF (TAB.OMSORG_KODE(I) ^= 'J') THEN    /*HL*/                          
            W_ANTALL_ÅR  = W_ANTALL_ÅR + 1;                                     
      END;                                                                      
                                                                                
      DO I = INNT_START_ÅR TO SLUTT_ÅR;                                         
         T_POENG_TAB(I) = O_POENG_TAB(I);                                       
         V_POENG_TAB(I) = O_POENG_TAB(I);                                       
         X_POENG_TAB(I) = O_POENG_TAB(I);                                       
         W_POENG_TAB(I) = O_POENG_TAB(I);                                       
         Z_POENG_TAB(I) = O_POENG_TAB(I);                                       
      END; /* I = INNT_START_ÅR TO SLUTT_ÅR */                                  
                                                                                
      DO A = SLUTT_ÅR TO INNT_START_ÅR BY -1;                                   
         IF (TAB.OMSORG_KODE(A)     ^= 'J' &           /*HL*/                   
             TAB.OMSORG_KODE(A - 1) ^= 'J' ) THEN      /*HL*/                   
            SLUTT_OMS_TRE  =  A - 1;                                            
         IF (SLUTT_OMS_TRE > 0) THEN                                            
            LEAVE;                                                              
      END; /* A = SLUTT_ÅR TO INNT_START_ÅR BY -1 */                            
                                                                                
      IF (SLUTT_OMS_TRE < INNT_START_ÅR) THEN                                   
          SLUTT_OMS_TRE = INNT_START_ÅR;                                        
                                                    /*HL: */                    
      IF (TAB.OMSORG_KODE(SLUTT_ÅR)     = 'J' !                                 
          TAB.OMSORG_KODE(SLUTT_ÅR - 1) = 'J' !                                 
          TAB.OMSORG_KODE(SLUTT_ÅR - 2) = 'J' ) THEN                            
         DO;                                                                    
            IF (TAB.OMSORG_KODE(SLUTT_ÅR)     = 'J') THEN                       
               TRE_SISTE_O = TRE_SISTE_O + 1;                                   
            IF (TAB.OMSORG_KODE(SLUTT_ÅR - 1) = 'J') THEN                       
               TRE_SISTE_O = TRE_SISTE_O + 1;                                   
            IF TAB.OMSORG_KODE(SLUTT_ÅR - 2) = 'J' THEN                         
               TRE_SISTE_O = TRE_SISTE_O + 1;                                   
            W_TABELL_SLUTT_ÅR = SLUTT_ÅR - (TRE_SISTE_O     + 3);               
     /*   CALL TRE_SISTE_OMS;          */                                       
            CALL OMSORG;                                                        
         END;                                                                   
                                                                                
    CALL TJUE_BESTE_OMS;                                                        
                                                                                
    /* -------------------------------------------------------------- */        
    /* HER KOMMER INTERNE PROC I PROC REGN OMS..                      */        
    /* -------------------------------------------------------------- */        
                                                                                
    OMSORG: PROC;                                                               
       DCL                                                                      
          FØRSTE_OMÅR   FIXED BIN(15) INIT (0),                                 
          TELLER        FIXED BIN(15) INIT (0),                                 
          ALT(4)        FIXED DEC(7,4),                                         
          BESTE_ALTERNATIV FIXED DEC(7,4),                                      
          OMPOENG(3)    FIXED DEC(5,4);                                         
                                                                                
       OMPOENG(*) = 0;                                                          
       ALT    (*) = 0;                                                          
       /* FINN TRE BESTE ÅR MED OMSORGSPOENG, SORTER DEM MED HØYESTE */         
       /* POENG PÅ FØRSTE PLASS                                       */        
       /* VI GÅR IKKE LENGRE TILBAKE ENN SISTE TRE ÅR UTEN OMSORGSP.  */        
       DO I = SLUTT_ÅR TO 1989 BY -1 WHILE(TELLER < 4);                         
          IF TAB.OMSORG_KODE(I) ^= 'J' THEN                                     
             TELLER = TELLER + 1;                                               
          ELSE                                                                  
          DO;                                                                   
             IF OMPOENG(1) < O_POENG_TAB(I) THEN                                
                DO;                                                             
                   IF TELLER = 0             THEN   /*200010*/                  
                      OMPOENG(3) = OMPOENG(2);                                  
                   IF TELLER < 2             THEN                               
                      OMPOENG(2) = OMPOENG(1);                                  
                   OMPOENG(1) = O_POENG_TAB(I);                                 
                END;                                                            
             ELSE                                                               
             IF OMPOENG(2) < O_POENG_TAB(I) THEN                                
                DO;                                                             
                   IF TELLER = 0             THEN                               
                      OMPOENG(3) = OMPOENG(2);                                  
                   IF TELLER < 2             THEN                               
                      OMPOENG(2) = O_POENG_TAB(I);                              
                END;                                                            
             ELSE                                                               
             IF OMPOENG(3) < O_POENG_TAB(I) THEN                                
                DO;                                                             
                   IF TELLER = 0             THEN                               
                      OMPOENG(3) = O_POENG_TAB(I);                              
                END;                                                            
          END;                                                                  
       END; /*DO I = SLUTT_ÅR TO 89*/                                           
                                                                                
    /*LAG INNTIL FIRE ALTERNATIVER */                                           
                                                                                
       TELLER = 3;                                                              
       ALT(4) = OMPOENG(1) + OMPOENG(2) + OMPOENG(3);                           
       DO I = SLUTT_ÅR TO 1989 BY -1 WHILE(TELLER > 0);                         
          IF TAB.OMSORG_KODE(I) ^= 'J'     THEN                                 
          DO;                                                                   
             OMPOENG(TELLER) = O_POENG_TAB(I);                                  
             ALT(TELLER) = OMPOENG(1) + OMPOENG(2) + OMPOENG(3);                
             TELLER = TELLER - 1;                                               
          END;                                                                  
       END;                                                                     
                                                                                
       BESTE_ALTERNATIV = MAX(ALT(1),ALT(2),ALT(3),ALT(4));                     
       W_RESULTAT_TRE   = BESTE_ALTERNATIV / 3 + RUNDING;                       
    END OMSORG;                                                                 
    /* -------------------------------------------------------------- */        
    /* -------------------------------------------------------------- */        
    TRE_SISTE_OMS : PROC;                                                       
       /*1.FINNE GJENNOMSNITT AV DE TRE SISTE ÅR MED PI:       */               
       /*DETTE RESULTAT FINNES I 4171 .                        */               
       DO N = 1 TO 3;                                                           
          STØRSTE_POENG_ALLE (N) = 0;                                           
       END;                                                                     
                                                                                
       /* FINNE SISTE ÅR UTEN OMSORGSPOENG                     */               
                                                                                
       /* VI MÅ FINNE GJENNOMSNITT AV TRE SISTE ÅR UTEN OMSORGSPOENG*/          
       /* DETTE TAS IKKE HENSYN TIL I 4171                          */          
       JJ = 0;                                                                  
       DO N = SLUTT_ÅR TO INNT_START_ÅR BY -1;                                  
            IF (TAB.OMSORG_KODE(N) ^= 'J') THEN                                 
               DO;                                                              
       /*          PI_BUP         = PI_BUP + TAB.POENG_ANVENDT(N)               
                   Z_POENG_TAB(N) = TAB.POENG_ANVENDT(N)      */                
                   PI_BUP         = PI_BUP + O_POENG_TAB(N);                    
                   Z_POENG_TAB(N) = O_POENG_TAB(N);                             
                   JJ             = JJ + 1;                                     
               END;                                                             
            IF   JJ = 3 THEN LEAVE;                                             
       END;                                                                     
                                                                                
       PI_BUP_SNITT  = PI_BUP / 3 + 0.005;                                      
                                                                                
       /* FINNE GJENNOMSNITT AV 'TRE SISTE' HVOR DET ER OPPTJENT*/              
       /* OMSORGSPOENG FOR SMÅ BARN.                            */              
       /* MÅ FINNE LAVESTE ÅR MED OMSORGSPOENG */                               
       DO N = 1992 TO SLUTT_ÅR ;                                                
          IF (TAB.OMSORG_KODE(N) = 'J') THEN                                    
             DO;                                                                
                LAVESTE_OMS_ÅR  = N;                                            
             END;                                                               
           IF  LAVESTE_OMS_ÅR  > 0 THEN                                         
             LEAVE;                                                             
        END;                                                                    
                                                                                
        DO N = 1992 TO SLUTT_ÅR ;                                               
           IF (TAB.OMSORG_KODE(N) = 'J' &                                       
 /*            TAB.POENG_ANVENDT(N)    > 0 THEN       */                        
               O_POENG_TAB(N)          > 0) THEN                                
              OMS_ÅR_PI  = OMS_ÅR_PI + 1;                                       
        END;                                                                    
                                                                                
   IF   OMS_ÅR_PI > 2 THEN                                                      
     DO;                                                                        
        DO   M = 1 TO 3;                                                        
          DO   N = SLUTT_ÅR TO SLUTT_OMS_TRE BY -1;                             
             IF   TAB.OMSORG_KODE(N) = 'J' &                                    
                  W_POENG_TAB(N) > 0 THEN                                       
               DO;                                                              
                 IF W_POENG_TAB(N) > STØRSTE_POENG_ALLE(M) THEN                 
                 DO;                                                            
                   STØRSTE_POENG_ALLE(M) = W_POENG_TAB(N);                      
                   K = N;                                                       
                 END;                                                           
               END;                                                             
          END;                                                                  
         TRE_BESTE_OMS = TRE_BESTE_OMS + W_POENG_TAB(K);                        
         W_POENG_TAB(K) = 0;                                                    
        END;                                                                    
                                                                                
       TRE_OMS_SNITT = TRE_BESTE_OMS / 3 + 0.005;                               
     END;                                                                       
                                                                                
                                                                                
   /* PLUKKE UT BESTE POENG I DENNE REKKEN MED OMSORGSPOENG   */                
   /* ÅR UTEN OMSORGSPOENG MÅ VÆRE MED -KAN IKKE HOPPES OVER  */                
   /* ELLERS KAN VI VELGE BESTE ÅR                            */                
                                                                                
      DO   N =  LAVESTE_OMS_ÅR TO SLUTT_ÅR ;                                    
        IF   TAB.OMSORG_KODE(N) ^= 'J' THEN                                     
          DO;                                                                   
             HJELP1 =  X_POENG_TAB(N) + HJELP1;                                 
             K      = N;                                                        
             T      = T + 1; /*TELLE OPP ÅR UTEN*/                              
          END;                                                                  
                                                                                
        X_POENG_TAB(K) = 0;                                                     
        IF T >= 3 THEN LEAVE;                                                   
      END;                                                                      
                                                                                
   DO   N = 1 TO 3;                                                             
        STØRSTE_POENG_ALLE (N) = 0;                                             
   END;                                                                         
                                                                                
                                                                                
   IF   T < 0 THEN T = 0;                                                       
                                                                                
   IF   T < 3  THEN    /* NÅ SKAL VI TA MED POENG OMS.ÅR     */                 
                       /* T = ANTALL ÅR UTEN OMS.ÅR          */                 
        Z = 3 - T;     /* Z = ANTALL ÅR VI SKAL HA MED OMS.ÅR*/                 
   ELSE                                                                         
        Z = 3;                                                                  
                                                                                
   N = 0;                                                                       
       DO   M = 1 TO (Z);                                                       
          DO   N =  (LAVESTE_OMS_ÅR - 1) TO SLUTT_ÅR ;                          
               IF X_POENG_TAB(N) > STØRSTE_POENG_ALLE(M) THEN                   
                DO;                                                             
                  STØRSTE_POENG_ALLE(M)  = X_POENG_TAB(N);                      
                  K = N;                                                        
                END;                                                            
                        /*    T             = T + 1;  */                        
            X_POENG_TAB(K) = 0;                                                 
          END;                                                                  
                       /*IF T >= 3 THEN LEAVE; */                               
       END;                                                                     
                                                                                
    /*LAGE ET ALTERNATIV TIL DER VI TAR BORT SISTE ÅR UTEN          */          
    /*OMSORGSPOENG, DVS SLUTT ÅR - 1 UNØDVENDIG PGA PLUKKE UT BESTE */          
    /* SE LOOP OVER  */                                                         
                                                                                
  SNITT_BLANDET =  SUM(STØRSTE_POENG_ALLE) ;                                    
  SNITT_BLANDET =  (SNITT_BLANDET + HJELP1)  / 3 + 0.005;                       
                                                                                
  W_RESULTAT_TRE = MAX(PI_BUP_SNITT,SNITT_BLANDET,TRE_OMS_SNITT);               
                                                                                
  END TRE_SISTE_OMS;                                                            
  /*-------------------------------*/                                           
 /* ***************************************************************** */        
 TJUE_BESTE_OMS: PROC;                                                          
    VANLIG        = 0;                                                          
    TELLER        = 0;      /*HL 20021209*/                                     
    /*  PLUKKER UT HALVPARTEN AV DE BESTE VANLIGE POENGENE  */                  
    /*  DETTE ER GJORT I 4171                               */                  
                                                                                
    /*  FINNE HALVPARTEN UTEN OMSORGSPOENG                  */                  
    DO I = 1 TO 25;                                                             
       STØRSTE_POENG(I)   = 0;                                                  
       W_STØRSTE_POENG(I) = 0;                                                  
       OMSÅR_HALVPARTEN(I) = 0;                                                 
    END;                                                                        
                                                                                
    K = INNT_START_ÅR;                 /*0395 HL */                             
    HALVPARTEN = (W_ANTALL_ÅR / 2) + 0.5;                                       
    DO I = 1 TO HALVPARTEN;                                                     
       DO J = INNT_START_ÅR TO SLUTT_ÅR;                                        
 /*    IF   B01.PI_KODE(IND,J) ^= 'J' THEN  9903*/                              
          IF TAB.OMSORG_KODE(J) ^= 'J' THEN                                     
             DO;                                                                
                IF V_POENG_TAB(J) > STØRSTE_POENG(I) THEN                       
                   DO;                                                          
                      STØRSTE_POENG(I) = V_POENG_TAB(J);                        
                      W_STØRSTE_P(I)   = V_POENG_TAB(J);                        
                      K = J;                                                    
                      A = I;                                                    
                   END;                                                         
             END;                                                               
       END; /* DO J = INNT_START_ÅR TO SLUTT_ÅR */                              
       IF (K > 0) THEN                                                          
          DO;  /* PRØVDE Å OPPDATERE MED K = 0 */                               
             VANLIG  = VANLIG + V_POENG_TAB(K);                                 
             V_POENG_TAB(K) = 0;                                                
          END; /* PRØVDE Å OPPDATERE MED K = 0 */                               
    END; /* DO I = 1 TO HALVPARTEN */                                           
                                                                                
    IF HALVPARTEN > 0 THEN          /*9905*/                                    
       POENG_HALVPARTEN = SUM(STØRSTE_POENG) / HALVPARTEN + 0.005;              
                                                                                
    /* LEGGE TIL ET ÅR I POENGREKKEN MED BESTE OMSÅR */                         
                                                                                
    DO I = 1992 TO SLUTT_ÅR;                                                    
 /*  IF B01.PI_KODE(IND,I)  = 'J' &      9903*/                                 
       IF (TAB.OMSORG_KODE(I)  = 'J' &                                          
           T_POENG_TAB(I)      > 0   )  THEN                                    
          TELLER = TELLER + 1; /*MULIGE ANTALL OMSÅR*/                          
    END;                                                                        
                                                                                
   /*-------GÅR DETTE ???--------------------------------*/                     
                                                                                
    IF TELLER > 0 THEN                                                          
       DO I = 1 TO TELLER;                                                      
          DO J = INNT_START_ÅR TO SLUTT_ÅR;                                     
          /* IF B01.PI_KODE(IND,J) = 'J' &   9903*/                             
             IF TAB.OMSORG_KODE(J) = 'J' &                                      
                T_POENG_TAB(J) > 0 THEN                                         
                DO;                                                             
                   IF T_POENG_TAB(J) > W_STØRSTE_POENG(I) THEN                  
                      DO;                                                       
                         W_STØRSTE_POENG(I) = T_POENG_TAB(J);                   
                         W_STØRSTE_P(I + A) = T_POENG_TAB(J);                   
                         STØRSTE_POENG(I + A) = T_POENG_TAB(J);                 
                         K = J;                                                 
                      END;                                                      
                END;                                                            
              END; /* J = INNT_START_ÅR TO SLUTT_ÅR */                          
              CALL W_BEREGNING;                                                 
              T_POENG_TAB(K) = 0;                                               
           END; /* I = 1 TO TELLER */                                           
                                                                                
           /* ------------------------------------------------------- */        
                                                                                
           W_BEREGNING: PROC;                                                   
              /* ------- PROC UNDER TJUE_BESTE_OMS ------------------ */        
              W_ANTALL_ÅR = W_ANTALL_ÅR + 1;                                    
              HALVPARTEN = (W_ANTALL_ÅR / 2) + 0.5;                             
 /*           IF   HALVPARTEN > 20 THEN                                         
                 HALVPARTEN = 20;                   */                          
              DO X = 1 TO HBOUND(Y_PI_POENG,1);                                 
                 Y_PI_POENG(X)  =  W_STØRSTE_P(X);                              
                 X_STØRSTE_P(X)  =  0;                                          
              END;                                                              
                                                                                
              DO B = 1 TO HALVPARTEN;                                           
              /*  PLUKKER UT HALVPART AV DE BESTE    POENGENE  */               
                 DO N = 1 TO (HALVPARTEN + TELLER);                             
                    IF Y_PI_POENG(N) > X_STØRSTE_P(B) THEN                      
                       DO;                                                      
                          X_STØRSTE_P(B) = Y_PI_POENG(N);                       
                          M = N;                                                
                       END;                                                     
                 END;                                                           
                 Y_PI_POENG(M) = 0;                                             
              END;                                                              
                                                                                
              OMSÅR_HALVPARTEN(S) = SUM(X_STØRSTE_P)                            
                                  / HALVPARTEN + 0.005;                         
              /* S ER INITIERT MED 1 */                                         
              S = S + 1;                                                        
           END W_BEREGNING;                                                     
           /* ------------------------------------------------------- */        
           BESTE_HALVPARTEN = 0;                                                
           DO   T = 1 TO S;                                                     
              IF (OMSÅR_HALVPARTEN(T) > BESTE_HALVPARTEN) THEN                  
                 BESTE_HALVPARTEN = OMSÅR_HALVPARTEN(T);                        
           END;                                                                 
           W_RESULTAT_20 = MAX(POENG_HALVPARTEN,BESTE_HALVPARTEN);              
       END TJUE_BESTE_OMS;                                                      
       /* ----------------------------------------------------------- */        
                                                                                
       W1_RESULTAT = MAX(W_RESULTAT_TRE,W_RESULTAT_20);                         
    END REGN_OMS;                                                               
    /* -------------------------------------------------------------- */        
 END REGN_BUP_FPP;                                                              
