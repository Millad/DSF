 /*   SIST ENDRET PÅ PROD   2005.06.24 12.44.30 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2005.04.26 12.54.25 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2004.09.13 13.19.49 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.09.13 11.20.10 AV   JDA2970          */        
 /*       SIST ENDRET 04/05-98 12.58.33 AV   JDA7339                  */        
 /*                                                                   */        
 /*IDENTIFIKASJON:                                                    */        
 /*    R0014162 - SUBRUTINE I PLI                                     */        
 /*    PROGRAMMERER: PER F. BERGESTAD, JUNI 1982                      */        
 /*HENSIKT:                                                           */        
 /*    RUTINEN REGNER UT PENSJONSGIVENDE INNTEKT I ÅRENE FØR 1967,    */        
 /*    OG PENSJONSPOENG FOR DISSE ÅRENE.                              */        
 /*TILKNYTNING :                                                      */        
 /*    PROGRAMMET INCLUDES I 4141                                     */        
 /*    PROGRAMMET CALLES   I 4154                                     */        
 /*                       OG 4164                                     */        
 /*                                                                   */        
 /*                                                                   */        
 /* ***************************************************************** */        
  LEGG_INNT_FØR_67_I_TAB:                                                       
    PROC(IND,START_ÅR) OPTIONS(REENTRANT);                                      
 %SKIP(5);                                                                      
    DCL                                                                         
       IND          FIXED BIN(15);                                              
    DCL                                                                         
       I            FIXED BIN(15)  INIT(0),                                     
       W_INNT_POENG FIXED DEC(5,3) INIT(0),                                     
       DIFF         FIXED DEC(7)   INIT(0),                                     
       DIFF66       FIXED DEC(7)   INIT(0),                                     
       DIFF67       FIXED DEC(7)   INIT(0);                                     
    DCL                                                                         
       START_ÅR     FIXED DEC(5)          ;   /*2000*/                          
 %PAGE;                                                                         
                                                                                
                                                                                
                                                                                
    /* ************************************************************ */          
    /* INNTEKTS-SEGMENTET                                           */          
    /* ************************************************************ */          
                                                                                
     DCL 1  W01_PINT               UNALIGNED,                                   
         %INCLUDE P0019928;                                                     
                                                                                
    %PAGE;                                                                      
    /* ************************************************************ */          
    /*                                                              */          
    /* DLI CALL-PARAMETRE                                           */          
    /*                                                              */          
    /* ************************************************************ */          
                                                                                
     DCL 1  W01_DLI_PARAM,                                                      
            2   W02_GU             CHAR (4)       INIT('GU  '),                 
            2   W02_PARM_CT_5      FIXED BIN(31)  INIT (5);                     
                                                                                
    %PAGE;                                                                      
    /* ************************************************************ */          
    /*                                                              */          
    /* DL1 SSA-OMRÅDER FOR ROT OG STATUS-SEGMENTER.                 */          
    /*                                                              */          
    /* ************************************************************ */          
                                                                                
                                                                                
     DCL 1  SSA1_RF0PERSN          UNALIGNED,                                   
            2    HDEL                            CHAR (17)  INIT                
                 ('RF0PERSN(FNR     '),                                         
            2    REL_OP                          CHAR (2)   INIT (' ='),        
            2    PKEY               FIXED        DEC  (11)  INIT ( 0 ),         
            2    HP                              CHAR (1)   INIT (')');         
                                                                                
                                                                                
     DCL 1  SSA1_BINNTEKT_67       UNALIGNED,                                   
            2    SEGM_FELT1_NAVN                 CHAR (17)  INIT                
                 ('PINNTEKT(INNTÅR'),                                           
            2    REL_OP1                         CHAR (2)   INIT (' ='),        
  /*2000*/  2    ÅR                 FIXED        DEC  (5)   INIT (1967),        
            2    CON_CAT                         CHAR (1)   INIT ( '&'),        
            2    FELT2_NAVN                      CHAR (8)   INIT                
                 ('INNTTYPE'),                                                  
            2    REL_OP2                         CHAR (2)   INIT (' ='),        
            2    TYPE                            CHAR (1)   INIT ('B'),         
            2    HP                              CHAR (1)   INIT (')');         
                                                                                
     DCL                                                                        
         UIB_RC_OK                 BIT (8)       INIT( 0 );                     
                                                                                
                                                                                
       IF START_ÅR = 1967 THEN                      /*2000*/                    
                                                                                
 /*                                                                   */        
 /*  NÅR STARTÅR ER 1967, ER DET DIFFEN MELLOM PI66 OG AI66 SOM SKAL  */        
 /*  PLUSSES PÅ AI I ÅRENE 63 TIL 65..                                */        
 /*                                                                   */        
                                                                                
          DIFF = B02.TILBERPO.PI66(IND) * 100 - B02.TILBERPO.AI66(IND)          
                                                                * 100;          
       ELSE                                                                     
                                                                                
 /*                                                                   */        
 /*  NÅR STARTÅR ER SEINERE ENN 1967, ER DET DIFFEN MELLOM PI OG AI   */        
 /*  I 1967 SOM SKAL PLUSSES PÅ AI I ÅRENE 64 TIL 66..                */        
 /*                                                                   */        
 /*  OBS OBS HER ER DET EN ENDRING - SE NEDENFOR  OBS OBS  HL         */        
                                                                                
          DO;                                                                   
                                                                                
             SSA1_RF0PERSN.PKEY  =        B02.FNR(IND);                         
                                                                                
                                                                                
             CALL PLITDLI                (W02_PARM_CT_5,                        
                                         W02_GU,                                
                                         RF1,                                   
                                         W01_PINT,                              
                                         SSA1_RF0PERSN,                         
                                         SSA1_BINNTEKT_67);                     
                                                                                
             IF UIBFCTR         ^=        UIB_RC_OK         THEN                
                DO;                                                             
                  /* ****************************************** */              
                  /*   UAKSEPTABEL KODE FRA :    UIB            */              
                  /* ****************************************** */              
  L100:                                                                         
                   FEIL_MELD_NR     =     500;                                  
                   FEIL_VED_LABEL   =    '100';                                 
                   GO TO   RETUR;                                               
                END;                                                            
                                                                                
                                                                                
             IF RF1.STATUS_KODE    =        '  '       THEN                     
        /*200504: */                                                            
                DIFF67 = W02_PI - B02.RF0PERSN.AI67(IND) * 100;                 
                                                                                
             ELSE                                                               
                                                                                
                IF RF1.STATUS_KODE     =        'GE'       THEN                 
                                                                                
                   DIFF67 = 0;                                                  
                                                                                
                ELSE                                                            
                                                                                
                   DO;                                                          
                /* ****************************************** */                
                /*   UAKSEPTABEL STATUSKODE                   */                
                /* ****************************************** */                
  L110:                                                                         
                      FEIL_MELD_NR     =     500;                               
                      FEIL_VED_LABEL   =    '110';                              
                      DB_STATUS_KODE   =    RF1.STATUS_KODE;                    
                      GO TO   RETUR;                                            
                   END;                                                         
                                                                                
                                                                                
             IF UFØR_START_MND(1967) > 0      THEN     /*2000*/                 
                DO;                                                             
                                                                                
                   DIFF66 = B02.TILBERPO.PI66(IND) * 100                        
                            - B02.TILBERPO.AI66(IND) * 100;                     
                                                                                
                   IF DIFF66 > DIFF67                        THEN               
                                                                                
                      DIFF = DIFF66;                                            
                                                                                
                   ELSE                                                         
                                                                                
                      DIFF = DIFF67;                                            
                                                                                
                END;                                                            
                                                                                
             ELSE                                                               
                                                                                
                DIFF = DIFF67;                                                  
                                                                                
          END;                                                                  
 %SKIP(2);                                                                      
 /*                                                                   */        
 /*  BARE DE 4 NÆRMESTE ÅRENE FØR STARTÅR ER AKTUELLE...              */        
 /*                                                                   */        
                                                                                
                  /*2000 : */                                                   
       IF START_ÅR < 1968 THEN                                                  
          TAB.INNTEKT(1963) = B02.TILBERPO.AI63(IND) * 100 + DIFF;              
       IF START_ÅR < 1969 THEN                                                  
          TAB.INNTEKT(1964) = B02.TILBERPO.AI64(IND) * 100 + DIFF;              
       IF START_ÅR < 1970 THEN                                                  
          TAB.INNTEKT(1965) = B02.TILBERPO.AI65(IND) * 100 + DIFF;              
       IF START_ÅR < 1971 THEN                                                  
          TAB.INNTEKT(1966) = B02.TILBERPO.AI66(IND) * 100 + DIFF;              
                                                                                
 %PAGE;                                                                         
       DO I = 1963 TO 1966;                                                     
                                                                                
          IF TAB.INNTEKT(I) < 0 THEN                                            
             TAB.INNTEKT(I) = 0;                                                
                                                                                
          IF TAB.INNTEKT(I) > 0 THEN                                            
             DO;                                                                
                IF (TAB.INNTEKT(I) > 5000 * 8) THEN                             
                   TAB.INNT_POENG(I) = 7;                                       
                ELSE                                                            
                   IF (TAB.INNTEKT(I) > 5000) THEN                              
                      TAB.INNT_POENG(I) =                                       
                          (TAB.INNTEKT(I) - 5000) / 5000;                       
           /*    W_INNT_POENG      = (TAB.INNTEKT(I) - 5000) / 5000;            
                                                                                
                 IF W_INNT_POENG      > 7.0000        THEN                      
                    W_INNT_POENG      = 7.0000;                                 
                                                                                
                 IF W_INNT_POENG      < 0    THEN                               
                    TAB.INNT_POENG(I) = 0;                                      
                 ELSE                                                           
                    TAB.INNT_POENG(I) = W_INNT_POENG; */                        
             END;                                                               
          ELSE                                                                  
             TAB.INNT_POENG(I) = 0;                                             
                                                                                
 /* 6.3.86 HL   FEILRAPP 40/88                            */                    
                                                                                
          TAB.POENG_ANVENDT(I) = TAB.INNT_POENG(I);                             
                                                                                
 /* HIT 6.3.86 HL                                         */                    
                                                                                
       END;                                                                     
                                                                                
 /* 7.3.86 HL  END;                                       */                    
                                                                                
  RETUR:                                                                        
                                                                                
  END LEGG_INNT_FØR_67_I_TAB;                                                   
                                                                                
