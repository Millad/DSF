 /*   SIST ENDRET PÅ PROD   2003.02.27 12.27.12 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2002.11.21 13.14.51 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.10.22 12.34.49 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2000.08.18 14.48.21 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.02.18 14.34.20 AV   JDA7339          */        
 /*       SIST ENDRET 16/11-98 10.04.58 AV   JDA7339                  */        
 /*       SIST ENDRET 16/11-98 10.01.52 AV   JDA7339                  */        
 /*       SIST ENDRET 21/08-98 13.09.36 AV   JDA7339                  */        
 /*       SIST ENDRET 22/06-98 11.51.15 AV   JDA7339                  */        
 /*       SIST ENDRET 04/06-98 15.09.52 AV   RFA9991                  */        
 /* ***************************************************************** */        
 /*IDENTIFIKASJON:                                                    */        
 /*    R001N803 - UNDERPROGRAM I PLI                                  */        
 /*    PROGRAMMERER:                                                  */        
 /* ***************************************************************** */        
 /*HENSIKT:                                                           */        
 /*    PROSEDYREN FORETAR KONTROLL AV DATA FOR EP_SØKER               */        
 /*    FRA ETTERLATTPENSJONSGRUNNBLANKETTEN.NY VERSJON                */        
 /*PROGRAMTILKNYTNING:                                                */        
 /*    INCLUDES I  R001N801 - ETTERLATTEPENSJON                       */        
 /*BRUK:                                                              */        
 /*    CALL KONTROLL_E3_SØKER(FEIL_I_EP_SØKER);                       /*        
 ***************************************************************** */           
 %PAGE;                                /*   */                                  
 /* ***************************************************************** */        
 /*                                                                   */        
 /*   MODULEN KONTROLLERER EP_SØKER, OG RETURNERER                    */        
 /*   MED FEIL_FUNNET = '1'B HVIS FEIL, '0'B HVIS RIKTIG              */        
 /*                                                                   */        
 /* ***************************************************************** */        
 %SKIP(2);                                                                      
 KONTROLL_E3_SØKER:                                                             
   PROC (FEIL_FUNNET);                                                          
   DCL                                                                          
      FEIL_FUNNET BIT (1);                                                      
 %SKIP;                                                                         
   DCL                                                                          
      KEY_BIT                    BIT(32) BASED (KEY_PEKER),                     
      KEY_PEKER                  POINTER,                                       
      TK_RECL                    CHAR (101);                                    
   DCL                                                                          
      ALDER FIXED DEC (7),                                                      
      IÅR   FIXED BIN (15),                                                     
      TELL  FIXED BIN (15),                                                     
      DAGENS_DATO_ÅMD PIC '99999999',                                           
      DAGENS_DATO_ÅM DEF DAGENS_DATO_ÅMD PIC '999999',                          
      DAGENS_DATO_Å  DEF DAGENS_DATO_ÅMD PIC '9999';                            
 %PAGE;                                /*   */                                  
 /*  HJELPE DCL FOR Å SNU DATO           */                                     
 %SKIP(1);                                                                      
   DCL                                                                          
      HJ_VIRK_DATO_ÅMD                               PIC '99999999',            
      VIRK_DATO_Å DEF HJ_VIRK_DATO_ÅMD POS(1)        PIC '9999',                
      VIRK_DATO_M DEF HJ_VIRK_DATO_ÅMD POS(5)        PIC '99',                  
      HJ_VIRK_DATO_ÅM DEF HJ_VIRK_DATO_ÅMD POS(1)    PIC '999999',              
      HJ_FRAMLEGGSDATO_ÅMD                            PIC '99999999',           
      FRAMLEGGSDATO_Å DEF HJ_FRAMLEGGSDATO_ÅMD POS(1) PIC '9999',               
      FRAMLEGGSDATO_M DEF HJ_FRAMLEGGSDATO_ÅMD POS(5) PIC '99',                 
      HJ_DØDSDATO_ÅMD                       PIC '99999999',                     
      DØDSDATO_Å DEF HJ_DØDSDATO_ÅMD POS(1) PIC '9999',                         
      DØDSDATO_M DEF HJ_DØDSDATO_ÅMD POS(5) PIC '99',                           
      DØDSDATO_D DEF HJ_DØDSDATO_ÅMD POS(7) PIC '99',                           
      DØDSDATO_ÅM DEF HJ_DØDSDATO_ÅMD POS(1) PIC '999999';                      
 %SKIP;                                                                         
   DCL                                                                          
      SEKSTISEKS_ÅR                            PIC '999999',                    
      TT_TIL_Å      DEF SEKSTISEKS_ÅR POS(1)   PIC '9999',                      
      TT_TIL_M      DEF SEKSTISEKS_ÅR POS(5)   PIC '99',                        
      HJ_FNR                                   PIC '(11)9',                     
      HJ_FNR_MND    DEF HJ_FNR          POS(3) PIC '99',                        
      HJ_FNR_ÅR     DEF HJ_FNR          POS(5) PIC '99',                        
      HJ_FNR_PNR    DEF HJ_FNR          POS(7) PIC '999',                       
      HJ_FNR13                                 PIC '(13)9',                     
      HJ_FNR_MND13  DEF HJ_FNR13        POS(3) PIC '99',                        
      HJ_FNR_ÅR13   DEF HJ_FNR13        POS(5) PIC '9999',                      
      HJ_FNR_PNR13  DEF HJ_FNR13        POS(9) PIC '999';                       
    DCL TT_FRA_1937ÅM                            PIC '999999';                  
    DCL TT_FRA37_M                      FIXED DEC(5) INIT (0);                  
    DCL TT_FRA37_Å                      FIXED DEC(5) INIT (0);                  
    DCL TT_FRA_1937_ÅRH DEF TT_FRA_1937ÅM POS(1) PIC '9999',                    
        TT_FRA_1937_ÅR  DEF TT_FRA_1937ÅM POS(3) PIC '99',                      
        TT_FRA_1937_MND DEF TT_FRA_1937ÅM POS(5) PIC '99';                      
    DCL TT_HJELP_M    FIXED DEC (5) INIT (0); /*Y2K*/                           
    DCL                                                                         
      1 FNR_REG,                                                                
        2 FNR1 FIXED DEC(11),                                                   
        2 FNR2 FIXED DEC(11),                                                   
        2 BRUKERID CHAR (4);                                                    
                                                                                
    DCL TEKST CHAR(4);                                                          
 %PAGE;                                                                         
                                                                                
                                                                                
 /*LOVENDRING MED VIRKNING FRA 1.1.91 SP : */                                   
      DIV_PARAM_OMR.REGN_1291 = ' ';                                            
                                                                                
      FNR_REG.BRUKERID = DIV_PARAM_OMR.CICS_IND;                                
      FNR_REG.FNR2     = 0;                                                     
      FEIL_FUNNET      = '0'B;                                                  
      KEY_PEKER        = ADDR(E3S.TKNR);                                        
      HJ_VIRK_DATO_ÅMD = E3S.VIRK_DATO_ÅMD;                                     
      ANT_FEIL_SKREVET = 0;                                                     
      DAGENS_DATO_ÅMD  = DATO_2000;                                             
      IF ^F_NUMERISK(F_FELT_PIC_CHAR11(E3S.FNR)) THEN                           
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            E3S.FNRNR   = 200;                                                  
         END;                                                                   
      ELSE                                                                      
      IF ^F_GYLDIG_FNR(E3S.FNR) THEN                                            
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            E3S.FNRNR   = 1;                                                    
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
            FNR_REG.FNR1 = E3S.FNR;                                             
            EXEC CICS LINK PROGRAM('R0019906') COMMAREA(FNR_REG);               
            IF FNR_REG.FNR2 > 0 THEN                                            
               DO;                                                              
                  E3S.FNRNR = 203;                                              
                  E3S.FNR_GML = E3S.FNR;                                        
                  E3S.FNR = FNR_REG.FNR2;                                       
               END;                                                             
         END;                                                                   
      IF E3S.NAVN = (25)' ' THEN                                                
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            E3S.NAVNNR = 2;                                                     
         END;                                                                   
      IF VERIFY(E3S.SPRÅK,'NBE ') ^= 0 THEN                                     
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            E3S.SPRÅKNR = 6;                                                    
         END;                                                                   
      IF ^ F_NUMERISK(F_FELT_PIC_CHAR4(E3S.TKNR)) THEN                          
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            E3S.TKNRNR = 200;                                                   
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
               EXEC CICS HANDLE CONDITION NOTFND(NOTFND);                       
               EXEC CICS READ DATASET ('TKNRTAB') RIDFLD(KEY_BIT)               
                                                  INTO(TK_RECL);                
               LEAVE;                                                           
           NOTFND:                                                              
               FEIL_FUNNET = '1'B;                                              
               E3S.TKNRNR  = 3;                                                 
         END;                                                                   
                                                                                
    /* ************************************************** */                    
    /* KONTROLL AV ACF2 FOR TILLGANGE TIL TKNR            */                    
    /*                                                    */                    
          /* ************************************************** */              
    BO_TKNR  = TKNR;                                                            
     CALL ATK_TILGANG_KONTROLL;                                                 
                                                                                
                                                                                
       IF VERIFY(E3S.SIVILSTAND,'SEU') ^= 0 THEN                                
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            E3S.SIVILSTANDNR = 496;   /* KONS RF */                             
         END;                                                                   
      IF VERIFY(E3S.NORSK_BOSATT,'JN') ^= 0 THEN                                
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            E3S.NORSK_BOSATTNR = 8;                                             
         END;                                                                   
      IF ^ (E3S.VILKÅR_10_5 = 'J' ! E3S.VILKÅR_10_5 = 'N') THEN                 
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            E3S.VILKÅR_10_5NR = 66;                                             
         END;                                                                   
      IF ^ (E3S.OMSORG_AVD_BA = 'J' ! E3S.OMSORG_AVD_BA = 'N') THEN             
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            E3S.OMSORG_AVD_BANR = 65;                                           
         END;                                                                   
      IF ^ F_NUMERISK(F_FELT_PIC_CHAR7(E3S.FORVENTET)) THEN                     
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            E3S.FORVENTETNR = 200;                                              
         END;                                                                   
      IF ^ F_NUMERISK(F_FELT_PIC_CHAR7(E3S.FAI)) THEN                           
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            E3S.FAINR = 200;                                                    
         END;                                                                   
       IF VERIFY(E3S.YSKADE_TILLEGG,'J ') > 0 THEN                              
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            E3S.YSKADE_TILLEGGNR = 300;                                         
         END;                                                                   
      IF VERIFY(E3S.GP_OS_KODE,'OP') > 0 THEN                                   
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            E3S.GP_OS_KODENR = 68;                                              
         END;                                                                   
                                                                                
      IF ^ F_NUMERISK(F_FELT_PIC_CHAR8(E3S.FRAMLEGGSDATO_ÅMD)) THEN             
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            E3S.FRAMLEGGSDATO_ÅMDNR = 200;                                      
         END;                                                                   
      ELSE                                                                      
      IF F_FELT_PIC_CHAR8(E3S.FRAMLEGGSDATO_ÅMD) ^= '00000000' THEN             
         IF ^ F_GYLDIG_DATO(E3S.FRAMLEGGSDATO_ÅMD) THEN                         
            DO;                                                                 
               FEIL_FUNNET = '1'B;                                              
               E3S.FRAMLEGGSDATO_ÅMDNR = 71;                                    
            END;                                                                
      IF VERIFY(E3S.FRAMLEGGSKODE,' OA') > 0 THEN                               
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            E3S.FRAMLEGGSKODENR = 73;                                           
         END;                                                                   
      IF ^ F_NUMERISK(F_FELT_PIC_CHAR8(E3S.VIRK_DATO_ÅMD)) THEN                 
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            E3S.VIRK_DATO_ÅMDNR = 200;                                          
         END;                                                                   
      ELSE                                                                      
         IF ^ F_GYLDIG_DATO(E3S.VIRK_DATO_ÅMD) THEN                             
            DO;                                                                 
               FEIL_FUNNET = '1'B;                                              
               E3S.VIRK_DATO_ÅMDNR = 9;                                         
            END;                                                                
                                                                                
 /* ***************************************************** */                    
 /* NY PARAGRAF 10-6 FRA 1.1.89.  YTELSEN SKAL ALLTID GIS */                    
 /* MED 55 PROSENT TILLEGGSPENSJON.  ERIK/HL 11.4.89      */                    
 /* ***************************************************** */                    
                                                                                
       HJ_DØDSDATO_ÅMD = E3S.DØDSDATO_ÅMD_EK;                                   
                                                                                
                                                                                
      /* ENDRET 15/2-84 AV KARIN - UOPPGITT AVDØD EKTEFELLE */                  
      IF ^F_NUMERISK(F_FELT_PIC_CHAR11(E3S.FNR_EK)) THEN                        
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            E3S.FNR_EKNR = 200;                                                 
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
            IF E3S.FNR_EK > 0  THEN                                             
               DO;                                                              
                 /*** AVDØDES FNR ER UTFYLT  ***/                               
                 IF E3S.FNR_EK ^= 11111111111 THEN                              
                    DO;                                                         
                      /*** AVDØDES FNR ER ET VANLIG FNR ***/                    
                      IF ^F_GYLDIG_FNR(E3S.FNR_EK) THEN                         
                         DO;                                                    
                            FEIL_FUNNET = '1'B;                                 
                            E3S.FNR_EKNR = 15;                                  
                         END;                                                   
                      ELSE                                                      
                         DO;                                                    
                            /*** AVD-FNR ER ET GYLDIG FNR ***/                  
                            FNR_REG.FNR1 = E3S.FNR_EK;                          
                            EXEC CICS LINK PROGRAM('R0019906')                  
                            COMMAREA(FNR_REG);                                  
                            IF FNR_REG.FNR2 > 0 THEN                            
                               DO;                                              
                                  /* FNR ER ENDRET */                           
                                  E3S.FNR_EKNR = 203;                           
                                  E3S.FNR_GML_EK = E3S.FNR_EK;                  
                                  E3S.FNR_EK = FNR_REG.FNR2;                    
                               END;                                             
                         END;   /* END GYLDIG FNR */                            
                      IF ^ F_NUMERISK(F_FELT_PIC_CHAR8                          
                           (E3S.DØDSDATO_ÅMD_EK)) THEN                          
                         DO;                                                    
                            FEIL_FUNNET = '1'B;                                 
                            E3S.DØDSDATO_ÅMD_EKNR = 200;                        
                         END;                                                   
                      ELSE                                                      
                         IF E3S.DØDSDATO_ÅMD_EK = 0 THEN                        
                            DO;                                                 
                               FEIL_FUNNET = '1'B;                              
                               E3S.DØDSDATO_ÅMD_EKNR = 115;                     
                            END;                                                
                         ELSE                                                   
                            IF ^ F_GYLDIG_DATO                                  
                                (E3S.DØDSDATO_ÅMD_EK) THEN                      
                               DO;                                              
                                  FEIL_FUNNET = '1'B;                           
                                  E3S.DØDSDATO_ÅMD_EKNR = 75;                   
                               END;                                             
                      IF VERIFY(E3S.NORSK_BOSATT_EK,' JN') > 0 THEN             
                         DO;                                                    
                            FEIL_FUNNET = '1'B;                                 
                            E3S.NORSK_BOSATT_EKNR = 8;                          
                         END;                                                   
                      IF ^ (E3S.VILKÅR_8_4_EK = 'J' !                           
                            E3S.VILKÅR_8_4_EK = 'N' !                           
                            E3S.VILKÅR_8_4_EK = ' ') THEN                       
                         DO;                                                    
                            FEIL_FUNNET = '1'B;                                 
                            E3S.VILKÅR_8_4_EKNR = 45;                           
                         END;                                                   
                                                                                
                   /* DERSOM VP_ÅR  ER UTFYLT BLIR DISSE NULLSTILLT */          
                                                                                
                      DO J = 1 TO 4 UNTIL                                       
                         (F_FELT_PIC_CHAR4(E3S.VP_ÅR_EK(J) = '0000'));          
                       IF F_FELT_PIC_CHAR4(E3S.VP_ÅR_EK(J)) ='0000' THEN        
                            DO TELL = J TO 4;                                   
                               E3S.VP_ÅR_EK(TELL) = 0;                          
                            END;                                                
                      END;                                                      
                                                                                
                      DO IÅR = 1 TO 4 WHILE                                     
                       (F_FELT_PIC_CHAR4(E3S.VP_ÅR_EK(IÅR)) ^='0000');          
                         IF ^ F_NUMERISK(F_FELT_PIC_CHAR4                       
                              (E3S.VP_ÅR_EK(IÅR))) THEN                         
                            DO;                                                 
                               FEIL_FUNNET = '1'B;                              
                               E3S.VP_ÅR_EKNR(IÅR) = 200;                       
                            END;                                                
                         ELSE                                                   
                            IF ^(E3S.VP_ÅR_EK(IÅR) > 1966                       
                            & E3S.VP_ÅR_EK(IÅR) <= DAGENS_DATO_Å) THEN          
                               DO;                                              
                                  FEIL_FUNNET = '1'B;                           
                                  E3S.VP_ÅR_EKNR(IÅR) = 47;                     
                               END;                                             
                      END;      /* DO IÅR = 1 TO 4 */                           
                    END; /* END AVDØDES FNR ER ET VANLIG FNR */                 
                 ELSE                                                           
                    DO;  /* AVDØDES FNR = 11111111111, DVS UKJENT   */          
                         /* OG INGEN AV FELTENE TIL AVDØDE SKAL     */          
                         /* VÆRE UTFYLT                             */          
 /*******************************************************************/          
 /* FOR Å AVVISE ALLE YRKESSKADE-DØDE MED FNR. 11111111111, HAR JEG */          
 /* SATT INN TESTEN NEDENFOR.                        ERIK.          */          
 /*******************************************************************/          
                                                                                
          /*           IF E3S.YSKADE_TILLEGG = 'J'  THEN  */                    
          /*              DO;                             */                    
          /*                 FEIL_FUNNET = '1'B;          */                    
          /*                 E3S.FNR_EKNR         = 321;  */                    
          /*                 E3S.YSKADE_TILLEGGNR = 999;  */                    
          /*              END;                            */                    
          /*           ELSE                               */                    
                                                                                
                                                                                
                         IF E3S.NAVN_EK               ^= (25)' ' !              
                            E3S.DØDSDATO_ÅMD_EK       ^= 0 !                    
                            E3S.NORSK_BOSATT_EK       ^= ' ' !                  
                            E3S.VILKÅR_8_4_EK         ^= ' ' !                  
                            E3S.PI_EK                 ^= 0 !                    
                            E3S.VP_ÅR_EK(1)           ^= 0 THEN                 
                            DO;                                                 
                               FEIL_FUNNET = '1'B;                              
                               E3S.FNR_EKNR          = 999;                     
                               E3S.NAVN_EKNR         = 999;                     
                               E3S.DØDSDATO_ÅMD_EKNR = 999;                     
                               E3S.NORSK_BOSATT_EKNR = 999;                     
                               E3S.VILKÅR_8_4_EKNR   = 999;                     
                               E3S.PI_EKNR           = 999;                     
                               E3S.VP_ÅR_EKNR(1)     = 999;                     
                            END;                                                
                    END; /* AVDØDES FNR = 11111111111, DVS UKJENT   */          
               END;      /* AVDØDES FNR ULIK 0, DVS UTFYLT          */          
           ELSE                                                                 
               DO;                                                              
                  /* AVDØDES FNR = 0, DVS IKKE UTFYLT */                        
                  FEIL_FUNNET = '1'B;                                           
                  E3S.FNR_EKNR = 201;                                           
               END;                                                             
         END;                                                                   
                                                                                
               EXEC CICS HANDLE CONDITION NOTFND(NOTFND);                       
                                                                                
 /*******************************************************************/          
                                                                                
  /* HER SETTES VERDIER IN I MENY_OMR */                                        
      FØDSNUMMER = E3S.FNR;                                                     
      VIRKNINGSDATO_ÅMD = E3S.VIRK_DATO_ÅMD;                                    
 %SKIP(3);                             /*  */                                   
 /*   KRYSS KONTROLLER       */                                                 
 %SKIP;                                                                         
      IF ^ FEIL_FUNNET THEN                                                     
         DO;                                                                    
                                                                                
                                                                                
  /*LOVENDRING MED VIRKNING FRA 1.1.91 SP : */                                  
            IF  E3S.VIRK_DATO_ÅMD > 19920000 &         /* 2000-3 */             
                E3S.DØDSDATO_ÅMD_EK < 19920000    THEN    /*"*/                 
                                                                                
                 DIV_PARAM_OMR.REGN_1291 = 'J';                                 
                                                                                
                                                                                
                                                                                
            /* DIVERSE TESTER VEDR. RETT TIL PENSJON/OVERGANSST. */             
            IF E3S.GP_OS_KODE  = 'O' !                                          
               E3S.GP_OS_KODE  = 'P' THEN                                       
              DO;                                                               
                IF E3S.SIVILSTAND = 'E' THEN                                    
                  DO;                                                           
                           IF E3S.OMSORG_AVD_BA = 'J' THEN                      
                              DO;                                               
                                 /*** OMSORG = J        ***/                    
                                 IF E3S.VILKÅR_10_5 = 'N' THEN                  
                                    DO;                                         
                                       FEIL_FUNNET = '1'B;                      
                                       E3S.OMSORG_AVD_BANR=255;                 
                                       E3S.VILKÅR_10_5NR=999;                   
                                    END;                                        
                                 ELSE                                           
                                    DO;                                         
                                       /**** VILKÅR 10-5 = J   ***/             
                                       IF E3S.GP_OS_KODE = 'O' THEN             
                                          DO;                                   
                                             FEIL_FUNNET = '1'B;                
                                             E3S.SIVILSTANDNR =254;             
                                             E3S.VILKÅR_10_5NR=999;             
                                             E3S.GP_OS_KODENR=999;              
                                          END;                                  
                                       /***  END VILKÅR 10-5 = J   ***/         
                                    END;                                        
                                 /*** END OMSORG = J   ***/                     
                              END;                                              
                           ELSE                                                 
                              DO;                                               
                                 /*** OMSORG = N ***/                           
                                 IF E3S.VILKÅR_10_5 = 'J' THEN                  
                                    DO;                                         
                                       /*** VILKÅR 10-5 = J ***/                
                                       IF E3S.GP_OS_KODE = 'O' THEN             
                                          DO;                                   
                                             FEIL_FUNNET = '1'B;                
                                             E3S.SIVILSTANDNR =254;             
                                             E3S.VILKÅR_10_5NR=999;             
                                             E3S.GP_OS_KODENR=999;              
                                          END;                                  
                                       /*** END VILKÅR 10-5 = J ***/            
                                    END;                                        
                                 ELSE                                           
                                    DO;                                         
                                       /*** VILKÅR 10-5 = N ***/                
                                       IF E3S.GP_OS_KODE = 'P' THEN             
                                          DO;                                   
                                            /* PENSJON */                       
                                            FEIL_FUNNET = '1'B;                 
                                            E3S.SIVILSTANDNR =253;              
                                            E3S.VILKÅR_10_5NR=999;              
                                            E3S.GP_OS_KODENR=999;               
                                          END;                                  
                                       /*** END VILKÅR 10-5 = N ***/            
                                    END;                                        
                                 /*** END OMSORG = N ***/                       
                        END;                                                    
                     /*** END SIVILSTAND = E ***/                               
                  END;                                                          
                ELSE                                                            
                  DO;                                                           
                     IF E3S.SIVILSTAND = 'S' THEN                               
                        DO;                                                     
                           /*** SIVILSTAND = S ***/                             
                           IF E3S.OMSORG_AVD_BA = 'J' THEN                      
                              DO;                                               
                                 /*** OMSORG BARN = J ***/                      
                                 IF E3S.VILKÅR_10_5 = 'N' THEN                  
                                    DO;                                         
                                       FEIL_FUNNET = '1'B;                      
                                       E3S.OMSORG_AVD_BANR =255;                
                                       E3S.VILKÅR_10_5NR=999;                   
                                    END;                                        
                                 /*** END OMSORG BARN = J ***/                  
                              END;                                              
                           ELSE                                                 
                              DO;                                               
                                 /*** OMSORG BARN = N ***/                      
                                 IF E3S.VILKÅR_10_5 = 'N' THEN                  
                                    DO;                                         
                                       IF E3S.GP_OS_KODE = 'P' THEN             
                                          DO;                                   
                                             FEIL_FUNNET = '1'B;                
                                             E3S.SIVILSTANDNR=256;              
                                             E3S.VILKÅR_10_5NR=999;             
                                             E3S.GP_OS_KODENR=999;              
                                          END;                                  
                                    END;                                        
                                 /*** END OMSORG BARN = N ***/                  
                              END;                                              
                        END;                                                    
                     /*** END SIVILSTAND = S ***/                               
                     ELSE                                                       
                        /*** SIVILSTAND = U ***/                                
                        DO;                                                     
                           IF E3S.OMSORG_AVD_BA = 'N' THEN                      
                              DO;                                               
                                  FEIL_FUNNET = '1'B;                           
                                  E3S.SIVILSTANDNR = 322;                       
                                  E3S.OMSORG_AVD_BANR = 999;                    
                              END;                                              
                        END;                                                    
                  END;                                                          
              END;   /* GP_OS KODE = O ELLER P */                               
         END;   /* FEIL_FUNNET */                                               
         /**************************/                                           
                                                                                
                                                                                
                                                                                
         IF ^ FEIL_FUNNET THEN                                                  
            DO;                                                                 
               ALDER = F_ALDER(E3S.FNR,E3S.VIRK_DATO_ÅMD);                      
               IF ((ALDER > 6612 & VIRK_DATO_Å < 1991) !                        
 /*HL*/            (ALDER > 6700 & VIRK_DATO_Å > 1990)) &                       
                    E3S.FORVENTET > 0                THEN                       
                  DO;                                                           
                     FEIL_FUNNET = '1'B;                                        
                     E3S.FORVENTETNR = 67;                                      
                  END;                                                          
               IF ((ALDER < 6700 ! ALDER > 6912) & E3S.FAI > 0   &              
                    VIRK_DATO_Å < 1991 )                !                       
                  ((ALDER < 6701 ! ALDER > 7000) & E3S.FAI > 0   &              
                    VIRK_DATO_Å < 1991 )              THEN                      
                  DO;                                                           
                     FEIL_FUNNET = '1'B;                                        
                     E3S.FAINR = 117;                                           
                  END;                                                          
               IF E3S.GP_OS_KODE   = 'O' &                                      
              (E3S.FRAMLEGGSKODE = ' ' ! E3S.FRAMLEGGSDATO_ÅMD = 0) THEN        
                  DO;                                                           
                     FEIL_FUNNET = '1'B;                                        
                     E3S.FRAMLEGGSKODENR = 74;                                  
                     E3S.FRAMLEGGSDATO_ÅMDNR = 999;                             
                     E3S.GP_OS_KODENR = 999;                                    
                  END;                                                          
               IF E3S.GP_OS_KODE   = 'P' &                                      
                  (E3S.FRAMLEGGSKODE ^= ' ' !                                   
                   E3S.FRAMLEGGSDATO_ÅMD > 0) THEN                              
                  DO;                                                           
                     FEIL_FUNNET = '1'B;                                        
                     E3S.FRAMLEGGSKODENR = 263;                                 
                     E3S.FRAMLEGGSDATO_ÅMDNR = 999;                             
                     E3S.GP_OS_KODENR = 999;                                    
                  END;                                                          
                                                                                
               IF ^F_40DAGER(E3S.VIRK_DATO_ÅMD)  THEN  /* 2000-3 */             
                  DO;                                                           
                     FEIL_FUNNET = '1'B;                                        
                     E3S.VIRK_DATO_ÅMDNR = 10;                                  
                  END;                                                          
               IF E3S.VIRK_DATO_ÅMD < 19670100 THEN /* 2000-3 */                
                  DO;                                                           
                     FEIL_FUNNET = '1'B;                                        
                     E3S.VIRK_DATO_ÅMDNR = 9;                                   
                  END;                                                          
               IF E3S.VIRK_DATO_ÅMD < 19910100 THEN /* 2000-3 */                
                  DO;                                                           
                     FEIL_FUNNET = '1'B;                                        
                     E3S.VIRK_DATO_ÅMDNR = 364;                                 
                  END;                                                          
               IF E3S.VIRK_DATO_ÅMD   < 19710100   &/* 2000-3 */                
                  E3S.YSKADE_TILLEGG  = 'J'    THEN                             
                  DO;                                                           
                     FEIL_FUNNET = '1'B;                                        
                     E3S.VIRK_DATO_ÅMDNR   = 310;/* 2000-3 */                   
                     E3S.YSKADE_TILLEGGNR = 999;                                
                  END;                                                          
            END;   /* FEIL_FUNNET */                                            
                                                                                
                                                                                
                                                                                
      IF ^ FEIL_FUNNET & HJ_DØDSDATO_ÅMD > 19670000 THEN                        
         DO;                                                                    
            IF E3S.FNR_EK = 0 THEN                                              
               DO;                                                              
                                                                                
                                                                                
                  FEIL_FUNNET = '1'B;                                           
                  E3S.FNR_EKNR = 201;                                           
               END;                                                             
            IF E3S.NAVN_EK = (25)' ' THEN                                       
               DO;                                                              
                                                                                
                                                                                
                  FEIL_FUNNET = '1'B;                                           
                  E3S.NAVN_EKNR = 201;                                          
               END;                                                             
            IF E3S.NORSK_BOSATT_EK = ' ' THEN                                   
               DO;                                                              
                                                                                
                                                                                
                  FEIL_FUNNET = '1'B;                                           
                  E3S.NORSK_BOSATT_EKNR = 201;                                  
               END;                                                             
            IF E3S.VILKÅR_8_4_EK = ' ' THEN                                     
               DO;                                                              
                  FEIL_FUNNET = '1'B;                                           
                  E3S.VILKÅR_8_4_EKNR = 201;                                    
               END;                                                             
         END;                                                                   
                                                                                
         IF E3S.NORSK_BOSATT = 'J' THEN                                         
            DO;                                                                 
               HJ_FNR      = E3S.FNR;                                           
               HJ_FNR13    = KONV_FNR11_FNR13(E3S.FNR);                         
                                                                                
               SEKSTISEKS_ÅR =(( HJ_FNR_ÅR13 + 66) * 100)                       
                              + HJ_FNR_MND13;  /* 2000-3 */                     
               IF SEKSTISEKS_ÅR   >   DØDSDATO_ÅM THEN                          
                    /* NY BEREGNING   TRUDE 12.08.98 */                         
                 TT_FRA37_M  = (DØDSDATO_Å -(HJ_FNR_ÅR13        /*Y2K*/         
                       + 16 ))* 12 + DØDSDATO_M - HJ_FNR_MND13 ;                
               ELSE                                                             
                 TT_FRA37_M  = (TT_TIL_Å - (HJ_FNR_ÅR13        /*Y2K*/          
                       + 16 ))* 12 + TT_TIL_M - HJ_FNR_MND13;                   
                                                                                
                TT_FRA37_Å  = TT_FRA37_M / 12;                  /*Y2K*/         
                TT_HJELP_M  = TT_FRA37_Å * 12;                  /*Y2K*/         
                E3SP.TT_FRA_1937ÅR = TT_FRA37_Å;                /*Y2K*/         
                E3SP.TT_FRA_1937MND = TT_FRA37_M - TT_HJELP_M;  /*Y2K*/         
                    /* NY BEREGNING   TRUDE 12.08.98 */                         
            END;                                                                
                                                                                
         IF E3S.NORSK_BOSATT_EK = 'J' THEN                                      
            DO;                                                                 
               HJ_FNR      = E3S.FNR_EK;                                        
               HJ_FNR13    = KONV_FNR11_FNR13(E3S.FNR_EK);                      
               SEKSTISEKS_ÅR = ((HJ_FNR_ÅR13 + 66) * 100)                       
                              + HJ_FNR_MND13;      /*2000-3*/                   
               IF SEKSTISEKS_ÅR   >   DØDSDATO_ÅM THEN                          
                    /* NY BEREGNING   TRUDE 12.08.98 */                         
                 TT_FRA37_M  = (DØDSDATO_Å -(HJ_FNR_ÅR13        /*Y2K*/         
                       + 16 ))* 12 + DØDSDATO_M - HJ_FNR_MND13 ;                
               ELSE                                                             
                 TT_FRA37_M  = (TT_TIL_Å - (HJ_FNR_ÅR13        /*Y2K*/          
                       + 16 ))* 12 + TT_TIL_M - HJ_FNR_MND13;                   
                                                                                
                TT_FRA37_Å  = TT_FRA37_M / 12;                  /*Y2K*/         
                TT_HJELP_M  = TT_FRA37_Å * 12;                  /*Y2K*/         
                E3SP.TT_FRA_1937ÅR_EK = TT_FRA37_Å;             /*Y2K*/         
                E3SP.TT_FRA_1937MND_EK = TT_FRA37_M - TT_HJELP_M;/*Y2K*/        
                    /* NY BEREGNING   TRUDE 12.08.98 */                         
            END;                                                                
                                                                                
                                                                                
 /*******************************************************************/          
 /* ALLE DØDSFALL MED YSKADE_TILLEGG SKAL AVVISES HVIS DØDSDATO ER  */          
 /* FØR 1.1.71.  HUSK F.MELD.NR.  ERIK 19.1.88.                     */          
 /*******************************************************************/          
                                                                                
              IF ^ FEIL_FUNNET & HJ_DØDSDATO_ÅMD < 19710100  &                  
                   HJ_DØDSDATO_ÅMD > 0  &                                       
                   E3S.FNR_EK  = 11111111111  THEN                              
                       DO;                                                      
                          FEIL_FUNNET          = '1'B;                          
                          E3S.DØDSDATO_ÅMD_EKNR= 323;                           
                          E3S.YSKADE_TILLEGGNR = 999;                           
                       END;                                                     
                                                                                
 /*******************************************************************/          
 /* TIL HIT.  ERIK 19.1.88.                                         */          
 /*******************************************************************/          
  /* FJERNET 9708 HL : *********************                                    
      IF ^ FEIL_FUNNET & HJ_DØDSDATO_ÅMD < 67000000 THEN                        
         DO;                                                                    
            IF E3S.FNR_EK > 0 &                                                 
               E3S.FNR_EK ^= 11111111111 THEN                                   
               DO;                                                              
                  FEIL_FUNNET = '1'B;                                           
                  E3S.FNR_EKNR = 202;                                           
               END;                                                             
            IF E3S.NAVN_EK ^= (25)' ' THEN                                      
               DO;                                                              
                  FEIL_FUNNET = '1'B;                                           
                  E3S.NAVN_EKNR = 202;                                          
               END;                                                             
            IF E3S.NORSK_BOSATT_EK ^= ' ' THEN                                  
               DO;                                                              
                  FEIL_FUNNET = '1'B;                                           
                  E3S.NORSK_BOSATT_EKNR = 202;                                  
               END;                                                             
               IF E3S.VILKÅR_8_4_EK ^= ' ' THEN                                 
                  DO;                                                           
                     FEIL_FUNNET = '1'B;                                        
                     E3S.VILKÅR_8_4_EKNR = 202;                                 
                  END;                                                          
               IF E3S.PI_EK > 0 THEN                                            
                  DO;                                                           
                     FEIL_FUNNET = '1'B;                                        
                     E3S.PI_EKNR = 202;                                         
                  END;                                                          
               DO IÅR = 1 TO 4;                                                 
                  IF E3S.VP_ÅR_EK(IÅR) > 0 THEN                                 
                     DO;                                                        
                        FEIL_FUNNET = '1'B;                                     
                        E3S.VP_ÅR_EKNR(IÅR) = 202;                              
                     END;                                                       
               END;                                                             
         END;                                                                   
   ******************************** HIT HL ********************* */             
      IF ^ FEIL_FUNNET  THEN /* & HJ_DØDSDATO_ÅMD > 67000000   */               
         DO;                                                                    
  /* FJERNET 9809 ****************************                                  
            IF E3S.FNR_EK >0 & F_KJØNN(E3S.FNR)=F_KJØNN(E3S.FNR_EK) THEN        
               DO;                                                              
                  FEIL_FUNNET = '1'B;                                           
                  E3S.FNRNR = 16;                                               
                  E3S.FNR_EKNR = 999;                                           
               END;                                                             
  ******************************* HIT 9809 */                                   
            IF E3S.VP_ÅR_EK(1) > 0 &                                            
            E3S.VP_ÅR_EK(1) = E3S.VP_ÅR_EK(2) THEN                              
               DO;                                                              
                  FEIL_FUNNET = '1'B;                                           
                  E3S.VP_ÅR_EKNR(1) = 124;                                      
                  E3S.VP_ÅR_EKNR(2) = 999;                                      
               END;                                                             
            IF HJ_DØDSDATO_ÅMD >= DAGENS_DATO_ÅMD THEN                          
               DO;                                                              
                  FEIL_FUNNET = '1'B;                                           
                  E3S.DØDSDATO_ÅMD_EKNR = 76;                                   
               END;                                                             
            ELSE                                                                
            IF (HJ_VIRK_DATO_ÅM < DØDSDATO_ÅM  &                                
 /*HL*/           DØDSDATO_ÅM < 199100) !                                       
               (HJ_VIRK_DATO_ÅM < DØDSDATO_ÅM + 1   &                           
                  DØDSDATO_ÅM > 199012)               THEN                      
               DO;                                                              
                  FEIL_FUNNET = '1'B;                                           
                  E3S.DØDSDATO_ÅMD_EKNR = 77;                                   
                  E3S.VIRK_DATO_ÅMDNR = 999;                                    
               END;                                                             
                                                                                
 /* FJERNET 15.3.00                                                             
            IF ^FEIL_FUNNET THEN                                                
               IF F_KJØNN(E3S.FNR_EK) = 'K' THEN                                
                  DO IÅR = 1 TO 4;                                              
                     IF E3S.VP_ÅR_EK(IÅR) > 0 THEN                              
                        DO;                                                     
                           FEIL_FUNNET = '1'B;                                  
                           E3S.VP_ÅR_EKNR(IÅR) = 47;                            
                           E3S.FNR_EKNR = 999;                                  
                        END;                                                    
                  END;                                                          
 ****************************************** */                                  
         END;                                                                   
                                                                                
                                                                                
 END KONTROLL_E3_SØKER;                                                         
                                                                                
       /* ************************************************ */                   
       /* PROSEDYREN KONTROLLERER OPPLYSNINGENE PÅ         */                   
       /* YRKESSKADEBILDET.                                */                   
       /* ************************************************ */                   
                                                                                
       KONTROLL_E3_YRKESSKADE:                                                  
          PROC(FEIL_FUNNET);                                                    
                                                                                
        DCL FEIL_FUNNET         BIT (1);                                        
                                                                                
                                                                                
        DCL 1 FNR_REG,                                                          
              2 FNR1 FIXED DEC(11),                                             
              2 FNR2 FIXED DEC(11);                                             
                                                                                
                                                                                
        ANT_FEIL_SKREVET = 0;                                                   
 /*YS   HVIS INGEN FELT I UFØREDELEN ER UTFYLT, SÅ GÅR VI UT FRA */             
 /*YS   AT DET ER DØDSFALL PGA EN GAMMEL YRKESSKADE - DA SKAL    */             
 /*YS   NEMLIG INGEN OPPLYSNINGER ENDRES                         */             
                                                                                
        IF ^ (                                                                  
            E3Y.VILKÅR_1_2_2A   = ' '         &                                 
            E3Y.VILKÅR_1_2_2B   = ' '         &                                 
            E3Y.YST_ÅMD         = 0           &                                 
            E3Y.YSYKDOM         = 'N'         &                                 
            E3Y.YUG             = 0           &                                 
            E3Y.KAP_11_7_UTLØST = ' '         &                                 
            E3Y.YRKE11          = ' '         &                                 
            E3Y.AÅI             = 9999999        )   THEN                       
                                                                                
        DO;                                                                     
                                                                                
        IF ^(E3Y.VILKÅR_1_2_2A = 'J' ! E3Y.VILKÅR_1_2_2A = ' ') THEN            
           DO;                                                                  
              FEIL_FUNNET   ='1'B;                                              
              E3Y.VILKÅR_1_2_2ANR = 301;                                        
           END;                                                                 
                                                                                
                                                                                
        IF ^(E3Y.VILKÅR_1_2_2B = 'J' ! E3Y.VILKÅR_1_2_2B = ' ') THEN            
           DO;                                                                  
              FEIL_FUNNET   ='1'B;                                              
              E3Y.VILKÅR_1_2_2BNR = 302;                                        
           END;                                                                 
                                                                                
        IF ^F_NUMERISK(F_FELT_PIC_CHAR8(E3Y.YST_ÅMD)) THEN                      
           DO;                                                                  
              FEIL_FUNNET    = '1'B;                                            
              E3Y.YST_ÅMDNR = 200;                                              
           END;                                                                 
        ELSE                                                                    
           DO;                                                                  
              IF ^F_GYLDIG_DATO(E3Y.YST_ÅMD) THEN                               
                 DO;                                                            
                    FEIL_FUNNET    = '1'B;                                      
                    E3Y.YST_ÅMDNR = 304;                                        
                 END;                                                           
              ELSE                                                              
                 IF E3Y.YST_ÅMD < 19710101 THEN                                 
                    DO;                                                         
                       FEIL_FUNNET    = '1'B;                                   
                       E3Y.YST_ÅMDNR = 305;                                     
                    END;                                                        
           END;                                                                 
                                                                                
        IF ^(E3Y.YSYKDOM = 'J' ! E3Y.YSYKDOM = 'N') THEN                        
           DO;                                                                  
              FEIL_FUNNET   ='1'B;                                              
              E3Y.YSYKDOMNR = 306;                                              
           END;                                                                 
                                                                                
        IF F_FELT_PIC_CHAR3(E3Y.YUG) ^= '000' THEN                              
           DO;                                                                  
              FEIL_FUNNET = '1'B;                                               
              E3Y.YUGNR = 326;                                                  
           END;                                                                 
                                                                                
        IF E3Y.KAP_11_7_UTLØST ^= ' ' THEN                                      
           DO;                                                                  
              FEIL_FUNNET   ='1'B;                                              
              E3Y.KAP_11_7_UTLØSTNR = 326;                                      
           END;                                                                 
                                                                                
        IF VERIFY(E3Y.YRKE11,'FMUEVSA') ^= 0 THEN                               
           DO;                                                                  
              FEIL_FUNNET = '1'B;                                               
              E3Y.YRKE11NR =  309 ;                                             
           END;                                                                 
                                                                                
        IF ^F_NUMERISK(F_FELT_PIC_CHAR7(E3Y.AÅI)) THEN                          
           DO;                                                                  
              FEIL_FUNNET = '1'B;                                               
              E3Y.AÅINR = 200 ;                                                 
           END;                                                                 
                                                                                
        IF E3Y.YST_ÅMD > E3S.VIRK_DATO_ÅMD !                                    
           E3Y.YST_ÅMD >                                                        
                         E3S.DØDSDATO_ÅMD_EK &    /*2000*/                      
            E3S.FNR_EK  > 11111111111  THEN                                     
           DO;                                                                  
                                                                                
                    /* ****************************************** */            
                    /* FEIL,YSKADETIDSPUNKTET > VIRK ELLER        */            
                    /* DØDSDATO PÅ KAP.10-BLANKETTEN.             */            
                    /* ****************************************** */            
                                                                                
              FEIL_FUNNET    = '1'B;                                            
              E3Y.YST_ÅMDNR  =  328 ;                                           
           END;                                                                 
        IF ^FEIL_FUNNET THEN                                                    
           DO;                                                                  
                                                                                
              IF (E3Y.YRKE11    = 'F' !                                         
                  E3Y.YRKE11    = 'V' !                                         
                  E3Y.YRKE11    = 'U' !                                         
                  E3Y.YRKE11    = 'E' )      THEN                               
                  IF E3Y.AÅI      ^= 9999999 THEN                               
                    DO;                                                         
                                                                                
        /* ********************************************************** */        
        /* KODE F = FISKER.  KODE V = ELEV.  INNTEKT SKAL IKKE OPPGIS */        
        /* FOR DISSE.                                                 */        
        /* ********************************************************** */        
                                                                                
                       FEIL_FUNNET     = '1'B;                                  
                       E3Y.AÅINR       =  314;                                  
                       E3Y.YRKE11NR    =  999;                                  
                    END;                                                        
                 ELSE;                                                          
              ELSE    /*YRKE ER DA M,S,A */                                     
                 IF E3Y.AÅI      = 9999999 THEN                                 
                    DO;                                                         
                                                                                
        /* ********************************************************** */        
        /* INNTEKT SKAL REGISTRERES FOR ALLE ANDRE KODER.             */        
        /* ********************************************************** */        
                                                                                
                       FEIL_FUNNET     = '1'B;                                  
                       E3Y.AÅINR       =  316;                                  
                       E3Y.YRKE11NR    =  999;                                  
                    END;                                                        
           END;                                                                 
                                                                                
        END; /*SLUTT PÅ TEST OM ALLE FELT ER BLANKE*/                           
                                                                                
        IF F_FELT_PIC_CHAR11(E3Y.FNR_EK) ^= (11) '0' THEN                       
           IF ^F_NUMERISK(F_FELT_PIC_CHAR11(E3Y.FNR_EK)) THEN                   
              DO;                                                               
                 FEIL_FUNNET = '1'B;                                            
                 E3Y.FNR_EKNR = 200;                                            
              END;                                                              
           ELSE                                                                 
              IF ^F_GYLDIG_FNR(E3Y.FNR_EK) THEN                                 
                 DO;                                                            
                    FEIL_FUNNET = '1'B;                                         
                    E3Y.FNR_EKNR = 1;                                           
                 END;                                                           
              ELSE                                                              
                 DO;                                                            
                    FNR_REG.FNR1 = E3Y.FNR_EK;                                  
                    EXEC CICS LINK PROGRAM ('R0019906')                         
                                   COMMAREA(FNR_REG   );                        
                    IF FNR_REG.FNR2 > 0 THEN                                    
                       DO;                                                      
                          E3Y.FNR_EKNR = 203;                                   
                          E3Y.FNR_GML_EK = E3Y.FNR_EK;                          
                          E3Y.FNR_EK = FNR_REG.FNR2;                            
                       END;                                                     
                    IF F_FELT_PIC_CHAR11(E3Y.FNR_SAMBOER) ^= (11)'0'            
                                                             THEN               
                       DO;                                                      
                          FEIL_FUNNET = '1'B;                                   
                          E3Y.FNR_EKNR      = 336;                              
                          E3Y.FNR_SAMBOERNR = 999;                              
                       END;                                                     
                    IF E3Y.FNR_EK ^= E3S.FNR                 THEN               
                       DO;                                                      
                          FEIL_FUNNET = '1'B;                                   
                          E3Y.FNR_EKNR = 335;                                   
                       END;                                                     
                 END;                                                           
        ELSE                                                                    
           IF F_FELT_PIC_CHAR11(E3Y.FNR_SAMBOER) ^= (11) '0' THEN               
              IF ^F_NUMERISK(F_FELT_PIC_CHAR11(E3Y.FNR_SAMBOER)) THEN           
                 DO;                                                            
                    FEIL_FUNNET = '1'B;                                         
                    E3Y.FNR_SAMBOERNR = 200;                                    
                 END;                                                           
              ELSE                                                              
                 IF ^F_GYLDIG_FNR(E3Y.FNR_SAMBOER) THEN                         
                    DO;                                                         
                       FEIL_FUNNET = '1'B;                                      
                       E3Y.FNR_SAMBOERNR = 1;                                   
                    END;                                                        
                 ELSE                                                           
                    DO;                                                         
                       FNR_REG.FNR1 = E3Y.FNR_SAMBOER;                          
                       EXEC CICS LINK PROGRAM ('R0019906')                      
                                      COMMAREA( FNR_REG  );                     
                       IF FNR_REG.FNR2 > 0 THEN                                 
                          DO;                                                   
                             E3Y.FNR_SAMBOERNR = 203;                           
                             E3Y.FNR_GML_SAMBOER = E3Y.FNR_SAMBOER;             
                             E3Y.FNR_SAMBOER = FNR_REG.FNR2;                    
                          END;                                                  
                       IF F_FELT_PIC_CHAR11(E3Y.FNR_EK) ^=                      
                                                     (11)'0' THEN               
                          DO;                                                   
                             FEIL_FUNNET = '1'B;                                
                             E3Y.FNR_EKNR      = 336;                           
                             E3Y.FNR_SAMBOERNR = 999;                           
                          END;                                                  
                       IF E3Y.FNR_SAMBOER ^= E3S.FNR          THEN              
                          DO;                                                   
                             FEIL_FUNNET = '1'B;                                
                             E3Y.FNR_SAMBOERNR = 335;                           
                          END;                                                  
                    END;                                                        
           ELSE                                                                 
              /* ****************************************** */                  
              /* VERKEN FNR_EK ELLER FNR_SAMBOER ER FYLT UT */                  
              /* ****************************************** */                  
              DO;                                                               
                 FEIL_FUNNET         = '1'B;                                    
                 E3Y.FNR_EKNR        = 327 ;                                    
              END;                                                              
                                                                                
        IF F_FELT_PIC_CHAR11(E3Y.FNR_Y_BARN) ^= (11)'0'  THEN                   
           DO;                                                                  
              FEIL_FUNNET         = '1'B;                                       
              E3Y.FNR_Y_BARNNR    =  326 ;                                      
           END;                                                                 
                                                                                
        IF E3Y.PLEIEBARN ^= ' '  THEN                                           
           DO;                                                                  
              FEIL_FUNNET         = '1'B;                                       
              E3Y.PLEIEBARNNR     = 326 ;                                       
           END;                                                                 
                                                                                
                                                                                
                                                                                
        IF ^FEIL_FUNNET THEN                                                    
           DO;                                                                  
              IF ^(E3Y.VILKÅR_1_2_2A   = ' '  !                                 
                   E3Y.VILKÅR_1_2_2B   = ' '  )  &                              
                   E3S.NORSK_BOSATT = 'J'  THEN                                 
                 DO;                                                            
                                                                                
        /* ********************************************************** */        
        /* VILKÅR 1-2-2 ER UTFYLT.  PERSONEN ER DA IKKE BOSATT I      */        
        /* NORGE, MEN EP-BLANKETTEN ER KRYSSET AV MED JA I FELTET     */        
        /* NORSK BOSATT.  BLANKETTEN AVVISES.                         */        
        /* JEG HAR IKKE SATT OPP NOE FEILMELDINGSNUMMER HER.          */        
        /* ********************************************************** */        
                                                                                
                    FEIL_FUNNET         = '1'B;                                 
                    E3Y.VILKÅR_1_2_2ANR =  318 ;                                
                    E3Y.VILKÅR_1_2_2BNR =  999 ;                                
                 END;                                                           
           END;                                                                 
        END KONTROLL_E3_YRKESSKADE;                                             
