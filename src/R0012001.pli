 /*   SIST ENDRET PÅ PROD   2008.05.31 11.27.32 AV   SPA2990          */        
 /*   SIST ENDRET PÅ PROD   2004.12.17 15.28.18 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.07.14 13.03.34 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2003.10.22 15.25.34 AV   SPA2970          */        
 /*   SIST ENDRET PÅ TEST   2003.10.22 15.22.00 AV   SPA2970          */        
 /*   SIST ENDRET PÅ PROD   2003.09.26 11.32.44 AV   SPA2970          */        
 /*   SIST ENDRET PÅ TEST   2003.09.26 11.31.23 AV   SPA2970          */        
 /*   SIST ENDRET PÅ PROD   2003.09.19 12.55.42 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2003.08.28  9.40.13 AV   SPA2970          */        
 /*   SIST ENDRET PÅ QASS   2003.08.22 13.44.00 AV   SPA2970          */        
 /*   SIST ENDRET PÅ TEST   2003.08.22 13.42.02 AV   SPA2970          */        
 /*   SIST ENDRET PÅ PROD   2003.04.02 10.01.21 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2003.04.01 15.24.49 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2003.02.27 12.41.39 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2002.11.21 13.33.28 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.07.09  9.36.38 AV   HLA2970          */        
 /*       SIST ENDRET 05/01-99 10.59.58 AV   JDA7339                  */        
 /*       SIST ENDRET 05/01-99 10.59.28 AV   JDA7339                  */        
 /*       SIST ENDRET 10/12-98 13.30.51 AV   JDA7339                  */        
 /*       SIST ENDRET 10/12-98 13.28.10 AV   JDA7339                  */        
 /*       SIST ENDRET 04/05-98 14.27.09 AV   JDA7339                  */        
 /*       SIST ENDRET 16/01-98 12.21.32 AV   SPA7339                  */        
 R00120:                                                                        
   PROC(COMMAREA_PEKER) OPTIONS(MAIN);                                          
                                                                                
                                                                                
 /* ***************************************************************** */        
 /*                                                                   */        
 /*IDENTIFIKASJON:                                                    */        
 /*     R0012001 - PROSEDYRE I PLI                                    */        
 /*     PROGRAMMERER: P. BERGESTAD, MARS 1982                         */        
 /*                                                                   */        
 /*HENSIKT:                                                           */        
 /*     RUTINEN TAR UTGANGSPUNKT I STYRINGSKODEN (BLANKETT REGISTRERT)*/        
 /*     OG OVERLATER KONTROLL TIL RUTINEN SOM BESTEMMER OM OG         */        
 /*     EVENTUELT HVORDAN "BEHANDLE-TRANS" SKAL ANVENDES.             */        
 /*                                                                   */        
 /*     DEN FORETAR OGSÅ INITIERINGS-KALLET HVOR PCB-ADDRESSENE       */        
 /*     OPPRETTES. FÅR EN FEIL I INITIERINGS-KALLET AVSLUTTES         */        
 /*     BEHANDLINGEN OG KONTROLLEN RETURNERES TIL    C I C S .        */        
 /*                                                                   */        
 /*     TRANSKODEN ER R200.                                           */        
 /*                                                                   */        
 /*                                                                   */        
 /*PROGRAMTILKNYTNING:                                                */        
 /*     PROGRAMMET KALLES OPP FRA R0010401 OG R0010480.               */        
 /*                                                                   */        
 /*BRUK:                                                              */        
 /*     EXEC CICS XCTL PROGRAM('R0012001') COMMAREA (KOM_OMR)         */        
 /*                                                                   */        
 /* ***************************************************************** */        
 %PAGE;                                                                         
                                                                                
   DCL                                                                          
      (COMMAREA_PEKER,BMSMAPBR)              PTR;                               
   DCL                                                                          
      (CSTG , STG, ADDR , UNSPEC , NULL , SUBSTR )       BUILTIN;               
      %INCLUDE S00118;  /*  PENSJON BREV          */                            
 %SKIP(2);                                                                      
      %INCLUDE P0019908;               /*  KOM_OMR         (BASED)  */          
 %SKIP;                                                                         
      %INCLUDE P0019906;               /*  TRANS_OPPL_OMR  (BASED)  */          
 %PAGE;                                                                         
      %INCLUDE P0019910;               /*  STYRINGS_OMR    (BASED)  */          
 %PAGE;                                                                         
      %INCLUDE P0019912;               /*  DIV_PARAM_OMR   (BASED)  */          
 %PAGE;                                                                         
      %INCLUDE P0019924;               /* G_V_TAB                   */          
 %PAGE;                                                                         
      %INCLUDE P0019925;               /* G_TAB                     */          
    /*                                                                          
    */                                                                          
    DCL SYSPRINT           EXTERNAL FILE;                                       
    /* ************************************************************** */        
    /*                                                                */        
    /*   DL1-PARAMETRE SOM BRUKES VED INITIERINGS- OG AVSLUTNINGS-    */        
    /*   KALL                                                         */        
    /*                                                                */        
    /**************************************************************** */        
       DCL                                                                      
         PLITDLI                     ENTRY;                                     
                                                                                
       DCL                                                                      
         1 W001_DLI                  UNALIGNED,                                 
                                                                                
           2  W01_PSB_R001           CHAR (8)       INIT('        '),           
           2  W01_PSB_NAVN           CHAR (8)       INIT('        '),           
           2  W01_PCB_FUNCTION       CHAR (4)       INIT('PCB '),               
           2  W01_TERM_FUNCTION      CHAR (4)       INIT('TERM'),               
           2  S_MELDNR               PIC '9'        INIT( 0 ),                  
           2  W01_PARM_CT_1          FIXED BIN(31)  INIT(1),                    
           2  W01_PARM_CT_3          FIXED BIN(31)  INIT(3);                    
                                                                                
    /* ************************************************************** */        
    /*                                                                */        
    /*   ARBEIDSFELTER SOM BRUKES AV UIB (USER INTERFACE BLOCK)       */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
    %INCLUDE DLIUIB;                                                            
                                                                                
       DCL                                                                      
           UIB_RC_OK                 BIT  (8)             INIT ( 0 );           
                                                                                
                                                                                
    /* ************************************************************** */        
    /*                                                                */        
    /*   PCB- OG UIB-OMR.                                             */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
    %INCLUDE  P0012002;                  /* PCB-UIB-PEKER OMR  BASED  */        
    %INCLUDE  P0014009;                  /* POTALL_OPPL   BASED  */             
                                                                                
                                                                                
 DCL 01 BRUKERINFO_REC,                                                         
        02 FNR                  PIC '(11)9',                                    
        02 BLANKET_TYPE         CHAR (2)   ,                                    
        02 TERMINAL_NR          CHAR (4)   ,                                    
        02 BRUKER_ID            CHAR (8)   ,                                    
        02 F1                   CHAR (5)   ,                                    
        02 DATO                 PIC '(8)9' ,                                    
        02 TID                  PIC '(7)9' ,      /* HHHMMSS  */                
        02 KJORINGS_TYPE        CHAR (1)   ,                                    
        02 FIL1                 CHAR (06)  ;                                    
                                                                                
  DCL  HJELP_TID         FIXED DEC(7,0);                                        
                                                                                
   DCL BRUK_LOGG     POINTER;                                                   
   DCL ANT_FEIL_SKREVET        FIXED DEC (3)  INIT (0);                         
    /* ************************************************************** */        
    /*   FEILMELDINGS-LINJE .                                         */        
    /* ************************************************************** */        
                                                                                
    DCL   W01_UTLINJE           CHAR (78)     INIT ( (78)' ' );                 
                                                                                
                                                                                
    DCL GRUNN_OMR1         CHAR      (1440);                                    
    DCL GRUNN_OMR2         CHAR      (1440);                                    
    DCL GRUNN_IDENT        CHAR      ( 8);                                      
                                                                                
                                                                                
    /* ************************************************************** */        
    /*                                                                */        
    /*   SLUTT PÅ DEKLARASJONENE .   EKSEKVERINGEN STARTER            */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
    ALLOCATE                    POTALL_OPPL  ;                                  
    ALLOCATE                    GV_TAB_RE    ;                                  
    ALLOCATE                    G_TAB_RE     ;                                  
    KOM_OMR.GV_PEKER         = ADDR (GRUNN_OMR1 ) ;                             
    KOM_OMR.G_PEKER          = ADDR (GRUNN_OMR2 ) ;                             
    POTALL_OPPL              =  ''           ;                                  
    GV_TAB_RE                =  ''           ;                                  
    G_TAB_RE                 =  ''           ;                                  
                                                                                
       W01_PSB_NAVN     =  DIV_PARAM_OMR.PSB_NAVN;                              
                                                                                
                                                                                
                                                                                
                                                                                
    GRUNN_IDENT                     = 'P0019924';                               
                                                                                
    CALL P9956_BER_G_CICS(GRUNN_IDENT,GRUNN_OMR1);                              
                                                                                
                                                                                
    GRUNN_IDENT                     = 'P0019925';                               
                                                                                
    CALL P9956_BER_G_CICS(GRUNN_IDENT,GRUNN_OMR2);                              
                                                                                
                                                                                
    IF KOM_OMR.PCB_UIB_PEKER =  NULL THEN                                       
       CALL ÅPNE_DATABASE;                                                      
    ELSE                                                                        
       UIBPTR =  PCB_UIB_PEKER_OMR.UIB_PEKER;                                   
                                                                                
    SELECT(STYREKODE);                                                          
       WHEN ('AP','A1','UP','U2','U3','EF','E4','FT','F7','FO','O1',            
             'O2', 'EP','E3','EE','FB','BP','B6','E1','EN','AF','KF')           
            DO;                                                                 
               EXEC CICS LINK PROGRAM('R0012010') COMMAREA(KOM_OMR);            
            END;                                                                
                                                                                
         OTHERWISE;                                                             
    END;                                                                        
                                                                                
    SELECT(STYREKODE);                                                          
                                                                                
         WHEN ( 'TG' )                                                          
            DO;                                                                 
               EXEC CICS LINK PROGRAM('R0011520') COMMAREA(KOM_OMR);            
               TRANSKODE = 'R041';                                              
            END;                                                                
                                                                                
         WHEN ( 'E2' )                                                          
            DO;                                                                 
               EXEC CICS LINK PROGRAM('R0011520') COMMAREA(KOM_OMR);            
               TRANSKODE = 'R031';                                              
            END;                                                                
         WHEN ( 'EP' , 'FB' , 'BP', 'B6', 'E3','EE' )                           
            DO;                                                                 
                                                                                
               KJØRINGS_TYPE = 'O';                                             
               EXEC CICS LINK PROGRAM('R0012201') COMMAREA(KOM_OMR);            
               TRANSKODE = 'R041';                                              
            END;                                                                
         WHEN ('US')                                                            
            DO;                                                                 
               EXEC CICS LINK PROGRAM('R0016101') COMMAREA(KOM_OMR);            
               TRANSKODE = 'R041';                                              
            END;                                                                
         WHEN ('AU')                                                            
            DO;                                                                 
                                                                                
               KJØRINGS_TYPE = 'O';                                             
               EXEC CICS LINK PROGRAM('R0011820') COMMAREA(KOM_OMR);            
                                                                                
            END;                                                                
                                                                                
         WHEN ('NI')                                                            
            DO;                                                                 
               EXEC CICS LINK PROGRAM('R001I501') COMMAREA(KOM_OMR);            
               TRANSKODE = 'R031';                                              
     /*        TRANSKODE = TRANS_RETURKODE; */                                  
            END;                                                                
                                                                                
         OTHERWISE                                                              
            DO;                                                                 
                                                                                
               IF STYREKODE = 'O2'             &                                
                                                                                
                  TRANSTYPE = 68               THEN                             
                                                                                
                  DO;                                                           
                                                                                
                    EXEC CICS LINK PROGRAM('R0016501')COMMAREA(KOM_OMR);        
                     TRANSKODE = 'R041';                                        
                                                                                
                  END;                                                          
                                                                                
               ELSE                                                             
                                                                                
                  DO;                                                           
                                                                                
                                                                                
                    KJØRINGS_TYPE = 'O';                                        
                    EXEC CICS LINK PROGRAM('R0013001')COMMAREA(KOM_OMR);        
                                                                                
                     IF TRANS_RETURKODE = 'R040' THEN                           
                        TRANSKODE = 'R040';                                     
                     ELSE                                                       
                        TRANSKODE = 'R041';                                     
                                                                                
                  END;                                                          
                                                                                
            END;   /* SLUTT PÅ 'OTHERWISE' */                                   
                                                                                
      END;     /* SLUTT PÅ 'SELECT (STYREKODE)' */                              
                                                                                
                                                                                
   /* ****************************************************** */                 
   /* SKRIV BRUKINFO              REGISTERING                */                 
   /* ****************************************************** */                 
      /*  CALL SKRIV_BRUKER_ID; */                                              
     /* BRUKERINFO_REC.FNR           = DIV_PARAM_OMR.SEARCH_FNR;                
        DETTE ER ENDRET AV MARTIN - FIKK EKTEFELLEN NOEN GANGER */              
        BRUKERINFO_REC.FNR           = TRANS_OPPL_OMR.FØDSNUMMER;               
                                                                                
        BRUKERINFO_REC.BLANKET_TYPE  = STYRINGS_OMR.STYREKODE;                  
        BRUKERINFO_REC.BRUKER_ID     = DIV_PARAM_OMR.BRUKER_ID;                 
        BRUKERINFO_REC.DATO          = DATO_2000;                               
        BRUKERINFO_REC.TERMINAL_NR   =                                          
           KONV_HÅMD_MÅ(TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD);                      
   /*   BRUKERINFO_REC.TERMINAL_NR   = EIBTRMID;      */                        
        BRUKERINFO_REC.F1 = SUBSTR(DIV_PARAM_OMR.CICS_NAVN,1,4);                
        HJELP_TID                    = EIBTIME;                                 
        BRUKERINFO_REC.TID           = HJELP_TID;                               
        BRUKERINFO_REC.KJORINGS_TYPE = STYRINGS_OMR.FUNKSJONSKODE   ;           
     /* BRUKERINFO_REC.BRUKER_NAVN   = DIV_PARAM_OMR.BRUKER_PASS; */            
                                                                                
        IF BRUKERINFO_REC.BLANKET_TYPE  ^= 'US' THEN                            
           EXEC CICS WRITE DATASET('BRUKINFO')                                  
                    FROM (BRUKERINFO_REC)                                       
                    RIDFLD(BRUK_LOGG) RBA;                                      
     /* ****************************************************** */               
     /* SKRIV STATISTIKK REPORT FOR REGISTERING                */               
     /* ****************************************************** */               
          CALL UPDATE_STATISTIKK_REG (STYREKODE);                               
                                                                                
    /* ************************************************************** */        
    /*                                                                */        
    /*   TERMINERINGSKALLET FOR PCB'ENE                               */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
                                                                                
    IF FUNKSJONSKODE = 'V' !                                                    
       FUNKSJONSKODE = 'B' THEN                                                 
       DO;                                                                      
          TRANSKODE = 'R48B';                                                   
                                                                                
                                                                                
          CALL      PLITDLI               (W01_PARM_CT_1,                       
                                           W01_TERM_FUNCTION);                  
          KOM_OMR.PCB_UIB_PEKER = NULL;                                         
                                                                                
                                                                                
          EXEC CICS  XCTL  PROGRAM('R0010480')  COMMAREA(KOM_OMR);              
       END;                                                                     
    ELSE                                                                        
                                                                                
    IF FUNKSJONSKODE = 'P'  THEN                                                
       DO;                                                                      
                                                                                
          CALL   PLITDLI                        (W01_PARM_CT_1,                 
                                                 W01_TERM_FUNCTION);            
                                                                                
                                                                                
          DIV_PERIODE                   = '' ;                                  
          DIV_PARAM_OMR.FRA_CICS        = '1'B;                                 
          DIV_PARAM_OMR.HENT_FRAM_MAP   = '0'B;                                 
          DIV_PARAM_OMR.FRA_MED_DIALOG  = '0'B;                                 
          DIV_PARAM_OMR.FRA_UTEN_DIALOG = '0'B;                                 
          KOM_OMR.PCB_UIB_PEKER = NULL;                                         
          STYRINGS_OMR.STYREKODE  = 'AU';                                       
          ALLOCATE S001181O;                                                    
      /*  ALLOCATE S001181I; */                                                 
          EXEC CICS LOAD PROGRAM('S001183');                                    
          S001181O.ROLLEO =  DIV_PARAM_OMR.ATK_ROLLE;                           
          S001181O.CICS_INFOO = DIV_PARAM_OMR.CICS_NAVN ;                       
          S001181I.FNRL     = -1;                                               
          IF FEIL_MELD_NR > 0  THEN                                             
             DO;                                                                
               CALL SKRIV_FEIL((FEIL_MELD_NR));                                 
              EXEC CICS SEND MAP('S001181')                                     
                           MAPSET('S001183') DATAONLY;                          
             FEIL_MELD_NR = 0 ;                                                 
             END;                                                               
          ELSE                                                                  
              EXEC CICS SEND MAP('S001181')                                     
                               MAPSET('S001183') CURSOR ERASE;                  
                                                                                
          TRANSKODE = 'R180';                                                   
          EXEC CICS RETURN TRANSID(TRANSKODE) COMMAREA(KOM_OMR);                
     /*   EXEC CICS  XCTL  PROGRAM('R0010301')  COMMAREA(KOM_OMR); */           
                                                                                
       END;                                                                     
                                                                                
    ELSE                                                                        
    IF FUNKSJONSKODE = 'T' !                                                    
       FUNKSJONSKODE = 'N' THEN                                                 
       DO;                                                                      
                                                                                
          CALL   PLITDLI                        (W01_PARM_CT_1,                 
                                                 W01_TERM_FUNCTION);            
                                                                                
                                                                                
          EXEC CICS  XCTL  PROGRAM('R0010301')  COMMAREA(KOM_OMR);              
                                                                                
       END;                                                                     
    ELSE                                                                        
       DO;                                                                      
                                                                                
          CALL   PLITDLI                        (W01_PARM_CT_1,                 
                                                 W01_TERM_FUNCTION);            
                                                                                
          EXEC CICS  XCTL  PROGRAM('R0010401')  COMMAREA(KOM_OMR);              
                                                                                
       END;                                                                     
                                                                                
                                                                                
 ÅPNE_DATABASE:                                                                 
   PROC;                                                                        
    CALL   PLITDLI                              (W01_PARM_CT_3,                 
                                                 W01_PCB_FUNCTION,              
                                                 W01_PSB_NAVN,                  
                                                 UIBPTR);                       
                                                                                
                                                                                
    IF DLIUIB.UIBFCTR                      ^=    UIB_RC_OK       THEN           
      DO;                                                                       
        /* ********************************************************** */        
        /*                                                            */        
        /*     FEIL I INITIERINGKALL                                  */        
        /*                                                            */        
        /* ********************************************************** */        
                                                                                
        W01_UTLINJE    =    'FEIL VED INITIERINGS-KALLET :'         !!          
                            '     UIBFCTR = ' !!  UNSPEC (UIBFCTR)  !!          
                            '     UIBDLTR = ' !!  UNSPEC (UIBDLTR)  ;           
                                                                                
        EXEC CICS SEND TEXT FROM ( W01_UTLINJE )  ACCUM  PAGING ERASE           
                       JUSTIFY   ( 14          )  ;                             
        EXEC CICS SEND PAGE;                                                    
        EXEC CICS      RETURN;                                                  
                                                                                
      END;                                                                      
                                                                                
    ALLOCATE  PCB_UIB_PEKER_OMR;                                                
                                                                                
    /* ********************************************************** */            
    /*                                                            */            
    /*  SETTER  PCB_PEKER  =  UIBPCBAL  FRA INITIERINGS-KALLET    */            
    /*                                                            */            
    /* ********************************************************** */            
                                                                                
    PCB_UIB_PEKER_OMR.UIB_PEKER          =    UIBPTR  ;                         
    PCB_UIB_PEKER_OMR.PCB_PEKER          =    UIBPCBAL;                         
                                                                                
 END ÅPNE_DATABASE;                                                             
                                                                                
    /* ************************************************************** */        
    /*                                                                */        
    /*   PROC FOR OPPTATERING AV STATISTIKK REG.                      */        
    /*                                                                */        
    /*   ENDERET AV SATISH 050489                                     */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
 %PAGE;                                                                         
 %INCLUDE R0019956;          /*     P9956_BER_G_CICS    */                      
                                                                                
 %INCLUDE R0012004;          /*     STATISKK-REG        */                      
 %INCLUDE R0019944;          /*     SKRIV_FEIL          */                      
 %INCLUDE R0019989;          /*     KONV_HÅMD_MÅ        */                      
                                                                                
   END R00120;                                                                  
