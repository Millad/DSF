 /*   SIST ENDRET PÅ PROD   2002.06.21 14.35.21 AV   SPA2970          */        
 /*   SIST ENDRET PÅ PROD   2002.06.21 13.29.19 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2002.05.13 13.55.51 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2002.04.24 14.53.36 AV   JDA2970          */        
 /*       SIST ENDRET 03/09-98 14.16.51 AV   SPA7339                  */        
                                                                                
                                                                                
  /* **************************************************************** */        
  /*IDENTIFIKASJON:                                                   */        
  /*    R0019H50 - HOVEDPROGRAM I PLI                                 */        
  /*    PROGRAMMERER: LUNDEBY FEBRUAR  1982                           */        
  /*                                                                  */        
  /*HENSIKT:                                                          */        
  /*    SKRIVER STATUS TIL DATABASEN .BENYTTER REPLACE .              */        
  /*                                                                  */        
  /*PROGRAMTILKNYTNING:                                               */        
  /*    R005430  -->  SKRIVER  ENDRET SISTE STATUS                    */        
  /*                                                                  */        
  /*BRUK:                                                             */        
  /*                                                                  */        
  /* **************************************************************** */        
                                                                                
  R0019H5 :                                                                     
    PROC            (COMMAREA_PEKER)                                            
           OPTIONS  (MAIN);                                                     
                                                                                
                                                                                
    %PAGE;                                                                      
    %INCLUDE P0019908;                            /* KOM_OMR        */          
                                                                                
    %PAGE;                                                                      
    %INCLUDE P0019910;                            /* STYRINGS_OMR   */          
                                                                                
    %PAGE;                                                                      
    %INCLUDE P0019912;                            /* DIV_PARAM_OMR  */          
                                                                                
    %PAGE;                                                                      
       DCL 1  B02              BASED (B02_PEKER),                               
    %INCLUDE P0019921;                            /* B02            */          
                                                                                
                                                                                
    %PAGE;                                                                      
    /* ************************************************************ */          
    /*                                                              */          
    /*     INNEBYGDE PLI-FUNKSJONER                                 */          
    /*                                                              */          
    /* ************************************************************ */          
                                                                                
     DCL                                                                        
        ( ADDR , LOW , CSTG , DATE )  BUILTIN;                                  
     DCL                                                                        
          PLITDLI ENTRY;                                                        
     DCL                                                                        
         COMMAREA_PEKER            POINTER;                                     
     DCL W01_HJELPE_PTR            POINTER;                                     
     DCL W01_PTR                   POINTER;                                     
                                                                                
    /* ************************************************************ */          
    /*                                                              */          
    /* HJELPE-OMR. FOR BUILTIN-FUNC.     :    DATE                  */          
    /*                                                              */          
    /* ************************************************************ */          
                                                                                
     DCL    W01_DATE                         CHAR (6) INIT ('');                
                                                                                
     DCL    W01_ÅR  DEF     W01_DATE         PIC '99' ;                         
                                                                                
    /* ************************************************************ */          
    /*                                                              */          
    /* OPPTELINGS- OG LOOP-PARAMETRE M.M                            */          
    /*                                                              */          
    /* ************************************************************ */          
                                                                                
     DCL 1  W01_OPPTELL_OMR,                                                    
            2   W02_I              FIXED    BIN   (15)   INIT ( 0 ),            
            2   W02_ÅR             FIXED    BIN   (15)   INIT ( 0 ),            
            2   I                  FIXED    BIN   (15)   INIT ( 0 ),            
            2   J                  FIXED    BIN   (15)   INIT ( 0 ),            
            2   K_I                FIXED    BIN   (15)   INIT ( 0 ),            
            2   B                           CHAR  (1)    INIT (' ');            
                                                                                
    /* ************************************************************ */          
    /*                                                              */          
    /* DLI CALL-PARAMETRE                                           */          
    /*                                                              */          
    /* ************************************************************ */          
                                                                                
     DCL 1  W01_DLI_PARAM,                                                      
            2   W02_DLI_FUNC       CHAR (4)       INIT('    '),                 
            2   W02_GN             CHAR (4)       INIT('GN  '),                 
            2   W02_GU             CHAR (4)       INIT('GU  '),                 
            2   W02_GHNP           CHAR (4)       INIT('GHNP'),                 
            2   W02_GHU            CHAR (4)       INIT('GHU '),                 
            2   W02_DLET           CHAR (4)       INIT('DLET'),                 
            2   W02_REPL           CHAR (4)       INIT('REPL'),                 
            2   W02_ISRT           CHAR (4)       INIT ('ISRT'),                
            2   W02_PARM_CT_3      FIXED BIN(31)  INIT (3),                     
            2   W02_PARM_CT_4      FIXED BIN(31)  INIT (4),                     
            2   W02_PARM_CT_5      FIXED BIN(31)  INIT (5),                     
            2   W02_PARM_CT_6      FIXED BIN(31)  INIT (6),                     
            2   W02_PARM_CT_7      FIXED BIN(31)  INIT (7),                     
            2   W02_OUT_FELT       PIC '(11)9'    INIT (''),                    
            2   W02_DATO           PIC '(4)9'     INIT (''),                    
            2   SAVE_AFPDATO_ÅMD   DEC FIXED (9)  INIT (0),                     
            2   W02_ST_OK          BIT (1)        INIT (''),                    
            2   NEI                BIT (1)        INIT ('0'B),                  
            2   W02_OUT2           CHAR (20)      INIT ('');                    
                                                                                
     DCL    W01_IO                 CHAR (150)    INIT  ('');                    
     DCL    W01_HJELPE_IO          CHAR (150)    BASED (W01_HJELPE_PTR);        
                                                                                
     DCL  1 W01_IO_AFPHIST        BASED (W01_PTR),                              
            %INCLUDE P0019971;                                                  
                                                                                
     DCL  1 W01_HJELPE_IO_AFPHIST    BASED (W01_HJELPE_PTR),                    
            %INCLUDE P0019971;                                                  
    %PAGE;                                                                      
    /* ************************************************************ */          
    /*                                                              */          
    /* DL1 SSA-OMRÅDER FOR ROT OG STATUS-SEGMENTER.                 */          
    /*                                                              */          
    /* ************************************************************ */          
                                                                                
     DCL    SSA_UQUAL                            CHAR (9) INIT ((9)' ');        
                                                                                
     DCL 1  SSA1_RF0PERSN          UNALIGNED,                                   
            2    HDEL                            CHAR (17)  INIT                
                 ('RF0PERSN(FNR     '),                                         
            2    REL_OP                          CHAR (2)   INIT (' ='),        
            2    PKEY               FIXED        DEC  (11)  INIT ( 0 ),         
            2    HP                              CHAR (1)   INIT (')');         
                                                                                
     DCL 1  SSA1_STATUS            UNALIGNED,                                   
            2    HDEL                            CHAR (17)  INIT                
                 ('STATUS  (STATKODE'),                                         
            2    REL_OP                          CHAR (2)   INIT (' ='),        
            2    KODE                            CHAR (1)   INIT ('S'),         
            2    HP                              CHAR (1)   INIT (')');         
                                                                                
                                                                                
    /* ************************************************************ */          
    /*                                                              */          
    /* ARBEIDSFELTER SOM BRUKES AV UIB (USER INTERFACE BLOCK)       */          
    /*                                                              */          
    /* ************************************************************ */          
                                                                                
    %INCLUDE DLIUIB;                                                            
                                                                                
     DCL                                                                        
         UIB_RC_OK                 BIT (8)       INIT( 0 );                     
                                                                                
                                                                                
    /* ************************************************************ */          
    /*                                                              */          
    /* PCB- OG UIB-OMR .                                            */          
    /*                                                              */          
    /* ************************************************************ */          
                                                                                
     %INCLUDE   P0012002;                /* PCB_UIB_PEKER_OMR BASED */          
                                                                                
     DCL 1  RF1                     BASED (RF1_PCB_PEKER),                      
     %INCLUDE   P0012003;                /*    PCB                 */           
                                                                                
                                                                                
                                                                                
    %PAGE;                                                                      
    /* ************************************************************ */          
    /*                                                              */          
    /*        SLUTT PÅ DEKLARASJONENE , EKSEKVERINGEN STARTER .     */          
    /*       ---------------------------------------------------    */          
    /*                                                              */          
    /*                                                              */          
    /*        INITIERER FORSKJELLIGE PARAMETRE .                    */          
    /*                                                              */          
    /* ************************************************************ */          
                                                                                
    W01_DATE                             =       DATE;                          
    PROGRAM_ID                           =      'R0019H50';                     
    UIBPTR                               =       UIB_PEKER;                     
    W01_PTR                              =       ADDR(W01_IO);                  
                                                                                
    DO   W02_I  =  1  TO  14;                                                   
                                                                                
      IF B02.FNR  (W02_I)           >    0                  THEN                
         DO;                                                                    
            SSA1_RF0PERSN.PKEY      =    B02.FNR (W02_I);                       
                                                                                
            IF B02.SPERRE (W02_I)   =    'J'                THEN                
               DO;                                                              
                  /* ********************************************* */           
                  /*  NYE OPPLYSNINGER LEGGES INN I :  RF0PERSN    */           
                  /* ********************************************* */           
                                                                                
                  CALL PLITDLI              (W02_PARM_CT_4,                     
                                             W02_GHU,                           
                                             RF1,                               
                                             W01_IO,                            
                                             SSA1_RF0PERSN);                    
                                                                                
                  IF UIBFCTR         ^=      UIB_RC_OK         THEN             
                     DO;                                                        
                       /* ****************************************** */         
                       /*   UAKSEPTABEL KODE FRA :    UIB            */         
                       /* ****************************************** */         
  L100:                                                                         
                       FEIL_MELD_NR     =     500;                              
                       FEIL_VED_LABEL   =    '100';                             
                       GO TO    L999;                                           
                     END;                                                       
                                                                                
                  W01_HJELPE_PTR  =  ADDR (B02.RF0PERSN (W02_I)) ;              
                  W01_IO          =  W01_HJELPE_IO               ;              
                                                                                
                  SELECT (RF1.STATUS_KODE);                                     
                                                                                
                    WHEN ('  ')                                                 
                      DO;                                                       
                        CALL PLITDLI              (W02_PARM_CT_3,               
                                                   W02_REPL,                    
                                                   RF1,                         
                                                   W01_IO);                     
                                                                                
                        IF  UIBFCTR          ^=   UIB_RC_OK                     
                          ! RF1.STATUS_KODE  ^=   '  '        THEN              
                          DO;                                                   
                            /* ********************************** */            
                            /* UAKSEPTABEL KODE FRA :  UIB        */            
                            /*                 RF1.STATUS_KODE    */            
                            /* ********************************** */            
  L110:                                                                         
                            FEIL_MELD_NR    =     500;                          
                            FEIL_VED_LABEL  =    '110';                         
                            DB_STATUS_KODE  =     RF1.STATUS_KODE;              
                            GO TO     L999;                                     
                          END;                                                  
                       END;                                                     
                                                                                
                    WHEN ('GE')                                                 
                       DO;                                                      
                         /* **************************************** */         
                         /* EN DUMMY-ROT SKULLE EKSISTERT            */         
                         /* **************************************** */         
  L120:                                                                         
                         FEIL_MELD_NR            =    501;                      
                         FEIL_VED_LABEL          =   '120';                     
                         DB_STATUS_KODE          =    RF1.STATUS_KODE;          
                         GO TO     L999;                                        
                       END;                                                     
                                                                                
                    OTHERWISE                                                   
                       DO;                                                      
                         /* ************************************* */            
                         /*  UGYLDIG STATUSKODE FRA DLI           */            
                         /* ************************************* */            
  L130:                                                                         
                         FEIL_MELD_NR            =    500;                      
                         FEIL_VED_LABEL          =   '130';                     
                         DB_STATUS_KODE          =    RF1.STATUS_KODE;          
                         GO TO     L999;                                        
                      END;                                                      
                                                                                
                  END;   /*     SELECT  :   RF1.STATUS_KODE     */              
                                                                                
               END;      /*     IF      :   B02.SPERRE (W02_I)  */              
                                                                                
                                                                                
            /* ************************************************ */              
            /* SKRIVER UT :    S I S T E   S T A T U S          */              
            /* **************************************************/              
                                                                                
            B02.STATUS_KODE (W02_I)        =   'S';                             
            SSA1_STATUS.KODE               =   'S';                             
                                                                                
            IF  B02.VIRK_DATO_ÅMD(W02_I)   >    0    THEN                       
              DO;                                                               
                CALL   P_SKRIV_STATUS_YTELSER;                                  
                                                                                
                IF FEIL_MELD_NR         >    0    THEN                          
                   GO TO L999;                                                  
              END;                                                              
                                                                                
         END;      /*   IF    FNR   >   0                  */                   
                                                                                
    END;          /*   DO    I = 1  TO   14               */                    
                                                                                
                                                                                
                                                                                
                                                                                
    /* ************************************************************** */        
    /* ************************************************************** */        
    /* ********                                      **************** */        
    /* ********     INTERNE    PROCEDURER            **************** */        
    /* ********                                      **************** */        
    /* ************************************************************** */        
    /* ************************************************************** */        
                                                                                
                                                                                
                                                                                
                                                                                
    %PAGE;                                                                      
    /* ************************************************************** */        
    /*                                                                */        
    /* PROCEDURE FOR Å REPLACE             :  STATUS                  */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
    P_SKRIV_BARN_AV_ROT:                                                        
      PROC;                                                                     
                                                                                
      CALL PLITDLI                              (W02_PARM_CT_5,                 
                                                 W02_GHU,                       
                                                 RF1,                           
                                                 W01_IO,                        
                                                 SSA1_RF0PERSN,                 
                                                 SSA1_STATUS);                  
                                                                                
      IF  UIBFCTR                          ^=   UIB_RC_OK  THEN                 
        DO;                                                                     
          /* ******************************************************** */        
          /*  UAKSEPTABEL KODE FRA  :   UIB                           */        
          /* ******************************************************** */        
  L140:                                                                         
          FEIL_MELD_NR                      =   500;                            
          FEIL_VED_LABEL                    =  '140';                           
          GO   TO  RETUR;                                                       
        END;                                                                    
                                                                                
      W01_IO                                =    W01_HJELPE_IO;                 
                                                                                
      IF RF1.STATUS_KODE                    =    '  '             THEN          
        DO;                                                                     
          /* ******************************************************** */        
          /*  STATUS FANTES FRA FØR . NY OPPLYSNINGER LEGGES INN .    */        
          /* ******************************************************** */        
                                                                                
          CALL PLITDLI                          (W02_PARM_CT_3,                 
                                                 W02_REPL,                      
                                                 RF1,                           
                                                 W01_IO);                       
                                                                                
          IF  RF1.STATUS_KODE              ^=    '  '                           
            ! UIBFCTR                      ^=   UIB_RC_OK  THEN                 
            DO;                                                                 
            /* **************************************************** */          
            /*  UAKSEPTABEL KODE FRA  :   UIB                       */          
            /*                            RF1.STATUS_KODE           */          
            /* **************************************************** */          
  L150:                                                                         
            FEIL_MELD_NR              =   500;                                  
            FEIL_VED_LABEL            =  '150';                                 
            DB_STATUS_KODE            =   RF1.STATUS_KODE;                      
            GO TO     RETUR;                                                    
            END;                                                                
        END;                                                                    
      ELSE                                                                      
        DO;                                                                     
          /* **************************************************** */            
          /*  FEIL KODE VED KALL :   GHU                          */            
          /* **************************************************** */            
  L160:                                                                         
          FEIL_MELD_NR              =   500;                                    
          FEIL_VED_LABEL            =  '160';                                   
          DB_STATUS_KODE            =   RF1.STATUS_KODE;                        
          GO TO     RETUR;                                                      
        END;                                                                    
                                                                                
  RETUR:                                                                        
    END P_SKRIV_BARN_AV_ROT;                                                    
                                                                                
                                                                                
                                                                                
    %PAGE;                                                                      
    /* ************************************************************** */        
    /*                                                                */        
    /* PROCEDURE FOR Å REPLACE             :  UNDERSEGM. TIL STATUS   */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
    P_SKRIV_BARN_AV_STATUS :                                                    
      PROC;                                                                     
                                                                                
      CALL PLITDLI                              (W02_PARM_CT_4,                 
                                                 W02_GHNP,                      
                                                 RF1,                           
                                                 W01_IO,                        
                                                 SSA_UQUAL);                    
                                                                                
      IF  UIBFCTR                          ^=   UIB_RC_OK  THEN                 
        DO;                                                                     
          /* ******************************************************** */        
          /*  UAKSEPTABEL KODE FRA  :   UIB                           */        
          /* ******************************************************** */        
  L170:                                                                         
          FEIL_MELD_NR                      =   500;                            
          FEIL_VED_LABEL                    =  '170';                           
          GO   TO  RETUR;                                                       
        END;                                                                    
                                                                                
 /* ENDRET AV SATISH      13.05.2002  DENNE */                                  
      IF SSA_UQUAL = 'AFPHIST  '  &                                             
         W01_IO_AFPHIST.AFPDATO_ÅMD ^=                                          
                   W01_HJELPE_IO_AFPHIST.AFPDATO_ÅMD   THEN                     
         DO;                                                                    
              SAVE_AFPDATO_ÅMD =  W01_IO_AFPHIST.AFPDATO_ÅMD;                   
              W01_IO           =    W01_HJELPE_IO;                              
              CALL P_SKRIV_SEGM;                                                
              IF SAVE_AFPDATO_ÅMD >                                             
                            W01_HJELPE_IO_AFPHIST.AFPDATO_ÅMD THEN              
                 K_I = K_I - 1   ;                                              
              W01_HJELPE_IO_AFPHIST.AFPDATO_ÅMD = SAVE_AFPDATO_ÅMD;             
              GOTO RETUR;                                                       
         END;                                                                   
      ELSE                                                                      
         W01_IO               =    W01_HJELPE_IO;                               
                                                                                
      SELECT   ( RF1.STATUS_KODE );                                             
         WHEN  ( '  '            )                                              
            DO;                                                                 
               /* ************************************************** */         
               /* SEGM.FANTES FRA FØR . NY OPPLYSNINGER LEGGES INN . */         
               /* ************************************************** */         
                                                                                
               CALL PLITDLI                     (W02_PARM_CT_3,                 
                                                 W02_REPL,                      
                                                 RF1,                           
                                                 W01_IO);                       
                                                                                
               IF  RF1.STATUS_KODE         ^=    '  '                           
                 ! UIBFCTR                 ^=    UIB_RC_OK  THEN                
                 DO;                                                            
                   /* ******************************************* */            
                   /*  FEIL I KALL MOT DB                         */            
                   /* ******************************************* */            
  L180:                                                                         
                   FEIL_MELD_NR             =   500;                            
                   FEIL_VED_LABEL           =  '180';                           
                   DB_STATUS_KODE           =   RF1.STATUS_KODE;                
                   GO TO     RETUR;                                             
                 END;                                                           
            END;                                                                
                                                                                
        WHEN  ( 'GE')                                                           
           DO;                                                                  
              /* **************************************************** */        
              /*  SEGM FANTES IKKE FRA FØR . NYTT LEGGES INN .        */        
              /* **************************************************** */        
                                                                                
              CALL P_SKRIV_SEGM;                                                
                                                                                
              IF FEIL_MELD_NR          >   0                       THEN         
                 GO TO     RETUR;                                               
           END;                                                                 
                                                                                
        OTHERWISE                                                               
           DO;                                                                  
             /* **************************************************** */         
             /*  FEIL KODE VED KALL :   GHU                          */         
             /* **************************************************** */         
  L190:                                                                         
             FEIL_MELD_NR              =   500;                                 
             FEIL_VED_LABEL            =  '190';                                
             DB_STATUS_KODE            =   RF1.STATUS_KODE;                     
             GO TO     RETUR;                                                   
           END;                                                                 
     END;/*       SELECT   ( RF1:STATUS_KODE )                     */           
                                                                                
  RETUR:                                                                        
    END P_SKRIV_BARN_AV_STATUS;                                                 
                                                                                
                                                                                
                                                                                
    %PAGE;                                                                      
    /* ************************************************************ */          
    /*                                                              */          
    /* PROCEDURE FOR A SKRIVE UNDERLIGGENDE SEGMENTER TIL           */          
    /*                                                              */          
    /* STATUS-SEGM.                                                 */          
    /*                                                              */          
    /* ************************************************************ */          
                                                                                
    P_SKRIV_SEGM:                                                               
      PROC;                                                                     
                                                                                
      CALL PLITDLI                         (W02_PARM_CT_6,                      
                                            W02_ISRT,                           
                                            RF1,                                
                                            W01_IO,                             
                                            SSA1_RF0PERSN,                      
                                            SSA1_STATUS,                        
                                            SSA_UQUAL);                         
                                                                                
      IF   DLIUIB.UIBRCODE.UIBFCTR    ^=    UIB_RC_OK                           
        !  RF1.STATUS_KODE            ^=   '  '    THEN                         
        DO;                                                                     
           /* ******************************************************* */        
           /* UAKSEPTABEL KODE FRA   :      UIB                       */        
           /*                               RF1.STATUS_KODE           */        
           /* ******************************************************* */        
  L200:                                                                         
           FEIL_MELD_NR                     =     500;                          
           FEIL_VED_LABEL                   =    '200';                         
           DB_STATUS_KODE                   =     RF1.STATUS_KODE;              
        END;                                                                    
    END P_SKRIV_SEGM;                                                           
                                                                                
                                                                                
    %PAGE;                                                                      
    %INCLUDE R0019H55; /* PROC. FOR Å SKRIVE : SISTE STATUS YTELSER  */         
                                                                                
                                                                                
  L999:                                                                         
                                                                                
    EXEC CICS RETURN;                                                           
  END  R0019H5;                                                                 
