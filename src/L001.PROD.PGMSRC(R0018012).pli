 /*   SIST ENDRET PÅ PROD   2004.12.27 13.03.57 AV   SPA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.12.27 13.03.01 AV   SPA2970          */        
 /*   SIST ENDRET PÅ PROD   2004.07.16 12.54.24 AV   JDA2970          */        
 /*       SIST ENDRET 09/06-98 08.42.20 AV   SPA7339                  */        
 /*       SIST ENDRET 04/05-98 14.17.07 AV   JDA7339                  */        
 /* ***************************************************************** */        
 /*IDENTIFIKASJON:                                                    */        
 /* **********************                                            */        
 /*  PROGRAM-IDENT : R0018012  MAP_FRA_TRANS_LISTE    HOVEDPROGRAM    */        
 /*  PROGRAM-SPRÅK : PLI/CICS                                         */        
 /*  PROGRAMMERER  : SATISH PATHAK                                    */        
 /*  PROGRAMMET BLE LAGET : DESEMBER 2003                             */        
 /* ***************************************************************** */        
 /*HENSIKT:                                                           */        
 /* **********                                                        */        
 /*    PROGRAMMET SKAL BYGGE OPP SIDER SOM BESTÅR AV BLANKETTMAPENE.  */        
 /*    DETTE GJØRES UT FRA EN LISTE AV TRANSER SOM ER SORTERT På      */        
 /*    FORHÅND.                                                       */        
 /*    ALLE MAPS HØRER INN UNDER MAPSET 'S0010T'.                     */        
 /*                                                                   */        
 /* ***************************************************************** */        
 /*PROGRAMTILKNYTTING:                                                */        
 /* ********************                                              */        
 /*    PROGRAMMET BLIR GITT KONTROLL FRA R0010410 - FORESPØRSEL ELLER */        
 /*                                  FRA R0010450 - ENDRINGER.        */        
 /*    PROGRAMMETS TRANSKODE ER F41D.                                 */        
 /*    BRUK :                                                         */        
 /*    EXEC CICS XCTL PROGRAM ('R0018012') COMMAREA(KOM_OMR);         */        
 /*    PROGRAMMET INCLUDER :                                          */        
 /*               R0010506   FLYTT FRA ARB.OMRÅDE TIL MAP    AP       */        
 /*               R0010507   FORTEKSTER OG ATTRIBUTTER       AP       */        
 /*               R0010606   FLYTT FRA ARB.OMRÅDE TIL MAP    UP       */        
 /*               R0010607   FORTEKSTER OG ATTRIBUTTER       UP       */        
 /*               R0010706   FLYTT FRA ARB.OMRÅDE TIL MAP    ST       */        
 /*               R0010806   FLYTT FRA ARB.OMRÅDE TIL MAP    EP       */        
 /*               R0010807   FORTEKSTER OG ATTRIBUTTER       EP       */        
 /*               R0010906   FLYTT FRA ARB.OMRÅDE TIL MAP    EF       */        
 /*               R0010907   FORTEKSTER OG ATTRIBUTTER       EF       */        
 /*               R0011006   FLYTT FRA ARB.OMRÅDE TIL MAP    FB       */        
 /*               R0011106   FLYTT FRA ARB.OMRÅDE TIL MAP    BP       */        
 /*               R001NB06   FLYTT FRA ARB.OMRÅDE TIL MAP    B6       */        
 /*               R0011206   FLYTT FRA ARB.OMRÅDE TIL MAP    FT       */        
 /*               R0011207   FORTEKSTER OG ATTRIBUTTER       FT       */        
 /*               R0011406   FLYTT FRA ARB.OMRÅDE TIL MAP    E1       */        
 /*               R001UE06   FLYTT FRA ARB.OMRÅDE TIL MAP    EN       */        
 /*               R0011506   FLYTT FRA ARB.OMRÅDE TIL MAP    E2       */        
 /*               R0011606   FLYTT FRA ARB.OMRÅDE TIL MAP    O1       */        
 /*               R0011706   FLYTT FRA ARB.OMRÅDE TIL MAP    O2       */        
 /*               R0011836   FLYTT FRA ARB.OMRÅDE TIL MAP    UF       */        
 /*               R0019912   PIC TIL CHAR OG MOTSATT                  */        
 /*                                                                   */        
 /* ***************************************************************** */        
 /*DATASETTOPPLYSNINGER:                                              */        
 /* ***********************                                           */        
 /*  DET BLIR IKKE BRUKT NOEN DATASETT I DETTE PROGRAMMET             */        
 /*                                                                   */        
 /* ***************************************************************** */        
 /*FEILMELDINGER:                                                     */        
 /* *******************                                               */        
 /* PROGRAMMET GIR IKKE NOEN FEILMELDINGER.                           */        
 /*                                                                   */        
 /* ***************************************************************** */        
                                                                                
 R001801: PROC(LOKAL_KOM_PTR) OPTIONS (MAIN);                                   
                                                                                
 %PAGE;                                                                         
 %INCLUDE P0019906;  /* TRANS-OPPL-OMRÅDE (BASED) */                            
 %PAGE;                                                                         
 %INCLUDE P0019908;  /* KOM-OMRÅDE (BASED)        */                            
 %PAGE;                                                                         
 %INCLUDE P0019910;  /* STYRINGS - OMRÅDE (BASED) */                            
 %PAGE;                                                                         
 %INCLUDE P0019912;  /* DIV-PARAM-OMRÅDE(BASED)   */                            
 %PAGE;                                                                         
 %INCLUDE P0019913;  /* LOKAL - KOM_OMR (BASED)   */                            
 %PAGE;                                                                         
 DCL  FORKASTES      BIT (1)  INIT ('0'B);                                      
                                                                                
 DCL                                                                            
    QUENAME                CHAR(8),                                             
       QTERMID             CHAR(4) DEF QUENAME POS(1),                          
       REST                CHAR(4) DEF QUENAME POS(5);                          
                                                                                
 DCL                                                                            
    1 ROUTELIST,                                                                
      2 PRINTER,                                                                
        3 TERMID           CHAR(4),                                             
        3 REST             CHAR(12),                                            
      2 SISTE,                                                                  
        3 SLUTT            FIXED BIN(15),                                       
        3 REST             CHAR(14);                                            
                                                                                
 DCL      (CSTG,ADDR )                   BUILTIN;                               
                                                                                
 DCL  COMMAREA_PEKER         PTR,                                               
      LOKAL_KOM_PTR          PTR,                                               
                                                                                
      LOW_CHAR_BASED        CHAR(1200)  BASED(BMSMAPBR),                        
      I                      FIXED BIN(15) INIT (0),                            
      LENGTH                 FIXED BIN(15);                                     
                                                                                
        /**   DUMMY DEKLARASJONER FOR OVERFØRINGSMODULENE  **/                  
 DCL                                                                            
    CURSOR_POS FIXED BIN(15) INIT(-1),                                          
    DFHBMUNP             CHAR(1) INIT(' '),                                     
    DFHBMBRY             CHAR(1) INIT('H'),                                     
    DFHBMASK             CHAR(1) INIT('8'),   /* H.INT + ASKIP  */              
    FEIL_FUNNET          BIT(1)  INIT('1'B),                                    
    MELDING6O            CHAR(10),                                              
    YMELDING6O           CHAR(10);                                              
                                                                                
                                                                                
 QTERMID        = EIBTRMID;                                                     
 REST           = 'COMA';                                                       
 ROUTELIST      = '';                                                           
 PRINTER.TERMID = 'R41J';                                                       
 SISTE.SLUTT    = -1;                                                           
 W01_START          = 1;                                                        
                                                                                
 COMMAREA_PEKER = HOVED_KOM_OMR_PTR;                                            
                                                                                
 EXEC CICS READQ TS QUEUE(QUENAME) SET (COMMAREA_PEKER)                         
                               LENGTH(LENGTH) NOHANDLE;                         
 EXEC CICS DELETEQ TS QUEUE(QUENAME) NOHANDLE;                                  
                                                                                
                                                                                
 KOM_OMR.PEKER_LISTE.TRANS_OPPL_PEKER = ADDR(KOM_OMR.TRANS_OPPL_OMR);           
 KOM_OMR.PEKER_LISTE.TRANS_PEKER      = ADDR(KOM_OMR.TRANS_OMR);                
 KOM_OMR.PEKER_LISTE.STYRINGS_PEKER   = ADDR(KOM_OMR.STYRINGS_OMR);             
 KOM_OMR.PEKER_LISTE.DIV_PARAM_PEKER  = ADDR(KOM_OMR.DIV_PARAM_OMR);            
                                                                                
                                                                                
    /** STYRING FOR OVERFØRINGSMODULENE  **/                                    
    DIV_PARAM_OMR.FRA_CICS        = '0'B;                                       
    DIV_PARAM_OMR.HENT_FRAM_MAP   = '0'B;                                       
    DIV_PARAM_OMR.FRA_MED_DIALOG  = '0'B;                                       
    DIV_PARAM_OMR.FRA_UTEN_DIALOG = '0'B;                                       
    DIV_PARAM_OMR.FRA_UTEN_DIALOG = '1'B;                                       
                                                                                
    W01_TRANS_IND = 0;                                                          
                                                                                
    IF FUNKSJONSKODE = 'E' & EIBAID = '8' THEN       /* PF8  */                 
       EXEC CICS ROUTE LIST(ROUTELIST);                                         
                                                                                
    TRANS_ANT = ANTALL_TRANS_I_TRANS_LISTE + 1;                                 
                                                                                
       W01_SIDE_ANTALL = ANTALL_TRANS_I_TRANS_LISTE;                            
  /*   W01_SIDE_ANTALL = ANTALL_TRANS_I_TRANS_LISTE + 1; TEST*** */             
 MAPUT:                                                                         
                                                                                
        /* DO I  = W01_START TO ANTALL_TRANS_I_TRANS_LISTE;    */               
                                                                                
    DO I   = W01_START TO W01_SIDE_ANTALL;                                      
       W01_LOOP  = I;                                                           
       W01_START = W01_START + 1;                                               
       W01_MAP = '       ';                                                     
                                                                                
       IF ^W01_YSKADE      &                                                    
          ^W01_EØS         &                                                    
          ^W01_NORSK_BOSATT THEN                                                
          DO;                                                                   
                                                                                
             W01_TRANS_IND = W01_TRANS_IND + 1;                                 
             KOM_OMR.PEKER_LISTE.TRANS_OPPL_PEKER =                             
                                         TRANS_OPPL_PTR(W01_TRANS_IND);         
             KOM_OMR.PEKER_LISTE.TRANS_PEKER = TRANS_PTR(W01_TRANS_IND);        
             TRANS_ANT = TRANS_ANT - 1;                                         
          END;                                                                  
                                                                                
       CALL SELECT_BLANKETTYPE;                                                 
                                                                                
        /* IF I  = ANTALL_TRANS_I_TRANS_LISTE THEN   */                         
                                                                                
       IF I  = W01_SIDE_ANTALL THEN                                             
          DO;                                                                   
             KOM_OMR.TRANS_OPPL_PEKER =                                         
                                 ADDR(KOM_OMR.TRANS_OPPL_OMR);                  
             SISTE_SIDE = '1'B;                                                 
             TRANS_OPPL_OMR.TRANSKODE = 'RD80';                                 
          END;                                                                  
                                                                                
                                                                                
    END;                                                                        
                                                                                
    /** SLUTT PÅ EKSEKVERING I BYGG_TRANS  **/                                  
                                                                                
    SELECT_BLANKETTYPE:                                                         
      PROC;                                                                     
                                                                                
                                                                                
        HOVED_KOM_OMR_PTR  =    COMMAREA_PEKER;                                 
        SELECT(TRANS_OPPL_OMR.BLANKETTYPE);                                     
           WHEN('AP','AF','UF','UP','US','EP','EF','FB','BP','FT',              
                'E1','E2','O1','O2')                                            
              DO;                                                               
                EXEC CICS LINK PROGRAM('R0018A12')                              
                                         COMMAREA (LOKAL_KOM_OMR);              
              END;                                                              
                                                                                
           WHEN('A1','U2','E3','E4','B6','F7')                                  
              DO;                                                               
                EXEC CICS LINK PROGRAM('R0018B12')                              
                                         COMMAREA (LOKAL_KOM_OMR);              
              END;                                                              
           WHEN('KF','U3','EE','FO','EN')                                       
              DO;                                                               
                EXEC CICS LINK PROGRAM('R0018C12')                              
                                         COMMAREA (LOKAL_KOM_OMR);              
              END;                                                              
                                                                                
           OTHERWISE;                                                           
        END;                         /*   SELECT     */                         
                                                                                
                                                                                
    END SELECT_BLANKETTYPE;                                                     
                                                                                
                                                                                
                                                                                
 OVERFLOW:                                                                      
                                                                                
   IF SISTE_SIDE THEN                                                           
      DO;                                                                       
          IF FUNKSJONSKODE = 'E' THEN                                           
             DO;                                                                
               EXEC CICS SEND PAGE RETAIN NOAUTOPAGE;                           
               EXEC CICS RETURN;                                                
             END;                                                               
          ELSE                                                                  
             DO;                                                                
                EXEC CICS WRITEQ TS QUEUE(QUENAME) FROM(KOM_OMR);               
                EXEC CICS SEND PAGE TRANSID(TRANSKODE) RELEASE                  
                                                    NOAUTOPAGE;                 
             END;                                                               
      END;                                                                      
                                                                                
     GOTO MAPUT;                                                                
                                                                                
 END R001801;                                                                   
