 /*   SIST ENDRET PÅ PROD   2006.07.18 11.36.52 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2006.04.24  9.43.25 AV   SPA2970          */        
 /*   SIST ENDRET PÅ QASS   2006.04.19 12.10.56 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2006.03.24  8.43.58 AV   SPA2970          */        
 /*   SIST ENDRET PÅ PROD   2005.12.01  8.44.01 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2005.11.10 13.12.44 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2005.10.03 12.09.46 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2005.10.03 12.03.35 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2005.03.18  8.32.23 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.06.15 12.19.47 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2003.06.19 15.34.29 AV   SPA2970          */        
 /*   SIST ENDRET PÅ TEST   2003.03.14 10.37.10 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2002.05.06  8.53.33 AV   HLA2970          */        
 /*   SIST ENDRET PÅ PROD   2002.04.19 12.28.52 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.10.22 12.35.50 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2000.11.07 12.22.53 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.05.24 12.00.56 AV   JDA7339          */        
 /*       SIST ENDRET 03/08-99 13.13.54 AV   JDA7339                  */        
 /*       SIST ENDRET 18/03-99 09.16.22 AV   JDA7339                  */        
 /*       SIST ENDRET 18/03-99 09.13.25 AV   JDA7339                  */        
 /*       SIST ENDRET 20/07-98 13.06.06 AV   JDA7339                  */        
 /*       SIST ENDRET 04/05-98 13.15.31 AV   JDA7339                  */        
 /*       SIST ENDRET 11/03-98 12.05.47 AV   JDA7339                  */        
  /* *************************************************************** */         
  /* IDENTIFIKASJON:   R0017035                                      */         
  /* PROGRAMMERER:  HELEN    JULI, 1982                              */         
  /* HENSIKT:                                                        */         
  /*        LEGGER INFORMASJON FRA B02 OVER I ET HENDELSE-SEGMENT    */         
  /*        I STØNADSBREV-BASEN . PROGRAMMET KALLES OPP ETTER        */         
  /*        HVER AJOURFØRT TRANSAKSJON.                              */         
  /* PROGRAMTILKNYTNING:                                             */         
  /*        PROGRAMMET INKLUDERES I  R0017001                        */         
  /* *************************************************************** */         
  LAG_ETTERL_ALDERSP_HEND:PROC(IND);                                            
    DCL   IND             FIXED BIN (15);                                       
    DCL   EKIND           FIXED BIN (15);                                       
    DCL   AE_ALDER        FIXED DEC  (5);                                       
                                                                                
 DCL                                                                            
    W_TAI                 FIXED DEC (7),                                        
    W_BRP                 FIXED DEC (7,2),                                      
    W_APD                 FIXED DEC (7,3),                                      
    W_FAI_MAX             FIXED DEC (7),                                        
    W_FAI_MAX_FRAVIK      FIXED DEC (7);                                        
                                                                                
 DCL F_NUMMER                               PIC '9999999999999';                
                                                                                
 DCL FNR_ÅM                                 PIC '99999999';                     
                                                                                
 DCL  FYLL_67_ÅM                            PIC '999999';                       
                                                                                
 DCL FYLL_67_ÅR  DEF FYLL_67_ÅM  POS (1)    PIC '9999';                         
                                                                                
 DCL FYLL_67_MND DEF FYLL_67_ÅM  POS (5)    PIC '99';                           
                                                                                
 DCL  FYLL_70_ÅM                            PIC '999999';                       
                                                                                
 DCL FYLL_70_ÅR  DEF FYLL_70_ÅM  POS (1)    PIC '9999';                         
                                                                                
 DCL FYLL_70_MND DEF FYLL_70_ÅM  POS (5)    PIC '99';                           
                                                                                
 DCL MAX_ÅM                                 PIC '999999';                       
                                                                                
 DCL MAX_MND DEF MAX_ÅM            POS (5)  PIC '99';                           
                                                                                
 DCL MAX_ÅR  DEF MAX_ÅM            POS (1)  PIC '9999';                         
                                                                                
 DCL MIN_ÅM                                 PIC '999999';                       
                                                                                
 DCL MIN_MND DEF MIN_ÅM            POS (5)  PIC '99';                           
                                                                                
 DCL MIN_ÅR  DEF MIN_ÅM            POS (1)  PIC '9999';                         
                                                                                
 DCL MND                                    PIC '9999';                         
                                                                                
 DCL W_FNR13                                PIC '9999999999999';                
                                                                                
 DCL                                                                            
     1 FNR13_R DEF W_FNR13,                                                     
       2 DAG      PIC '99',                                                     
       2 MND      PIC '99',                                                     
       2 ÅR       PIC '9999',                                                   
       2 ÅRHUNDRE PIC '9',                                                      
       2 FILLER   PIC '9999';                                                   
                                                                                
 DCL                                                                            
     1 W_FNR_R13 DEF W_FNR13,                                                   
       2 DAG      PIC '99',                                                     
       2 MND      PIC '99',                                                     
       2 ÅR       PIC '9999',                                                   
       2 ÅRHUNDRE PIC '9',                                                      
       2 FILLER   PIC '9999';                                                   
                                                                                
  DCL M                 FIXED BIN(15) INIT (0);                                 
  DCL Y                 FIXED BIN(15) INIT (0);                                 
  /* *************************************************************** */         
  /*   PROGRAMMET BEGYNNER HER. VI BLANKER W_HENDELSE.               */         
  /*   ADDRESSERER EIB                                               */         
  /* *************************************************************** */         
                                                                                
    EXEC CICS ADDRESS EIB(DFHEIPTR);                                            
    W_FNR13  = KONV_FNR11_FNR13((B02.FNR(IND)));                                
    FNR_ÅM  =   FNR13_R.ÅR*100+FNR13_R.MND;                                     
                                                                                
    IF IND = 1 THEN                                                             
       EKIND = 2;                                                               
    ELSE                                                                        
       EKIND = 1;                                                               
                                                                                
    W_HENDELSE              =  '';                                              
    OMR_DATO                = TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD;                 
    AE_ALDER                = F_ALDER(TRANS_OPPL_OMR.FØDSNUMMER,                
                                   TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD);           
    W_APD                   = B02.ALDERSP.APD(IND);                             
                                                                                
    IF TRANS_OPPL_OMR.TRANSTYPE = 27     !                                      
       TRANS_OPPL_OMR.TRANSTYPE = 92     !  /*200302 HL */                      
       TRANS_OPPL_OMR.TRANSTYPE = 93     !                                      
       TRANS_OPPL_OMR.TRANSTYPE = 95 THEN                                       
       DO;                                                                      
          HENDDATO  =  OMR_DATO/100;                                            
       END;                                                                     
    ELSE                                                                        
       DO;                                                                      
          IF DIV_PERIODE.INTENDR_ÅMD     > 0 THEN                               
             HENDDATO     = DIV_PERIODE.INTENDR_ÅMD/100;                        
          ELSE                                                                  
             DO;                                                                
                R_HENDDATO_D =  B02.PERSON.STATUS.VIRK_DATO_ÅMD(IND);           
                HENDDATO     =  R_HENDDATO.AAR *100 + R_HENDDATO.MND;           
             END;                                                               
       END;                                                                     
                                                                                
                                                                                
  /* *************************************************************** */         
  /* ANTALL  MÅNEDER VENTET PÅ ALDERSPENSJON                         */         
  /* *************************************************************** */         
                                                                                
  /* *************************************************************** */         
  /*   VI SKRIVER BARE UT VENTEMÅNEDER NÅR DET TAS UT 100%           */         
  /* *************************************************************** */         
                                                                                
   MND                       = 0;                                               
   MAX_ÅM                    = 0;                                               
   MIN_ÅM                    = 0;                                               
   F_NUMMER                  = KONV_FNR11_FNR13((B02.FNR(IND)));                
                                                                                
   IF B02.ALDERSP.APD (IND) = 100  THEN                                         
     DO;                                                                        
       FYLL_67_ÅM                = FNR_ÅM  + 6700;                              
       IF FYLL_67_ÅM  < 197301   THEN                                           
          FYLL_67_ÅM  = 197301;                                                 
       FYLL_70_ÅM                = FNR_ÅM  + 7000;                              
       IF FYLL_70_ÅM  < 197301   THEN                                           
          FYLL_70_ÅM  = 197301;                                                 
                                                                                
       IF (B02.UTTAKSDATO_ÅMD(IND) / 100) > FYLL_70_ÅM THEN                     
          DO;                                                                   
                      IF FYLL_70_ÅM  > 198404     THEN                          
                         MAX_ÅM   = 198404;                                     
                      ELSE                                                      
                         MAX_ÅM   = FYLL_70_ÅM;                                 
                      MIN_ÅM   = FYLL_67_ÅM;                                    
                      MND      = (MAX_ÅR * 12 + MAX_MND)                        
                            - (MIN_ÅR * 12 + MIN_MND);                          
          END;                                                                  
       ELSE                                                                     
                                                                                
  /* *************************************************************** */         
  /* LOVENDRING - VENTETILLEGGET FALLER BORT 01.04.84                */         
  /* *************************************************************** */         
                                                                                
       IF FYLL_67_ÅM  < 198404 THEN                                             
          DO;                                                                   
                                                                                
  /* *************************************************************** */         
  /* FRA 67 ÅR TIL FØRSTE UTTAKSDATO                                 */         
  /* *************************************************************** */         
                                                                                
              MAX_ÅM   = B02.UTTAKSDATO_ÅMD(IND) / 100;                         
              MIN_ÅM   = FYLL_67_ÅM;                                            
              IF MAX_ÅM  > 198404 THEN                                          
                 MAX_ÅM  = 198404  ;                                            
              IF MIN_ÅM  > 198404 THEN                                          
                 MIN_ÅM  = 198404  ;                                            
              MND      = (MAX_ÅR * 12 + MAX_MND)                                
                       - (MIN_ÅR * 12 + MIN_MND);                               
                                                                                
  /* *************************************************************** */         
  /* VIDERE FRA FØRSTE UTTAKSDATO                                    */         
  /* *************************************************************** */         
                                                                                
                  IF B02.UTTAKS_APD(IND) < 100 THEN                             
                     MIN_ÅM  = B02.UTTAKSDATO_ÅMD(IND) / 100;                   
                  ELSE                                                          
                     MIN_ÅM  = 0;                                               
                  IF MIN_ÅM  > 198404   THEN                                    
                     MIN_ÅM  = 198404  ;                                        
                                                                                
  /* *************************************************************** */         
  /* VENTEMÅNEDER REGNES FREM TIL HVER VENTEPERIODE                 */          
  /* *************************************************************** */         
                                                                                
                  DO I = 1 TO 5 WHILE (B02.UTTAKSDATO_ÅMD_ØVRIGE                
                                    (IND,I) > 0);                               
                        IF MIN_ÅM  > 0 THEN                                     
                           DO;                                                  
                                                                                
                              MAX_ÅM  = B02.UTTAKSDATO_ÅMD_ØVRIGE               
                                                 (IND,I) / 100;                 
                              IF MAX_ÅM  > 198404   THEN                        
                                 MAX_ÅM  = 198404;                              
                              MND      = (MAX_ÅR * 12 + MAX_MND)                
                                        - (MIN_ÅR * 12 + MIN_MND)               
                                        + MND;                                  
                           END;                                                 
                                                                                
                         IF B02.APD_ØVRIGE(IND,I) < 100 THEN                    
                            MIN_ÅM = B02.UTTAKSDATO_ÅMD_ØVRIGE(IND,I)           
                                                   / 100;                       
                         ELSE                                                   
                            MIN_ÅM  = 0;                                        
                   END;                                                         
         END;                                                                   
     END;                                                                       
   W_HENDELSE.ANTALL_MND_VENTET  =  MND;                                        
   IF W_HENDELSE.ANTALL_MND_VENTET  > 36 THEN                                   
      W_HENDELSE.ANTALL_MND_VENTET  = 36;                                       
                                                                                
    W_HENDELSE.FNR_EKTEF  =    B02.PERSON.RF0PERSN.FNR(EKIND);                  
    W_HENDELSE.NAVN_EKTEF =    B02.PERSON.RF0PERSN.NAVN(EKIND);                 
    W_HENDELSE.SIVILSTAND_EK = B02.STATUS.SIVILSTAND(EKIND);                    
    W_HENDELSE.VERSJON       =  W_ROT.NESTE_SB_VERSJON_NR;                      
    W_HENDELSE.TERMINAL      = DIV_PARAM_OMR.BRUKER_ID;                         
    W_HENDELSE.PENSJONSTYPE1 = B02.STATUS.PENSJONSTYPE1(IND);                   
    W_HENDELSE.PENSJONSTYPE2 = B02.STATUS.PENSJONSTYPE2(IND);                   
    W_HENDELSE.PENSJONSTYPE3 = B02.STATUS.PENSJONSTYPE3(IND);                   
    W_HENDELSE.SIVILSTAND_EK = B02.STATUS.SIVILSTAND(EKIND);                    
    W_HENDELSE.GP_BRUTTO     =  B02.PERSON.ALDERSP.GP(IND);                     
    W_HENDELSE.TP_BRUTTO     =  B02.PERSON.ALDERSP.TP(IND) +                    
                                  B02.PERSON.ETTEPENS.TP_BRUTTO(IND);           
    W_HENDELSE.TP_NETTO      =  B02.PERSON.ETTEPENS.TP_NETTO(IND);              
  /*ENDRING I ST 200010 - HL */                                                 
    W_HENDELSE.ST_BRUTTO     =  B02.PERSON.ALDERSP.ST(IND);                     
    W_HENDELSE.KT            =  B02.PERSON.ALDERSP.KT(IND);                     
    W_HENDELSE.ET            =  B02.PERSON.ALDERSP.ET(IND);                     
    W_HENDELSE.BT            =  B02.PERSON.ALDERSP.BT(IND);                     
                                                                                
                                                                                
    W_HENDELSE.BT_SÆRBARN    =  B02.PERSON.FORSI.BT_SÆRBARN(IND);               
    W_HENDELSE.FT_SKAL_BEREGNES = B02.FORSI.FT_SKAL_BEREGNES(IND);              
    W_HENDELSE.ARBEIDSINNTEKT   = B02.FORSI.ARBEIDSINNTEKT(IND);                
    W_HENDELSE.PENSJONSINNTEKT   = B02.FORSI.PENSJONSINNTEKT(IND);              
    W_HENDELSE.ARBEIDSINNTEKT_EK = B02.FORSI.ARBEIDSINNTEKT_EK(IND);            
    W_HENDELSE.PENSJONSINNTEKT_EK= B02.FORSI.PENSJONSINNTEKT_EK(IND);           
    W_HENDELSE.FRIBELØP_MINSTE = DIV_PARAM_OMR.FRIBELØP_MINSTE(IND);            
    W_HENDELSE.FRIBELØP_FELLESBARN =                                            
                             DIV_PARAM_OMR.FRIBELØP_FELLESBARN(IND);            
    W_HENDELSE.FRIBELØP_SÆRBARN = DIV_PARAM_OMR.FRIBELØP_SÆRBARN(IND);          
    W_HENDELSE.FOLKETRYGD        = DIV_PARAM_OMR.FOLKETRYGD(IND);               
    W_HENDELSE.FOLKETRYGD_EK     = DIV_PARAM_OMR.FOLKETRYGD(EKIND);             
                                                                                
    W_HENDELSE.TEI   = DIV_PARAM_OMR.TAI(IND) / 1000;                           
                                                                                
    W_HENDELSE.BEREGN_ALT_AEY = DIV_PARAM_OMR.AP_KODE_AEY(IND);                 
                                                                                
    W_HENDELSE.FULLT_SÆRTILLEGG =                                               
           DIV_PARAM_OMR.FULLT_SÆRTILLEGG(IND);                                 
    IF B02.STATUS.SIVILSTAND(IND) = 'E' THEN                                    
       W_HENDELSE.TP_PROSENT     = 55;                                          
    ELSE                                                                        
       W_HENDELSE.TP_PROSENT     = B02.ETTEPENS.TP_PROSENT(IND);                
                                                                                
    IF IND < 3        THEN                                                      
       W_HENDELSE.GARANTITILLEGG_TP = DIV_PARAM_OMR.                            
                                GARANTITILLEGG_TP(IND);                         
                                                                                
                                                                                
    IF B02.PENSJONSTYPE1(EKIND)  =  'F'  THEN                                   
         DO I = 1 TO 13;                                                        
            IF B02.FNR_TILKN(IND,I) =   B02.FNR(EKIND) THEN                     
                DO;                                                             
                   W_HENDELSE.ET_FØR_0591 = B02.FT_FØR_91(IND,I);               
                   I = 13;                                                      
                END;                                                            
         END;                                                                   
                                                                                
    W_HENDELSE.GARANTITILLEGG_EK = B02.FORSI.GARANTITILLEGG_EK(IND);            
    W_HENDELSE.GARANTITILLEGG_BARN =                                            
                            B02.FORSI.GARANTITILLEGG_BARN(IND);                 
    W_HENDELSE.GARANTITILLEGG_SÆRBARN =                                         
                        B02.FORSI.GARANTITILLEGG_SÆRBARN(IND);                  
    IF AE_ALDER < 7000  & B02.ETTEPENS.FORVENTET(IND) = 0 THEN                  
       W_HENDELSE.FORVENTET_INNTEKT = B02.ALDERSP.FAI(IND);                     
    ELSE                                                                        
       W_HENDELSE.FORVENTET_INNTEKT = B02.ETTEPENS.FORVENTET(IND);              
    W_HENDELSE.TP_GAR_KODE   =  B02.PERSON.TP_GAR_KODE(IND);                    
    W_HENDELSE.TP_GAR_KODE_EK = B02.PERSON.TP_GAR_KODE(EKIND);                  
    DO L = 1 TO 7 WHILE (B02.UFØRHIST.UFT_ÅMD(IND,L) > 0);                      
       W_HENDELSE.BUP_GARANTI = B02.UFØRHIST.BUP_GARANTI(IND,L);                
    END;                                                                        
    W_HENDELSE.ANTALL_BARN   =  B02.PERSON.STATUS.ANTALL_BARN(IND);             
    W_HENDELSE.ANTALL_BARN_TIDL                                                 
                             =  B01.PERSON.STATUS.ANTALL_BARN(IND);             
    W_HENDELSE.VT_GP         =  B02.PERSON.ALDERSP.VT_GP(IND);                  
    IF B02.PERSON.ALDERSP.VT_TP(IND) > 0 THEN                                   
       DO;                                                                      
          W_HENDELSE.VT_TP   =  B02.PERSON.ALDERSP.VT_TP(IND);                  
       END;                                                                     
    W_HENDELSE.FAI           =  B02.PERSON.ALDERSP.FAI(IND);                    
    W_HENDELSE.APD           =  B02.PERSON.ALDERSP.APD(IND);                    
    W_HENDELSE.REDUSERT_GP_2G = DIV_PARAM_OMR.REDUSERT_GP_2G(IND);              
                                                                                
    IF B02.PERSON.ALDERSP.P67_KODE(IND) = '3'  THEN                             
       DO;                                                                      
          W_HENDELSE.ØNSKET_APD = B02.PERSON.ALDERSP.APD(IND);                  
       END;                                                                     
    ELSE                                                                        
       DO;                                                                      
          W_HENDELSE.ØNSKET_APD = 0;                                            
       END;                                                                     
                                                                                
  /* *************************************************************** */         
  /* HVIS SØKER ER UNDER 70 ÅR HENTES FØLGENDE RUTINER FRA BEREGNING */         
  /* *************************************************************** */         
                                                                                
    IF AE_ALDER < 7000 THEN                                                     
       DO;                                                                      
          W_FNR13 = KONV_FNR11_FNR13((B02.FNR(IND)));                           
          CALL TIDLIGERE_ARBEIDSINNTEKT(W_TAI,W_BRP);                           
                                                                                
          IF (B02.PERSON.ALDERSP.P67_KODE(IND) = '1' !                          
              B02.PERSON.ALDERSP.P67_KODE(IND) = '2'   )    THEN                
                                                                                
             DO;                                                                
                CALL FAI_GRENSER(W_APD,W_TAI,W_FAI_MAX);                        
                CALL FAI_GRENSER_FRAVIK(W_APD,W_FAI_MAX_FRAVIK);                
                                                                                
                IF W_FAI_MAX < W_FAI_MAX_FRAVIK      THEN                       
                   W_FAI_MAX = W_FAI_MAX_FRAVIK;                                
             END;                                                               
                                                                                
           ELSE                                                                 
                                                                                
                IF B02.PERSON.ALDERSP.P67_KODE(IND) = '3'  THEN                 
                                                                                
                   DO;                                                          
                      IF W_TAI * 1000 < G * 2 THEN                              
                         CALL FAI_GRENSER_FRAVIK(W_APD,W_FAI_MAX);              
                      ELSE                                                      
                         CALL FAI_GRENSER(W_APD,W_TAI,W_FAI_MAX);               
                   END;                                                         
                                                                                
                ELSE                                                            
                                                                                
                   CALL FAI_GRENSER_FRAVIK(W_APD,W_FAI_MAX);                    
                                                                                
          W_HENDELSE.FAI_MAX      = W_FAI_MAX/100;                              
       END;                                                                     
                                                                                
    W_HENDELSE.TT            = B02.PERSON.STATUS.TT_ANV(IND);                   
    W_HENDELSE.TT_EK         = B02.PERSON.STATUS.TT_ANV(EKIND);                 
    W_HENDELSE.VENTEFAKTOR   = B02.VENTEFAKTOR(IND);                            
                                                                                
    W_HENDELSE.PÅ            = B02.ALDERSP.PÅ      (IND);                       
    W_HENDELSE.OPT           = B02.ALDERSP.OPT     (IND);                       
    W_HENDELSE.SPT           = B02.ALDERSP.SPT     (IND);                       
  /*REMEDY 1477 - HL 200511 : */                                                
    IF B02.VENTEFAKTOR(IND)  > 0  THEN                                          
       DO;                                                                      
          W_HENDELSE.PÅ_P67        = B02.ALDERSP.VT_PÅ   (IND);                 
          W_HENDELSE.OPT_P67       = B02.ALDERSP.VT_OPT  (IND);                 
          W_HENDELSE.SPT_P67       = B02.ALDERSP.VT_SPT  (IND);                 
       END;                                                                     
                                                                                
                                                                                
  /* *************************************************************** */         
  /* NY LOV 0192     JD 11.91                                        */         
  /* *************************************************************** */         
                                                                                
    W_HENDELSE.GT_LOV92    =  B02.STATUS.GT_LOV92(IND);                         
    W_HENDELSE.GT_TILLEGG_LOV92 = B02.STATUS.GT_TILLEGG_LOV92(IND);             
    W_HENDELSE.GARANTI_DATO_ÅMD = B02.STATUS.GARANTI_DATO_ÅMD(IND);             
    W_HENDELSE.AP_TP_NETTO      = B02.ALDERSP.AP_TP_NETTO   (IND);              
    W_HENDELSE.AP_GP_NETTO      = B02.ALDERSP.AP_GP_NETTO   (IND);              
  /*HENDELSE.ST  ER NETTO-BELØPET - SKAL BARE VÆRE > 0 NÅR*/                    
  /* BRUTTO-BELØPET - ALDERSP.ST - ER STØRRE ENN 0 */                           
    IF B02.PERSON.ALDERSP.ST(IND)  > 0              THEN                        
       W_HENDELSE.ST            =  B02.PERSON.AP_ST_NETTO(IND);                 
    ELSE                                                                        
       W_HENDELSE.ST            =  0;                                           
    W_HENDELSE.SPT_1291         = B02.ALDERSP.SPT_1291      (IND);              
    W_HENDELSE.OPT_1291         = B02.ALDERSP.OPT_1291      (IND);              
    W_HENDELSE.PÅ_1291         = B02.ALDERSP.PÅ_1291      (IND);                
    W_HENDELSE.PÅ_ETTER91      = B02.ALDERSP.PÅ_ETTER91   (IND);                
    W_HENDELSE.SPT_1291_EK     = B02.ETTEPENS.SPT_1291    (IND);                
    W_HENDELSE.OPT_1291_EK     = B02.ETTEPENS.OPT_1291    (IND);                
    W_HENDELSE.PÅ_1291_EK      = B02.ETTEPENS.PÅ_1291     (IND);                
    W_HENDELSE.PÅ_ETTER91_EK   = B02.ETTEPENS.PÅ_ETTER91  (IND);                
    W_HENDELSE.YRKE_PÅ_ETTER91 = B02.YRKEPENS.PÅ_ETTER91  (IND);                
    W_HENDELSE.YRKE_PÅ_ETTER91_AVD = B02.YRKEPENS.PÅ_ETTER91(EKIND);            
    W_HENDELSE.YRKE_POENG_1291 = B02.YRKEPENS.YRKE_POENG_1291(IND);             
                                                                                
    W_HENDELSE.YRKE_BT       = B02.YRKEPENS.BT(IND);                            
    W_HENDELSE.YRKE_TP       = B02.YRKEPENS.TP(IND);                            
    W_HENDELSE.YRKE_GP       = B02.YRKEPENS.GP(IND);                            
    W_HENDELSE.YRKE_UFG      = B02.YRKEPENS.YUG(IND);                           
    W_HENDELSE.YRKE_KODE     = B02.YRKEPENS.YPT_KODE(IND);                      
                                                                                
    IF B02.YRKEPENS.YPT  (IND) < B02.YRKE_POENG_1291 (IND) THEN                 
       W_HENDELSE.YRKE_YPT     = B02.YRKE_POENG_1291 (IND);                     
    ELSE                                                                        
       W_HENDELSE.YRKE_YPT     = B02.YRKEPENS.YPT    (IND);                     
                                                                                
    W_HENDELSE.YRKE_SPT      = B02.YRKEPENS.SPT  (IND);                         
    W_HENDELSE.DØD_AV_YSKADE = B02.DØD_AV_YRKESSKADE(IND);                      
    W_HENDELSE.BOSATT        = B02.BOSATT(IND);                                 
    IF B02.YRKEPENS.YUG(EKIND) > 0 THEN                                         
       DO;                                                                      
         W_HENDELSE.YRKE_UFG_AVD = B02.YRKEPENS.YUG(EKIND);                     
         W_HENDELSE.YRKE_KODE_AVD = B02.YRKEPENS.YPT_KODE(EKIND);               
                                                                                
    IF B02.YRKEPENS.YPT(EKIND) < B02.YRKE_POENG_1291 (EKIND) THEN               
       W_HENDELSE.YRKE_YPT_AVD = B02.YRKE_POENG_1291 (EKIND);                   
    ELSE                                                                        
       W_HENDELSE.YRKE_YPT_AVD = B02.YRKEPENS.YPT    (EKIND);                   
                                                                                
         W_HENDELSE.YRKE_SPT_AVD = B02.YRKEPENS.SPT(EKIND);                     
      END;                                                                      
  /* *************************************************************** */         
  /* FØLGENDE FELT HENTES FRA YRKESSKADEHISTORIKKK                   */         
  /* *************************************************************** */         
                                                                                
                                                                                
                                                                                
  DO M = 10 TO 1 BY -1  UNTIL (B02.YRKEHIST.YUFT_ÅMD                            
                                       (EKIND,M) > 0);                          
  END;                                                                          
                                                                                
 IF M > 0 THEN                                                                  
 DO;                                                                            
                                                                                
  W_HENDELSE.YRKE_YUFT_ÅMD_AVD = B02.YRKEHIST.YUFT_ÅMD(EKIND,M);                
  W_HENDELSE.YRKE_YST_ÅMD_AVD = B02.YRKEHIST.YST_ÅMD (EKIND,M);                 
  IF B02.YRKEHIST.AÅI (EKIND,M) = 9999999 THEN                                  
     W_HENDELSE.YRKE_AÅI_AVD         = 0;                                       
  ELSE                                                                          
     W_HENDELSE.YRKE_AÅI_AVD  = B02.YRKEHIST.AÅI (EKIND,M);                     
  W_HENDELSE.YRKE_YRKE11_AVD  = B02.YRKEHIST.YRKE11 (EKIND,M);                  
  W_HENDELSE.YRKE_PAÅ_AVD     = B02.YRKEHIST.PAÅ   (EKIND,M);                   
                                                                                
 END;                                                                           
                                                                                
  DO Y = 10 TO 1 BY -1  UNTIL (B02.YRKEHIST.YUFT_ÅMD                            
                                       (IND,Y) > 0);                            
  END;                                                                          
 IF Y > 0 THEN                                                                  
 DO;                                                                            
  IF   DIV_PARAM_OMR.ENGANGSBELØP_11_7(IND) > 0 THEN                            
     DO;                                                                        
        W_HENDELSE.YRKE_UFG     = B02.YRKEHIST.YUG(IND,Y);                      
     END;                                                                       
                                                                                
  W_HENDELSE.YRKE_YUFT_ÅMD = B02.YRKEHIST.YUFT_ÅMD(IND,Y);                      
  W_HENDELSE.YRKE_YST_ÅMD = B02.YRKEHIST.YST_ÅMD (IND,Y);                       
  IF B02.YRKEHIST.AÅI (IND,Y) = 9999999 THEN                                    
     W_HENDELSE.YRKE_AÅI         = 0;                                           
  ELSE                                                                          
     W_HENDELSE.YRKE_AÅI         = B02.YRKEHIST.AÅI   (IND,Y);                  
  W_HENDELSE.YRKE_YRKE11  = B02.YRKEHIST.YRKE11 (IND,Y);                        
  W_HENDELSE.YRKE_PAÅ     = B02.YRKEHIST.PAÅ   (IND,Y);                         
 END;                                                                           
    W_HENDELSE.PÅ_EK         = B02.ETTEPENS.PÅ_AVD (IND);                       
    W_HENDELSE.OPT_EK        = B02.ETTEPENS.OPT_AVD(IND);                       
    W_HENDELSE.SPT_EK        = B02.ETTEPENS.SPT_AVD(IND);                       
                                                                                
       DO J = 3 TO 14 WHILE(B02.FNR(J) > 0);                                    
                                                                                
          DO K = 1 TO 13 WHILE (B02.FNR_TILKN(IND,K) > 0);                      
                                                                                
             IF B02.FNR(J) = B02.FNR_TILKN(IND,K) THEN                          
                                                                                
                DO;                                                             
                   W_HENDELSE.BARN.FNR_B(J-2)           = B02.FNR(J);           
                   W_HENDELSE.BARN.PENSJONSTYPE2_B(J-2) =                       
                                                  B02.PENSJONSTYPE2(J);         
                   W_HENDELSE.BARN.BT_FØR_0591(J-2) =                           
                              B02.FT_FØR_91(IND,K);                             
                   K = 13;                                                      
                END;                                                            
          END;                                                                  
       END;                                                                     
    W_HENDELSE.ÅR_BUP_FPP    = POREKKE.BUP_FPP_ÅR(IND);                         
    W_HENDELSE.ÅR_BUP_FPP_EK = POREKKE.BUP_FPP_ÅR(EKIND);                       
    W_HENDELSE.ALTERNATIV_GP_BRUTTO = B02.ALTERNATIV_GP_BRUTTO(EKIND);          
    W_HENDELSE.ALTERNATIV_TP_BRUTTO = B02.ALTERNATIV_TP_BRUTTO(EKIND);          
                                                                                
    CALL PLASSER_TEKST(IND,EKIND);                                              
                                                                                
    CALL INSERT_HENDELSE;                                                       
                                                                                
    IF W_HENDELSE.SPT > 0 ! W_HENDELSE.OPT > 0 THEN                             
       DO;                                                                      
          CALL BYGG_OPP_BERGRUNL_SEGMENT(IND);                                  
       END;                                                                     
                                                                                
  /* *************************************************************** */         
  /*   DERSOM AVDØDE EKTEFELLE HAR SPT STØRRE ENN 0                  */         
  /* *************************************************************** */         
                                                                                
    IF (B02.PENSJONSTYPE1(IND) = 'E' &                                          
        B02.PENSJONSTYPE1(EKIND) = 'D' ) !                                      
       (W_HENDELSE.SPT_EK       >  0 !                                          
        W_HENDELSE.OPT_EK       >  0 ) THEN                                     
        DO;                                                                     
           CALL BYGG_OPP_BERGRUNL_SEGMENT_DØD(EKIND);                           
        END;                                                                    
                                                                                
  END  LAG_ETTERL_ALDERSP_HEND;                                                 
