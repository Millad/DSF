 /*   SIST ENDRET PÅ PROD   2008.05.31 11.11.12 AV   SPA2990          */        
 /*       SIST ENDRET 18/05-99 14.20.39 AV   JDA7339                  */        
 /*       SIST ENDRET 18/05-99 14.20.00 AV   JDA7339                  */        
 /*       SIST ENDRET 07/05-98 14.13.32 AV   JDA7339                  */        
 /*       SIST ENDRET 05/05-98 08.48.33 AV   JDA7339                  */        
 /*       SIST ENDRET 01/04-98 08.20.10 AV   MEA9991                  */        
 /* ***************************************************************** */        
 /* IDENTIFIKASJON                                                    */        
 /*     R0019954 - RULL_FORVENTET.                                    */        
 /*                PROSEDYRE I PLI.                                   */        
 /*     PROGRAMMERER: JAN - H. KRISTENSEN OKTOBER 1983.               */        
 /* HENSIKT                                                           */        
 /*     SE FORKLARING NEDENFOR.                                       */        
 /* PROGRAMTILKNYTNING                                                */        
 /*     INCLUDE MODUL.                                                */        
 /* ***************************************************************** */        
 /* ***************************************************************** */        
 /* JUSTERING AV FORVENTET INNTEKT FRA EN DATO TIL EN ANNEN.          */        
 /*        MODULEN BLIR KALLT OPP MED FRADATO, TILDATO OG             */        
 /*           FORVENTET INNTEKT. MODULEN FORANDRER PÅ PARAMETER       */        
 /*           FORVENTET INNTEKT DIREKTE.                              */        
 /*                                                                   */        
 /* ***************************************************************** */        
 /*                                                                   */        
                                                                                
   RULL_FORVENTET: PROC(FRA_DATO,TIL_DATO,FORVENTET_INNT);       /*Y2K*/        
                                                                                
   %INCLUDE P0019925;          /***    G - BELØPSTABELL     ***/                
                                                                                
      DCL I                FIXED BIN (15),                                      
          FRA_DATO         FIXED DEC (9) UNAL,                   /*Y2K*/        
          TIL_DATO         FIXED DEC (9) UNAL,                   /*Y2K*/        
          FORVENTET_INNT   FIXED DEC (5) UNAL,                   /*Y2K*/        
          FIXED_DEC6       FIXED DEC (6) INIT(0),                /*Y2K*/        
          FRA_DATO_ÅM      FIXED DEC (6),                        /*Y2K*/        
          TIL_DATO_ÅM      FIXED DEC (6);                        /*Y2K*/        
                                                                                
       /* RUTINEN SKAL OVERSE DAGER SÅ DATOERS DD KLIPPES AV: */ /*Y2K*/        
       FRA_DATO_ÅM = FRA_DATO / 100;                             /*Y2K*/        
       TIL_DATO_ÅM = TIL_DATO / 100;                          /*9805*/          
                                                                                
       DO I = 1 TO 65 UNTIL(FIXED_DEC6 >= FRA_DATO_ÅM);          /*Y2K*/        
                                                                                
          FIXED_DEC6 = PERIODE_START_ÅMD(I)/100;                 /*Y2K*/        
       END;                                                                     
                                                                                
       IF FRA_DATO_ÅM > TIL_DATO_ÅM THEN                         /*Y2K*/        
          DO;                                                                   
             IF FIXED_DEC6 ^= FRA_DATO_ÅM THEN                   /*Y2K*/        
                DO;                                                             
                   I = I - 1;                                                   
                   FIXED_DEC6 = PERIODE_START_ÅMD(I)/100;        /*Y2K*/        
                END;                                                            
                                                                                
                                                                                
             DO WHILE (FIXED_DEC6 > TIL_DATO_ÅM);                /*Y2K*/        
                                                                                
                FORVENTET_INNT = FORVENTET_INNT * GRUNNBELØP(I-1)/*Y2K*/        
                                      / GRUNNBELØP(I    );                      
                                                                                
                IF GRUNNBELØP(I - 1) ^= GRUNNBELØP(I) THEN                      
                   FORVENTET_INNT = FORVENTET_INNT + 1;          /*Y2K*/        
                I = I - 1;                                                      
                FIXED_DEC6 = PERIODE_START_ÅMD(I)/100;           /*Y2K*/        
                                                                                
             END;                                                               
                                                                                
          END;                                                                  
                                                                                
       ELSE IF FRA_DATO_ÅM < TIL_DATO_ÅM THEN                    /*Y2K*/        
          DO;                                                                   
             IF FIXED_DEC6 = FRA_DATO_ÅM THEN                    /*Y2K*/        
                DO;                                                             
                   I = I + 1;                                                   
                   FIXED_DEC6 = PERIODE_START_ÅMD(I)/100;        /*Y2K*/        
                END;                                                            
                                                                                
             DO WHILE (FIXED_DEC6 <= TIL_DATO_ÅM);               /*Y2K*/        
                                                                                
                FORVENTET_INNT = FORVENTET_INNT * GRUNNBELØP(I ) /*Y2K*/        
                                      / GRUNNBELØP(I - 1);                      
                I = I + 1;                                                      
                FIXED_DEC6 = PERIODE_START_ÅMD(I)/100;           /*Y2K*/        
                                                                                
             END;                                                               
                                                                                
          END;                                                                  
                                                                                
    END RULL_FORVENTET;                                                         
