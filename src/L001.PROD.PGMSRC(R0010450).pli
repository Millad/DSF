 /*   SIST ENDRET PÅ PROD   2008.05.31 11.27.22 AV   SPA2990          */        
 /*   SIST ENDRET PÅ PROD   2006.05.10 13.04.35 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2006.03.24  8.04.36 AV   SPA2970          */        
 /*   SIST ENDRET PÅ PROD   2004.12.17 14.39.30 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.07.13 13.16.26 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2004.03.15 12.05.03 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.03.12 13.47.14 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2003.02.27 12.39.12 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2002.11.21 13.31.10 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2002.04.19 12.44.10 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.10.22 12.57.12 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2000.02.18 13.15.03 AV   JDA7339          */        
 /*       SIST ENDRET 06/05-98 10.06.57 AV   JDA7339                  */        
 /*       SIST ENDRET 14/01-98 15.58.29 AV   SPA7339                  */        
                                                                                
 /******************************************************************* */        
 /*IDENTIFIKASJON:                                                    */        
 /************************                                            */        
 /*  PROGRAM-IDENT : R0010450 - FJERNING AV TRANS - HOVEDPROGRAM      */        
 /*  PROGRAM-SPRÅK : PLI/CICS                                         */        
 /*  PROGRAMMERER  : JAN HARALD KRISTENSEN                            */        
 /*  PROGRAMMET BLE LAGET  :                                          */        
 /*  ENDRINGERSDATO :                                                 */        
 /*  ENDRINGEN GJELDER:                                               */        
 /*  ENDRINGEN BLE UTFØRT AV :                                        */        
 /*                                                                   */        
 /******************************************************************* */        
 /*HENSIKT:                                                           */        
 /************                                                        */        
 /*  HOVEDPROGRAM I DELSYSTEMET FJERNING.                             */        
 /*  PROGRAMMET AKTIVISERES NÅR BRUKEREN TASTER INN 'F' PÅ BILDE      */        
 /*  OVER FUNKSJONSVALG, MAP S001013. MENYEN FOR FJERNING BLIR DA     */        
 /*  SKREVET OG BRUKEREN MÅ ANGI HVA SOM VIDERE ØNSKES UTFØRT:        */        
 /*   ST _ FJERN SISTE TRANS                                          */        
 /*   AT - FJERN ALLE TRANSER                                         */        
 /*  NÅR ST VELGES GÅR KONTROLLEN TIL R0010451 -FJERN SISTE TRANS     */        
 /*  SOM SKRIVER ET SKJERMBILDE MED OPPLYSNINGER FRA NEST SISTE OG    */        
 /*  SISTE STATUS SAMT SISTE TRANSAKSJON.                             */        
 /*  VED ET BEKREFTENDE SVAR, BLIR SISTE TRANS FJERNET FRA TRANS-     */        
 /*  HIST, TRANSAKSJONEN LAGT UT PÅ VENTEREG. RUTINEN AUTOHENDELSE    */        
 /*  KJØRES, NYE STATUS SKRIVES PÅ BASEN                              */        
 /*            *******                                                */        
 /*  NÅR 'AT' VELGES GÅR KONTROLLEN TIL R0010452 - FJERN ALLE TRANSER */        
 /*  SOM LEGGER UT ET BILDE MED OPPLYSNINGER OM ALLE TRANSER:         */        
 /*  VIRKDAT, BLANKETTYPE, FNR-SØKER, FNR_EKTEF, ANTALL BARN.         */        
 /*  VED ET BEKREFTENDE SVAR, BLIR ALLE TRANSAKSJONENE FJERNET OG LAGT*/        
 /*  UT PÅ VENTEREG. DET BLIR SKREVET NY STATUS, OG DET GÅR MELDING   */        
 /*  TIL STATISTIKK-SYSTEMET.                                         */        
 /*  DERSOM PERSONEN BARE HADDE PENSJONSOPPLYSNINGER I REGISTERET     */        
 /*  OG DISSE FJERNES, SÅ BLIR PERSONEN SLETTET FRA REGISTERET.       */        
 /******************************************************************* */        
 /*PROGRAMTILKNYTTING:                                                */        
 /**********************                                              */        
 /*  PROGRAMMET BLIR AKTIVISERT FRA CICS MED TRANSKODE = R045.        */        
 /*  DENNE TRANSKODEN SETTES I R0010301:                              */        
 /*    EXEC CICS RETURN TRANSID(TRANSKODE) COMMAREA(KOM_OMR);         */        
 /*  KONTROLLEN GÅR VIDERE TIL :                                      */        
 /*  R0010301 NÅR RETURFUNKSJONEN SETTES TIL NOE ANNET ENN 'V'        */        
 /*  R0010480 NÅR RETURFUNKSJONEN SETTES TIL 'V'                      */        
 /*                                                                   */        
 /*  R0010451: PROCEDURER INNEN DETTE PROGAMMET CALLES HERFRA         */        
 /*            VIS_MEG_SISTE_TRANS                                    */        
 /*            MAPS_UT_ST                                             */        
 /*            LOW_VALUES_TIL_MAP                                     */        
 /*            MAP_UT_STATUS                                          */        
 /*            HENT_SVAR_OG_BEKREFT                                   */        
 /*  R0010452: PROCEDURER INNEN DETTE PROGAMMET CALLES HERFRA         */        
 /*            FORBEREDE TRANS                                        */        
 /*            HENT_TRANS                                             */        
 /*            MAPS_UT_AT                                             */        
 /*            FJERN ALLE TRANS                                       */        
 /*            BEHANDLE FNR                                           */        
 /*  EXEC LINK TIL:                                                   */        
 /*            R0019906     SJEKKE OM FNR-ENDRINGER                   */        
 /*            R0010490     SKRIV TRANS TIL VENTEREG                  */        
 /*            R0019921     HENTE DIV FEILMELDINGER                   */        
 /*            R0015101     SLETTE SPERREKODE                         */        
 /******************************************************************* */        
 /*DATASETTOPPLYSNINGER:                                              */        
 /*************************                                           */        
 /*  RF-BASEN:    ÅPNES OG CLOSES I DETTE PROGRAMMET                  */        
 /*               LESES I R0010451 OG 0452                            */        
 /*               R0015401 SKRIVER RESULTATET TILBAKE TIL DATABASEN   */        
 /*  VENTETRANSBASEN: BRUKES I R0010480, R0010490                     */        
 /*  FØDSELSNR-ENDRINGSREG: LESES I R0019906                          */        
 /*    ER DETTE NØDVENDIG????                                         */        
 /*  **DDNAVN,TYPE I/O DATASETTOPPLYSNINGER,PSB-NAVN **               */        
 /*                                                                   */        
 /******************************************************************* */        
 /*FEILMELDINGER:                                                     */        
 /*********************                                               */        
 /*  'FEIL KODE REGISTRERT' NÅR IKKE GYLDIG FJERNINGSKODE             */        
 /*  'FNR INNHOLDER IKKE-NUMERISKE FELT'                              */        
 /*  'FNR UGYLDIG ' NÅR FNR IKKE ER GYLDIG I FØLGE FNR-TESTEN         */        
 /*  'FØDSELSNUMMER NNNNNNNNNNN ER ENDRET, TRYKK ENTER'               */        
 /*  'PROGRAMM R001XXXX GÅR I FEIL VED LABEL NNN'                     */        
 /*  '         TASKEN BLIR KANSELERT!!!!!!!!' VED ALVORLIGE FEIL.     */        
 /*                                                                   */        
 /* ***************************************************************** */        
                                                                                
 R010450: PROC(COMMAREA_PEKER)     OPTIONS   (MAIN);                            
  /* *************************************************************** */         
  /*                                                                 */         
  /*       HOVED STYRING - DECLARASJONER                             */         
  /*                                                                 */         
  /* *************************************************************** */         
   DCL                                                                          
       1   B00 BASED(B00_PEKER), %INCLUDE P0019921;                             
   %PAGE;                                                                       
   DCL                                                                          
       1   B01 BASED(B01_PEKER), %INCLUDE P0019921;                             
   %PAGE;                                                                       
   DCL                                                                          
       1   B02 BASED(B02_PEKER), %INCLUDE P0019921;                             
   %PAGE;                                                                       
   %INCLUDE P0019912;         /*  DIV_PARAM_OMRÅDE     */                       
   %PAGE;                                                                       
   %INCLUDE P0019910;         /*  STYRINGS_OMRÅDE      */                       
   %PAGE;                                                                       
   %INCLUDE P0019908;         /*  KOM_OMRÅDE(BASED)    */                       
   %PAGE;                                                                       
   %INCLUDE P0019906;         /*  TRANS_OPPL_OMRÅDE    */                       
   %PAGE;                                                                       
   %INCLUDE P0019924;         /* G_V_TAB               */                       
   %PAGE;                                                                       
   %INCLUDE P0019925;         /* G_TAB                 */                       
   %PAGE;                                                                       
   %INCLUDE P0014009;         /*  POTALL_OPPL          */                       
   %PAGE;                                                                       
   %INCLUDE P0019014;         /*  FNRTAB  - STØNADSBREV*/                       
   %PAGE;                                                                       
   %INCLUDE P0019913;         /*  LOKAL_KOM - OMR      */                       
   %PAGE;                                                                       
   %INCLUDE S00101;           /*  HOVED MENY - MAPSET        */                 
   %PAGE;                                                                       
   %INCLUDE S0010E;           /*  ENDRINGS - MAPSET          */                 
   %PAGE;                                                                       
   %INCLUDE S001V0;           /*  VENTETRANS - MAPSET        */                 
   %PAGE;                                                                       
                                                                                
   DCL                                                                          
     1 TRANS_LISTE_OMR     BASED(TRANS_LISTE_PEKER),                            
       3 TRANS_LISTE_LINJE (5),                                                 
       %INCLUDE P0019911;         /*    TRANS_LISTE UNDERNIVÅ       */          
                                                                                
   DCL                                                                          
     1 WORK_TRANS_LISTE     BASED(WORK_TRANS_LISTE_PEKER),                      
       %INCLUDE P0019911;         /*    TRANS_LISTE UNDERNIVÅ       */          
                                                                                
   DCL                                                                          
          MAP_MELDING              CHAR(78)      INIT((78)' '),                 
          JA                       BIT(1)        INIT('1'B),                    
          NEI                      BIT(1)        INIT('0'B),                    
          FEIL                     BIT(1)        INIT('0'B),                    
          FATAL_FEIL               BIT(1)        INIT('0'B),                    
          (I,J,K)                  FIXED BIN(15),                               
          SØKER_IND_SS             FIXED BIN(15) INIT(0),                       
          SØKER_IND_NS             FIXED BIN(15) INIT(0),                       
          STOP                     CHAR(4)       INIT('0450'),                  
          STATUS_TYPE_SS           CHAR(1),                                     
          STATUS_TYPE_NS           CHAR(1),                                     
          W_PERSN_KODE             CHAR(1),                                     
          LOW_CHAR_BASED           CHAR(300)     BASED (BMSMAPBR),              
          FNR_DØD                  PIC '(11)9'   INIT(0),                       
          MOD                      BUILTIN,                                     
          LOW                      BUILTIN,                                     
          SUBSTR                   BUILTIN,                                     
          VERIFY                   BUILTIN,                                     
          NULL                     BUILTIN,                                     
          ADDR                     BUILTIN,                                     
          CSTG                     BUILTIN,                                     
          MAX_ANTALL_TRANS         FIXED DEC(2)  INIT(0),                       
                                                                                
       1  FNR_REG,                                                              
          2  FNR1                  FIXED DEC(11),                               
          2  FNR2                  FIXED DEC(11),                               
          2  BRUKERID              CHAR  (4)    ,                               
                                                                                
       1  FEIL_STRUC,                                                           
          2  FEIL_NR               FIXED DEC(5),                                
          2  FEIL_MELDING          CHAR     (78),                               
          2  KOM_OMR_PEKER         POINTER,                                     
          2  BRUK_LOGG             POINTER,                                     
                                                                                
          PIC_11                   PIC'(11)9',                                  
      DAGENS_DATO_PLUSS2_ÅMD   PIC'(8)9',                                       
      DAGENS_DATO_PLUSS2_Å DEF DAGENS_DATO_PLUSS2_ÅMD POS(1) PIC'9999',         
      DAGENS_DATO_PLUSS2_M DEF DAGENS_DATO_PLUSS2_ÅMD POS(5) PIC'99',           
                                                                                
  /*  FNR_SØKER       PIC'(11)9', DENNE FELT ER FLYTTET TIL DIV-PARA*/          
      W_TRANS_PEKER            POINTER,                                         
      BMSMAPBR                 POINTER,                                         
      LOKAL_KOM_PTR            POINTER,                                         
      COMMAREA_PEKER           POINTER;                                         
                                                                                
    DCL GRUNN_OMR1                 CHAR      (1440);                            
    DCL GRUNN_OMR2                 CHAR      (1440);                            
    DCL GRUNN_IDENT                CHAR      ( 8);                              
                                                                                
    DCL 1 PIC_8,                                                                
          2 ÅR                 PIC'(4)9',                                       
          2 MND                PIC'(2)9',                                       
          2 DD                 PIC'(2)9',                                       
      1 PIC_8DEF DEF PIC_8     PIC'(8)9';                                       
                                                                                
 DCL 01 BRUKERINFO_REC,                                                         
        02 FNR                  PIC '(11)9',                                    
        02 BLANKET_TYPE         CHAR (2)   ,                                    
        02 TERMINAL_NR          CHAR (4)   ,                                    
        02 BRUKER_ID            CHAR (8)   ,                                    
        02 FILLER               CHAR (5)   ,                                    
        02 DATO                 PIC '(8)9' ,                                    
        02 TID                  PIC '(7)9' ,      /* HHHMMSS  */                
        02 KJORINGS_TYPE        CHAR (1)   ,                                    
        02 FIL1                 CHAR (06)  ;                                    
                                                                                
  DCL  HJELP_TID         FIXED DEC(7,0);                                        
                                                                                
  /* *************************************************************** */         
  /*                                                                 */         
  /*       EKSEKVERING BEGYNNER HER.                                 */         
  /*                                                                 */         
  /* *************************************************************** */         
  /* PROGRAM_ID  = 'R0010450' */                                                
   ALLOCATE GV_TAB_RE                                                ;          
   ALLOCATE G_TAB_RE                                                 ;          
   KOM_OMR.DIV_PARAM_PEKER   = ADDR(KOM_OMR.DIV_PARAM_OMR);                     
   KOM_OMR.TRANS_OPPL_PEKER  = ADDR(KOM_OMR.TRANS_OPPL_OMR);                    
   KOM_OMR.TRANS_PEKER       = ADDR(KOM_OMR.TRANS_OMR);                         
   KOM_OMR.TRANS_LISTE_PEKER = ADDR(KOM_OMR.TRANS_LISTE_OMR);                   
   KOM_OMR.STYRINGS_PEKER    = ADDR(KOM_OMR.STYRINGS_OMR);                      
   KOM_OMR.GV_PEKER          = ADDR (GRUNN_OMR1) ;                              
   KOM_OMR.G_PEKER           = ADDR (GRUNN_OMR2) ;                              
   ATK_KOM_PTR           = ADDR  (KOM_OMR.ATK_COM_OMR  ) ;                      
   ALLOCATE WORK_TRANS_LISTE, LOW_CHAR_BASED, LOKAL_KOM_OMR,POTALL_OPPL,        
                                                             FNRTAB;            
   ALLOCATE B00, B01, B02;                                                      
   HOVED_KOM_OMR_PTR         = COMMAREA_PEKER;                                  
   IF FEIL_MELD_NR = 0 THEN /* AB 01.08.16 */                                   
      DIV_PARAM_OMR.PROGRAM_ID  = 'R0010450';                                   
      FNR_REG.BRUKERID          =  DIV_PARAM_OMR.CICS_IND;                      
                                                                                
   FNRTAB                    = '';                                              
   POTALL_OPPL               = '';                                              
   B00                       = '';                                              
   B01                       = '';                                              
   B02                       = '';                                              
   GV_TAB_RE                 = '';                                              
   G_TAB_RE                  = '';                                              
   ATK_KOM_OMR.ACF2_F_KODE   = ' ';                                             
                                                                                
   FEIL_MELD_NR             = 0;                                                
   DO K = 1 TO 14;                                                              
     FNR_ARRAY(K)  =  0;                                                        
   END;                                                                         
                                                                                
   FNR_ARRAY_IND             =  1;                                              
   FNR_ARRAY_ANT             =  0;                                              
   TRANS_ANT                 =  1;                                              
                                                                                
                                                                                
   DO I = 1 TO 40;                                                              
      TRANS_LISTE.TRANS_OPPL_PTR(I)  = NULL;                                    
      TRANS_LISTE.TRANS_PTR     (I)  = NULL;                                    
      TRANS_LISTE.FNR_EK        (I)  = 0;                                       
      TRANS_LISTE.ANT_BARN      (I)  = 0;                                       
   END;                                                                         
                                                                                
                                                                                
   GRUNN_IDENT                     = 'P0019924';                                
   CALL P9956_BER_G_CICS(GRUNN_IDENT,GRUNN_OMR1);                               
   GRUNN_IDENT                     = 'P0019925';                                
   CALL P9956_BER_G_CICS(GRUNN_IDENT,GRUNN_OMR2);                               
                                                                                
   /*--------------------------------------------------------------*/           
   /* LESER FJERNINGSKODE OG RETURFUNKSJON                         */           
   /*--------------------------------------------------------------*/           
                                                                                
   EXEC CICS RECEIVE MAP('S00101E') MAPSET('S001013') INTO(S00101EI);           
                                                                                
   IF S00101EI.RETUR_FUNKSJONL    > 0 THEN                                      
      DO;                                                                       
         FUNKSJONSKODE = S00101EI.RETUR_FUNKSJONI ;                             
                                                                                
         SELECT(FUNKSJONSKODE);                                                 
                                                                                
         WHEN('V')                                                              
            DO;                                                                 
               STYREKODE                    =  '  '   ;                         
               TRANS_OPPL_OMR.TRANSKODE     =  'R048' ;                         
               CALL DATABASE('CLOSE');                                          
               EXEC CICS XCTL  PROGRAM('R0010301') COMMAREA(KOM_OMR);           
              /*                                                                
               ALLOCATE S001481O ;                                              
               S001481O.FNRO             = FNR_SØKER  ;                         
               S001481O.CICS_INFOO       = DIV_PARAM_OMR.CICS_NAVN ;            
               S001481O.ROLLEO       = DIV_PARAM_OMR.ATK_ROLLE ;                
               **----------------------------------------------**               
               ** NULLSTILLER FNR DERSOM UGYLDIG               **               
               **----------------------------------------------**               
               IF ^ F_GYLDIG_FNR(S001481O.FNRO)  THEN                           
                   S001481I.FNRI = LOW(11) ;                                    
               **----------------------------------------------**               
               ** SKRIVER UT VENTETRANS-BILDET M/ OVERFØRT FNR **               
               **----------------------------------------------**               
               EXEC CICS SEND MAP('S001481') MAPSET('S001V03')                  
                                                    ERASE ;                     
               TRANS_OPPL_OMR.TRANSKODE   = 'R048';                             
               EXEC CICS RETURN TRANSID     (TRANS_OPPL_OMR.TRANSKODE)          
                                COMMAREA    (KOM_OMR);                          
               /*----------------------------------------------*/               
               /* ETTER TRYKKET ENTER VIA CICS TIL R0010480    */               
               /* - BEHANDLING AV VENTETRANS-REGISTERET        */               
               /*----------------------------------------------*/               
            END;                                                                
   /*                                                                           
         WHEN('F')                                                              
            DO;                                                                 
               ALLOCATE S001015O ;                                              
               S001015O.FNRO       = SEARCH_FNR ;                               
               S001015O.CICS_INFOO = DIV_PARAM_OMR.CICS_NAVN ;                  
               S001015O.ROLLEO       = DIV_PARAM_OMR.ATK_ROLLE ;                
               /*----------------------------------------------*/               
               /* SJEKKER FNR OG NULLSTILLER DERSOM UGYLDIG    */               
               /*----------------------------------------------*/               
     /*                                                                         
               IF ^ F_GYLDIG_FNR(S001015O.FNRO)  THEN                           
                  DO;                                                           
                     S001015I.FNRI = LOW(11) ;                                  
                     S001015I.FNRL = -1  ;                                      
                  END;                                                          
               ELSE                                                             
                  DO;                                                           
                     S001015O.OPPLYSNINGSTYPEO = 'S' ;                          
                     S001015I.FNRL             = -1  ;                          
                  END;                                                          
                                                                                
               /*-----------------------------------------------*/              
               /* SKRIVER UT FORESPØRSEL-BILDET M/ OVERFØRT FNR */              
               /*-----------------------------------------------*/              
   /*                                                                           
               EXEC CICS SEND MAP('S001015') MAPSET('S001013')                  
                                             CURSOR ERASE ;                     
               TRANS_OPPL_OMR.TRANSKODE  =  'F410';                             
                                                                                
               EXEC CICS RETURN TRANSID     (TRANS_OPPL_OMR.TRANSKODE)          
                                COMMAREA    (KOM_OMR);                          
            END;                                                                
   */                                                                           
         OTHERWISE                                                              
            DO;                                                                 
                                                                                
               STYREKODE = '  ';                                                
                                                                                
               IF FEIL THEN                                                     
                  DO;                                                           
                     KOM_OMR.TRANS_OPPL_PEKER  =                                
                                        ADDR(KOM_OMR.TRANS_OPPL_OMR);           
                     TRANS_OPPL_OMR.TRANSKODE  = 'R045';                        
                     EXEC CICS RETURN TRANSID (TRANS_OPPL_OMR.TRANSKODE)        
                                      COMMAREA(KOM_OMR);                        
                     /*----------------------------------------------*/         
                     /* VIA CICS TILBAKE TIL STARTEN AV R0010450     */         
                     /* FOR Å TA INN NY STYRINGSKODE / FUNKSJONSKODE */         
                     /*----------------------------------------------*/         
                  END;                                                          
                                                                                
               /*----------------------------------------------------*/         
               /* VIA CICS TILBAKE TIL R0010301, VELG FUNKSJONSKODE  */         
               /*----------------------------------------------------*/         
                                                                                
               EXEC CICS XCTL  PROGRAM('R0010301') COMMAREA(KOM_OMR);           
            END;                                                                
         END;  /* SELECT  */                                                    
      END;     /* IF THEN */                                                    
                                                                                
                                                                                
   SELECT(S00101EI.FJERNINGSKODEI);                                             
                                                                                
      WHEN('ST','AT')                                                           
         STYREKODE = S00101EI.FJERNINGSKODEI;                                   
                                                                                
      OTHERWISE                                                                 
        DO;                                                                     
           /*-------------------------------------------------------*/          
           /* SKRIVER MELDING OM 'FEIL KODE REGISTRERT              */          
           /*-------------------------------------------------------*/          
                                                                                
           MAP_MELDING    = 'FEIL KODE REGISTRERT';                             
           S00101EI.FJERNINGSKODEL = -1;    /* CURSOR POS           */          
           CALL SEND_MAP;                                                       
           GO TO RETUR;                                                         
        END;                                                                    
   END;              /*  SELECT          */                                     
                                                                                
   ALLOCATE WORK_TRANS_LISTE;                                                   
                                                                                
   WORK_TRANS_LISTE = '';                                                       
   DO I = 1 TO 5;                                                               
      TRANS_LISTE_LINJE(I) = WORK_TRANS_LISTE;                                  
   END;                                                                         
                                                                                
                                                                                
   S00101EI.FNRL     = -1;          /* CURSOR POS   */                          
                                                                                
   IF ^ F_NUMERISK(S00101EI.FNRI)  THEN                                         
      DO;                                                                       
         MAP_MELDING = 'FNR INNEHOLDER IKKE-NUMERISKE FELT';                    
         CALL SEND_MAP;                                                         
         GOTO RETUR;                                                            
      END;                                                                      
   ELSE                                                                         
      IF ^ F_GYLDIG_FNR(S00101EO.FNRO)  THEN                                    
         DO;                                                                    
            MAP_MELDING = 'FNR UGYLDIG';                                        
            CALL SEND_MAP;                                                      
            GOTO RETUR;                                                         
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
            /*----------------------------------------------------*/            
            /* VIA CICS TIL R0019906:SJEKKES MOT FNR-ENDRINGS-REG */            
            /* OM FØDSELSNUMMERET ER ENDRET                       */            
            /*----------------------------------------------------*/            
                                                                                
            FNR_REG.FNR1     = S00101EO.FNRO;                                   
                                                                                
            EXEC CICS LINK PROGRAM('R0019906') COMMAREA(FNR_REG);               
                                                                                
            IF FNR_REG.FNR2  > 0   THEN                                         
               DO;                                                              
                  PIC_11        =  S00101EO.FNRO;                               
                  S00101EO.FNRO =  FNR_REG.FNR2;                                
                  MAP_MELDING   = 'FØDSELSNUMMER '!!PIC_11!!                    
                                  ' ER ENDRET, TRYKK ENTER !';                  
                  CALL SEND_MAP;                                                
                  GOTO RETUR;                                                   
               END;                                                             
                                                                                
            FNR_SØKER  = S00101EO.FNRO;                                         
            SEARCH_FNR = S00101EO.FNRO;                                         
                                                                                
            /*----------------------------------------------------*/            
            /* SUB-PROGRAM AV R0010450 SOM ÅPNER DATABASEN        */            
            /*----------------------------------------------------*/            
                                                                                
            CALL DATABASE('OPEN');                                              
                                                                                
            IF FEIL THEN                                                        
               DO;                                                              
                  CALL SEND_MAP;                                                
                  GOTO RETUR;                                                   
               END;                                                             
                                                                                
                                                                                
            SELECT(STYREKODE);                                                  
               WHEN('ST')                                                       
                  DO;                                                           
                     IF SUBSTR(DIV_PARAM_OMR.CICS_NAVN,1,4) =                   
                                                           'HELP' THEN          
                        DO;                                                     
                           MAP_MELDING  =                                       
            'FJERNING AV SISTE TRANS. ER IKKE TILLATT FOR  US-BLANKET';         
                           S00101EI.FJERNINGSKODEL = -1;                        
                           FEIL        = '1'B;                                  
                           CALL SEND_MAP;                                       
                           GOTO RETUR;                                          
                        END;                                                    
                                                                                
                     CALL VIS_MEG_SISTE_TRANS;                                  
                                                                                
    /* ENDERING SP FOR CHECKING NEST_SISTE STATUS  FOR  ST   */                 
                                                                                
                     IF B01.STATUS.STATUS_KODE (1) ^=  'N' &                    
                        B01.STATUS.STATUS_KODE (2) ^=  'N' THEN                 
                        DO;                                                     
                           MAP_MELDING  =                                       
    'FJERNING AV SISTE TRANS. ER IKKE TILLATT...N_SISTE STATUS MANGLER';        
                           S00101EI.FJERNINGSKODEL = -1;                        
                           FEIL        = '1'B;                                  
                           CALL SEND_MAP;                                       
                           GOTO RETUR;                                          
                        END;                                                    
      /* ENDERING SP TIL HERE                                     */            
                     /*---------------------------------------------*/          
                     /* R0010451 : LESER SISTE TRANS FRA TRANS-HIST */          
                     /*---------------------------------------------*/          
                                                                                
                     IF FATAL_FEIL THEN                                         
                        GOTO FATAL_SLUTT;                                       
                     ELSE IF FEIL THEN                                          
                        DO;                                                     
                           CALL SEND_MAP        ;                               
                           GOTO RETUR           ;                               
                        END;                                                    
                                                                                
                     CALL MAPS_UT_ST;                                           
                                                                                
                     /*-----------------------------------------------*/        
                     /* PROCEDURE I R0010451 SOM SKRIVER SISTE-STATUS */        
                     /* OG NEST-SISTE STATUS TIL SKJERMEN             */        
                     /*-----------------------------------------------*/        
                                                                                
                     CALL HENT_SVAR_OG_BEKREFT;                                 
                                                                                
                     /*-----------------------------------------------*/        
                     /* PROCEDURE I R0010451 SOM HENTER SVAR OG       */        
                     /* BEKREFELSE PÅ FJERNING                        */        
                     /*-----------------------------------------------*/        
                                                                                
                     IF S0010E6I.FJERNEI = 'B' THEN                             
                        DO;                                                     
                           CALL FJERN_SISTE_TRANS;                              
                                                                                
                           /*-----------------------------------------*/        
                           /* PROCEDURE I R0010451: OMGJØR N-SISTE    */        
                           /* STATUS TIL SISTE STATUS, SKRIVER DETTE  */        
                           /* UT PÅ BASEN VED R0015401                */        
                           /*-----------------------------------------*/        
                                                                                
                           IF FATAL_FEIL THEN                                   
                              GOTO FATAL_SLUTT;                                 
                                                                                
                           W_TRANS_PEKER = TRANS_PEKER;                         
                                                                                
                           /*-----------------------------------------*/        
                           /* VIA CICS TIL R0018090:                  */        
                           /* SKRIV TRANS PÅ RIKSREVSJON DB           */        
                           /*-----------------------------------------*/        
                           /*-----------------------------------------*/        
                           /* VIA CICS TIL R0010490:                  */        
                           /* SKRIV TRANS PÅ VENTEREG                 */        
                           /*-----------------------------------------*/        
                                                                                
                           DO I = 1 TO ANTALL_TRANS_I_TRANS_LISTE;              
                              TRANS_PEKER      = TRANS_PTR      (I);            
                              TRANS_OPPL_PEKER = TRANS_OPPL_PTR (I);            
                                                                                
                           /* SKRIV TRANS PÅ RIKSREVSJON DB           */        
                              EXEC CICS LINK PROGRAM   ('R0018090')             
                                             COMMAREA  ( KOM_OMR  );            
                                                                                
                              IF FEIL_MELD_NR   >  0 THEN                       
                                 DO;                                            
                                    TRANS_PEKER =  W_TRANS_PEKER;               
                                    GOTO FATAL_SLUTT;                           
                                 END;                                           
                           /*-----------------------------------------*/        
                           /* VIA CICS TIL R0010490:                  */        
                           /* SKRIV TRANS PÅ VENTEREG                 */        
                           /*-----------------------------------------*/        
                                                                                
                           /* SKRIV TRANS PÅ VENTEREG                 */        
                              EXEC CICS LINK PROGRAM   ('R0010490')             
                                             COMMAREA  ( KOM_OMR  );            
                                                                                
                              IF FEIL_MELD_NR   >  0 THEN                       
                                 DO;                                            
                                    TRANS_PEKER =  W_TRANS_PEKER;               
                                    GOTO FATAL_SLUTT;                           
                                 END;                                           
                           END;                                                 
                                                                                
                           TRANS_PEKER = W_TRANS_PEKER;                         
                        END;                                                    
                  END;                                                          
                                                                                
               WHEN('AT')                                                       
                  DO;                                                           
                     /*---------------------------------------------*/          
                     /* PROCEDURE I R0010452:LESER FØRSTE TRANSEN   */          
                     /* FRA TRANSHIST VED R0015201                  */          
                     /*---------------------------------------------*/          
                                                                                
                     CALL FORBEREDE_TRANS;                                      
                                                                                
                     IF FEIL_MELD_NR > 650 &                                    
                        FEIL_MELD_NR < 660  THEN                                
                        DO;                                                     
                           MAP_MELDING = ACF2_FEIL(FEIL_MELD_NR);               
                           CALL SEND_MAP        ;                               
                                                                                
                           FATAL_FEIL     = '0'B;                               
                           GOTO RETUR           ;                               
                        END;                                                    
                     ELSE                                                       
                     IF FATAL_FEIL  THEN                                        
                        GOTO  FATAL_SLUTT;                                      
                     ELSE IF  FEIL  THEN                                        
                          DO;                                                   
                             CALL SEND_MAP        ;                             
                             GOTO RETUR           ;                             
                           END;                                                 
                     /*---------------------------------------------*/          
                     /* PROCEDURE I R0010452:LESER ALLE TRANSER     */          
                     /* OG LEGGER DEM UT I EN TRANSLISTE            */          
                     /*---------------------------------------------*/          
                                                                                
                     CALL HENT_TRANS;                                           
                                                                                
                     IF FATAL_FEIL  THEN                                        
                        GOTO  FATAL_SLUTT;                                      
                     ELSE IF  FEIL  THEN                                        
                        DO;                                                     
                           CALL SEND_MAP        ;                               
                           GOTO RETUR           ;                               
                        END;                                                    
                                                                                
                     /*----------------------------------------------*/         
                     /* PROCEDURE I R0010452 SOM SKRIVER             */         
                     /* EN OG EN TRANS UT PÅ SKJERMEN                */         
                     /*----------------------------------------------*/         
                                                                                
                     CALL MAPS_UT_AT;                                           
                                                                                
                     /*----------------------------------------------*/         
                     /* PROCEDURE I R0010451 SOM HENTER SVAR OG      */         
                     /* BEKREFELSE PÅ FJERNING                       */         
                     /*----------------------------------------------*/         
                                                                                
                     CALL HENT_SVAR_OG_BEKREFT;                                 
                                                                                
                     IF S0010E6I.FJERNEI = 'B' THEN                             
                        DO;                                                     
                           /*----------------------------------------*/         
                           /* PROCEDURE I R0010452 SOM FJERNER ALLE  */         
                           /* TRANSER, N-SISTE STATUS OG SISTE STATUS*/         
                           /*----------------------------------------*/         
                           W_PERSN_KODE   = B01.PERSN_KODE   (14);              
                           CALL FJERN_ALLE_TRANS;                               
                           B01.PERSN_KODE   (14) = W_PERSN_KODE;                
                                                                                
                           IF FATAL_FEIL THEN                                   
                              GOTO FATAL_SLUTT;                                 
                                                                                
                           /*----------------------------------------*/         
                           /*-----------------------------------------*/        
                           /* VIA CICS TIL R0018090:                  */        
                           /* SKRIV TRANS PÅ RIKSREVSJON DB           */        
                           /* VIA CICS TIL R0010490:                  */        
                           /* SKRIV TRANS PÅ VENTEREG                 */        
                           /*-----------------------------------------*/        
                           /*----------------------------------------*/         
                                                                                
                           W_TRANS_PEKER       = TRANS_PEKER;                   
                                                                                
                           DO I = 1 TO ANTALL_TRANS_I_TRANS_LISTE  ;            
                              TRANS_PEKER      = TRANS_PTR      (I);            
                              TRANS_OPPL_PEKER = TRANS_OPPL_PTR (I);            
                                                                                
                           /* SKRIV TRANS PÅ RIKSREVSJON DB           */        
                              EXEC CICS LINK PROGRAM  ('R0018090')              
                                             COMMAREA ( KOM_OMR  ) ;            
                                                                                
                                                                                
                              IF FEIL_MELD_NR   > 0 THEN                        
                                 DO;                                            
                                    TRANS_PEKER = W_TRANS_PEKER;                
                                    GOTO FATAL_SLUTT;                           
                                 END;                                           
                                                                                
                           /* SKRIV TRANS PÅ VENTEREG                 */        
                              EXEC CICS LINK PROGRAM  ('R0010490')              
                                             COMMAREA ( KOM_OMR  ) ;            
                                                                                
                                                                                
                              IF FEIL_MELD_NR   > 0 THEN                        
                                 DO;                                            
                                    TRANS_PEKER = W_TRANS_PEKER;                
                                    GOTO FATAL_SLUTT;                           
                                 END;                                           
                           END;                                                 
                           TRANS_PEKER = W_TRANS_PEKER;                         
                        END;                                                    
                  END;                                                          
               OTHERWISE ;                                                      
            END;        /*       SELECT          */                             
                                                                                
            KOM_OMR.TRANS_OPPL_PEKER  = ADDR(KOM_OMR.TRANS_OPPL_OMR);           
            TRANS_OPPL_OMR.TRANSKODE  = 'R045';                                 
                                                                                
         END;                                                                   
                                                                                
   /*----------------------------------------------------------------*/         
   /** VED NORMAL AVSLUTNING SENDES ENDRINGS - MAP UT PåNYTT         */         
   /*----------------------------------------------------------------*/         
                                                                                
   ALLOCATE S00101EO              ;                                             
   S00101EO.CICS_INFOO = DIV_PARAM_OMR.CICS_NAVN ;                              
   S00101EO.ROLLEO       = DIV_PARAM_OMR.ATK_ROLLE ;                            
                                                                                
   EXEC CICS SEND  MAP ('S00101E') MAPSET('S001013') ERASE ;                    
                                                                                
  RETUR:                                                                        
   IF FEIL THEN                                                                 
      DO;                                                                       
         KOM_OMR.TRANS_OPPL_PEKER  =  ADDR (KOM_OMR.TRANS_OPPL_OMR);            
         TRANS_OPPL_OMR.TRANSKODE  = 'R045';                                    
      END;                                                                      
                                                                                
   EXEC CICS RETURN TRANSID(TRANS_OPPL_OMR.TRANSKODE)COMMAREA(KOM_OMR);         
                                                                                
                                                                                
  FATAL_SLUTT:                                                                  
   FEIL_MELDING = 'PROGRAM ' !! DIV_PARAM_OMR.PROGRAM_ID   !!                   
                  ' GÅR I FEIL VED LABEL NR. '             !!                   
                    DIV_PARAM_OMR.FEIL_VED_LABEL;                               
                                                                                
   EXEC CICS PURGE MESSAGE;                                                     
   EXEC CICS SEND  TEXT FROM (FEIL_MELDING) ACCUM ERASE PAGING                  
                                                  JUSTIFY (10);                 
   FEIL_STRUC.KOM_OMR_PEKER = COMMAREA_PEKER;                                   
   FEIL_STRUC.FEIL_NR       = FEIL_MELD_NR;                                     
                                                                                
   EXEC CICS LINK PROGRAM ('R0019921') COMMAREA (FEIL_STRUC);                   
                                                                                
   /*---------------------------------------------------------------*/          
   /* HENTER FEILMELDING FRA FEILMELDINGSBASEN                      */          
   /*---------------------------------------------------------------*/          
                                                                                
   EXEC CICS SEND TEXT FROM (FEIL_MELDING)  JUSTIFY (14)                        
                                            ACCUM   PAGING;                     
   IF FEIL_MELD_NR ^=  499 THEN                                                 
      FEIL_MELDING  = 'RETUR_KODE = ' !! DIV_PARAM_OMR.DB_STATUS_KODE;          
   ELSE                                                                         
      DO;                                                                       
         PIC_11       =  SEARCH_FNR;                                            
         FEIL_MELDING = 'FEIL PÅ FNR: ' !! PIC_11;                              
      END;                                                                      
                                                                                
   EXEC CICS SEND TEXT FROM (FEIL_MELDING)  JUSTIFY(18) ACCUM PAGING;           
                                                                                
   FEIL_MELDING = 'TASKEN BLIR KANSELERT !!!!               ' !!                
                  'TASKEN BLIR KANSELERT !!!!';                                 
                                                                                
   EXEC CICS SEND  TEXT   FROM (FEIL_MELDING) JUSTIFY(22) ACCUM PAGING;         
   EXEC CICS SEND  PAGE;                                                        
                                                                                
   /*---------------------------------------------------------------*/          
   /*        NB!   NB! TASKEN BLIR KANSELERT                        */          
   /*---------------------------------------------------------------*/          
                                                                                
   EXEC CICS ABEND ABCODE(STOP);                                                
                                                                                
                                                                                
                                                                                
  /* *************************************************************** */         
  /*       PROCEDURE FOR Å SENDE MAP_MELDING TIL SKJERM.             */         
  /* *************************************************************** */         
                                                                                
  SEND_MAP:                                                                     
     PROC;                                                                      
         S00101EO.MELDINGO   = MAP_MELDING;                                     
         S00101EO.CICS_INFOO = DIV_PARAM_OMR.CICS_NAVN ;                        
         S00101EO.ROLLEO       = DIV_PARAM_OMR.ATK_ROLLE ;                      
         EXEC CICS SEND  MAP('S00101E') MAPSET('S001013') ERASE CURSOR;         
  END SEND_MAP;                                                                 
                                                                                
                                                                                
  /* --------------------------------------------------------------- */         
  /*       PROCEDURE SOM ÅPNER DATABASEN                             */         
  /* --------------------------------------------------------------- */         
                                                                                
 DATABASE:                                                                      
   PROC(STYRING);                                                               
                                                                                
   DCL                                                                          
     STYRING                               CHAR(5),                             
     NULL                                  BUILTIN,                             
     UNSPEC                                BUILTIN,                             
     PLITDLI                               ENTRY;                               
                                                                                
   %INCLUDE P0012002;   /* PCB - PEKER - OMR   */                               
                                                                                
   /*---------------------------------------------------------------*/          
   /* DL1-PARAMETRE SOM BRUKES VED INITIERINGS- OG AVSLUTTNINGS-KALL*/          
   /*---------------------------------------------------------------*/          
                                                                                
   DCL                                                                          
      W01_PSB_NAVN                CHAR (8)       INIT('        '),              
      W01_PCB_FUNCTION            CHAR (4)       INIT('PCB '),                  
      W01_TERM_FUNCTION           CHAR (4)       INIT('TERM');                  
   /*                                                                           
   +----------------------------------------------------------------+           
   !    ARBEIDSFELTER SOM BRUKES AV UIB (USER INTERFACE BLOCK)      !           
   +----------------------------------------------------------------+           
   */                                                                           
   %INCLUDE DLIUIB;                                                             
                                                                                
   DCL 1  W01_DLI_PARAM,                                                        
          2   W02_GU             CHAR (4)       INIT('GU  '),                   
          2   W02_PARM_CT_1      FIXED BIN(31)  INIT(1)     ,                   
          2   W02_PARM_CT_3      FIXED BIN(31)  INIT(3)     ,                   
          2   W02_PARM_CT_4      FIXED BIN(31)  INIT(4)     ;                   
                                                                                
                                                                                
     IF STYRING = 'OPEN' THEN                                                   
        DO;                                                                     
           W01_PSB_NAVN    =   DIV_PARAM_OMR.PSB_NAVN;                          
                                                                                
           CALL   PLITDLI                        (W02_PARM_CT_3,                
                                                  W01_PCB_FUNCTION,             
                                                  W01_PSB_NAVN,                 
                                                  UIBPTR);                      
                                                                                
           IF DLIUIB.UIBFCTR                  =  '00000000'B THEN               
              DO;                                                               
                 ALLOCATE PCB_UIB_PEKER_OMR;                                    
                 PCB_UIB_PEKER_OMR.PCB_PEKER  =   UIBPCBAL;                     
                 PCB_UIB_PEKER_OMR.UIB_PEKER  =   UIBPTR;                       
              END;                                                              
           ELSE                                                                 
            DO;                                                                 
               FEIL        = '1'B;                                              
               MAP_MELDING = /*'NOE ER GALT MED DATABASEN, KODE: ' !!*/         
            'PENSJONSREGISTERET ER IKKE TILGJENGELIG. KODE:'!!                  
                     UNSPEC(UIBFCTR) !! ' ' !! UNSPEC(UIBDLTR);                 
            END;                                                                
        END;                                                                    
     ELSE                                                                       
        DO;                                                                     
           CALL   PLITDLI                        (W02_PARM_CT_1,                
                                                  W01_TERM_FUNCTION);           
           KOM_OMR.PCB_UIB_PEKER = NULL;                                        
        END;                                                                    
                                                                                
 END DATABASE;                                                                  
                                                                                
  /*                                                                            
  + --------------------------------------------------------------- +           
  !       PROCEDURE FOR LEGGE LOW-VALUES I MAP'EN                   !           
  + --------------------------------------------------------------- +           
  */                                                                            
 LOW_VALUES_TIL_MAP:                                                            
   PROC;                                                                        
      LOW_CHAR_BASED = (300)' ';           /* X'00'  */                         
 END LOW_VALUES_TIL_MAP;                                                        
                                                                                
                                                                                
   %INCLUDE R0010451;           /* VIS_MEG_SISTE_TRANS      */                  
   %PAGE;                                                                       
   %INCLUDE R0010452;           /* VIS_MEG_ALLE_TRANS       */                  
   %PAGE;                                                                       
   %INCLUDE R0019904;           /* F_GYLDIG_FNR             */                  
   %PAGE;                                                                       
   %INCLUDE R0019910;           /* F_NUMERISK               */                  
   %PAGE;                                                                       
   %INCLUDE R0019956;           /* P9956_BER_G_CICS         */                  
   %INCLUDE R0019989;           /* KONV_HÅMD_MÅ             */                  
   %INCLUDE R0019998;           /* ACF2_FEIL                */                  
   %INCLUDE R0019999;           /* ACF2 KONTROLL            */                  
   %PAGE;                                                                       
                                                                                
 /* **************************************************************** */         
 /*    SKRIV INFORMASJON TIL BURKER REG                              */         
 /* **************************************************************** */         
  SKRIV_BRUKER_ID: PROC;                                                        
                                                                                
        BRUKERINFO_REC.FNR           = DIV_PARAM_OMR.SEARCH_FNR;                
        BRUKERINFO_REC.BLANKET_TYPE  = STYRINGS_OMR.STYREKODE;                  
        BRUKERINFO_REC.BRUKER_ID     = DIV_PARAM_OMR.BRUKER_ID;                 
        BRUKERINFO_REC.DATO          = DIV_PARAM_OMR.DATO_2000;                 
                                                                                
        HJELP_TID                    = EIBTIME;                                 
        BRUKERINFO_REC.TID           = HJELP_TID;                               
        BRUKERINFO_REC.KJORINGS_TYPE = STYRINGS_OMR.FUNKSJONSKODE   ;           
        BRUKERINFO_REC.TERMINAL_NR   = DIV_PARAM_OMR.TERMINAL_NR;               
     /* BRUKERINFO_REC.BRUKER_NAVN   = DIV_PARAM_OMR.BRUKER_PASS; */            
                                                                                
        IF BRUKERINFO_REC.BLANKET_TYPE  ^= 'US' THEN                            
           EXEC CICS WRITE DATASET('BRUKINFO')                                  
                    FROM (BRUKERINFO_REC)                                       
                    RIDFLD(BRUK_LOGG) RBA;                                      
  END SKRIV_BRUKER_ID;                                                          
 END R010450;                                                                   
