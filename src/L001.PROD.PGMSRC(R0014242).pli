 /*   SIST ENDRET PÅ PROD   2006.07.18 11.36.23 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2006.04.24  9.42.45 AV   SPA2970          */        
 /*   SIST ENDRET PÅ QASS   2006.04.19 12.10.45 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2006.03.24  8.43.48 AV   SPA2970          */        
 /*       SIST ENDRET 04/05-98 12.59.31 AV   JDA7339                  */        
 /*       SIST ENDRET 25/02-98 09.47.59 AV   RFA9991                  */        
 /* *************************************************************** */          
 /*IDENTIFIKASJON:                                                  */          
 /*    R0014242 - UNDERPROGRAM I PLI                                */          
 /*    PROGRAMMERER: HERMAN LARSSEN - OKT 89                        */          
 /*HENSIKT:                                                         */          
 /*    OPPDATE_2000RE YRKEPENS.SPT OG YPT                                       
 /*                                                                 */          
 /*    REGULERE YRKEFORDELER VED ENDRING I POENGTILLEGG             */          
 /*                                                                 */          
 /*    HVIS KODE FOR POENGTILLEGG ENDRES, SÅ SKAL SLUTTPOENGTALLET  */          
 /*    SOM UFØREPENSJONIST ENDRES.                                  */          
 /*                                                                 */          
 /*    HVIS YRKESSKADEFORDELER REGNES AV AÅI, SÅ ER DET IKKE AKTUELT*/          
 /*    MED POENGTILLEGG.                                            */          
 /*                                                                 */          
 /*    KODENE FOR POENGTILLEGG BETYR :                              */          
 /*                                                                 */          
 /*       A : SØKEREN FÅR HELE TILLEGGET (ETT POENG)                */          
 /*       B : SØKER OG EKTEFELLE FåR ET HALVT POENG HVER            */          
 /*       C : EKTEFELLEN FÅR HELE TILLEGGET                         */          
 /*                                                                 */          
 /*PROGRAMTILKNYTNING:                                              */          
 /*    INCLUDES I R0014201 - ALDERSPENSJON                          */          
 /*    CALL FRA   R0014222                                          */          
 /*BRUK:                                                            */          
 /*    CALL POENGTILLEGG_ENDRES_YP                                  */          
 /* *************************************************************** */          
 /*   */                                                                        
 POENGTILLEGG_ENDRES_YP :                                                       
   PROC;                                                                        
 /*   */                                                                        
                                                                                
       DCL SISTE      FIXED BIN (15);                                           
                                                                                
                                                                                
      /** FINNE SISTE UTFYLTE YUFT_ÅMD  **/                                     
      DO J = 10  TO 1 BY -1 UNTIL(B02.YRKEHIST.YUFT_ÅMD                         
                            (BER_SØ_IND,J) > 0);                                
         SISTE = J;                                                             
      END;                                                                      
                                                                                
                                                                                
      IF B02.YRKEPENS.SPT(BER_SØ_IND) = 0         THEN                          
 /*   IF B02.YRKEPENS.YPT(BER_SØ_IND) >                                         
         B02.YRKEHIST.PAÅ(BER_SØ_IND,SISTE)       THEN    */                    
                                                                                
         DO;                                                                    
                                                                                
            IF B01.POENGTILLEGG_KODE(BER_SØ_IND) = 'A'  THEN                    
               IF B02.POENGTILLEGG_KODE(BER_SØ_IND) = 'B'  THEN                 
                                                                                
                 /*GÅR NED 0.5 POENG*/                                          
                                                                                
                  DO;                                                           
                     IF B02.YRKEPENS.YPT(BER_SØ_IND) - 0.5  <                   
                        B02.YRKEHIST.PAÅ(BER_SØ_IND,SISTE) THEN                 
                                                                                
                        B02.YRKEPENS.YPT(BER_SØ_IND) =                          
                        B02.YRKEHIST.PAÅ(BER_SØ_IND,SISTE);                     
                                                                                
                     ELSE                                                       
                                                                                
                        B02.YRKEPENS.YPT(BER_SØ_IND) =                          
                        B02.YRKEPENS.YPT(BER_SØ_IND) - 0.5;                     
                  END;                                                          
               ELSE /*KODE ER IKKE = B, - HELE TILLEGGET FALLER BORT*/          
                  DO;                                                           
                     IF B02.YRKEPENS.YPT(BER_SØ_IND) - 1  <                     
                        B02.YRKEHIST.PAÅ(BER_SØ_IND,SISTE) THEN                 
                                                                                
                        B02.YRKEPENS.YPT(BER_SØ_IND) =                          
                        B02.YRKEHIST.PAÅ(BER_SØ_IND,SISTE);                     
                                                                                
                     ELSE                                                       
                                                                                
                        B02.YRKEPENS.YPT(BER_SØ_IND) =                          
                        B02.YRKEPENS.YPT(BER_SØ_IND) - 1;                       
                  END;                                                          
            ELSE                                                                
            IF B01.POENGTILLEGG_KODE(BER_SØ_IND) = 'B'  THEN                    
               IF B02.POENGTILLEGG_KODE(BER_SØ_IND) = 'A'  THEN                 
                                                                                
                  DO;                                                           
                     IF B02.YRKEPENS.YPT(BER_SØ_IND) + 0.5  < 8.33              
                                                           THEN                 
                                                                                
                        B02.YRKEPENS.YPT(BER_SØ_IND) =                          
                        B02.YRKEPENS.YPT(BER_SØ_IND) + 0.5;                     
                     ELSE                                                       
                        B02.YRKEPENS.YPT(BER_SØ_IND) = 8.33;                    
                  END;                                                          
               ELSE /*DET HALVE POENGTILLEGGET FALLER BORT*/                    
                  DO;                                                           
                     IF B02.YRKEPENS.YPT(BER_SØ_IND) - 0.5  <                   
                        B02.YRKEHIST.PAÅ(BER_SØ_IND,SISTE) THEN                 
                                                                                
                        B02.YRKEPENS.YPT(BER_SØ_IND) =                          
                        B02.YRKEHIST.PAÅ(BER_SØ_IND,SISTE);                     
                                                                                
                     ELSE                                                       
                                                                                
                        B02.YRKEPENS.YPT(BER_SØ_IND) =                          
                        B02.YRKEPENS.YPT(BER_SØ_IND) - 0.5;                     
                  END;                                                          
            ELSE                                                                
                                                                                
 /* FJERNET 21.11.89 HL : */                                                    
                                                                                
 /*         IF B01.POENGTILLEGG_KODE(BER_SØ_IND) = 'C'  THEN     */             
               IF B02.POENGTILLEGG_KODE(BER_SØ_IND) = 'B'  THEN                 
                                                                                
                  DO;                                                           
                     IF B02.YRKEPENS.YPT(BER_SØ_IND) + 0.5  < 8.33              
                                                           THEN                 
                                                                                
                        B02.YRKEPENS.YPT(BER_SØ_IND) =                          
                        B02.YRKEPENS.YPT(BER_SØ_IND) + 0.5;                     
                     ELSE                                                       
                        B02.YRKEPENS.YPT(BER_SØ_IND) = 8.33;                    
                  END;                                                          
               ELSE                                                             
               IF B02.POENGTILLEGG_KODE(BER_SØ_IND) = 'A'  THEN                 
                  DO;                                                           
                     IF B02.YRKEPENS.YPT(BER_SØ_IND) + 1  < 8.33                
                                                           THEN                 
                                                                                
                        B02.YRKEPENS.YPT(BER_SØ_IND) =                          
                        B02.YRKEPENS.YPT(BER_SØ_IND) + 1;                       
                     ELSE                                                       
                        B02.YRKEPENS.YPT(BER_SØ_IND) = 8.33;                    
                  END;                                                          
         END;                                                                   
                                                                                
      ELSE                                                                      
           /*SPT ER NÅ > 0 */                                                   
         DO;                                                                    
                                                                                
            IF B01.POENGTILLEGG_KODE(BER_SØ_IND) = 'A'  THEN                    
               IF B02.POENGTILLEGG_KODE(BER_SØ_IND) = 'B'  THEN                 
                                                                                
                 /*GÅR NED 0.5 POENG*/                                          
                                                                                
                  B02.YRKEPENS.SPT(BER_SØ_IND) =                                
                      B02.YRKEPENS.SPT(BER_SØ_IND) - 0.5;                       
                                                                                
                                                                                
               ELSE /*KODE ER IKKE = B, - HELE TILLEGGET FALLER BORT*/          
                                                                                
                  B02.YRKEPENS.SPT(BER_SØ_IND) =                                
                      B02.YRKEPENS.SPT(BER_SØ_IND) - 1;                         
                                                                                
            ELSE                                                                
            IF B01.POENGTILLEGG_KODE(BER_SØ_IND) = 'B'  THEN                    
               IF B02.POENGTILLEGG_KODE(BER_SØ_IND) = 'A'  THEN                 
                                                                                
                 /*GÅR OPP 0.5 POENG*/                                          
                                                                                
                  B02.YRKEPENS.SPT(BER_SØ_IND) =                                
                      B02.YRKEPENS.SPT(BER_SØ_IND) + 0.5;                       
                                                                                
               ELSE /*DET HALVE POENGTILLEGGET FALLER BORT*/                    
                                                                                
                  B02.YRKEPENS.SPT(BER_SØ_IND) =                                
                      B02.YRKEPENS.SPT(BER_SØ_IND) - 0.5;                       
                                                                                
            ELSE   /*IKKE POENGTILLEGG FRA FØR*/                                
            IF B02.POENGTILLEGG_KODE(BER_SØ_IND) = 'B'  THEN                    
                                                                                
                 /*GÅR OPP 0.5 POENG*/                                          
                                                                                
               B02.YRKEPENS.SPT(BER_SØ_IND) =                                   
                   B02.YRKEPENS.SPT(BER_SØ_IND) + 0.5;                          
                                                                                
                                                                                
               ELSE                                                             
               IF B02.POENGTILLEGG_KODE(BER_SØ_IND) = 'A'  THEN                 
                                                                                
                 /*GÅR OPP 1 POENG*/                                            
                                                                                
                   B02.YRKEPENS.SPT(BER_SØ_IND) =                               
                       B02.YRKEPENS.SPT(BER_SØ_IND) + 1;                        
                                                                                
            IF B02.YRKEPENS.SPT(BER_SØ_IND) < 0  THEN                           
               DO;                                                              
                  B02.YRKEPENS.SPT(BER_SØ_IND) = 0;                             
                  B02.YRKEPENS.YPT(BER_SØ_IND) =                                
                      B02.YRKEHIST.PAÅ(BER_SØ_IND,SISTE);                       
               END;                                                             
            ELSE                                                                
            IF B02.YRKEPENS.SPT(BER_SØ_IND) <                                   
               B02.YRKEHIST.PAÅ(BER_SØ_IND,SISTE)      THEN                     
                                                                                
                  B02.YRKEPENS.YPT(BER_SØ_IND) =                                
                      B02.YRKEHIST.PAÅ(BER_SØ_IND,SISTE);                       
                                                                                
            ELSE                                                                
                                                                                
                  B02.YRKEPENS.YPT(BER_SØ_IND) =                                
                      B02.YRKEPENS.SPT(BER_SØ_IND);                             
                                                                                
                                                                                
         END;                                                                   
                                                                                
   END POENGTILLEGG_ENDRES_YP;                                                  
