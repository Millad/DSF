 /*       SIST ENDRET 25/02-94 17.59.07 AV   HERMAN                   */00000000
 /*IDENTIFIKASJON:                                                   */ 00000010
 /*    R0014012 - UNDERPROGRAM I PLI                                 */ 00000020
 /*    PROGRAMMERER:           HERMAN LARSSEN APRIL 1993             */ 00000030
 /*HENSIKT:                                                          */ 00000040
 /*    BEREGNE FORSØRGINGSTILLEGG - PRO RATA                         */ 00000050
 /*                                                                  */ 00000060
 /*    BEREGNER :                                                    */ 00000070
 /*        FULLT FORSØRGINGSTILLEGG                                  */ 00000080
 /*        INNTEKTS-REDUSERT FORSØRGINGSTILLEGG                      */ 00000090
 /*    OPPDATERER :                                                  */ 00000100
 /*        ET OG BT I ALDERSP OG UFØRPENS                            */ 00000110
 /*        BT_SÆRBARN    I FORSI                                     */ 00000120
 /*                   ALTERNATIV_SUM_PENSJON I EØSINFO               */ 00000130
 /*                                                                  */ 00000140
 /*PROGRAMTILKNYTNING:                                               */ 00000150
 /*    INKLUDERES I 4470, ----------------                           */ 00000160
 /*PARAMETER : INDEKS_I_B02                                          */ 00000170
 /* **************************************************************** */ 00000180
 FORSØRG_PRO_RATA:                                                      00000190
 PROC (IND);                                                            00000200
 DCL ANT_V             FIXED DEC (3)   INIT (0);                        00000210
 DCL ANT_W             FIXED DEC (3)   INIT (0);                        00000220
 DCL BT_V              FIXED DEC (7)   INIT (0);                        00000230
 DCL BT_W              FIXED DEC (7)   INIT (0);                        00000240
 DCL W_FOLKTRYGD       FIXED DEC (7)   INIT (0);                        00000250
 DCL FOLKETRYGD_ÅR     FIXED DEC (7)   INIT (0);                        00000260
 DCL FOLKETRYGD_EK     FIXED DEC (7)   INIT (0);                        00000270
 DCL FOLKETRYGD_EK_ÅR  FIXED DEC (7)   INIT (0);                        00000280
 DCL FORDELINGS_INNT   FIXED DEC (7)   INIT (0);                        00000290
 DCL FRADRAG           FIXED DEC (7)   INIT (0);                        00000300
 DCL FRADRAG_ET        FIXED DEC (7)   INIT (0);                        00000310
 DCL FRIBELØP          FIXED DEC (7)   INIT (0);                        00000320
 DCL FRIBELØP_V        FIXED DEC (7)   INIT (0);                        00000330
 DCL FRIBELØP_W        FIXED DEC (7)   INIT (0);                        00000340
 DCL FULL_ET           FIXED DEC (7)   INIT (0);                        00000350
 DCL FULL_BT           FIXED DEC (7)   INIT (0);                        00000360
 DCL FULL_V            FIXED DEC (7)   INIT (0);                        00000370
 DCL FULL_W            FIXED DEC (7)   INIT (0);                        00000380
 DCL HJELPEFELT       FIXED DEC (13,4) INIT (0);                        00000390
 DCL HJELP1           FIXED DEC (13,4) INIT (0);                        00000400
 DCL HJELP2           FIXED DEC (13,4) INIT (0);                        00000410
 DCL HJELP3           FIXED DEC (13,4) INIT (0);                        00000420
 DCL HJELP4           FIXED DEC (13,4) INIT (0);                        00000430
 DCL HJELP5           FIXED DEC (13,4) INIT (0);                        00000440
 DCL HJELP6           FIXED DEC (13,4) INIT (0);                        00000450
 DCL IND               FIXED BIN (15);                                  00000460
 DCL IND_EK            FIXED BIN (15);                                  00000470
 DCL INNTEKT           FIXED DEC (7)   INIT (0);                        00000480
 DCL INNTEKT_V         FIXED DEC (7)   INIT (0);                        00000490
 DCL INNTEKT_W         FIXED DEC (7)   INIT (0);                        00000500
 DCL INNTEKT_EK        FIXED DEC (7)   INIT (0);                        00000510
 DCL JA                     CHAR (1)  INIT ('J');                       00000520
 DCL RETUR_ET          FIXED DEC (7)   INIT (0);                        00000530
 DCL RETUR_SB          FIXED DEC (7)   INIT (0);                        00000540
 DCL RETUR_FB          FIXED DEC (7)   INIT (0);                        00000550
 DCL TILLEGG_FØR       FIXED DEC (7)   INIT (0);                        00000560
 DCL TILLEGGSINNT_V    FIXED DEC (7)   INIT (0);                        00000570
 DCL TILLEGGSINNT_W    FIXED DEC (7)   INIT (0);                        00000580
 DCL TOTALT_V          FIXED DEC (3)   INIT (0);                        00000590
 DCL TOTALT_W          FIXED DEC (3)   INIT (0);                        00000600
                                                                        00000610
                                                                        00000620
 /* FINNE INDEX FOR EKTEFELLE ELLER SAMBOER : */                        00000630
                                                                        00000640
 IND_EK = 3 - IND;                                                      00000650
                                                                        00000660
 /* ******                                                              00000670
 DO I = 3 TO 13 WHILE (B02.FNR_TILKN(IND,I) > 0);                       00000680
    IF B02.TILKNYTNINGSKODE(IND,I) = 'S'             THEN               00000690
       DO J = 3 TO 14 WHILE (B02.FNR(J) > 0);                           00000700
          IF B02.FNR_TILKN(IND,I) = B02.FNR(J)       THEN               00000710
             DO;                                                        00000720
                IND_EK = J ;                                            00000730
                J      = 14;                                            00000740
             END;                                                       00000750
       END;                                                             00000760
 END;                                                                   00000770
                                                                        00000780
  ************** */                                                     00000790
                                                                        00000800
                                                                        00000810
 /* ******************************************************** */         00000820
 /*             HOVED-PROSEDYRE                              */         00000830
 /*                                                          */         00000840
 /*   OPPDATERER RETUR_ET OG B02.BT_SÆRKULL        */                   00000850
 /* ******************************************************** */         00000860
                                                                        00000870
 CALL P1_REGN_YTELSER;                                                  00000880
 CALL P2_TELL_FORSØRGEDE;                                               00000890
 CALL P3_REGN_INNT_FRIBELØP;                                            00000900
                                                                        00000910
    IF B02.FT_SKAL_BEREGNES(IND) = 'J'         THEN                     00000920
       CALL P4_REGN_FORS;                                               00000930
 /* ******************************************************** */         00000940
 /*              SLUTT PÅ HOVEDPROSEDYRA                     */         00000950
 /* ******************************************************** */         00000960
                                                                        00000970
                                                                        00000980
 /* ****************************************************** */           00000990
 /* RUTINE FOR Å SUMMERE OPP FULLE YTELSER :               */           00001000
 /* ****************************************************** */           00001010
                                                                        00001020
 P1_REGN_YTELSER : PROC;                                                00001030
                                                                        00001040
 FULL_BT    = G * 0.25 * B02.ANTALL_BARN(IND)                           00001050
              * DIV_PARAM_OMR.TT_TEORETISK                              00001060
              * DIV_PARAM_OMR.TT_NORSK_FAKTISK                          00001070
              / DIV_PARAM_OMR.TT_SUM_FAKTISK                            00001080
              / (40 * 12) + 0.5;                                        00001090
                                                                        00001100
                                                                        00001110
 IF B02.PENSJONSTYPE2(IND) = 'F'  THEN                                  00001120
    FULL_ET    = G * 0.5                                                00001130
              * DIV_PARAM_OMR.TT_TEORETISK                              00001140
              * DIV_PARAM_OMR.TT_NORSK_FAKTISK                          00001150
              / DIV_PARAM_OMR.TT_SUM_FAKTISK                            00001160
                 / (40 * 12) + 0.5;                                     00001170
                                                                        00001180
                                                                        00001190
 W_FOLKTRYGD = B02.ALTERNATIV_SUM_PENSJON(IND);                         00001200
                                                                        00001210
 DIV_PERIODE.FOLKETRYGD(IND) = W_FOLKTRYGD * 12;                        00001220
                                                                        00001230
 IF B02.PENSJONSTYPE2(IND) = 'S'        THEN                            00001240
    DO;                                                                 00001250
       FOLKETRYGD_EK_ÅR  =  B02.SUM_YTELSE(IND_EK) * 12;                00001260
       DIV_PERIODE.FOLKETRYGD(IND_EK) = FOLKETRYGD_EK_ÅR;               00001270
    END;                                                                00001280
                                                                        00001290
 END P1_REGN_YTELSER;                                                   00001300
                                                                        00001310
 P2_TELL_FORSØRGEDE : PROC;                                             00001320
                                                                        00001330
                                                                        00001340
 DO I = 1 TO 13 WHILE (B02.FNR_TILKN(IND,I) > 0);                       00001350
    IF B02.TILKNYTNINGSKODE(IND,I) = 'W'  THEN                          00001360
       TOTALT_W = TOTALT_W + 1;                                         00001370
    ELSE                                                                00001380
    IF B02.TILKNYTNINGSKODE(IND,I) = 'V'  THEN                          00001390
       TOTALT_V = TOTALT_V + 1;                                         00001400
                                                                        00001410
 END;                                                                   00001420
                                                                        00001430
 IF (TOTALT_W + TOTALT_V) > 0      THEN                                 00001440
    DO;                                                                 00001450
       BT_W = FULL_BT * TOTALT_W / ( TOTALT_W + TOTALT_V) + 0.5;        00001460
       BT_V = FULL_BT - BT_W;                                           00001470
    END;                                                                00001480
                                                                        00001490
 END P2_TELL_FORSØRGEDE;                                                00001500
                                                                        00001510
                                                                        00001520
 /* ******************************************************** */         00001530
 /*PROSEDYRE FOR Å BEREGNE INNTEKT OG FRIBELØP               */         00001540
 /*                                                          */         00001550
 /* REGNER : INNTEKT, INNTEKT_EK, INNTEKT_V, INNTEKT_W,      */         00001560
 /*          FRIBELØP_V, FRIBELØP_W                          */         00001570
 /* ******************************************************** */         00001580
                                                                        00001590
 P3_REGN_INNT_FRIBELØP : PROC;                                          00001600
                                                                        00001610
 INNTEKT       = (B02.ARBEIDSINNTEKT(IND) * 100 +                       00001620
                  B02.PENSJONSINNTEKT(IND) * 100 )                      00001630
                 * DIV_PARAM_OMR.TT_NORSK_FAKTISK                       00001640
                 / DIV_PARAM_OMR.TT_SUM_FAKTISK                         00001650
                 + W_FOLKTRYGD * 12;                                    00001660
 INNTEKT_EK    = (B02.ARBEIDSINNTEKT_EK(IND) * 100 +                    00001670
                  B02.PENSJONSINNTEKT_EK(IND) * 100);                   00001680
                                                                        00001690
  /*EKTEFELLES INNTEKT SKAL IKKE PRO-RATA-REDUSERES IFLG. RANDI OHNA*/  00001700
                                                                        00001710
 IF INNTEKT_EK > G   THEN                                               00001720
    INNTEKT_EK = INNTEKT_EK + FOLKETRYGD_EK_ÅR - G;                     00001730
 ELSE                                                                   00001740
    INNTEKT_EK = FOLKETRYGD_EK_ÅR;                                      00001750
                                                                        00001760
 IF TOTALT_V > 0            THEN                                        00001770
    DO;                                                                 00001780
       IF B02.PENSJONSTYPE2(IND) = 'F'             &                    00001790
          B02.SIVILSTAND   (IND) = 'A'             THEN                 00001800
          DO;                                                           00001810
             HJELP2     = ST_PROSENT_SAMST * 2 /100;                    00001820
             HJELP3     = 0.25 * TOTALT_V;                              00001830
             HJELP4     = 1.75 + HJELP2 + HJELP3;                       00001840
             HJELP5     = G * HJELP4;                                   00001850
             HJELP6     = HJELP5                                        00001860
              * DIV_PARAM_OMR.TT_TEORETISK/ 40 + 0.5;                   00001870
                                                                        00001880
             FULL_V = FULL_ET + BT_V;                                   00001890
             FULL_W = BT_W;                                             00001900
          END;                                                          00001910
       ELSE                                                             00001920
          DO;                                                           00001930
             HJELP2     = ST_PROSENT_ENSLIG /100;                       00001940
             HJELP3     = 0.25 * TOTALT_V;                              00001950
             HJELP4     = 1.5 + HJELP2 + HJELP3;                        00001960
             HJELP5     = G * HJELP4;                                   00001970
             HJELP6     = HJELP5                                        00001980
              * DIV_PARAM_OMR.TT_TEORETISK/ 40 + 0.5;                   00001990
             FULL_V = BT_V;                                             00002000
             FULL_W = BT_W + FULL_ET;                                   00002010
          END;                                                          00002020
       FRIBELØP_V = HJELP6                                              00002030
                 * DIV_PARAM_OMR.TT_NORSK_FAKTISK                       00002040
                 / DIV_PARAM_OMR.TT_SUM_FAKTISK;                        00002050
       DIV_PERIODE.FRIBELØP_SÆRBARN(IND) = FRIBELØP_V;                  00002060
    END;                                                                00002070
 ELSE                                                                   00002080
    FULL_W = BT_W + FULL_ET;                                            00002090
                                                                        00002100
 IF TOTALT_W > 0 !                                                      00002110
    FULL_ET  > 0             THEN                                       00002120
    DO;                                                                 00002130
             HJELP2     = ST_PROSENT_SAMST * 2 /100;                    00002140
             HJELP3     = 0.25 * TOTALT_W;                              00002150
             HJELP4     = 1.75 + HJELP2 + HJELP3;                       00002160
             HJELP5     = G * HJELP4;                                   00002170
             HJELP6     = HJELP5                                        00002180
              * DIV_PARAM_OMR.TT_TEORETISK/ 40 + 0.5;                   00002190
       FRIBELØP_W = HJELP6                                              00002200
                 * DIV_PARAM_OMR.TT_NORSK_FAKTISK                       00002210
                 / DIV_PARAM_OMR.TT_SUM_FAKTISK;                        00002220
       DIV_PERIODE.FRIBELØP_FELLESBARN(IND) = FRIBELØP_W;               00002230
    END;                                                                00002240
                                                                        00002250
 IF FRIBELØP_V > 0             &                                        00002260
    FRIBELØP_W > 0             &                                        00002270
    INNTEKT > FRIBELØP_V       &                                        00002280
    INNTEKT > FRIBELØP_W             THEN                               00002290
    DO;                                                                 00002300
       IF FRIBELØP_W > FRIBELØP_V                 THEN                  00002310
             FORDELINGS_INNT = INNTEKT - FRIBELØP_W;                    00002320
       ELSE                                                             00002330
             FORDELINGS_INNT = INNTEKT - FRIBELØP_V;                    00002340
                                                                        00002350
       HJELPEFELT      = FORDELINGS_INNT * FULL_V  /                    00002360
                         (FULL_V + FULL_W) + 0.5;                       00002370
       TILLEGGSINNT_V  = HJELPEFELT;                                    00002380
       TILLEGGSINNT_W  = FORDELINGS_INNT - TILLEGGSINNT_V;              00002390
       INNTEKT_V = INNTEKT + TILLEGGSINNT_V - FORDELINGS_INNT;          00002400
       INNTEKT_W = INNTEKT + TILLEGGSINNT_W - FORDELINGS_INNT;          00002410
    END;                                                                00002420
                                                                        00002430
 ELSE                                                                   00002440
    DO;                                                                 00002450
       INNTEKT_V = INNTEKT;                                             00002460
       INNTEKT_W = INNTEKT;                                             00002470
    END;                                                                00002480
 IF FRIBELØP_V > FRIBELØP_W THEN                                        00002490
    IF FRIBELØP_W > 0          THEN                                     00002500
       DIV_PERIODE.FRIBELØP_MINSTE(IND) = FRIBELØP_W;                   00002510
    ELSE                                                                00002520
       DIV_PERIODE.FRIBELØP_MINSTE(IND) = FRIBELØP_V;                   00002530
 ELSE                                                                   00002540
    IF FRIBELØP_V > 0          THEN                                     00002550
       DIV_PERIODE.FRIBELØP_MINSTE(IND) = FRIBELØP_V;                   00002560
    ELSE                                                                00002570
       DIV_PERIODE.FRIBELØP_MINSTE(IND) = FRIBELØP_W;                   00002580
                                                                        00002590
 END P3_REGN_INNT_FRIBELØP;                                             00002600
                                                                        00002610
 /* ******************************************************** */         00002620
 /*      PROSEDYRE FOR Å BEREGNE FORSØRGINGSTILLEGG          */         00002630
 /*                                                          */         00002640
 /* REGNER     : RETUR_ET, RETUR_FB OG RETUR_SB              */         00002650
 /* OPPDATERER : B02.ET, B02.BT, B02.SB,                     */         00002660
 /*              B02.EØSINFO.ALTERNATIV_SUM_PENSJON          */         00002670
 /*              MED RETUR-BELØPENE                          */         00002680
 /* ******************************************************** */         00002690
                                                                        00002700
 P4_REGN_FORS : PROC;                                                   00002710
                                                                        00002720
                                                                        00002730
 IF B02.ANTALL_BARN(IND) = 0        THEN /*BARE RETUR_ET*/              00002740
    CALL P51_EK;                                                        00002750
 ELSE                                                                   00002760
    DO;                                                                 00002770
       IF B02.PENSJONSTYPE2(IND) = 'F'           THEN                   00002780
          IF TOTALT_V > 0                           THEN                00002790
             IF B02.SIVILSTAND(IND) = 'A'              THEN             00002800
                CALL P52_EK_V;                                          00002810
             ELSE                                                       00002820
                DO;                                                     00002830
                   CALL P55_V;                                          00002840
                   IF TOTALT_W > 0                           THEN       00002850
                      CALL P53_EK_W;                                    00002860
                   ELSE                                                 00002870
                      CALL P51_EK;                                      00002880
                END;                                                    00002890
          ELSE                                                          00002900
          IF TOTALT_W > 0                           THEN                00002910
             CALL P53_EK_W;                                             00002920
          ELSE                                                          00002930
             CALL P51_EK;                                               00002940
       ELSE                                                             00002950
       IF TOTALT_W > 0                           THEN                   00002960
          DO;                                                           00002970
             CALL P54_W;                                                00002980
             IF TOTALT_V > 0                        THEN                00002990
                CALL P55_V;                                             00003000
          END;                                                          00003010
       ELSE                                                             00003020
       IF TOTALT_V > 0                           THEN                   00003030
          CALL P55_V;                                                   00003040
                                                                        00003050
    END;                                                                00003060
                                                                        00003070
   IF RETUR_SB = BT_V THEN DIV_PERIODE.FULLT_BT_V = JA;                 00003080
   IF RETUR_FB = BT_W THEN DIV_PERIODE.FULLT_BT_W = JA;                 00003090
   IF RETUR_ET = FULL_ET THEN DIV_PERIODE.FULLT_ET = JA;                00003100
                                                                        00003110
   B02.BT_SÆRBARN(IND)  = RETUR_SB;                                     00003120
   B02.ALTERNATIV_SUM_PENSJON(IND) =                                    00003130
                        B02.ALTERNATIV_SUM_PENSJON(IND) + RETUR_SB;     00003140
                                                                        00003150
 SELECT (B02.PENSJONSTYPE1(IND));                                       00003160
                                                                        00003170
   WHEN ('A','K')                                                       00003180
      DO;                                                               00003190
         B02.ALDERSP.ET (IND) = RETUR_ET;                               00003200
         B02.ALDERSP.BT (IND) = RETUR_FB;                               00003210
         B02.ALTERNATIV_SUM_PENSJON(IND) =                              00003220
                               B02.ALTERNATIV_SUM_PENSJON(IND)          00003230
               + RETUR_ET             + RETUR_FB;                       00003240
      END;                                                              00003250
                                                                        00003260
   WHEN ('U','Y')                                                       00003270
      DO;                                                               00003280
         B02.UFØRPENS.ET(IND) = RETUR_ET;                               00003290
         B02.UFØRPENS.BT(IND) = RETUR_FB;                               00003300
         B02.YRKEPENS.BT(IND) = 0;                                      00003310
         B02.ALTERNATIV_SUM_PENSJON(IND) =                              00003320
                               B02.ALTERNATIV_SUM_PENSJON(IND)          00003330
               + RETUR_ET             + RETUR_FB;                       00003340
      END;                                                              00003350
                                                                        00003360
 END; /*SELECT*/                                                        00003370
                                                                        00003380
 P51_EK : PROC;      /*RETUR_ET */                                      00003390
                                                                        00003400
  IF INNTEKT_W > FRIBELØP_W            THEN                             00003410
     DO;                                                                00003420
        FRADRAG = (INNTEKT_W - FRIBELØP_W) * 0.5;                       00003430
        FRADRAG = FRADRAG / 12;                                         00003440
        IF FULL_ET    > FRADRAG         THEN                            00003450
           RETUR_ET = FULL_ET - FRADRAG;                                00003460
     END;                                                               00003470
  ELSE                                                                  00003480
     RETUR_ET = FULL_ET;                                                00003490
                                                                        00003500
 END P51_EK;                                                            00003510
                                                                        00003520
 P52_EK_V : PROC;/*RETUR_EK,RETUR_SB*/                                  00003530
                 /*EKTEFELLETILLEGGET REGNES SAMMEN MED SÆRK.BARN*/     00003540
                                                                        00003550
  IF INNTEKT_V > FRIBELØP_V          THEN                               00003560
     DO;                                                                00003570
        FRADRAG = (INNTEKT_V - FRIBELØP_V) / (12 * 2);                  00003580
        TILLEGG_FØR  = FULL_ET + BT_V;                                  00003590
        IF TILLEGG_FØR  > FRADRAG         THEN                          00003600
           DO;                                                          00003610
              HJELPEFELT = FRADRAG * FULL_ET /                          00003620
                                (FULL_ET + BT_V) + 0.5;                 00003630
              FRADRAG_ET = HJELPEFELT;                                  00003640
              RETUR_ET = FULL_ET - FRADRAG_ET;                          00003650
              RETUR_SB = BT_V - FRADRAG + FRADRAG_ET;                   00003660
           END;                                                         00003670
     END;                                                               00003680
  ELSE                                                                  00003690
     DO;                                                                00003700
        RETUR_ET = FULL_ET;                                             00003710
        RETUR_SB = BT_V;                                                00003720
     END;                                                               00003730
                                                                        00003740
 END P52_EK_V;                                                          00003750
                                                                        00003760
 P53_EK_W : PROC;/*RETUR_ET,RETUR_FB*/                                  00003770
                 /*EKTEFELLETILLEGGET REGNES SAMMEN MED FELLESBARN*/    00003780
                                                                        00003790
  IF INNTEKT_W > FRIBELØP_W          THEN                               00003800
     DO;                                                                00003810
        FRADRAG = (INNTEKT_W - FRIBELØP_W) / (12 * 2);                  00003820
        TILLEGG_FØR  = FULL_ET + BT_W;                                  00003830
        IF TILLEGG_FØR  > FRADRAG         THEN                          00003840
           DO;                                                          00003850
              HJELPEFELT = FRADRAG * FULL_ET /                          00003860
                                (FULL_ET + BT_W) + 0.5;                 00003870
              FRADRAG_ET = HJELPEFELT;                                  00003880
              RETUR_ET = FULL_ET - FRADRAG_ET;                          00003890
              RETUR_FB = BT_W - FRADRAG + FRADRAG_ET;                   00003900
           END;                                                         00003910
     END;                                                               00003920
  ELSE                                                                  00003930
     DO;                                                                00003940
        RETUR_ET = FULL_ET;                                             00003950
        RETUR_FB = BT_W;                                                00003960
     END;                                                               00003970
                                                                        00003980
 END P53_EK_W;                                                          00003990
                                                                        00004000
 P54_W : PROC; /* RETUR_FB  */                                          00004010
               /* FELLESBARN, IKKE TILLEGG EKTEFELLE*/                  00004020
                                                                        00004030
  FRADRAG = (INNTEKT_W + INNTEKT_EK - FRIBELØP_W)/(12 * 2);             00004040
  IF FRADRAG > 0              THEN                                      00004050
     IF BT_W > FRADRAG              THEN                                00004060
        RETUR_FB = BT_W - FRADRAG;                                      00004070
     ELSE;                                                              00004080
                                                                        00004090
  ELSE RETUR_FB = BT_W;                                                 00004100
                                                                        00004110
 END P54_W;                                                             00004120
                                                                        00004130
 P55_V : PROC; /* RETUR_SB */                                           00004140
               /* SÆRKULLSBARN */                                       00004150
                                                                        00004160
  IF INNTEKT_V > FRIBELØP_V          THEN                               00004170
     DO;                                                                00004180
        FRADRAG = (INNTEKT_V - FRIBELØP_V)/(12 * 2);                    00004190
        IF BT_V > FRADRAG              THEN                             00004200
           RETUR_SB = BT_V - FRADRAG;                                   00004210
     END;                                                               00004220
  ELSE                                                                  00004230
     RETUR_SB = BT_V;                                                   00004240
                                                                        00004250
                                                                        00004260
 END P55_V;                                                             00004270
                                                                        00004280
 END P4_REGN_FORS;                                                      00004290
                                                                        00004300
 P99 :                                                                  00004310
                                                                        00004320
                                                                        00004330
 END FORSØRG_PRO_RATA;                                                  00004340
