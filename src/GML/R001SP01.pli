 /*       SIST ENDRET 16/05-88 12.37.00 AV   DYBVIK                   */00000000
 /*       SIST ENDRET 20/11-84 12.31.39 AV   LUNDEBY                  */00000010
 /*       SIST ENDRET 31/08-84 12.20.13 AV   LUNDEBY                  */00000020
 /*       SIST ENDRET 28/03-83 09.33.21 AV   LUNDEBY                  */00000030
                                                                        00000040
                                                                        00000050
 /* ***************************************************************** */00000060
 /* IDENTIFIKASJON                                                    */00000070
 /*  R  R001SP01 - PROCEDURE I PLI SOM FJERNER SPERRE I RF0PESN       */00000080
 /*     PROGRAMMERER: SVERRRE LUNDEBY MARS   1983.                    */00000090
 /* HENSIKT                                                           */00000100
 /*     KJØRES NÅR EN PERSNON ER SPERRET                              */00000110
 /* PROGRAMTILKNYTNING                                                */00000120
 /*     FRITTSTÅENDE PROGRAM I DLI-ONLINE                             */00000130
 /* ***************************************************************** */00000140
 /*                                                                   */00000150
                                                                        00000160
 R001SP0: PROC                    OPTIONS (MAIN);                       00000170
                                                                        00000180
   DCL 1 INN_OMR,                                                       00000190
         2 TRANS_KODE            CHAR      ( 4)          INIT (''),     00000200
         2 EN_BLANK              CHAR      ( 1)          INIT (''),     00000210
         2 FNR                   PIC      '(11)9'        INIT (''),     00000220
                                                                        00000230
           FNR_CHAR              CHAR      (11)  DEF     INN_OMR.FNR  ; 00000240
                                                                        00000250
                                                                        00000260
   DCL 1 IO_OMR       UNALIGNED,                                        00000270
         2 FNR                   FIXED DEC (11)          INIT (''),     00000280
         2 SPERRE                CHAR      ( 1)          INIT ('');     00000290
                                                                        00000300
                                                                        00000310
    DCL                                                                 00000320
        FEIL                  BIT       ( 1)    INIT ('0'B   ),         00000330
        W01_GHU               CHAR      ( 4)    INIT ('GHU ' ),         00000340
        W01_REPL              CHAR      ( 4)    INIT ('REPL' ),         00000350
        W01_OPEN              CHAR      ( 5)    INIT ('OPEN '),         00000360
        W01_CLOSE             CHAR      ( 5)    INIT ('CLOSE'),         00000370
        MELDING               CHAR      (78)    INIT (''     ),         00000380
        LENGDE_INN_OMR        FIXED BIN (15)    INIT ( 16    ),         00000390
                                                                        00000400
        VERIFY                BUILTIN,                                  00000410
        NULL                  BUILTIN,                                  00000420
        CSTG                  BUILTIN,                                  00000430
        UNSPEC                BUILTIN;                                  00000440
                                                                        00000450
    DCL EN      FIXED BIN (31) INIT (1);                                00000460
    DCL TO      FIXED BIN (31) INIT (2);                                00000470
    DCL TRE     FIXED BIN (31) INIT (3);                                00000480
    DCL FIRE    FIXED BIN (31) INIT (4);                                00000490
    DCL FEM     FIXED BIN (31) INIT (5);                                00000500
    DCL SEKS    FIXED BIN (31) INIT (6);                                00000510
                                                                        00000520
    DCL 1 SSA1_RF0PERSN        UNALIGNED,                               00000530
           2 SEGM_NAVN         CHAR       ( 9)  INIT ('RF0PERSN(' ),    00000540
           2 FELT1             CHAR       ( 8)  INIT ('FNR     '  ),    00000550
           2 REL_OP            CHAR       ( 2)  INIT (' ='        ),    00000560
           2 FNR               FIXED DEC  (11)  INIT ( 0          ),    00000570
           2 HP                CHAR       ( 1)  INIT (')'         );    00000580
                                                                        00000590
                                                                        00000600
   /* ********************************************************** */     00000610
   /* DLI - FUNKSJONER :  PSB OG DLIUIB                          */     00000620
   /* ********************************************************** */     00000630
                                                                        00000640
                                                                        00000650
   DCL PLITDLI ENTRY;                                                   00000660
                                                                        00000670
 %INCLUDE DLIUIB;                                                       00000680
                                                                        00000690
    DCL 1 PCB_PEKERE   BASED   (UIBPCBAL),                              00000700
           2  RF1_PCB_PEKER     POINTER;                                00000710
                                                                        00000720
    DCL 1 RF1 BASED            (RF1_PCB_PEKER),                         00000730
    %INCLUDE  P0012003;                                                 00000740
                                                                        00000750
                                                                        00000760
    EXEC CICS HANDLE CONDITION LENGERR (FEIL_I_INPUT);                  00000770
                                                                        00000780
                                                                        00000790
    EXEC CICS RECEIVE INTO              ( INN_OMR)                      00000800
                                 LENGTH ( LENGDE_INN_OMR    );          00000810
                                                                        00000820
    IF VERIFY (INN_OMR.FNR,'0123456789') >   0     THEN                 00000830
       DO;                                                              00000840
          MELDING      =   'UGYLDIG FNR : ' !! FNR_CHAR ;               00000850
          GO TO AVSLUTT;                                                00000860
       END;                                                             00000870
                                                                        00000880
    CALL DATABASE  (W01_OPEN);                                          00000890
                                                                        00000900
    IF FEIL                                        THEN                 00000910
       GOTO AVSLUTT;                                                    00000920
                                                                        00000930
    CALL LES_ROT_FJERN_SPERRE;                                          00000940
    GOTO AVSLUTT;                                                       00000950
                                                                        00000960
                                                                        00000970
   FEIL_I_INPUT:                                                        00000980
    MELDING =  'FEIL INNTASTING. TAST INN  : RSP0 99999999999 '   ;     00000990
                                                                        00001000
                                                                        00001010
   AVSLUTT:                                                             00001020
    CALL DATABASE  (W01_CLOSE);                                         00001030
                                                                        00001040
    EXEC CICS SEND TEXT FROM (MELDING) JUSTIFY(14) ERASE ACCUM PAGING;  00001050
    EXEC CICS SEND PAGE RELEASE;                                        00001060
                                                                        00001070
                                                                        00001080
  /* **************************************************************** */00001090
  /* **************************************************************** */00001100
  /* ******                                                  ******** */00001110
  /* ******        UNDER - MODULER                           ******** */00001120
  /* ******                                                  ******** */00001130
  /* **************************************************************** */00001140
  /* **************************************************************** */00001150
                                                                        00001160
                                                                        00001170
 DATABASE:                                                              00001180
   PROC    (STYRING);                                                   00001190
                                                                        00001200
   DCL                                                                  00001210
     STYRING                               CHAR (5)              ;      00001220
                                                                        00001230
  /* **************************************************************** */00001240
  /*     DL1-PARAMETRE SOM BRUKES VED INITIERINGS- OG AVSLUTTNINGS-   */00001250
  /*     KALL                                                         */00001260
  /* **************************************************************** */00001270
                                                                        00001280
     DCL                                                                00001290
       W01_PSB_NAVN                CHAR (8)       INIT('B001SP01');     00001300
     DCL                                                                00001310
       W01_PCB_FUNCTION            CHAR (4)       INIT('PCB ');         00001320
     DCL                                                                00001330
       W01_TERM_FUNCTION           CHAR (4)       INIT('TERM');         00001340
                                                                        00001350
  /* **************************************************************** */00001360
  /*     DLI CALL-PARAMETRE                                           */00001370
  /* **************************************************************** */00001380
                                                                        00001390
     DCL 1  W01_DLI_PARAM,                                              00001400
            2   W02_PARM_CT_1      FIXED BIN (31)  INIT ( 1 ),          00001410
            2   W02_PARM_CT_3      FIXED BIN (31)  INIT ( 3 ),          00001420
            2   W02_PARM_CT_4      FIXED BIN (31)  INIT ( 4 );          00001430
                                                                        00001440
                                                                        00001450
                                                                        00001460
     IF STYRING = 'OPEN '                         THEN                  00001470
        DO;                                                             00001480
           CALL   PLITDLI                        (W02_PARM_CT_3,        00001490
                                                  W01_PCB_FUNCTION,     00001500
                                                  W01_PSB_NAVN,         00001510
                                                  UIBPTR);              00001520
                                                                        00001530
           IF DLIUIB.UIBFCTR               ^=    '00000000'B THEN       00001540
             DO;                                                        00001550
                MELDING =                                               00001560
                 'NOE ER GALT MED DATABASEN PÅ ÅPNINGSKALLET, KODE: ' !!00001570
                  UNSPEC(UIBFCTR) !! ' ' !! UNSPEC(UIBDLTR);            00001580
                FEIL  =  '1'B;                                          00001590
             END;                                                       00001600
        END;                                                            00001610
     ELSE                                                               00001620
        DO;                                                             00001630
           CALL   PLITDLI                        (W02_PARM_CT_1,        00001640
                                                  W01_TERM_FUNCTION);   00001650
        END;                                                            00001660
                                                                        00001670
 END DATABASE;                                                          00001680
                                                                        00001690
                                                                        00001700
                                                                        00001710
 LES_ROT_FJERN_SPERRE :                                                 00001720
  PROC;                                                                 00001730
                                                                        00001740
    SSA1_RF0PERSN.FNR =   INN_OMR.FNR   ;                               00001750
                                                                        00001760
    CALL PLITDLI (FIRE,W01_GHU,RF1,IO_OMR,SSA1_RF0PERSN);               00001770
                                                                        00001780
                                                                        00001790
    IF DLIUIB.UIBFCTR             ^=    '00000000'B THEN                00001800
       DO;                                                              00001810
          MELDING =                                                     00001820
           'NOE ER GALT MED DATABASEN PÅ KALL: ' !! W01_GHU !! ' KODE: '00001830
            !! UNSPEC(UIBFCTR) !! ' ' !! UNSPEC(UIBDLTR);               00001840
          FEIL    =    '1'B;                                            00001850
          GO TO SLUTT;                                                  00001860
       END;                                                             00001870
                                                                        00001880
    SELECT (RF1.STATUS_KODE);                                           00001890
                                                                        00001900
       WHEN  ('  ')                                                     00001910
          DO;                                                           00001920
             IF IO_OMR.SPERRE              =   ' '             THEN     00001930
                DO;                                                     00001940
                   MELDING = 'FNR : ' !! FNR_CHAR !! ' ER IKKE SPERRET';00001950
                   FEIL    = '1'B;                                      00001960
                   GO TO SLUTT;                                         00001970
                END;                                                    00001980
                                                                        00001990
             IO_OMR.SPERRE                 =   ' ';                     00002000
                                                                        00002010
             CALL PLITDLI (TRE,W01_REPL,RF1,IO_OMR);                    00002020
                                                                        00002030
             IF RF1.STATUS_KODE            ^=    '  '          !        00002040
                DLIUIB.UIBFCTR             ^=    '00000000'B   THEN     00002050
                DO;                                                     00002060
                  MELDING = 'FEIL VED REPL :   ' !!                     00002070
                            'DB_STATUS_KODE =  ' !! RF1.STATUS_KODE !!  00002080
                            '     UBIFCTR , UIBDLTR : '  !!             00002090
                            UNSPEC(UIBFCTR) !! ' , ' !! UNSPEC(UIBDLTR);00002100
                  FEIL    =    '1'B;                                    00002110
                  GO TO SLUTT;                                          00002120
                END;                                                    00002130
             ELSE                                                       00002140
                  MELDING = '        SPERRE - KODEN ER FJERNET !  ';    00002150
          END;                                                          00002160
                                                                        00002170
       WHEN ('GE')                                                      00002180
          DO;                                                           00002190
             MELDING = 'FNR : '!! FNR_CHAR !!' FINNES IKKE I DATABASEN';00002200
             FEIL    = '1'B ;                                           00002210
             GO TO SLUTT    ;                                           00002220
          END;                                                          00002230
                                                                        00002240
       OTHERWISE                                                        00002250
          /* ******************************************************* */ 00002260
          /* FEIL STATUSKODE                                         */ 00002270
          /* ******************************************************* */ 00002280
          DO;                                                           00002290
             MELDING = 'FEIL VED GHU  :   ' !!                          00002300
                       'DB_STATUS_KODE =  ' !! RF1.STATUS_KODE ;        00002310
             FEIL    =    '1'B;                                         00002320
             GO TO SLUTT;                                               00002330
          END;                                                          00002340
                                                                        00002350
    END;                                                                00002360
                                                                        00002370
   SLUTT:                                                               00002380
                                                                        00002390
 END LES_ROT_FJERN_SPERRE;                                              00002400
                                                                        00002410
                                                                        00002420
                                                                        00002430
 END R001SP0;                                                           00002440
