 /*       SIST ENDRET 12/06-98 13.33.54 AV   SPA7339                  */00000000
 /*       SIST ENDRET 25/05-98 12.45.05 AV   SPA7339                  */00000010
 /*       SIST ENDRET 01/11-93 15.35.00 AV   TRUDE                    */00000020
 /*       SIST ENDRET 02/05-91 13.54.23 AV   DYBVIK                   */00000030
 /*       SIST ENDRET 06/03-91 11.50.41 AV   DYBVIK                   */00000040
  /*----------------------------------------------------------------*/  00000050
  /* IDENTIFIKASJON : R001N824 - FUNKSJON  I PLI                    */  00000060
  /* PROGRAMMERER   : TRUDE SPONBERG OKTOBER 1990                   */  00000070
  /* HENSIKT        : BEREGNE FREMTIDIG TRYGDETID                   */  00000080
  /*                  ETTER 01.01.91                                */  00000090
  /* BRUKER         : CALL F_BEREGN_TT_FRAMT_UTLAND(FNR,STI_ÅM,IND) */  00000100
  /*                  RETURNERER FIXED DEC(3)                       */  00000110
  /* UNDERPROGRAM I : R001N620 : KONTROLL U2 TRANS MOT STATUS I B.  */  00000120
  /*                  R001N820 : ---"---- E3 --"-- -"- --"--- " "   */  00000130
  /*                  R001N920 : ---"---- E4 --"-- -"- --"--- " "   */  00000140
  /*                  R001U620 : ---"---- U3 --"-- -"- --"--- " "   */  00000150
  /*                  R001U820 : ---"---- EE --"-- -"- --"--- " "   */  00000160
  /*----------------------------------------------------------------*/  00000170
  /* NY REGEL :                                                     */  00000180
  /* FOR DEM SOM HAR BODD I UTLANDET, VIL FREMTIDIG TRYGDETID       */  00000190
  /* KUNNE BLI BEGRENSET.                                           */  00000200
  /* MED VIRKNING FRA 01.01.91                                      */  00000210
  /* DERSOM 4/5 AV TIDEN MELLOM FYLTE 16 ÅR OG STØNADSTILFELLETS    */  00000220
  /* INNTREDEN (OPPTJENINGSPERIODEN) KAN REGNES SOM TRYGDETID,      */  00000230
  /* MEDREGNES ALL FREMTIDIG TRYGDETID. DET SAMME GJELDER DERSOM    */  00000240
  /* STØNADSTILFELLET ER INNTRÅDT FØR FYLTE 16 ÅR.                  */  00000250
  /* (HVORDAN SKAL VI FANGE OPP DEN ? UFT=16ÅRSDAG??) TRUDE.        */  00000260
  /* I ANDRE TILFELLE SKAL FREMTIDIG TRYGDETID UTGJØRE 40 ÅR MED    */  00000270
  /* FRADRAG FOR 4/5 AV OPPTJENINGSPERIODEN.                        */  00000280
  /* TRUDE, 9.10.90                                                 */  00000290
  /*----------------------------------------------------------------*/  00000300
  /* ENDRET: 020591                                                 */  00000310
  /* ENDRINGEN GJELDER: FJERNE MND VI LEGGER TIL I OPPTJ.PERIODEN.  */  00000320
  /*                    MINSTEVILKÅR SKAL REGNE MED DESIMALER.      */  00000330
  /*                    TT_FRAMT_UTLAND   REGNE MED DESIMALER.      */  00000340
  /*                    ENDRINGEN ER I SAMARBEID MED KARI SANNES.   */  00000350
  /*----------------------------------------------------------------*/  00000360
                                                                        00000370
                                                                        00000380
  %SKIP(2);                                                             00000390
  F_BEREGN_TT_FRAMT_UTLAND:                                             00000400
    PROC(FNR,STI_ÅM,IND) RETURNS(FIXED DEC(3));                         00000410
                                                                        00000420
    DCL FNR                  FIXED DEC (11);                            00000430
    DCL STI_ÅM               FIXED DEC (5);                             00000440
    DCL IND                  FIXED BIN (15);                            00000450
    DCL TT_FRAMT_UTLAND      FIXED DEC (3) INIT (0);                    00000460
    DCL W_TT_FRAMT_UTLAND    FIXED DEC (5,2) INIT (0);                  00000470
                                                                        00000480
    DCL W_STI_ÅM          PIC '(4)9';                                   00000490
    DCL W_STI_ÅR          PIC '(2)9' DEF W_STI_ÅM POS(1),               00000500
        W_STI_MND         PIC '(2)9' DEF W_STI_ÅM POS(3);               00000510
                                                                        00000520
    DCL OPPTJENINGSPERIODEN  FIXED DEC (3) INIT (0),                    00000530
        FAKTISK_TT           FIXED DEC (3) INIT (0),                    00000540
        MINSTEVILKÅR         FIXED DEC (5,2) INIT (0); /*TS,020591*/    00000550
                                                                        00000560
    DCL                                                                 00000570
        W_TT_FRAM_TIL_DATO                                PIC '(5)9',   00000580
        W_TT_FRAM_TIL_ÅR  DEF W_TT_FRAM_TIL_DATO POS (1)  PIC '999',    00000590
        W_TT_FRAM_TIL_MND DEF W_TT_FRAM_TIL_DATO POS (4)  PIC '99';     00000600
                                                                        00000610
    DCL W_FNR                PIC '(11)9';                               00000620
                                                                        00000630
    DCL                                                                 00000640
       1 W_FNR_R             DEF      W_FNR,                            00000650
         2 DAG               PIC        '99',                           00000660
         2 MND               PIC        '99',                           00000670
         2 ÅR                PIC        '99',                           00000680
         2 ÅRHUNDRE          PIC         '9',                           00000690
         2 REST              PIC      '9999';                           00000700
                                                                        00000710
    W_FNR  = FNR;                                                       00000720
                                                                        00000730
           /* IFLG. REFERAT FRA ARBEIDSGRUPPEN 12.9.90 SKAL  */         00000740
           /* MND 16. ÅR FYLLES IKKE TELLE MED. TRUDE, 10.90 */         00000750
                                                                        00000760
     W_STI_ÅM = STI_ÅM;              /*TOM. MND FØR DØD ELLER STI_ÅM */ 00000770
                                        /*TOM. MND ETTER F_MND       */ 00000780
     OPPTJENINGSPERIODEN    = ((W_STI_ÅR *12) + W_STI_MND - 1) -        00000790
             ((( W_FNR_R.ÅR + 16 ) * 12) + W_FNR_R.MND);                00000800
                                                                        00000810
     IF B02.TT_16_66(IND) > 0 THEN                                      00000820
        FAKTISK_TT = B02.TT_16_66(IND);                                 00000830
     ELSE                                                               00000840
           FAKTISK_TT        =  B02.TT_FØR_1967(IND) * 12 +             00000850
                                B02.TT_ETTER_1966(IND);                 00000860
                                                                        00000870
     MINSTEVILKÅR  = (OPPTJENINGSPERIODEN * 4) / 5;  /*AVRUNDES IKKE*/  00000880
                                                                        00000890
     IF MINSTEVILKÅR > FAKTISK_TT        THEN                           00000900
      DO;                                                               00000910
                                        /* TT SKAL REDUSERES         */ 00000920
                                        /* NOTAT TIL SLETNER SIDE 3  */ 00000930
                                        /* N.SISTE AVS. ANG AVRUNDING*/ 00000940
        W_TT_FRAMT_UTLAND   = 480 - MINSTEVILKÅR ;                      00000950
        TT_FRAMT_UTLAND   = W_TT_FRAMT_UTLAND + 0.5; /*TS*/             00000960
        TT_RED  = 'J';                                                  00000970
        DIV_PARAM_OMR.REDUSERT_FRAMTIDIG_TT(IND) = 'J';                 00000980
      END;                                                              00000990
     ELSE                                                               00001000
      DO;                                                               00001010
        TT_RED    = ' ';                                                00001020
         IF W_FNR_R.ÅRHUNDRE < 5 THEN                                   00001030
            DO;                                                         00001040
              IF STI_ÅM < 7301 THEN                                     00001050
                 W_TT_FRAM_TIL_DATO =                                   00001060
                          (W_FNR_R.ÅR + 70) * 100 + W_FNR_R.MND;        00001070
              ELSE                                                      00001080
              IF B02.PENSJONSRETT_FØR_91(IND) = 'J' THEN                00001090
                 W_TT_FRAM_TIL_DATO =                                   00001100
                          (W_FNR_R.ÅR + 67) * 100 + W_FNR_R.MND;        00001110
              ELSE                                                      00001120
                 W_TT_FRAM_TIL_DATO =                                   00001130
 /*HL*/                   (W_FNR_R.ÅR + 66) * 100 + 12;                 00001140
            END;                                                        00001150
                                                                        00001160
          ELSE                                                          00001170
                                                                        00001180
             W_TT_FRAM_TIL_DATO =                                       00001190
                         ((W_FNR_R.ÅR + 70) - 100) * 100 + W_FNR_R.MND; 00001200
                                                                        00001210
       TT_FRAMT_UTLAND = (W_TT_FRAM_TIL_ÅR * 12 + W_TT_FRAM_TIL_MND) -  00001220
                      (W_STI_ÅR  * 12 + W_STI_MND - 1);                 00001230
                                                                        00001240
      END;                                                              00001250
                                                                        00001260
      IF TT_FRAMT_UTLAND < 0 THEN                                       00001270
         TT_FRAMT_UTLAND = 0;                                           00001280
                                                                        00001290
     RETURN(TT_FRAMT_UTLAND);                                           00001300
  END F_BEREGN_TT_FRAMT_UTLAND;                                         00001310
