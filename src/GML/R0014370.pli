 /*       SIST ENDRET 12/10-95 14.14.29 AV   JDA0310                  */00000000
 /* ***************************************************************** */00000010
 /* IDENTIFIKASJON                                                    */00000020
 /*     R0014370 - UE - EØS-REGLER      - HOVEDPROG                   */00000030
 /*     PROGRAMMERER: HERMAN LARSSEN 0195                             */00000040
 /* HENSIKT                                                           */00000050
 /*     BEREGNE ETTERLATTEPENSJON ETTER EØS-REGLER                    */00000060
 /*     OPPDATERER ETTEPENS MED :                                     */00000070
 /*     OPPDATERER STATUS   MED :                                     */00000080
 /*     OPPDATERER EØS      MED :                                     */00000090
 /* PROGRAMTILKNYTNING.                                               */00000100
 /*     PROSEDYREN BLIR LINKET FRA  R0014001                          */00000110
 /* ***************************************************************** */00000120
  UE_EØS :                                                              00000130
   PROC (COMMAREA_PEKER) OPTIONS(MAIN);                                 00000140
 %INCLUDE P0019906;                                                     00000150
 %INCLUDE P0019908;                                                     00000160
 %INCLUDE P0019910;                                                     00000170
 %INCLUDE P0019912;                                                     00000180
   DCL 1 B01 BASED (B01_PEKER),                                         00000190
 %INCLUDE P0019921;                                                     00000200
   DCL 1 B02 BASED (B02_PEKER),                                         00000210
 %INCLUDE P0019921;                                                     00000220
   DCL 1 WSPESOPPL,                                                     00000230
 %INCLUDE P0019939;                                                     00000240
   DCL 1 WEØSINFO_AVD,                                                  00000250
 %INCLUDE P0019966;                                                     00000260
   DCL 1 WEØSINFO_ETTERL,                                               00000270
 %INCLUDE P0019966;                                                     00000280
   % INCLUDE P0014002;  /*W_POREKKE */                                  00000290
   % INCLUDE P0014003;  /*TAB       */                                  00000300
   % INCLUDE P0014009;  /*POREKKE  */                                   00000310
   DCL COMMAREA_PEKER PTR;                                              00000320
   DCL 1 WB02 LIKE B02;                                                 00000330
   DCL ALDER_66ÅR                     FIXED DEC (3)   INIT (0);         00000340
   DCL ALDER_EK                       FIXED DEC (5)   INIT (0);         00000350
   DCL FAKTISK_TT                     FIXED DEC (3)   INIT (0);         00000360
   DCL FAKTISK_TT_MND                 FIXED DEC (3)   INIT (0);         00000370
   DCL FRADRAG_F                      DEC FIXED(7);                     00000380
   DCL G_HALVE                        DEC FIXED(7);                     00000390
   DCL GP_NETTO                       DEC FIXED(5)    INIT (0);         00000400
   DCL HJELP_ÅM                       PIC     '9999';                   00000410
   DCL MAX_BUP                        FIXED DEC (3)   INIT (0);         00000420
   DCL MINSTEVILKÅR                   FIXED DEC (5,2) INIT (0);         00000430
   DCL NORSK_PENSJON_SUM              FIXED DEC (7)   INIT (0);         00000440
   DCL OPPTJENINGSPERIODEN            FIXED DEC (3)   INIT (0);         00000450
   DCL PRO_RATA_ST                    FIXED DEC (7)   INIT (0);         00000460
   DCL PRO_RATA_GP                    FIXED DEC (7)   INIT (0);         00000470
   DCL PRO_RATA_TP                    FIXED DEC (7)   INIT (0);         00000480
   DCL PÅ_SUM_FAKTISK                 FIXED DEC (3)   INIT (0);         00000490
   DCL ST_NETTO                       DEC FIXED(5)    INIT (0);         00000500
   DCL ST_PROSENT                     FIXED DEC (5,2) INIT (0);         00000510
   DCL TEORETISK_GP                   FIXED DEC (7)   INIT (0);         00000520
   DCL TEORETISK_TP                   FIXED DEC (7)   INIT (0);         00000530
   DCL TIL_ÅR                         FIXED DEC (3)   INIT (0);         00000540
   DCL TP_NETTO                       DEC FIXED(5)    INIT (0);         00000550
   DCL TT_FRAMT_MAX                   FIXED DEC (3)   INIT (0);         00000560
   DCL TT_NORSK_FAKTISK_MND           FIXED DEC (3)   INIT (0);         00000570
   DCL TT_TEORETISK                   FIXED DEC (3)   INIT (0);         00000580
   DCL TT_FRAMT_EØS                   FIXED DEC (3)   INIT (0);         00000590
   DCL UF_IND                         FIXED BIN (15);                   00000600
   DCL UFØREÅR  DEF HJELP_ÅM POS(1)   PIC       '99';                   00000610
   DCL VIRK_LOV92_ÅM                  PIC     '9999';                   00000620
   DCL W_TT_FRAMT_EØS                 FIXED DEC (5,2) INIT (0);         00000630
   DCL W_STI_ÅM                       PIC '(4)9';                       00000640
   DCL W_STI_ÅR                       PIC '(2)9' DEF W_STI_ÅM POS(1);   00000650
   DCL W_STI_MND                      PIC '(2)9' DEF W_STI_ÅM POS(3);   00000660
   DCL W_TT_FRAM_TIL_DATO             PIC '(5)9';                       00000670
   DCL W_TT_FRAM_TIL_ÅR DEF W_TT_FRAM_TIL_DATO POS (1) PIC '999';       00000680
   DCL W_TT_FRAM_TIL_MND DEF W_TT_FRAM_TIL_DATO POS (4) PIC '99';       00000690
   DCL W_FNR                          PIC      '(11)9';                 00000700
   DCL 1 FNR_R DEF W_FNR,                                               00000710
      2 DAG                   PIC '(2)9',                               00000720
      2 MND                   PIC '(2)9',                               00000730
      2 ÅR                    PIC '(2)9',                               00000740
      2 ÅRHUNDRE              PIC '9',                                  00000750
      2 FILLER                PIC '(4)9';                               00000760
                                                                        00000770
 DCL W_FNR_EK           PIC'(11)9';                                     00000780
 DCL W_DATO_ÅM          PIC'(4)9';                                      00000790
 DCL W_ÅR  DEF W_DATO_ÅM POS(1)   PIC'99';                              00000800
 DCL W_MN  DEF W_DATO_ÅM POS(3)   PIC'99';                              00000810
 DCL W_DATO_MÅ          PIC'9999';                                      00000820
 DCL W_ST                         FIXED DEC (15,4)  INIT (0);           00000830
 /******                                                                00000840
   DCL ADDR BUILTIN;                                                    00000850
   COMMAREA_PEKER           = ADDR(KOM_OMR);                            00000860
   KOM_OMR.DIV_PARAM_PEKER  = ADDR(KOM_OMR.DIV_PARAM_OMR);              00000870
  ******/                                                               00000880
   DCL HLTEST1  FIXED DEC (3) INIT (0);                                 00000890
   DCL HLTEST2  FIXED DEC (3) INIT (0);                                 00000900
   HLTEST1 = DIV_PARAM_OMR.PÅ_NORSK_FAKTISK(BER_EK_IND);                00000910
   HLTEST2 = DIV_PARAM_OMR.PÅ_EØS_FAKTISK(BER_EK_IND);                  00000920
   WB02 = B02;                                                          00000930
                                                                        00000940
   W_FNR          = B02.FNR(BER_EK_IND);                                00000950
   VIRK_LOV92_ÅM  = F_SNU_DATO(VIRKNINGSDATO);                          00000960
   W_DATO_ÅM      = B02.VIRK_DATO_ÅM(BER_EK_IND);                       00000970
   W_DATO_MÅ      = W_MN !! W_ÅR;                                       00000980
   W_FNR_EK       = B02.FNR(BER_SØ_IND);                                00000990
  /* DENNE INFORMASJONEN TAR VI MED    : 0994 HL */                     00001000
                                                                        00001010
   B02.ANTALL_NORSKE_PÅ(BER_EK_IND) = POREKKE.PÅ(BER_EK_IND);           00001020
     B02.ANTALL_NORSKE_PÅ_ETTER91(BER_EK_IND)                           00001030
                  = POREKKE.PÅ_ETTER91 (BER_EK_IND);                    00001040
  /*     B02.ANTALL_NORSKE_PÅ(BER_EK_IND)       = 0;     */             00001050
  /*     B02.ANTALL_NORSKE_PÅ_ETTER91(BER_EK_IND) = 0;   */             00001060
                                                                        00001070
   IF B02.BEREGN_FOLKETRYGD(BER_EK_IND) = 'J' THEN                      00001080
      DO;                                                               00001090
         W_POREKKE = POREKKE(BER_EK_IND);                               00001100
         POREKKE(BER_EK_IND) = '';                                      00001110
         CALL REGN_PRO_RATA;                                            00001120
         WSPESOPPL = B02.SPESOPPL(BER_EK_IND);                          00001130
         WEØSINFO_AVD       = B02.EØSINFO (BER_EK_IND);                 00001140
         WEØSINFO_ETTERL    = B02.EØSINFO (BER_SØ_IND);                 00001150
         IF B02.ALTERNATIV_SUM_PENSJON(BER_SØ_IND) > /*EØS-PENSJON*/    00001160
            B02.SUM_YTELSE(BER_SØ_IND)      THEN   /*NORSK-PENSJON*/    00001170
 /* ***************************************************** */            00001180
 /* PRO RATA GIR BEST RESULTAT :                          */            00001190
 /* ***************************************************** */            00001200
            DO;                                                         00001210
               B02.PRO_RATA_BEREGNING(BER_EK_IND) = 'J';                00001220
  /*EØSINFO : */                                                        00001230
     B02.SUM_YTELSE(BER_SØ_IND) =                                       00001240
                     B02.ALTERNATIV_SUM_PENSJON(BER_SØ_IND);            00001250
     B02.ALTERNATIV_GP(BER_SØ_IND) =                                    00001260
                          B02.ETTEPENS.GP_NETTO(BER_SØ_IND);            00001270
     B02.ALTERNATIV_TP(BER_SØ_IND) =                                    00001280
                          B02.ETTEPENS.TP_NETTO(BER_SØ_IND);            00001290
     B02.ALTERNATIV_ST(BER_SØ_IND) = B02.ETTEPENS.ST(BER_SØ_IND);       00001300
     B02.ALTERNATIV_SUM_PENSJON(BER_SØ_IND) =                           00001310
                             + B02.ALTERNATIV_GP(BER_SØ_IND)            00001320
                             + B02.ALTERNATIV_TP(BER_SØ_IND)            00001330
                             + B02.ALTERNATIV_ST(BER_SØ_IND);           00001340
                                                                        00001350
               CALL OPPDATER;                                           00001360
         WEØSINFO_ETTERL  = B02.EØSINFO (BER_SØ_IND);                   00001370
         WEØSINFO_AVD     = B02.EØSINFO (BER_EK_IND);                   00001380
            END;                                                        00001390
         ELSE                                                           00001400
            DO;                                                         00001410
 /* ***************************************************** */            00001420
 /* VANLIG NORSK BEREGNING GIR BEST RESULTAT              */            00001430
 /* VI LEGGER TILBAKE B02, MEN SØRGER FOR OPPDATERING     */            00001440
 /* ***************************************************** */            00001450
         B02.PRO_RATA_BEREGNING(BER_EK_IND) = 'N';                      00001460
         POREKKE(BER_EK_IND) = W_POREKKE;                               00001470
         WEØSINFO_ETTERL  = B02.EØSINFO (BER_SØ_IND);                   00001480
         WEØSINFO_AVD     = B02.EØSINFO (BER_EK_IND);                   00001490
         WEØSINFO_ETTERL.ALTERNATIV_GP = GP_NETTO;                      00001500
         WEØSINFO_ETTERL.ALTERNATIV_TP = TP_NETTO;                      00001510
         WEØSINFO_ETTERL.ALTERNATIV_ST = ST_NETTO;                      00001520
                                                                        00001530
         B02 = WB02;                                                    00001540
         B02.EØSINFO (BER_SØ_IND) = WEØSINFO_ETTERL;                    00001550
         B02.EØSINFO (BER_EK_IND) = WEØSINFO_AVD;                       00001560
            END;                                                        00001570
      END;                                                              00001580
   ELSE  /*IKKE RETT TIL NORSK PENSJON*/                                00001590
      DO;                                                               00001600
         B02.PRO_RATA_BEREGNING(BER_EK_IND) = 'J';                      00001610
                                                                        00001620
         B02.ALTERNATIV_GP(BER_SØ_IND)          = 0;                    00001630
         B02.ALTERNATIV_TP(BER_SØ_IND)          = 0;                    00001640
         B02.ALTERNATIV_ST(BER_SØ_IND)          = 0;                    00001650
         B02.ALTERNATIV_BT(BER_SØ_IND)          = 0;                    00001660
         B02.ALTERNATIV_SUM_PENSJON(BER_SØ_IND) = 0;                    00001670
                                                                        00001680
         W_POREKKE = POREKKE(BER_EK_IND);                               00001690
         POREKKE(BER_EK_IND) = '';                                      00001700
         CALL REGN_PRO_RATA;                                            00001710
         B02.SUM_YTELSE(BER_SØ_IND)  =                                  00001720
                     B02.ALTERNATIV_SUM_PENSJON(BER_SØ_IND);            00001730
         CALL OPPDATER;                                                 00001740
         WEØSINFO_ETTERL  = B02.EØSINFO (BER_SØ_IND);                   00001750
         WEØSINFO_AVD     = B02.EØSINFO (BER_EK_IND);                   00001760
         B02.ALTERNATIV_SUM_PENSJON(BER_SØ_IND) = 0;                    00001770
      END;                                                              00001780
                                                                        00001790
  REGN_PRO_RATA : PROC;                                                 00001800
  /*BEREGNER PRO_RATA_GP OG PRO_RATA_TP*/                               00001810
  /*INFORMASJON TIL STØNADSBREV : PÅ_EØS_FAKTISK, PÅ_NORSK_FAKTISK*/    00001820
  /*        PÅ_ETTER_91   (DETTE ER EØS-TILLEGG)                  */    00001830
  /*        TT_NORSK_FAKTISK, TT_SUM_FAKTISK                      */    00001840
  /*        TEORETISK_GP, TEORETISK_TP                            */    00001850
                                                                        00001860
   HJELP_ÅM = B02.DØDSDATO_ÅMD(BER_EK_IND) / 100;                       00001870
                                                                        00001880
   ALDER_66ÅR = FNR_R.ÅR + 66;                                          00001890
 /* I REGN_TT_EØS REGNER VI TT_FRAMT_EØS OG TT_TEORETISK */             00001900
 /* TT_NORSK_FAKTISK ER FAKTISK TT I NORGE         */                   00001910
   IF B02.TT_ANV(BER_EK_IND) > 0              THEN                      00001920
   DO;                                                                  00001930
      CALL REGN_TT_EØS;                                                 00001940
      IF ((TT_NORSK_FAKTISK_MND = 0) &                                  00001950
                 (B02.TT_PRO_RATA_MND(BER_EK_IND) = 0))  THEN ;         00001960
      ELSE                                                              00001970
                                                                        00001980
      DO;                                                               00001990
                                                                        00002000
         DIV_PARAM_OMR.TT_SUM_FAKTISK = (TT_NORSK_FAKTISK_MND +         00002010
                  B02.TT_PRO_RATA_MND(BER_EK_IND) + 6) / 12;            00002020
         IF DIV_PARAM_OMR.TT_SUM_FAKTISK > 40    THEN                   00002030
            DIV_PARAM_OMR.TT_SUM_FAKTISK = 40;                          00002040
         IF B02.PENSJONSTYPE2(BER_SØ_IND) = 'E'   THEN                  00002050
            TEORETISK_GP = DIV_PARAM_OMR.TT_TEORETISK * G /(40*12)      00002060
                  * 0.75  + 0.5;                                        00002070
         ELSE                                                           00002080
            TEORETISK_GP = DIV_PARAM_OMR.TT_TEORETISK * G /(40*12)      00002090
                          + 0.5;                                        00002100
         DIV_PARAM_OMR.TEORETISK_BELØP_GP = TEORETISK_GP;               00002110
         PRO_RATA_GP = TEORETISK_GP                                     00002120
                     * DIV_PARAM_OMR.TT_NORSK_FAKTISK                   00002130
                     / DIV_PARAM_OMR.TT_SUM_FAKTISK + 0.5;              00002140
          IF B02.PENSJONSTYPE2(BER_SØ_IND) = 'E'      THEN              00002150
             ST_PROSENT =  ST_PROSENT_SAMST;                            00002160
          ELSE                                                          00002170
             ST_PROSENT =  ST_PROSENT_ENSLIG;                           00002180
                                                                        00002190
          W_ST = ST_PROSENT * G / 100;                                  00002200
          W_ST = W_ST * DIV_PARAM_OMR.TT_TEORETISK;                     00002210
          W_ST = W_ST * DIV_PARAM_OMR.TT_NORSK_FAKTISK                  00002220
                      / DIV_PARAM_OMR.TT_SUM_FAKTISK;                   00002230
          W_ST = W_ST / (40 * 12) + 0.5;                                00002240
                                                                        00002250
       END;                                                             00002260
                                                                        00002270
    END;                                                                00002280
   DIV_PARAM_OMR.REGN_PRO_RATA(BER_EK_IND) = 'J';                       00002290
   B01_B02_IND =        2;                                              00002300
   POTALL_OPPL.IND = BER_EK_IND;                                        00002310
   EXEC CICS LINK PROGRAM('R0014141') COMMAREA (KOM_OMR);               00002320
   IF FEIL_MELD_NR > 0  THEN                                            00002330
      GO TO L999;                                                       00002340
   ELSE                                                                 00002350
      PROGRAM_ID = 'R0014370';                                          00002360
   DO I = 67 TO 91;                                                     00002370
      IF B02.INNTEKTSÅR_EØS(BER_EK_IND,I)   &                           00002380
        (POREKKE.POENG     (BER_EK_IND,I) = 0)  THEN                    00002390
         IF POREKKE.PÅ(BER_EK_IND) = 40 THEN                            00002400
            POREKKE.PÅ_ETTER91(BER_EK_IND) =                            00002410
            POREKKE.PÅ_ETTER91(BER_EK_IND) - 1;                         00002420
         ELSE                                                           00002430
            POREKKE.PÅ(BER_EK_IND) =                                    00002440
            POREKKE.PÅ(BER_EK_IND) + 1;                                 00002450
   END;                                                                 00002460
   DO I = 92 TO 106 WHILE (POREKKE.PÅ(BER_EK_IND) < 40);                00002470
      IF B02.INNTEKTSÅR_EØS(BER_EK_IND,I)   &                           00002480
        (POREKKE.POENG     (BER_EK_IND,I) = 0)  THEN                    00002490
         DO;                                                            00002500
            POREKKE.PÅ_ETTER91(BER_EK_IND) =                            00002510
            POREKKE.PÅ_ETTER91(BER_EK_IND) + 1;                         00002520
            POREKKE.PÅ(BER_EK_IND) =                                    00002530
            POREKKE.PÅ(BER_EK_IND) + 1;                                 00002540
         END;                                                           00002550
   END;                                                                 00002560
                                                                        00002570
   IF B02.DØDSDATO_ÅMD(BER_EK_IND) < 920000   THEN                      00002580
      POREKKE.PÅ_ETTER91(BER_EK_IND) = 0;                               00002590
                                                                        00002600
   DIV_PARAM_OMR.TEORETISK_BELØP_TP = F_TP92(FNR_R.ÅR,G,                00002610
                        POREKKE.SPT(BER_EK_IND),                        00002620
                        POREKKE.OPT(BER_EK_IND),                        00002630
                        POREKKE.PÅ(BER_EK_IND),                         00002640
                        POREKKE.PÅ_ETTER91(BER_EK_IND),                 00002650
                        100,55,J);                                      00002660
                                                                        00002670
   PÅ_SUM_FAKTISK = DIV_PARAM_OMR.PÅ_NORSK_FAKTISK(BER_EK_IND)          00002680
                    + DIV_PARAM_OMR.PÅ_EØS_FAKTISK(BER_EK_IND);         00002690
   IF PÅ_SUM_FAKTISK > 40 THEN                                          00002700
      PÅ_SUM_FAKTISK = 40;                                              00002710
   IF (DIV_PARAM_OMR.TEORETISK_BELØP_TP > 0)  &                         00002720
      (DIV_PARAM_OMR.PÅ_NORSK_FAKTISK(BER_EK_IND) > 0)   THEN           00002730
   PRO_RATA_TP = DIV_PARAM_OMR.TEORETISK_BELØP_TP                       00002740
                 * DIV_PARAM_OMR.PÅ_NORSK_FAKTISK(BER_EK_IND)           00002750
                 / PÅ_SUM_FAKTISK       + 0.5;                          00002760
                                                                        00002770
   HLTEST1 = DIV_PARAM_OMR.PÅ_NORSK_FAKTISK(BER_EK_IND);                00002780
   HLTEST2 = DIV_PARAM_OMR.PÅ_EØS_FAKTISK(BER_EK_IND);                  00002790
   IF W_ST > PRO_RATA_TP            THEN                                00002800
      PRO_RATA_ST = W_ST - PRO_RATA_TP;                                 00002810
   FRADRAG_F = (B02.FORVENTET(BER_SØ_IND) * 100);                       00002820
   G_HALVE   = G / 2;                                                   00002830
   FRADRAG_F = FRADRAG_F - G_HALVE;                                     00002840
   IF FRADRAG_F > 0                          THEN                       00002850
      CALL FORVENTET_INNTEKT;                                           00002860
   ELSE                                                                 00002870
      DO;                                                               00002880
         B02.ALTERNATIV_SUM_PENSJON(BER_SØ_IND) = PRO_RATA_TP           00002890
          + PRO_RATA_GP + PRO_RATA_ST - B02.PENSJON_EØS(BER_SØ_IND);    00002900
         GP_NETTO = PRO_RATA_GP;                                        00002910
         TP_NETTO = PRO_RATA_TP;                                        00002920
         ST_NETTO = PRO_RATA_ST;                                        00002930
      END;                                                              00002940
                                                                        00002950
  END REGN_PRO_RATA;                                                    00002960
                                                                        00002970
                                                                        00002980
  REGN_TT_EØS : PROC;                                                   00002990
   /* SUMMERER OPP TT_NORSK_FAKTISK_MND                      */         00003000
   /* OPPDATERER DIV_PARAM_OMR MED TT_NORSK_FAKTISK (ÅR)     */         00003010
   /* FAKTISK_TT_MND = TT_NORSK_FAKTISK_MND + TT_TEORETISK_EØS*/        00003020
   /* FINNER TT_FRAMT_EØS                             MåNEDER */        00003030
   /* FINNER TT_TEORETISK = (FAKTISK_TT + TT_FRAMT_EØS + 6)/12*/        00003040
                                                                        00003050
     IF B02.ETTEPENS.STI(BER_SØ_IND) > 0             THEN               00003060
        W_STI_ÅM = B02.ETTEPENS.STI(BER_SØ_IND);                        00003070
     ELSE                                                               00003080
        W_STI_ÅM = HJELP_ÅM;                                            00003090
                                                                        00003100
     IF W_STI_ÅM  < 7301 THEN                                           00003110
                  W_TT_FRAM_TIL_DATO =                                  00003120
                          (FNR_R.ÅR + 70) * 100 + FNR_R.MND;            00003130
     ELSE                                                               00003140
        W_TT_FRAM_TIL_DATO =                                            00003150
                          (FNR_R.ÅR + 66) * 100 + 12;                   00003160
                                                                        00003170
     TT_FRAMT_MAX    = (W_TT_FRAM_TIL_ÅR * 12 + W_TT_FRAM_TIL_MND) -    00003180
                      (W_STI_ÅR  * 12 + W_STI_MND - 1);                 00003190
                                                                        00003200
     IF B02.TT_16_66(BER_EK_IND) > 0 THEN                               00003210
        TT_NORSK_FAKTISK_MND = B02.TT_16_66(BER_EK_IND);                00003220
     ELSE                                                               00003230
        TT_NORSK_FAKTISK_MND  =  B02.TT_FØR_1967(BER_EK_IND) * 12 +     00003240
                             B02.TT_ETTER_1966(BER_EK_IND);             00003250
     FAKTISK_TT_MND = TT_NORSK_FAKTISK_MND +                            00003260
                      B02.TT_TEORETISK_EØS_MND(BER_EK_IND);             00003270
                                                                        00003280
     OPPTJENINGSPERIODEN    = ((W_STI_ÅR *12) + W_STI_MND - 1) -        00003290
             ((( FNR_R.ÅR + 16 ) * 12) + FNR_R.MND);                    00003300
                                                                        00003310
     MINSTEVILKÅR  = (OPPTJENINGSPERIODEN * 4) / 5;  /*AVRUNDES IKKE*/  00003320
                                                                        00003330
     IF MINSTEVILKÅR > FAKTISK_TT_MND    THEN                           00003340
        DO;                                                             00003350
           W_TT_FRAMT_EØS      = 480 - MINSTEVILKÅR ;                   00003360
           TT_FRAMT_EØS        = W_TT_FRAMT_EØS + 0.5;                  00003370
           MAX_BUP = (W_TT_FRAMT_EØS + 6) / 12;                         00003380
           TIL_ÅR = HJELP_ÅM / 100                                      00003390
                    + MAX_BUP - 1;                                      00003400
           IF ALDER_66ÅR > TIL_ÅR          THEN                         00003410
              B02.REDUSERT_ANT_BUP_ÅR(BER_EK_IND,UF_IND) = MAX_BUP;     00003420
        END;                                                            00003430
     ELSE                                                               00003440
        DO;                                                             00003450
           TT_FRAMT_EØS = TT_FRAMT_MAX;                                 00003460
           B02.REDUSERT_ANT_BUP_ÅR(BER_EK_IND,UF_IND) = 0;              00003470
        END;                                                            00003480
                                                                        00003490
     DIV_PARAM_OMR.TT_NORSK_FAKTISK =                                   00003500
                        (TT_NORSK_FAKTISK_MND + 6) / 12;                00003510
     IF DIV_PARAM_OMR.TT_NORSK_FAKTISK > 40      THEN                   00003520
        DIV_PARAM_OMR.TT_NORSK_FAKTISK = 40;                            00003530
     DIV_PARAM_OMR.TT_TEORETISK =                                       00003540
               (FAKTISK_TT_MND + TT_FRAMT_EØS + 6) / 12;                00003550
     IF DIV_PARAM_OMR.TT_TEORETISK > 40 THEN                            00003560
        DIV_PARAM_OMR.TT_TEORETISK = 40;                                00003570
  END REGN_TT_EØS;                                                      00003580
                                                                        00003590
  FORVENTET_INNTEKT: PROC;                                              00003600
                                                                        00003610
    DCL                                                                 00003620
       I             FIXED BIN(15),                                     00003630
       W_BRUTTO_EP   DEC FIXED(7),                                      00003640
       W_NETTO_EP    DEC FIXED(7),                                      00003650
       W_ST_FRADRAG  DEC FIXED(5),                                      00003660
       REST_FRADRAG  DEC FIXED(5),                                      00003670
       W_GP_FRADRAG  DEC FIXED(5);                                      00003680
    DCL ROUND        BUILTIN;                                           00003690
                                                                        00003700
    FRADRAG_F = FRADRAG_F * 40 / (100 * 12);                            00003710
                                                                        00003720
    W_BRUTTO_EP                 = PRO_RATA_GP                           00003730
                                + PRO_RATA_TP                           00003740
                                + PRO_RATA_ST                           00003750
                                - B02.PENSJON_EØS(BER_SØ_IND);          00003760
    IF FRADRAG_F > W_BRUTTO_EP THEN                                     00003770
       DO;                                                              00003780
          PRO_RATA_GP = 0;                                              00003790
          PRO_RATA_TP = 0;                                              00003800
          PRO_RATA_ST = 0;                                              00003810
       END;                                                             00003820
    ELSE                                                                00003830
       DO;                                                              00003840
                                                                        00003850
 /* ***************************************************************** */00003860
 /* FRADRAGET ER MINDRE ENN GP + TP + ST                              */00003870
 /* ***************************************************************** */00003880
                                                                        00003890
          W_NETTO_EP                 = W_BRUTTO_EP - FRADRAG_F;         00003900
                                                                        00003910
          GP_NETTO        = ROUND(PRO_RATA_GP *                         00003920
                                    W_NETTO_EP / W_BRUTTO_EP , 0);      00003930
          REST_FRADRAG = FRADRAG_F - PRO_RATA_GP + GP_NETTO;            00003940
          IF PRO_RATA_ST            = 0         THEN                    00003950
             TP_NETTO = PRO_RATA_TP - REST_FRADRAG;                     00003960
          ELSE                                                          00003970
            DO;                                                         00003980
               TP_NETTO = ROUND(PRO_RATA_TP *                           00003990
                                  W_NETTO_EP / W_BRUTTO_EP , 0);        00004000
               REST_FRADRAG = REST_FRADRAG - PRO_RATA_TP + TP_NETTO;    00004010
               ST_NETTO = PRO_RATA_ST - REST_FRADRAG;                   00004020
            END;                                                        00004030
       END;                                                             00004040
       B02.ALTERNATIV_SUM_PENSJON(BER_SØ_IND) = TP_NETTO                00004050
              + GP_NETTO + ST_NETTO - B02.PENSJON_EØS(BER_SØ_IND);      00004060
       DIV_PARAM_OMR.FULLT_SÆRTILLEGG(BER_SØ_IND) = W_ST;               00004070
                                                                        00004080
 END FORVENTET_INNTEKT;                                                 00004090
                                                                        00004100
                                                                        00004110
  OPPDATER      : PROC;                                                 00004120
                                                                        00004130
  /*ØVRIG :   */                                                        00004140
     B02.ETTEPENS.GP_BRUTTO(BER_SØ_IND) = PRO_RATA_GP;                  00004150
     B02.ETTEPENS.GP_NETTO(BER_SØ_IND) = GP_NETTO;                      00004160
     B02.ETTEPENS.TP_BRUTTO(BER_SØ_IND) = PRO_RATA_TP;                  00004170
     B02.ETTEPENS.TP_NETTO(BER_SØ_IND) = TP_NETTO;                      00004180
     B02.ETTEPENS.ST(BER_SØ_IND) = PRO_RATA_ST;                         00004190
     B02.ETTEPENS.SPT_AVD(BER_SØ_IND) = POREKKE.SPT(BER_EK_IND);        00004200
     B02.ETTEPENS.OPT_AVD(BER_SØ_IND) = POREKKE.OPT(BER_EK_IND);        00004210
     B02.ETTEPENS.PÅ_AVD (BER_SØ_IND) = POREKKE.PÅ (BER_EK_IND);        00004220
     B02.ETTEPENS.PÅ_ETTER91 (BER_SØ_IND) =                             00004230
                      POREKKE.PÅ_ETTER91 (BER_EK_IND);                  00004240
     B02.ETTEPENS.SPT_1291(BER_SØ_IND) = 0;                             00004250
     B02.ETTEPENS.OPT_1291(BER_SØ_IND) = 0;                             00004260
     B02.ETTEPENS.PÅ_1291(BER_SØ_IND) = 0;                              00004270
                                                                        00004280
     B02.GARANTI_TP(BER_SØ_IND) = 0;                                    00004290
     B02.GARANTI_DATO_ÅM(BER_SØ_IND) = 0;                               00004300
     B02.GT_TILLEGG_LOV92(BER_SØ_IND) = 0;                              00004310
     B02.GT_LOV92(BER_SØ_IND) = 0;                                      00004320
                                                                        00004330
  END OPPDATER;                                                         00004340
   % INCLUDE R0014148;      /* F_TP92                        */         00004350
   % INCLUDE R0019913;      /* F_SNU_DATO                    */         00004360
                                                                        00004370
 L999 :                                                                 00004380
  END UE_EØS;                                                           00004390
