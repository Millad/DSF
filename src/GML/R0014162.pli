 /*       SIST ENDRET 23/04-91 10.56.27 AV   DYBVIK                   */00000000
 /*       SIST ENDRET 25/08-87 08.17.02 AV   DYBVIK                   */00000010
 /*       SIST ENDRET 07/07-87 13.23.56 AV   DYBVIK                   */00000020
 /*       SIST ENDRET 12/06-86 07.57.53 AV   HERMAN                   */00000030
 /*       SIST ENDRET 07/03-86 15.34.09 AV   HERMAN                   */00000040
 /*       SIST ENDRET 05/03-86 07.49.27 AV   HERMAN                   */00000050
 /*       SIST ENDRET 05/01-84 13.01.47 AV   OLAV                     */00000060
 /*       SIST ENDRET 02/11-83 12.06.01 AV   OLAV                     */00000070
 /*       SIST ENDRET 14/10-83 12.24.28 AV   OLAV                     */00000080
 /*                                                                   */00000090
 /*IDENTIFIKASJON:                                                    */00000100
 /*    R0014162 - SUBRUTINE I PLI                                     */00000110
 /*    PROGRAMMERER: PER F. BERGESTAD, JUNI 1982                      */00000120
 /*HENSIKT:                                                           */00000130
 /*    RUTINEN REGNER UT PENSJONSGIVENDE INNTEKT I ÅRENE FØR 1967,    */00000140
 /*    OG PENSJONSPOENG FOR DISSE ÅRENE.                              */00000150
 /*TILKNYTNING :                                                      */00000160
 /*    PROGRAMMET INCLUDES I 4141                                     */00000170
 /*    PROGRAMMET CALLES   I 4154                                     */00000180
 /*                       OG 4164                                     */00000190
 /*                                                                   */00000200
 /*                                                                   */00000210
 /* ***************************************************************** */00000220
  LEGG_INNT_FØR_67_I_TAB:                                               00000230
    PROC(IND,START_ÅR) OPTIONS(REENTRANT);                              00000240
 /* PROC(IND,TAB,START_ÅR) OPTIONS(REENTRANT); ENDERET SATISH 21.9.91*/ 00000250
 /*  %INCLUDE P0014003;   ENDRET AV SATISH  21.04.91                */  00000260
                                                                        00000270
 /* 6.3.86 HL : NYTT PARAMETER 'START_ÅR'                       */      00000280
                                                                        00000290
 %SKIP(2);                                                              00000300
    DCL                                                                 00000310
       IND          FIXED BIN(15);                                      00000320
    DCL                                                                 00000330
       I            FIXED BIN(15)  INIT(0),                             00000340
       W_INNT_POENG FIXED DEC(5,3) INIT(0),                             00000350
       DIFF         FIXED DEC(7)   INIT(0),                             00000360
       DIFF66       FIXED DEC(7)   INIT(0),                             00000370
       DIFF67       FIXED DEC(7)   INIT(0);                             00000380
    DCL                                                                 00000390
       START_ÅR     FIXED DEC(3)          ;                             00000400
 %PAGE;                                                                 00000410
                                                                        00000420
                                                                        00000430
                                                                        00000440
    /* ************************************************************ */  00000450
    /* INNTEKTS-SEGMENTET                                           */  00000460
    /* ************************************************************ */  00000470
                                                                        00000480
     DCL 1  W01_PINT               UNALIGNED,                           00000490
         %INCLUDE P0019928;                                             00000500
                                                                        00000510
    %PAGE;                                                              00000520
    /* ************************************************************ */  00000530
    /*                                                              */  00000540
    /* DLI CALL-PARAMETRE                                           */  00000550
    /*                                                              */  00000560
    /* ************************************************************ */  00000570
                                                                        00000580
     DCL 1  W01_DLI_PARAM,                                              00000590
            2   W02_GU             CHAR (4)       INIT('GU  '),         00000600
            2   W02_PARM_CT_5      FIXED BIN(31)  INIT (5);             00000610
                                                                        00000620
    %PAGE;                                                              00000630
    /* ************************************************************ */  00000640
    /*                                                              */  00000650
    /* DL1 SSA-OMRÅDER FOR ROT OG STATUS-SEGMENTER.                 */  00000660
    /*                                                              */  00000670
    /* ************************************************************ */  00000680
                                                                        00000690
                                                                        00000700
     DCL 1  SSA1_RF0PERSN          UNALIGNED,                           00000710
            2    HDEL                            CHAR (17)  INIT        00000720
                 ('RF0PERSN(FNR     '),                                 00000730
            2    REL_OP                          CHAR (2)   INIT (' ='),00000740
            2    PKEY               FIXED        DEC  (11)  INIT ( 0 ), 00000750
            2    HP                              CHAR (1)   INIT (')'); 00000760
                                                                        00000770
                                                                        00000780
     DCL 1  SSA1_BINNTEKT_67       UNALIGNED,                           00000790
            2    SEGM_FELT1_NAVN                 CHAR (17)  INIT        00000800
                 ('PINNTEKT(INNTÅR'),                                   00000810
            2    REL_OP1                         CHAR (2)   INIT (' ='),00000820
            2    ÅR                 FIXED        DEC  (3)   INIT ( 67 ),00000830
            2    CON_CAT                         CHAR (1)   INIT ( '&'),00000840
            2    FELT2_NAVN                      CHAR (8)   INIT        00000850
                 ('INNTTYPE'),                                          00000860
            2    REL_OP2                         CHAR (2)   INIT (' ='),00000870
            2    TYPE                            CHAR (1)   INIT ('B'), 00000880
            2    HP                              CHAR (1)   INIT (')'); 00000890
                                                                        00000900
     DCL                                                                00000910
         UIB_RC_OK                 BIT (8)       INIT( 0 );             00000920
                                                                        00000930
 /* 7.3.86 HL : FEILRAPP 114                             */             00000940
 /*    DET ER INGEN BETINGELSE AT HER FINNES INNTEKTER   */             00000950
                                                                        00000960
 /* IF B02.TILBERPO.AI63(IND) > 0 !                       */            00000970
 /*    B02.TILBERPO.AI64(IND) > 0 !                       */            00000980
 /*    B02.TILBERPO.AI65(IND) > 0 !                       */            00000990
 /*    B02.TILBERPO.AI66(IND) > 0 !                       */            00001000
 /*    B02.TILBERPO.PI66(IND) > 0          THEN           */            00001010
 /*                                                       */            00001020
 /*    DO;                                                */            00001030
                                                                        00001040
 /* HIT 7.3.86 HL                                         */            00001050
                                                                        00001060
 /* 6.3.86 HL : FEILRAPP 40/88                            */            00001070
                                                                        00001080
 /*    DO I = 67 TO 70;                                   */            00001090
 /*       IF UFØR_START_MND(I) > 0 THEN                   */            00001100
 /*          DO;                                          */            00001110
 /*             START_ÅR = I;                             */            00001120
 /*             I        = 70;                            */            00001130
 /*          END;                                         */            00001140
 /*    END;                                               */            00001150
                                                                        00001160
 /* HIT 6.3.86 HL                                        */             00001170
                                                                        00001180
       IF START_ÅR = 67 THEN                                            00001190
                                                                        00001200
 /*                                                                   */00001210
 /*  NÅR STARTÅR ER 1967, ER DET DIFFEN MELLOM PI66 OG AI66 SOM SKAL  */00001220
 /*  PLUSSES PÅ AI I ÅRENE 63 TIL 65..                                */00001230
 /*                                                                   */00001240
                                                                        00001250
          DIFF = B02.TILBERPO.PI66(IND) * 100 - B02.TILBERPO.AI66(IND)  00001260
                                                                * 100;  00001270
       ELSE                                                             00001280
                                                                        00001290
 /*                                                                   */00001300
 /*  NÅR STARTÅR ER SEINERE ENN 1967, ER DET DIFFEN MELLOM PI OG AI   */00001310
 /*  I 1967 SOM SKAL PLUSSES PÅ AI I ÅRENE 64 TIL 66..                */00001320
 /*                                                                   */00001330
 /*  OBS OBS HER ER DET EN ENDRING - SE NEDENFOR  OBS OBS  HL         */00001340
                                                                        00001350
          DO;                                                           00001360
                                                                        00001370
             SSA1_RF0PERSN.PKEY  =        B02.FNR(IND);                 00001380
                                                                        00001390
                                                                        00001400
             CALL PLITDLI                (W02_PARM_CT_5,                00001410
                                         W02_GU,                        00001420
                                         RF1,                           00001430
                                         W01_PINT,                      00001440
                                         SSA1_RF0PERSN,                 00001450
                                         SSA1_BINNTEKT_67);             00001460
                                                                        00001470
             IF UIBFCTR         ^=        UIB_RC_OK         THEN        00001480
                DO;                                                     00001490
                  /* ****************************************** */      00001500
                  /*   UAKSEPTABEL KODE FRA :    UIB            */      00001510
                  /* ****************************************** */      00001520
  L100:                                                                 00001530
                   FEIL_MELD_NR     =     500;                          00001540
                   FEIL_VED_LABEL   =    '100';                         00001550
                   GO TO   RETUR;                                       00001560
                END;                                                    00001570
                                                                        00001580
                                                                        00001590
             IF RF1.STATUS_KODE    =        '  '       THEN             00001600
                                                                        00001610
 /*HL*/         DIFF67 = W02_PI * 100 - B02.RF0PERSN.AI67(IND) * 100;   00001620
                                                                        00001630
             ELSE                                                       00001640
                                                                        00001650
                IF RF1.STATUS_KODE     =        'GE'       THEN         00001660
                                                                        00001670
 /*HL*/            DIFF67 = 0;                                          00001680
                                                                        00001690
                ELSE                                                    00001700
                                                                        00001710
                   DO;                                                  00001720
                /* ****************************************** */        00001730
                /*   UAKSEPTABEL STATUSKODE                   */        00001740
                /* ****************************************** */        00001750
  L110:                                                                 00001760
                      FEIL_MELD_NR     =     500;                       00001770
                      FEIL_VED_LABEL   =    '110';                      00001780
                      DB_STATUS_KODE   =    RF1.STATUS_KODE;            00001790
                      GO TO   RETUR;                                    00001800
                   END;                                                 00001810
                                                                        00001820
 /*ENDRET 6.7.87 HL - DET ER BESTE ALTERNATIV AV DIFF66 OG DIFF67 SOM */00001830
 /*                   SKAL BRUKES. JF BREV FRA P1 18.5.87 - FRAPP 474 */00001840
                                                                        00001850
                                                                        00001860
             IF UFØR_START_MND(67) > 0                 THEN             00001870
                                                                        00001880
 /*ENDRET 21.8.87 HL - DET ER BARE NÅR UFØRHET INNTRAFF I 67 AT DET */  00001890
 /*                    ER TALE OM BESTE ALTERNATIV - FEILRAPP 525   */  00001900
                                                                        00001910
                DO;                                                     00001920
                                                                        00001930
                   DIFF66 = B02.TILBERPO.PI66(IND) * 100                00001940
                            - B02.TILBERPO.AI66(IND) * 100;             00001950
                                                                        00001960
                   IF DIFF66 > DIFF67                        THEN       00001970
                                                                        00001980
                      DIFF = DIFF66;                                    00001990
                                                                        00002000
                   ELSE                                                 00002010
                                                                        00002020
                      DIFF = DIFF67;                                    00002030
                                                                        00002040
                END;                                                    00002050
                                                                        00002060
             ELSE                                                       00002070
                                                                        00002080
                DIFF = DIFF67;                                          00002090
                                                                        00002100
 /*HIT 21.8.87 HL */                                                    00002110
                                                                        00002120
 /*HIT 6.7.87 HL */                                                     00002130
                                                                        00002140
          END;                                                          00002150
 %SKIP(2);                                                              00002160
 /*                                                                   */00002170
 /*  BARE DE 4 NÆRMESTE ÅRENE FØR STARTÅR ER AKTUELLE...              */00002180
 /*                                                                   */00002190
                                                                        00002200
                                                                        00002210
       IF START_ÅR < 68 THEN                                            00002220
          TAB.INNTEKT(63) = B02.TILBERPO.AI63(IND) * 100 + DIFF;        00002230
       IF START_ÅR < 69 THEN                                            00002240
          TAB.INNTEKT(64) = B02.TILBERPO.AI64(IND) * 100 + DIFF;        00002250
       IF START_ÅR < 70 THEN                                            00002260
          TAB.INNTEKT(65) = B02.TILBERPO.AI65(IND) * 100 + DIFF;        00002270
       IF START_ÅR < 71 THEN                                            00002280
          TAB.INNTEKT(66) = B02.TILBERPO.AI66(IND) * 100 + DIFF;        00002290
                                                                        00002300
 %PAGE;                                                                 00002310
       DO I = 63 TO 66;                                                 00002320
                                                                        00002330
          IF TAB.INNTEKT(I) < 0 THEN                                    00002340
             TAB.INNTEKT(I) = 0;                                        00002350
                                                                        00002360
          IF TAB.INNTEKT(I) > 0 THEN                                    00002370
             DO;                                                        00002380
                 W_INNT_POENG      = (TAB.INNTEKT(I) - 5000) / 5000;    00002390
                                                                        00002400
                 IF W_INNT_POENG      > 7.0000        THEN              00002410
                    W_INNT_POENG      = 7.0000;                         00002420
                                                                        00002430
                 IF W_INNT_POENG      < 0    THEN                       00002440
                    TAB.INNT_POENG(I) = 0;                              00002450
                 ELSE                                                   00002460
                    TAB.INNT_POENG(I) = W_INNT_POENG;                   00002470
             END;                                                       00002480
          ELSE                                                          00002490
             TAB.INNT_POENG(I) = 0;                                     00002500
                                                                        00002510
 /* 6.3.86 HL   FEILRAPP 40/88                            */            00002520
                                                                        00002530
          TAB.POENG_ANVENDT(I) = TAB.INNT_POENG(I);                     00002540
                                                                        00002550
 /* HIT 6.3.86 HL                                         */            00002560
                                                                        00002570
       END;                                                             00002580
                                                                        00002590
 /* 7.3.86 HL  END;                                       */            00002600
                                                                        00002610
  RETUR:                                                                00002620
                                                                        00002630
  END LEGG_INNT_FØR_67_I_TAB;                                           00002640
                                                                        00002650
