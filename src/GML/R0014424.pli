 /*       SIST ENDRET 19/05-98 13.01.39 AV   JDA7339                  */00000000
 /*       SIST ENDRET 19/05-98 12.48.48 AV   JDA7339                  */00000010
 /*       SIST ENDRET 22/11-94 15.25.17 AV   HLB0310                  */00000020
 /*       SIST ENDRET 17/02-92 13.46.25 AV   DYBVIK                   */00000030
 /*       SIST ENDRET 04/02-91 15.00.24 AV   DYBVIK                   */00000040
 /*       SIST ENDRET 11/12-90 13.54.15 AV   DYBVIK                   */00000050
 /*       SIST ENDRET 13/07-89 13.21.48 AV   DYBVIK                   */00000060
 /*       SIST ENDRET 11/07-89 12.52.56 AV   DYBVIK                   */00000070
 /*       SIST ENDRET 22/06-89 13.50.07 AV   DYBVIK                   */00000080
 /*       SIST ENDRET 11/05-89 08.20.12 AV   DYBVIK                   */00000090
 /*       SIST ENDRET 14/04-89 13.38.26 AV   HERMAN                   */00000100
 /* ***************************************************************** */00000110
 /* IDENTIFIKASJON                                                    */00000120
 /*     R0014424 - BEREGN_POENG_AV_AÅI                                */00000130
 /*     PROGRAMMERER: HERMAN LARSSEN                                  */00000140
 /* HENSIKT                                                           */00000150
 /*                                                                   */00000160
 /*  1. REGNER UT OG OPPDATERER POENGTALLET  YRKEHIST.PAÅ             */00000170
 /*     (POENGTALLET UT FRA DEN ANTATTE ÅRLIGE ARBEIDSINNTEKTEN       */00000180
 /*      PÅ SKADETIDSPUNKTET),                                        */00000190
 /*      EVT BEREGNET UT FRA LOVREGLER FOR SPESIELLE YRKESGRUPPER.    */00000200
 /*                                                                   */00000210
 /*  2. REGNER UT OG OPPDATERER YRKEPENS.PÅ OG PÅ_ETTER91             */00000220
 /*                                                                   */00000230
 /*  3.  SETTER FORELØPIG YPT_KODE (KAN BLI ENDRET I 4425)            */00000240
 /*                                                                   */00000250
 /* PROGRAMTILKNYTNING                                                */00000260
 /*     PROSEDYREN BLIR INKLUDERT I R0014401 - BEREGN_UFØREPENSJON.   */00000270
 /* ***************************************************************** */00000280
 BEREGN_POENG_AV_AÅI :                                                  00000290
  PROC;                                                                 00000300
                                                                        00000310
                                                                        00000320
    /*WORKING STORAGE*/                                                 00000330
                                                                        00000340
    DCL 1      W1,                                                      00000350
          2    ALDER                    FIXED DEC     (5) INIT(0),      00000360
          2    FNR_PIC11                PIC '(11)9',                    00000370
          2    GAMMEL_GRAD              FIXED DEC     (3) INIT(0),      00000380
          2    GV                       FIXED DEC     (7) INIT(0),      00000390
          2    G_YST                    FIXED DEC     (7) INIT(0),      00000400
          2    INNTEKT                  FIXED DEC     (7) INIT(0),      00000410
          2    POENG                    FIXED DEC   (5,2) INIT(0),      00000420
          2    POENG92                  FIXED DEC   (5,2) INIT(0),      00000430
          2    POENG1                   FIXED DEC   (5,2) INIT(0),      00000440
          2    POENG2                   FIXED DEC   (5,2) INIT(0),      00000450
          2    POENG3                   FIXED DEC   (5,2) INIT(0),      00000460
          2    POENG_GRUNNLAG           FIXED DEC     (7) INIT(0),      00000470
          2    POENG_GRUNNLAG92         FIXED DEC     (7) INIT(0),      00000480
          2    PÅ_FØR92                 FIXED DEC     (3) INIT(0),      00000490
          2    SYTTI                    FIXED DEC     (3) INIT (70),    00000500
          2    NITTIEN                  FIXED DEC     (3) INIT (91),    00000510
          2    UFG_FØR_KAP11            FIXED DEC     (3) INIT (0),     00000520
          2    YHIST_IND                FIXED BIN    (15),              00000530
          2    YST_ÅM                         PIC  '9999',              00000540
          2    YST_ÅMD                   FIXED DEC    (7),              00000550
          2    ÅRET                     FIXED DEC     (3);              00000560
                                                                        00000570
    DCL        YST_Å DEF W1.YST_ÅM POS (1)    PIC    '99';              00000580
                                                                        00000590
    DCL        YST_M DEF W1.YST_ÅM POS (3)    PIC    '99';              00000600
                                                                        00000610
    DCL        VIRK_ÅR_MND                  FIXED DEC (4);              00000620
    DCL        ATTEN                        FIXED DEC (5);              00000630
    DCL        NITTEN                       FIXED DEC (5);              00000640
    DCL        TYVE                         FIXED DEC (5);              00000650
    DCL        FNR_ÅR DEF FNR_PIC11 POS(5)    PIC '99';                 00000660
    DCL        POENG_IND (3)                FIXED DEC (5,2);            00000670
                                                                        00000680
                                                                        00000690
 /*PROGRAMMET BEGYNNER HER:*/                                           00000700
                                                                        00000710
  FNR_PIC11 = B02.FNR(BER_SØ_IND);                                      00000720
                                                                        00000730
  DO YHIST_IND = 1 TO 5 WHILE (B02.YRKEHIST.OPPH_DATO_ÅM                00000740
                                      (BER_SØ_IND,YHIST_IND) > 0);      00000750
  END;                                                                  00000760
                                                                        00000770
                                                                        00000780
                                                                        00000790
 /* ************************************************************ */     00000800
 /* FOR Å REGNE PAÅ MÅ VI BRUKE GRUNNBELØPET PÅ SKADETIDSPUNKTET */     00000810
 /* DET GRUNNBELØP VI HAR FUNNET TIDLIGERE (I 4001) GJELDER PÅ   */     00000820
 /* VIRKNINGSTIDSPUNKTET.                                        */     00000830
 /* ************************************************************ */     00000840
                                                                        00000850
                                                                        00000860
    W1.YST_ÅM = B02.YST_ÅM(BER_SØ_IND,YHIST_IND);                       00000870
    YST_ÅMD = YST_Å * 10000 + YST_M * 100 + 01;                         00000880
                                                                        00000890
                                                                        00000900
    DO J = 1 TO 60;                                                     00000910
                                                                        00000920
       IF YST_ÅMD         >= PERIODE_START_ÅMD(J) &                     00000930
          YST_ÅMD         <= PERIODE_SLUTT_ÅMD(J) THEN                  00000940
                                                                        00000950
          DO;                                                           00000960
             G_YST             = G_TAB_PERIODE.GRUNNBELØP(J);           00000970
             J                 = 99;                                    00000980
          END;                                                          00000990
                                                                        00001000
    END;                                                                00001010
                                                                        00001020
                                                                        00001030
  POENG_GRUNNLAG = B02.YRKEHIST.AÅI(BER_SØ_IND,YHIST_IND);              00001040
  POENG_GRUNNLAG92 = POENG_GRUNNLAG;                                    00001050
                                                                        00001060
  IF POENG_GRUNNLAG   > (G_YST * 12)      THEN                          00001070
                                                                        00001080
     POENG_GRUNNLAG   = G_YST * 12;                                     00001090
                                                                        00001100
  IF POENG_GRUNNLAG   > (G_YST * 8)       THEN                          00001110
                                                                        00001120
     POENG_GRUNNLAG   = (G_YST * 8) +                                   00001130
                        ((POENG_GRUNNLAG - (G_YST*8)) / 3) + 0.5;       00001140
                                                                        00001150
  IF POENG_GRUNNLAG   > G_YST   THEN                                    00001160
                                                                        00001170
     POENG = (POENG_GRUNNLAG - G_YST) / G_YST + 0.005;                  00001180
                                                                        00001190
  ELSE                                                                  00001200
                                                                        00001210
     POENG = 0;                                                         00001220
                                                                        00001230
                                                                        00001240
    IF W1.YST_ÅM > 9200              THEN                               00001250
       DO;                                                              00001260
          IF FNR_ÅR > 40                THEN                            00001270
             B02.YRKEPENS.PÅ(BER_SØ_IND) = 40;                          00001280
          ELSE                                                          00001290
             B02.YRKEPENS.PÅ(BER_SØ_IND) = FNR_ÅR;                      00001300
                                                                        00001310
          IF FNR_ÅR > 75 THEN                                           00001320
             DO;                                                        00001330
                PÅ_FØR92 = 0;                                           00001340
                B02.YRKEPENS.PÅ_ETTER91(BER_SØ_IND) = 40;               00001350
             END;                                                       00001360
          ELSE                                                          00001370
          DO;                                                           00001380
             PÅ_FØR92 = 75 - FNR_ÅR;                                    00001390
             IF PÅ_FØR92 > 25 THEN                                      00001400
                PÅ_FØR92 = 25;                                          00001410
             IF PÅ_FØR92 <  B02.YRKEPENS.PÅ(BER_SØ_IND)    THEN         00001420
                B02.YRKEPENS.PÅ_ETTER91(BER_SØ_IND) =                   00001430
                      B02.YRKEPENS.PÅ(BER_SØ_IND) - PÅ_FØR92;           00001440
          END;                                                          00001450
                                                                        00001460
          IF POENG_GRUNNLAG92 > (G_YST * 12)      THEN                  00001470
             POENG_GRUNNLAG92 = G_YST * 12;                             00001480
                                                                        00001490
          IF POENG_GRUNNLAG92 > (G_YST * 6)       THEN                  00001500
             POENG_GRUNNLAG92 = (G_YST * 6) +                           00001510
            ((POENG_GRUNNLAG92 - (G_YST*6)) / 3) + 0.5;                 00001520
                                                                        00001530
          IF POENG_GRUNNLAG92 > G_YST   THEN                            00001540
             POENG92 = (POENG_GRUNNLAG92 - G_YST) / G_YST + 0.005;      00001550
          ELSE                                                          00001560
             POENG92 = 0;                                               00001570
                                                                        00001580
          IF PÅ_FØR92 < 20 THEN                                         00001590
             POENG = (POENG * PÅ_FØR92 + POENG92 * (20 - PÅ_FØR92))     00001600
                     / 20 + 0.005;                                      00001610
       END;                                                             00001620
                                                                        00001630
 /* VI INITIERER YPT_KODE MED 'A' SOM BETYR AT POENG ER BEREGNET */     00001640
 /* PÅ GRUNNLAG AV FASTSATT ANTATT ÅRLIG INNTEKT - KAN BLI ENDRET*/     00001650
                                                                        00001660
  B02.YPT_KODE (BER_SØ_IND) = 'A';                                      00001670
                                                                        00001680
 /* ************************************************************ */     00001690
 /* PÅ EN ELLER ANNEN MÅTE SÅ MÅ DET TESTES PÅ OM DET ER YRKES-  */     00001700
 /* KODE FRA FØR.  DENNE MÅ STÅ, MEN NY KODE MÅ OGSÅ INN.  ERIK. */     00001710
 /*                                                              */     00001720
 /*  IF B02.YRKE11(BER_SØ_IND,YHIST_IND) ^=                      */     00001730
 /*     B02.YRKE11(BER_SØ_IND,YHIST_IND - 1) THEN  ?????         */     00001740
 /* ************************************************************ */     00001750
                                                                        00001760
                                                                        00001770
    SELECT (B02.YRKE11(BER_SØ_IND,YHIST_IND));                          00001780
                                                                        00001790
       WHEN ('F')                                                       00001800
                                                                        00001810
    /*KAP 11-11 NR 1   FISKERE, FANGSTMENN MV  */                       00001820
                                                                        00001830
          DO;                                                           00001840
                                                                        00001850
             B02.YPT_KODE (BER_SØ_IND) = 'Y';                           00001860
                                                                        00001870
     /* FISKERE FÅR ALLTID YPT_KODE = 'Y' - POENG SKAL BEREGNES */      00001880
     /* PÅ GRUNNLAG AV YRKE, OG IKKE PÅ GRUNNLAG AV AÅI         */      00001890
                                                                        00001900
             W1.YST_ÅM = B02.YRKEHIST.YST_ÅM(BER_SØ_IND,YHIST_IND);     00001910
                                                                        00001920
 /* ************************************************************ */     00001930
 /* VI SKAL NÅ FINNE ET GJENNOMSNITT AV INNTEKTENE I DE SISTE    */     00001940
 /* 3 ÅR FØR SKADEÅRET.  FØR 1971 REGNES MED MAX 7.00 I POENG    */     00001950
 /* ************************************************************ */     00001960
                                                                        00001970
             DO I = 1 TO 3;                                             00001980
                POENG_IND (I) = 0;                                      00001990
                ÅRET    = YST_Å - I;                                    00002000
                                                                        00002010
                INNTEKT = B02.PI(BER_SØ_IND,ÅRET) * 100;                00002020
                                                                        00002030
                GV      = GV_TAB_ARRAY.G_V(ÅRET);                       00002040
                                                                        00002050
                IF INNTEKT > (GV * 12)        THEN                      00002060
                                                                        00002070
                   INNTEKT = GV * 12;                                   00002080
                                                                        00002090
                IF INNTEKT > (GV * 6)  &                                00002100
                   ÅRET > NITTIEN          THEN                         00002110
                      INNTEKT = GV*6 + ((INNTEKT - (GV*6)) / 3) + 0.5;  00002120
                ELSE                                                    00002130
                IF INNTEKT > (GV * 8)         THEN                      00002140
                                                                        00002150
                   IF ÅRET > SYTTI            THEN                      00002160
                                                                        00002170
                      INNTEKT = GV*8 + ((INNTEKT - (GV*8)) / 3) + 0.5;  00002180
                                                                        00002190
                ELSE                                                    00002200
                   INNTEKT = GV * 8;                                    00002210
                                                                        00002220
                POENG_IND(I) = (INNTEKT - GV) / GV + 0.005;             00002230
                                                                        00002240
                IF POENG_IND (I) < 0 THEN POENG_IND(I) = 0;             00002250
                                                                        00002260
             END; /*DO I = 1 TO 3*/                                     00002270
                                                                        00002280
             POENG = (POENG_IND (1)  + POENG_IND (2)                    00002290
                      + POENG_IND (3)) / 3 + 0.005;                     00002300
                                                                        00002310
                                                                        00002320
             IF POENG < 0.5   THEN                                      00002330
                                                                        00002340
                POENG = 0.5;                                            00002350
                                                                        00002360
                                                                        00002370
          END;                                                          00002380
                                                                        00002390
       WHEN ('M')                                                       00002400
                                                                        00002410
    /*KAP 11-11 NR 2   MILITÆRPERSON MV        */                       00002420
                                                                        00002430
          DO;                                                           00002440
                                                                        00002450
             IF POENG < 2 THEN                                          00002460
                                                                        00002470
                DO;                                                     00002480
                   POENG = 2;                                           00002490
                   B02.YPT_KODE (BER_SØ_IND) = 'Y';                     00002500
                END;                                                    00002510
                                                                        00002520
          END;                                                          00002530
                                                                        00002540
       WHEN ('U','E')                                                   00002550
                                                                        00002560
    /*KAP 11-11 NR 3   UNGDOM, ELEV MV         */                       00002570
                                                                        00002580
          DO;                                                           00002590
                                                                        00002600
             IF POENG < 1 THEN                                          00002610
                                                                        00002620
                DO;                                                     00002630
                   POENG = 1;                                           00002640
                   B02.YPT_KODE (BER_SØ_IND) = 'Y';                     00002650
                END;                                                    00002660
                                                                        00002670
          END;                                                          00002680
                                                                        00002690
       WHEN ('V')                                                       00002700
                                                                        00002710
    /*KAP 11-11 NR 3   ELEV UTEN YRKESUTDANNING   */                    00002720
                                                                        00002730
          DO;                                                           00002740
                                                                        00002750
                                                                        00002760
 /*LOVENDRING FRA 1.1.91 HL ; */                                        00002770
                                                                        00002780
             VIRK_ÅR_MND = B02.VIRK_DATO_ÅM(BER_SØ_IND);                00002790
                                                                        00002800
             ALDER = F_ALDER(FNR_PIC11,F_SNU_DATO(VIRK_ÅR_MND));        00002810
                                                                        00002820
             IF VIRK_ÅR_MND > 9100      THEN                            00002830
                DO;                                                     00002840
                   ATTEN  = 1801;                                       00002850
                   NITTEN = 1901;                                       00002860
                   TYVE   = 2001;                                       00002870
                END;                                                    00002880
             ELSE                                                       00002890
                DO;                                                     00002900
                   ATTEN  = 1800;                                       00002910
                   NITTEN = 1900;                                       00002920
                   TYVE   = 2000;                                       00002930
                END;                                                    00002940
                                                                        00002950
             IF ALDER < ATTEN      THEN;                                00002960
                                                                        00002970
             ELSE IF ALDER < NITTEN   THEN                              00002980
                                                                        00002990
                  DO;                                                   00003000
                     IF POENG  < 1  THEN                                00003010
                                                                        00003020
                        DO;                                             00003030
                           POENG  = 1;                                  00003040
                           B02.YPT_KODE (BER_SØ_IND) = 'Y';             00003050
                        END;                                            00003060
                                                                        00003070
                  END;                                                  00003080
                                                                        00003090
             ELSE IF ALDER < TYVE  THEN                                 00003100
                                                                        00003110
                  DO;                                                   00003120
                     IF POENG  < 1.5  THEN                              00003130
                                                                        00003140
                        DO;                                             00003150
                           POENG  = 1.5;                                00003160
                           B02.YPT_KODE (BER_SØ_IND) = 'Y';             00003170
                        END;                                            00003180
                                                                        00003190
                  END;                                                  00003200
                                                                        00003210
             ELSE                                                       00003220
                                                                        00003230
                  IF POENG  < 2  THEN                                   00003240
                                                                        00003250
                     DO;                                                00003260
                        POENG =  2;                                     00003270
                        B02.YPT_KODE (BER_SØ_IND) = 'Y';                00003280
                     END;                                               00003290
                                                                        00003300
          END;                                                          00003310
                                                                        00003320
       OTHERWISE;                                                       00003330
                                                                        00003340
          END;                                                          00003350
                                                                        00003360
       B02.PAÅ (BER_SØ_IND,YHIST_IND) = POENG;                          00003370
                                                                        00003380
                                                                        00003390
 END BEREGN_POENG_AV_AÅI;                                               00003400
