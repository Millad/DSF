 /*       SIST ENDRET 31/03-87 08.54.00 AV   DYBVIK                   */00000000
 /*       SIST ENDRET 02/12-86 13.22.34 AV   DYBVIK                   */00000010
 /*       SIST ENDRET 08/02-85 12.15.06 AV   OLAV                     */00000020
 /*       SIST ENDRET 09/02-84 13.10.48 AV   OLAV                     */00000030
 /*       SIST ENDRET 27/10-83 13.45.07 AV   OLAV                     */00000040
 /* ***************************************************************** */00000050
 /*IDENTIFIKASJON:                                                    */00000060
 /*    R0014143 - POENG_TILLEGG - INTERN PROC I PLI                   */00000070
 /*    PROGRAMMERER: KRISTENSEN, APRIL 1982                           */00000080
 /*    ENDRET AV KARIN I JULI                                         */00000090
 /*HENSIKT:                                                           */00000100
 /*    PROGRAMMET OPPDATERER POENGREKKER MED POENGTILLEGG.            */00000110
 /*               LEGGES IN I POENGREKKE,POENGREKKE_EK)               */00000120
 /*PROGRAMTILKNYTNING:                                                */00000130
 /*    PROGRAMMET BLIR INKLUDERT I OVERLIGGENDE PROGRAM.              */00000140
 /*BRUK:                                                              */00000150
 /*    CALL POENG_TILLEGG;                                            */00000160
 /*ENDRING:                                                           */00000170
 /*    DET ER LAGT INN FJERNING AV POENGILLEGGSOPPLYSNINGER NÅR       */00000180
 /*    IKKE GIS POENGTILLEGG LENGER           6/2 84   OLAV           */00000190
 /*ENDRING:                                                           */00000200
 /*    VALG AV HVEM SOM SKAL HA POENGTILLEGG TAR UTGANGSPUNKT I       */00000210
 /*    LENGSTE REKKE SELV OM DENNE IKKE BRUKES  25.1.85   OLAV        */00000220
 /*ENDRING 28.11.86 - HL                                              */00000230
 /*    VED VALG AV BESTE POENGREKKE SKAL POENGTILLEGGET FØRST VÆRE    */00000240
 /*    LAGT TIL. VI HAR OPPBEVART ALTERNATIV POENGREKKE PÅ PLASS      */00000250
 /*    12(KVINNE) ELLER 13(MANN). POENGTILLEGG PLUSSES PÅ OGSÅ DER.   */00000260
 /* ***************************************************************** */00000270
 POENG_TILLEGG:                                                         00000280
 PROC;                                                                  00000290
 DCL                                                                    00000300
    I       FIXED BIN(15),                                              00000310
    PT_IND  FIXED BIN(15),                                              00000320
    LOOPI   FIXED BIN(15),    /* 28.11.86 */                            00000330
    LOOPIN  FIXED BIN(15);    /* 28.11.86 */                            00000340
                                                                        00000350
 DCL                                                                    00000360
    PÅ_SØ   FIXED(3),                                                   00000370
    PÅ_EK   FIXED(3);                                                   00000380
                                                                        00000390
 IF POREKKE.SPT(BER_SØ_IND) > 0                        &                00000400
    POREKKE.SPT(BER_EK_IND) > 0                        &                00000410
    ((B02.POENGTILLEGG_DATO_ÅM(BER_SØ_IND) = 0 &                        00000420
      B02.VIRK_DATO_ÅM        (BER_SØ_IND) < 8404) !                    00000430
     (B02.POENGTILLEGG_DATO_ÅM(BER_SØ_IND) > 0 &                        00000440
      B02.POENGTILLEGG_DATO_ÅM(BER_SØ_IND) < 8404)   )       THEN       00000450
                                                                        00000460
    DO;              /* POENGTILLEGG   */                               00000470
       PÅ_SØ = 0;                                                       00000480
       PÅ_EK = 0;                                                       00000490
                                                                        00000500
                                                                        00000510
       DO I = 67 TO 150;                                                00000520
                                                                        00000530
         /* POREKKE_PT INNEHOLDER DEN LENGSTE POENGREKKEN     */        00000540
                                                                        00000550
         IF POREKKE_PT.POENG(BER_SØ_IND,I) > 0         THEN             00000560
            PÅ_SØ = PÅ_SØ + 1;                                          00000570
                                                                        00000580
         IF POREKKE_PT.POENG(BER_EK_IND,I) > 0         THEN             00000590
            PÅ_EK = PÅ_EK + 1;                                          00000600
                                                                        00000610
       END;                                                             00000620
                                                                        00000630
       IF B02.POENGTILLEGG_DATO_ÅM(BER_SØ_IND) = 0   THEN               00000640
                                                                        00000650
          DO;                                                           00000660
             B02.POENGTILLEGG_DATO_ÅM(BER_SØ_IND) =                     00000670
                          B02.VIRK_DATO_ÅM        (BER_SØ_IND);         00000680
                                                                        00000690
             B02.POENGTILLEGG_DATO_ÅM(BER_EK_IND) =                     00000700
                          B02.VIRK_DATO_ÅM        (BER_SØ_IND);         00000710
          END;                                                          00000720
                                                                        00000730
       IF PÅ_SØ < PÅ_EK       THEN                                      00000740
          PT_IND = BER_SØ_IND;                                          00000750
                                                                        00000760
       ELSE                                                             00000770
                                                                        00000780
          IF PÅ_SØ > PÅ_EK        THEN                                  00000790
             PT_IND = BER_EK_IND;                                       00000800
                                                                        00000810
       IF PÅ_SØ ^= PÅ_EK      THEN                                      00000820
                                                                        00000830
 /*  EN AV EKTEFELLEN FÅR TILLAGT 1 POENG KODE A               */       00000840
 /*  KODE C BETYR AT DEN ANDRE EKTEFELLEN FÅR TILLAGT 1 POENG  */       00000850
                                                                        00000860
          DO;                                                           00000870
                                                                        00000880
 /*19.3.*/   B02.POENGTILLEGG_KODE(PT_IND)     = 'A';                   00000890
                                                                        00000900
             IF PT_IND = 1                 THEN                         00000910
 /*1987 */      B02.POENGTILLEGG_KODE(2) = 'C';                         00000920
             ELSE                                                       00000930
 /* HL  */      B02.POENGTILLEGG_KODE(1) = 'C';                         00000940
                                                                        00000950
 /*28.11.86 HL FRAPP 270 - VI MÅ OPPBEVARE ALTERNATIV POENGREKKE */     00000960
 /*                        PÅ INDEKS 12 ELLER 13                 */     00000970
                                                                        00000980
        LOOPI = PT_IND + 11;                                            00000990
                                                                        00001000
        DO LOOPIN = PT_IND,LOOPI;                                       00001010
                                                                        00001020
             DO I = 67 TO 70;                                           00001030
                                                                        00001040
                IF POREKKE.POENG(LOOPIN,I) > 0 THEN                     00001050
                   DO;                                                  00001060
                      POREKKE.POENG(LOOPIN,I) =                         00001070
                              POREKKE.POENG(LOOPIN,I) + 1;              00001080
                                                                        00001090
                      IF POREKKE.POENG(LOOPIN,I) > 7 THEN               00001100
                         POREKKE.POENG(LOOPIN,I) = 7;                   00001110
                   END;                                                 00001120
                                                                        00001130
                IF POREKKE_P67.POENG(LOOPIN,I) > 0 THEN                 00001140
                                                                        00001150
                   DO;                                                  00001160
                                                                        00001170
                      POREKKE_P67.POENG(LOOPIN,I) =                     00001180
                                POREKKE_P67.POENG(LOOPIN,I) + 1;        00001190
                                                                        00001200
                      IF POREKKE_P67.POENG(LOOPIN,I) > 7 THEN           00001210
                         POREKKE_P67.POENG(LOOPIN,I) = 7;               00001220
                   END;                                                 00001230
             END;    /* 67   TIL  70  */                                00001240
                                                                        00001250
             DO I = 71 TO 150;                                          00001260
                                                                        00001270
                IF POREKKE.POENG(LOOPIN,I) > 0 THEN                     00001280
                   DO;                                                  00001290
                      POREKKE.POENG(LOOPIN,I) =                         00001300
                                 POREKKE.POENG(LOOPIN,I) + 1;           00001310
                                                                        00001320
                      IF POREKKE.POENG(LOOPIN,I) > 8.33 THEN            00001330
                         POREKKE.POENG(LOOPIN,I) = 8.33;                00001340
                   END;                                                 00001350
                                                                        00001360
                IF POREKKE_P67.POENG(LOOPIN,I) > 0 THEN                 00001370
                                                                        00001380
                   DO;                                                  00001390
                                                                        00001400
                      POREKKE_P67.POENG(LOOPIN,I) =                     00001410
                                 POREKKE_P67.POENG(LOOPIN,I) + 1;       00001420
                                                                        00001430
                      IF POREKKE_P67.POENG(LOOPIN,I) > 8.33 THEN        00001440
                         POREKKE_P67.POENG(LOOPIN,I) = 8.33;            00001450
                   END;                                                 00001460
             END;  /*  71  TIL  150  */                                 00001470
                                                                        00001480
           END; /* LOOPIN = PT_IND,LOOPI        */                      00001490
                                                                        00001500
          END;     /*  PÅ ^= PÅ_EK    */                                00001510
                                                                        00001520
                                                                        00001530
      ELSE  /*  PÅ = PÅ_EK   */                                         00001540
                                                                        00001550
  /*  BEGGE FÅR POENGTILLEGG DVS. 0,5 POENG KODE B           */         00001560
                                                                        00001570
         DO;                                                            00001580
                                                                        00001590
 /*190387*/ B02.POENGTILLEGG_KODE(BER_SØ_IND) = 'B';                    00001600
 /* HL   */ B02.POENGTILLEGG_KODE(BER_EK_IND) = 'B';                    00001610
                                                                        00001620
                                                                        00001630
 /*28.11.86 HL - FRAPP 270 - VI Må OPPBEVARE POENG MED PT */            00001640
 /*                          På PLASS 12 OG 13 I POREKKEN */            00001650
                                                                        00001660
         DO LOOPIN = 1,2,12,13;                                         00001670
                                                                        00001680
                                                                        00001690
            DO I = 67 TO 70;                                            00001700
                                                                        00001710
               IF POREKKE.POENG(LOOPIN,I) > 0 THEN                      00001720
                                                                        00001730
                  DO;                                                   00001740
                     POREKKE.POENG(LOOPIN,I) =                          00001750
                                  POREKKE.POENG(LOOPIN,I) + 0.5;        00001760
                                                                        00001770
                     IF POREKKE.POENG(LOOPIN,I) > 7 THEN                00001780
                        POREKKE.POENG(LOOPIN,I) = 7;                    00001790
                  END;                                                  00001800
                                                                        00001810
               IF POREKKE_P67.POENG(LOOPIN,I) > 0 THEN                  00001820
                                                                        00001830
                  DO;                                                   00001840
                                                                        00001850
                     POREKKE_P67.POENG(LOOPIN,I) =                      00001860
                                POREKKE_P67.POENG(LOOPIN,I) + 0.5;      00001870
                                                                        00001880
                     IF POREKKE_P67.POENG(LOOPIN,I) > 7 THEN            00001890
                        POREKKE_P67.POENG(LOOPIN,I) = 7;                00001900
                  END;                                                  00001910
                                                                        00001920
            END;          /*  67  TIL  70  */                           00001930
                                                                        00001940
            DO I = 71 TO 150;                                           00001950
                                                                        00001960
               IF POREKKE.POENG(LOOPIN,I) > 0 THEN                      00001970
                                                                        00001980
                  DO;                                                   00001990
                                                                        00002000
                     POREKKE.POENG(LOOPIN,I) =                          00002010
                                  POREKKE.POENG(LOOPIN,I) + 0.5;        00002020
                                                                        00002030
                     IF POREKKE.POENG(LOOPIN,I) > 8.33 THEN             00002040
                        POREKKE.POENG(LOOPIN,I) = 8.33;                 00002050
                  END;                                                  00002060
                                                                        00002070
               IF POREKKE_P67.POENG(LOOPIN,I) > 0 THEN                  00002080
                                                                        00002090
                  DO;                                                   00002100
                     POREKKE_P67.POENG(LOOPIN,I) =                      00002110
                                 POREKKE_P67.POENG(LOOPIN,I) + 0.5;     00002120
                                                                        00002130
                     IF POREKKE_P67.POENG(LOOPIN,I) > 8.33              00002140
                                                               THEN     00002150
                        POREKKE_P67.POENG(LOOPIN,I) = 8.33;             00002160
                  END;                                                  00002170
                                                                        00002180
            END;     /*  71  TIL   150   */                             00002190
                                                                        00002200
          END;       /* LOOPIN = 1,2,12,13 */ /* 28.11.86 HL */         00002210
                                                                        00002220
         END;        /* PÅ  = PÅ_EK  */                                 00002230
                                                                        00002240
    END;             /* POENGTILLEGG   */                               00002250
                                                                        00002260
 ELSE       /*    IKKE POENGTILLEGG      */                             00002270
                                                                        00002280
    DO;                                                                 00002290
       B02.POENGTILLEGG_DATO_ÅM(BER_SØ_IND) = 0;                        00002300
       B02.POENGTILLEGG_KODE   (BER_SØ_IND) = ' ';  /*19.3.87*/         00002310
       B02.POENGTILLEGG_DATO_ÅM(BER_EK_IND) = 0;                        00002320
       B02.POENGTILLEGG_KODE   (BER_EK_IND) = ' ';  /* HL    */         00002330
    END;                                                                00002340
                                                                        00002350
 END POENG_TILLEGG;                                                     00002360
