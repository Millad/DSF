 /*       SIST ENDRET 26/11-97 14.30.17 AV   TSB7339                  */00000000
 /*       SIST ENDRET 26/11-97 14.28.55 AV   TSB7339                  */00000010
 /*       SIST ENDRET 09/06-94 12.40.58 AV   DYBVIK                   */00000020
 /*       SIST ENDRET 01/12-89 08.47.42 AV   DYBVIK                   */00000030
 /*       SIST ENDRET 11/10-89          AV   TUYEN                    */00000040
 /*           ENDRING BESTÅR I Å LEGGE UT TIL EN ANNEN VSAMFILE       */00000050
 /*           FNR, TKNR OG DATO + TID DERSOM OPPGITT TKNR ER ET       */00000060
 /*           INFO-KONTOR.                                            */00000070
 /*       SIST ENDRET 28/12-87 12.13.01 AV   DYBVIK                   */00000080
 /*       SIST ENDRET 23/06-87 12.00.14 AV   LUNDEBY                  */00000090
 /*       SIST ENDRET 12/07-85 07.42.02 AV   LUNDEBY                  */00000100
 /*       SIST ENDRET 15/01-85 15.54.24 AV   LUNDEBY                  */00000110
 /* ***************************************************************** */00000120
 /* IDENTIFIKASJON                                                    */00000130
 /*     R0019957 - P900_NOR_DATA         PROCEDURE I  PLI             */00000140
 /*                                                                   */00000150
 /*     PROGRAMMERER: S. LUNDEBY  FRA    DATA-UTVIKLING A/S  JAN 1985 */00000160
 /*                                                                   */00000170
 /* HENSIKT                                                           */00000180
 /*     SJEKKER OM OPPGITT TKNR ER ET NOR-KONTOR. ER DET TILFELLE     */00000190
 /*     SKRIVES FNR , TKNR OG DATO + TID UT PÅ EN VSAMFILE .          */00000200
 /*     DENNE BRUKES SAMMEN MED DE ANDRE SPØRRE-TRANSENE FRA NORTRYGD */00000210
 /*     OM KVELDEN.                                                   */00000220
 /*                                                                   */00000230
 /* PARAMETRE                                                         */00000240
 /*     FNR OG TKNR                                                   */00000250
 /*                                                                   */00000260
 /*     VED ONLINE INNTEKTS-OPPDATERING REG. KOMMUNENR. ISTEDENFOR    */00000270
 /*     TKNR. FOR NOEN FÅ KOMMUNER ER DISSE FORSKELLIGE. DE ER :      */00000280
 /*                                                                   */00000290
 /*        0703  HORTEN       GÅR   TIL   0701   HORTEN               */00000300
 /*        0717  BORRE            ------   "     ------               */00000310
 /*                                                                   */00000320
 /*        0705  TØNSBERG     GÅR   TIL   0704   TØNSBERG             */00000330
 /*        0721  SEM              ------   "     ------               */00000340
 /*                                                                   */00000350
 /*        0707  LARVIK       GÅR   TIL   0709   LARVIK               */00000360
 /*        0708  STAVERN          ------   "     ------               */00000370
 /*        0725  TJØLLING         ------   "     ------               */00000380
 /*        0726  BRUNLANES        ------   "     ------               */00000390
 /*        0727  HEDRUM           ------   "     ------               */00000400
 /*                                                                   */00000410
 /*                                                                   */00000420
 /*                                                                   */00000430
 /* PROGRAMTILKNYTNING                                                */00000440
 /*     INCLUDE MODUL.                                                */00000450
 /*                                                                   */00000460
 /* ***************************************************************** */00000470
                                                                        00000480
    P900_NOR_DATA:                                                      00000490
       PROC                (W900_FNR , W900_TKNR);                      00000500
                                                                        00000510
                                                                        00000520
       DCL W900_FNR         FIXED DEC (11) UNAL,                        00000530
           W900_TKNR        FIXED DEC ( 4) UNAL;                        00000540
                                                                        00000550
                                                                        00000560
      /*--------------------------------------------------------------*/00000570
      /*  RECORD FOR DATA TIL NORTRYGD OG FOR SJEKK OM NOR-SENTRAL    */00000580
      /*--------------------------------------------------------------*/00000590
                                                                        00000600
                                                                        00000610
       %INCLUDE P0019957;     /***                 NOR_REC          ***/00000620
                                                                        00000630
                                                                        00000640
       DCL 1 NOR_SENTRAL_REC                            ,               00000650
             2 TKNR         PIC      '( 4)9'  INIT ( 0 ),               00000660
             2 KD_SENTRAL   FIXED DEC ( 3)    INIT ( 0 ),               00000670
             2 NOR_SENTRAL  FIXED DEC ( 3)    INIT ( 0 ),               00000680
             2 FILLER1      CHAR      (31)    INIT ( '');               00000690
                                                                        00000700
                                                                        00000710
                                                                        00000720
      /*--------------------------------------------------------------*/00000730
      /*  DIV. HJELPE-OMRÅDER                                         */00000740
      /*--------------------------------------------------------------*/00000750
                                                                        00000760
                                                                        00000770
      DCL I                FIXED BIN (15)     INIT ( 0 ),               00000780
          NOR_REC_RBA      POINTER                      ,               00000790
         (DATE , STG)      BUILTIN                      ,               00000800
          TID              PIC      '( 4)9'   INIT ( 0 ),               00000810
          TID_TIM DEF TID  CHAR      ( 2)     POS  ( 1 ),               00000820
          TID_MIN DEF TID  CHAR      ( 2)     POS  ( 3 ),               00000830
          D                CHAR      ( 6)     INIT ( ''),               00000840
          D_ÅR    DEF D    CHAR      ( 2)     POS  ( 1 ),               00000850
          D_MND   DEF D    CHAR      ( 2)     POS  ( 3 ),               00000860
          D_DAG   DEF D    CHAR      ( 2)     POS  ( 5 ),               00000870
          KEY_FNR          PIC      '(11)9'   INIT ( 0 ),               00000880
          KEY_TKNR         PIC      '( 4)9'   INIT ( 0 );               00000890
                                                                        00000900
                                                                        00000910
                                                                        00000920
                                                                        00000930
      /* ************************************************************ */00000940
      /* *******    EKSEKVERING STARTER HER                ********** */00000950
      /* ************************************************************ */00000960
                                                                        00000970
      PROGRAM_ID              =        'R0019957'                     ; 00000980
                                                                        00000990
                                                                        00001000
      /*--------------------------------------------------------------*/00001010
      /*  ADDRESSERER EIB-OMR.                                        */00001020
      /*  SJEKKER OM DET ER ET NORTRYGD-KONTOR                        */00001030
      /*--------------------------------------------------------------*/00001040
                                                                        00001050
      EXEC CICS  ADDRESS EIB(DFHEIPTR)                                ; 00001060
                                                                        00001070
      SELECT   ( W900_TKNR   )                                        ; 00001080
                                                                        00001090
         WHEN  ( 0703 ! 0717 )                                          00001100
            W900_TKNR         =       0701                            ; 00001110
                                                                        00001120
         WHEN  ( 0705 ! 0721 )                                          00001130
            W900_TKNR         =       0704                            ; 00001140
                                                                        00001150
         WHEN  ( 0707 ! 0708 ! 0725 ! 0726 ! 0727 )                     00001160
            W900_TKNR         =       0709                            ; 00001170
                                                                        00001180
         OTHERWISE                                                    ; 00001190
      END;     /* SELECT   (W900_TKNR)                               */ 00001200
                                                                        00001210
                                                                        00001220
      KEY_TKNR                =          W900_TKNR                    ; 00001230
                                                                        00001240
      EXEC CICS HANDLE CONDITION ERROR  (L900)                        ; 00001250
      EXEC CICS HANDLE CONDITION NOTFND (ALT_OK)                      ; 00001260
      EXEC CICS READ   DATASET ('REGION') INTO   ( NOR_SENTRAL_REC )    00001270
                                          RIDFLD ( KEY_TKNR        )  ; 00001280
                                                                        00001290
                                                                        00001300
      D                 =  DATE                                       ; 00001310
      TID               =  EIBTIME / 100                              ; 00001320
                                                                        00001330
      NOR_REC           =  ''                                         ; 00001340
      NOR_REC.TKNR      =  W900_TKNR                                  ; 00001350
      NOR_REC.RECTYPE   = 'PO'                                        ; 00001360
      NOR_REC.TRANSTYPE =  05                                         ; 00001370
      NOR_REC.ÅR        =  D_ÅR                                       ; 00001380
      NOR_REC.MND       =  D_MND                                      ; 00001390
      NOR_REC.DAG       =  D_DAG                                      ; 00001400
      NOR_REC.TIM       =  TID_TIM                                    ; 00001410
      NOR_REC.MIN       =  TID_MIN                                    ; 00001420
      NOR_REC.FNR       =  W900_FNR                                   ; 00001430
      /*                                                                00001440
      IF NOR_SENTRAL          >  0                          THEN        00001450
         DO;                                                            00001460
            EXEC CICS HANDLE CONDITION ERROR  (L910)                  ; 00001470
            EXEC CICS HANDLE CONDITION DUPREC (ALT_OK)                ; 00001480
                                                                        00001490
                                                                        00001500
            KEY_FNR           =  W900_FNR                             ; 00001510
            EXEC CICS WRITE DATASET ('NORINFO'    ) FROM ( NOR_REC )    00001520
                            RIDFLD  ( KEY_FNR     )                   ; 00001530
                                                                        00001540
         END;                                                           00001550
                                                                        00001560
         ELSE DO;                                                       00001570
     */                                                                 00001580
            /*--------------------------------------------------------*/00001590
            /* DET VAR ET INFOTRYGD-KONTOR.  AKT. DATA SKRIVES        */00001600
            /*--------------------------------------------------------*/00001610
                                                                        00001620
            EXEC CICS HANDLE CONDITION ERROR  (L910)                  ; 00001630
            EXEC CICS HANDLE CONDITION DUPREC (ALT_OK)                ; 00001640
                                                                        00001650
                                                                        00001660
            KEY_FNR           =  W900_FNR                             ; 00001670
            EXEC CICS WRITE DATASET ('INFOINFO' ) FROM ( NOR_REC )      00001680
                            RIDFLD  ( KEY_FNR   )                   ;   00001690
                                                                        00001700
     /*  END; */                                                        00001710
                                                                        00001720
      GOTO ALT_OK;                                                      00001730
                                                                        00001740
  L900:                                                                 00001750
      FEIL_MELD_NR                =    503 ;                            00001760
      FEIL_VED_LABEL              =   '900';                            00001770
                                                                        00001780
      GOTO ALT_OK;                                                      00001790
                                                                        00001800
  L910:                                                                 00001810
      FEIL_MELD_NR                =    504 ;                            00001820
      FEIL_VED_LABEL              =   '910';                            00001830
                                                                        00001840
  ALT_OK:                                                               00001850
                                                                        00001860
                                                                        00001870
    END P900_NOR_DATA;                                                  00001880
