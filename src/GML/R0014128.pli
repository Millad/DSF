 /*       SIST ENDRET 19/05-98 13.01.34 AV   JDA7339                  */00000000
 /*       SIST ENDRET 19/05-98 12.48.43 AV   JDA7339                  */00000010
 /*       SIST ENDRET 19/03-98 10.58.06 AV   JDA7339                  */00000020
 /*       SIST ENDRET 19/03-98 10.54.54 AV   JDA7339                  */00000030
 /*       SIST ENDRET 22/11-94 15.25.06 AV   HLB0310                  */00000040
 /*       SIST ENDRET 17/02-92 13.44.26 AV   DYBVIK                   */00000050
 /*       SIST ENDRET 04/02-91 15.00.07 AV   DYBVIK                   */00000060
 /*       SIST ENDRET 11/12-90 13.21.56 AV   DYBVIK                   */00000070
 /*       SIST ENDRET 01/12-89 10.09.38 AV   DYBVIK                   */00000080
 /* ***************************************************************** */00000090
 /* IDENTIFIKASJON                                                    */00000100
 /*     R0014128 - BEREGN_POENG_AÅI_AVDØD                             */00000110
 /*     PROGRAMMERER: HERMAN LARSSEN                                  */00000120
 /* HENSIKT                                                           */00000130
 /*                                                                   */00000140
 /*  1. REGNER UT OG OPPDATERER POENGTALLET  YRKEHIST.PAÅ FOR AVDØDE  */00000150
 /*     (POENGTALLET UT FRA DEN ANTATTE ÅRLIGE ARBEIDSINNTEKTEN       */00000160
 /*      PÅ SKADETIDSPUNKTET),                                        */00000170
 /*      EVT BEREGNET UT FRA LOVREGLER FOR SPESIELLE YRKESGRUPPER.    */00000180
 /*                                                                   */00000190
 /*  2.  OPPDATERER EVT NY YRKEPENS.YUG                               */00000200
 /*                                                                   */00000210
 /*  3.  SETTER FORELØPIG YPT_KODE (KAN BLI ENDRET I ????)            */00000220
 /*                                                                   */00000230
 /*  4.  OPPDATERER YPT                                               */00000240
 /*                                                                   */00000250
 /* PROGRAMTILKNYTNING                                                */00000260
 /*     PROSEDYREN BLIR INKLUDERT I R0014101, 4301, 4501              */00000270
 /* ***************************************************************** */00000280
 BEREGN_POENG_AÅI_AVDØD:                                                00000290
  PROC;                                                                 00000300
                                                                        00000310
                                                                        00000320
    /*WORKING STORAGE*/                                                 00000330
                                                                        00000340
    DCL 1      W1,                                                      00000350
                                                                        00000360
          2    ALDER                    FIXED DEC     (5) INIT(0),      00000370
          2    FNR_PIC11                PIC '(11)9',                    00000380
          2    GAMMEL_GRAD              FIXED DEC     (3) INIT(0),      00000390
          2    GV                       FIXED DEC     (7) INIT(0),      00000400
          2    G_YST                    FIXED DEC     (7) INIT(0),      00000410
          2    INNTEKT                  FIXED DEC     (7) INIT(0),      00000420
          2    POENG                    FIXED DEC   (5,2) INIT(0),      00000430
          2    POENG92                  FIXED DEC   (5,2) INIT(0),      00000440
          2    POENG1                   FIXED DEC   (5,2) INIT(0),      00000450
          2    POENG2                   FIXED DEC   (5,2) INIT(0),      00000460
          2    POENG3                   FIXED DEC   (5,2) INIT(0),      00000470
          2    POENG_GRUNNLAG           FIXED DEC     (7) INIT(0),      00000480
          2    POENG_GRUNNLAG92         FIXED DEC     (7) INIT(0),      00000490
          2    PÅ_FØR92                 FIXED DEC     (3) INIT(0),      00000500
          2    SYTTI                    FIXED DEC     (3) INIT (70),    00000510
          2    NITTIEN                  FIXED DEC     (3) INIT (91),    00000520
          2    UFØRGRAD                 FIXED DEC     (3) INIT(0),      00000530
          2    YHIST_IND                FIXED BIN    (15),              00000540
          2    YPT_KODE                      CHAR     (1) INIT(' '),    00000550
          2    YST_ÅM                         PIC  '9999',              00000560
          2    YST_ÅMD                   FIXED DEC    (7),              00000570
          2    ÅRET                     FIXED DEC     (3);              00000580
                                                                        00000590
    DCL        YST_Å DEF W1.YST_ÅM POS (1)    PIC    '99';              00000600
                                                                        00000610
    DCL        YST_M DEF W1.YST_ÅM POS (3)    PIC    '99';              00000620
                                                                        00000630
    DCL        VIRK_ÅR_MND                  FIXED DEC (4);              00000640
    DCL        ATTEN                        FIXED DEC (5);              00000650
    DCL        NITTEN                       FIXED DEC (5);              00000660
    DCL        TYVE                         FIXED DEC (5);              00000670
    DCL        FNR_ÅR DEF FNR_PIC11 POS(5)         PIC '99';            00000680
    DCL        POENG_IND (3)                FIXED DEC (5,2);            00000690
                                                                        00000700
 /*PROGRAMMET BEGYNNER HER:*/                                           00000710
                                                                        00000720
  FNR_PIC11 = B02.FNR(BER_EK_IND);                                      00000730
                                                                        00000740
 /* ************************************************************ */     00000750
 /*   FINN INDEX FOR SISTE YRKEHIST (MED OPPHØRSDATO = DØDSM + 1 */     00000760
 /* ************************************************************ */     00000770
                                                                        00000780
  DO YHIST_IND = 1 TO 5 WHILE (B02.YRKEHIST.OPPH_DATO_ÅM                00000790
                                      (BER_EK_IND,YHIST_IND) > 0);      00000800
  END;                                                                  00000810
                                                                        00000820
  YHIST_IND = YHIST_IND -1 ;                                            00000830
                                                                        00000840
                                                                        00000850
 /* ************************************************************ */     00000860
 /*   FINN SISTE UFØREGRAD FØR DØDSFALLET                        */     00000870
 /* ************************************************************ */     00000880
                                                                        00000890
  DO I = 1 TO 7; /* 5 -> 7  9803 HL */                                  00000900
                                                                        00000910
     IF B02.UFØRHIST.OPPHØRSDATO_ÅM(BER_EK_IND,I) = 0 THEN              00000920
        I = 7;                                                          00000930
     ELSE                                                               00000940
     IF B02.UFØRHIST.OPPHØRSDATO_ÅM(BER_EK_IND,I) =                     00000950
          B02.YRKEHIST.OPPH_DATO_ÅM(BER_EK_IND,YHIST_IND)  THEN         00000960
             DO;                                                        00000970
                                                                        00000980
                /*AVDØDE VAR UFØR PÅ DØDSTIDSPUNKTET*/                  00000990
                                                                        00001000
                UFØRGRAD = B02.UFØRHIST.UFG(BER_EK_IND,I);              00001010
                DO J = 1 TO 7;                                          00001020
                   IF B02.UFØRGRAD.UFG_ØVRIGE(BER_EK_IND,I,J) = 0 THEN  00001030
                      J = 7;                                            00001040
                   ELSE                                                 00001050
                      UFØRGRAD = B02.UFG_ØVRIGE(BER_EK_IND,I,J);        00001060
                END;                                                    00001070
                I = 7;                                                  00001080
             END;                                                       00001090
  END;                                                                  00001100
                                                                        00001110
 /* ************************************************************ */     00001120
 /*   HVIS GAMMEL UFØRGRAD VAR 100, SKAL B02 IKKE OPPDATERES     */     00001130
 /* ************************************************************ */     00001140
                                                                        00001150
  IF UFØRGRAD  = 100             THEN                                   00001160
     GOTO RETUR;                                                        00001170
                                                                        00001180
 /* ************************************************************ */     00001190
 /*   OPPGRADERING AV YRKEPENS.YUG                               */     00001200
 /* ************************************************************ */     00001210
                                                                        00001220
 /*   B02.YRKEPENS.YUG(BER_EK_IND) = 100;     */                        00001230
 /*9804 HL */                                                           00001240
   B02.YRKEPENS.YUG(BER_EK_IND) = 100 - B02.UFØRPENS.UFG(BER_EK_IND);   00001250
                                                                        00001260
 /* ************************************************************ */     00001270
 /*   HVIS DET IKKE ER NY SKADE, SKAL IKKE PAÅ/YPT OPPDATERES    */     00001280
 /* ************************************************************ */     00001290
                                                                        00001300
  IF B02.YRKEHIST.YUG_ØVRIGE(BER_EK_IND,YHIST_IND,1) > 0   THEN         00001310
     GOTO RETUR;                                                        00001320
                                                                        00001330
 /* ************************************************************ */     00001340
 /* FINN GRUNNBELØP PÅ SKADETIDSPUNKT                            */     00001350
 /* ************************************************************ */     00001360
                                                                        00001370
                                                                        00001380
    W1.YST_ÅM = B02.YST_ÅM(BER_EK_IND,YHIST_IND);                       00001390
                                                                        00001400
    YST_ÅMD = YST_Å * 10000 + YST_M * 100 + 01;                         00001410
                                                                        00001420
    DO J = 1 TO 60;                                                     00001430
                                                                        00001440
       IF YST_ÅMD         >= PERIODE_START_ÅMD(J) &                     00001450
          YST_ÅMD         <= PERIODE_SLUTT_ÅMD(J) THEN                  00001460
                                                                        00001470
          DO;                                                           00001480
             G_YST             = G_TAB_PERIODE.GRUNNBELØP(J);           00001490
             J                 = 99;                                    00001500
          END;                                                          00001510
                                                                        00001520
    END;                                                                00001530
                                                                        00001540
  POENG_GRUNNLAG = B02.YRKEHIST.AÅI(BER_EK_IND,YHIST_IND);              00001550
  POENG_GRUNNLAG92 = POENG_GRUNNLAG;                                    00001560
                                                                        00001570
                                                                        00001580
  IF POENG_GRUNNLAG   > (G_YST * 12)      THEN                          00001590
     POENG_GRUNNLAG   = G_YST * 12;                                     00001600
                                                                        00001610
  IF POENG_GRUNNLAG   > (G_YST * 8)       THEN                          00001620
     POENG_GRUNNLAG   = (G_YST * 8) +                                   00001630
                        ((POENG_GRUNNLAG - (G_YST*8)) / 3) + 0.5;       00001640
                                                                        00001650
  IF POENG_GRUNNLAG   > G_YST   THEN                                    00001660
     POENG = (POENG_GRUNNLAG - G_YST) / G_YST + 0.005;                  00001670
  ELSE                                                                  00001680
     POENG = 0;                                                         00001690
                                                                        00001700
                                                                        00001710
    IF W1.YST_ÅM > 9200              THEN                               00001720
       DO;                                                              00001730
          IF FNR_ÅR > 40                THEN                            00001740
             B02.YRKEPENS.PÅ(BER_EK_IND) = 40;                          00001750
          ELSE                                                          00001760
             B02.YRKEPENS.PÅ(BER_EK_IND) = FNR_ÅR;                      00001770
                                                                        00001780
          IF FNR_ÅR > 75 THEN                                           00001790
             DO;                                                        00001800
                PÅ_FØR92 = 0;                                           00001810
                B02.YRKEPENS.PÅ_ETTER91(BER_EK_IND) = 40;               00001820
             END;                                                       00001830
          ELSE                                                          00001840
          DO;                                                           00001850
             PÅ_FØR92 = 75 - FNR_ÅR;                                    00001860
             IF PÅ_FØR92 > 25 THEN                                      00001870
                PÅ_FØR92 = 25;                                          00001880
             IF PÅ_FØR92 <  B02.YRKEPENS.PÅ(BER_EK_IND)    THEN         00001890
                B02.YRKEPENS.PÅ_ETTER91(BER_EK_IND) =                   00001900
                      B02.YRKEPENS.PÅ(BER_EK_IND) - PÅ_FØR92;           00001910
          END;                                                          00001920
                                                                        00001930
          IF POENG_GRUNNLAG92 > (G_YST * 12)      THEN                  00001940
             POENG_GRUNNLAG92 = G_YST * 12;                             00001950
                                                                        00001960
          IF POENG_GRUNNLAG92 > (G_YST * 6)       THEN                  00001970
             POENG_GRUNNLAG92 = (G_YST * 6) +                           00001980
            ((POENG_GRUNNLAG92 - (G_YST*6)) / 3) + 0.5;                 00001990
                                                                        00002000
          IF POENG_GRUNNLAG92 > G_YST   THEN                            00002010
             POENG92 = (POENG_GRUNNLAG92 - G_YST) / G_YST + 0.005;      00002020
          ELSE                                                          00002030
             POENG92 = 0;                                               00002040
                                                                        00002050
          IF PÅ_FØR92 < 20 THEN                                         00002060
             POENG = (POENG * PÅ_FØR92 + POENG92 * (20 - PÅ_FØR92))     00002070
                     / 20 + 0.005;                                      00002080
       END;                                                             00002090
                                                                        00002100
    W1.YPT_KODE  = 'A';                                                 00002110
                                                                        00002120
    SELECT (B02.YRKE11(BER_EK_IND,YHIST_IND));                          00002130
                                                                        00002140
       WHEN ('F')                                                       00002150
                                                                        00002160
    /*KAP 11-11 NR 1   FISKERE, FANGSTMENN MV  */                       00002170
                                                                        00002180
          DO;                                                           00002190
                                                                        00002200
             W1.YPT_KODE  = 'Y';                                        00002210
                                                                        00002220
     /* FISKERE FÅR ALLTID YPT_KODE = 'Y' - POENG SKAL BEREGNES */      00002230
     /* PÅ GRUNNLAG AV YRKE, OG IKKE PÅ GRUNNLAG AV AÅI         */      00002240
                                                                        00002250
             W1.YST_ÅM = B02.YRKEHIST.YST_ÅM(BER_EK_IND,YHIST_IND);     00002260
                                                                        00002270
 /* ************************************************************ */     00002280
 /* VI SKAL NÅ FINNE ET GJENNOMSNITT AV INNTEKTENE I DE SISTE    */     00002290
 /* 3 ÅR FØR SKADEÅRET.  FØR 1971 REGNES MED MAX 7.00 I POENG    */     00002300
 /* ************************************************************ */     00002310
                                                                        00002320
             DO I = 1 TO 3;                                             00002330
                POENG_IND (I) = 0;                                      00002340
                ÅRET    = YST_Å - I;                                    00002350
                                                                        00002360
                INNTEKT = B02.PI(BER_EK_IND,ÅRET) * 100;                00002370
                                                                        00002380
                GV      = GV_TAB_ARRAY.G_V(ÅRET);                       00002390
                                                                        00002400
                IF INNTEKT > (GV * 12)        THEN                      00002410
                                                                        00002420
                   INNTEKT = GV * 12;                                   00002430
                                                                        00002440
                IF INNTEKT > (GV * 6)  &                                00002450
                   ÅRET > NITTIEN          THEN                         00002460
                      INNTEKT = GV*6 + ((INNTEKT - (GV*6)) / 3) + 0.5;  00002470
                ELSE                                                    00002480
                IF INNTEKT > (GV * 8)         THEN                      00002490
                                                                        00002500
                   IF ÅRET > SYTTI            THEN                      00002510
                                                                        00002520
                      INNTEKT = GV*8 + ((INNTEKT - (GV*8)) / 3) + 0.5;  00002530
                                                                        00002540
                ELSE                                                    00002550
                   INNTEKT = GV * 8;                                    00002560
                                                                        00002570
                POENG_IND(I) = (INNTEKT - GV) / GV + 0.005;             00002580
                                                                        00002590
                IF POENG_IND (I) < 0 THEN POENG_IND(I) = 0;             00002600
                                                                        00002610
             END; /*DO I = 1 TO 3*/                                     00002620
                                                                        00002630
 /*          ÅRET = YST_Å - 2;                                          00002640
                                                                        00002650
             INNTEKT = B02.PI(BER_EK_IND,ÅRET) * 100;                   00002660
                                                                        00002670
             GV      = GV_TAB_ARRAY.G_V(ÅRET);                          00002680
                                                                        00002690
             IF INNTEKT > (GV * 12)        THEN                         00002700
                                                                        00002710
                INNTEKT = GV * 12;                                      00002720
                                                                        00002730
             IF INNTEKT > (GV * 8)         THEN                         00002740
                                                                        00002750
                IF ÅRET > SYTTI            THEN                         00002760
                                                                        00002770
                   INNTEKT = GV * 8 + ((INNTEKT - (GV*8)) / 3) + 0.5;   00002780
                                                                        00002790
                ELSE                                                    00002800
                                                                        00002810
                   INNTEKT = GV * 8;                                    00002820
                                                                        00002830
             POENG2 = (INNTEKT - GV) / GV + 0.005;                      00002840
                                                                        00002850
             IF POENG2 < 0 THEN POENG2 = 0;                             00002860
                                                                        00002870
             ÅRET = YST_Å - 3;                                          00002880
                                                                        00002890
             INNTEKT = B02.PI(BER_EK_IND,ÅRET) * 100;                   00002900
                                                                        00002910
             GV      = GV_TAB_ARRAY.G_V(ÅRET);                          00002920
                                                                        00002930
             IF INNTEKT > (GV * 12)        THEN                         00002940
                                                                        00002950
                INNTEKT = GV * 12;                                      00002960
                                                                        00002970
             IF INNTEKT > (GV * 8)         THEN                         00002980
                                                                        00002990
                IF ÅRET > SYTTI            THEN                         00003000
                                                                        00003010
                   INNTEKT = GV * 8 + ((INNTEKT - (GV*8)) / 3) + 0.5;   00003020
                                                                        00003030
                ELSE                                                    00003040
                                                                        00003050
                   INNTEKT = GV * 8;                                    00003060
                                                                        00003070
             POENG3 = (INNTEKT - GV) / GV + 0.005;                      00003080
                                                                        00003090
             IF POENG3 < 0 THEN POENG3 = 0;                             00003100
             POENG = (POENG1 + POENG2 + POENG3) / 3 + 0.005;   */       00003110
                                                                        00003120
             POENG = (POENG_IND (1)  + POENG_IND (2)                    00003130
                      + POENG_IND (3)) / 3 + 0.005;                     00003140
                                                                        00003150
                                                                        00003160
             IF POENG < 0.5   THEN                                      00003170
                                                                        00003180
                POENG = 0.5;                                            00003190
                                                                        00003200
                                                                        00003210
          END;                                                          00003220
                                                                        00003230
       WHEN ('M')                                                       00003240
                                                                        00003250
    /*KAP 11-11 NR 2   MILITÆRPERSON MV        */                       00003260
                                                                        00003270
          DO;                                                           00003280
                                                                        00003290
             IF POENG < 2 THEN                                          00003300
                                                                        00003310
                DO;                                                     00003320
                   POENG = 2;                                           00003330
                   W1.YPT_KODE  = 'Y';                                  00003340
                END;                                                    00003350
                                                                        00003360
          END;                                                          00003370
                                                                        00003380
       WHEN ('U','E')                                                   00003390
                                                                        00003400
    /*KAP 11-11 NR 3   UNGDOM, ELEV MV         */                       00003410
                                                                        00003420
          DO;                                                           00003430
                                                                        00003440
             IF POENG < 1 THEN                                          00003450
                                                                        00003460
                DO;                                                     00003470
                   POENG = 1;                                           00003480
                   W1.YPT_KODE  = 'Y';                                  00003490
                END;                                                    00003500
                                                                        00003510
          END;                                                          00003520
                                                                        00003530
       WHEN ('V')                                                       00003540
                                                                        00003550
    /*KAP 11-11 NR 3   ELEV UTEN YRKESUTDANNING   */                    00003560
                                                                        00003570
          DO;                                                           00003580
                                                                        00003590
 /*LOVENDRING FRA 1.1.91 HL ; */                                        00003600
                                                                        00003610
             VIRK_ÅR_MND = B02.VIRK_DATO_ÅM(BER_EK_IND);                00003620
                                                                        00003630
             IF VIRK_ÅR_MND > 9100      THEN                            00003640
                DO;                                                     00003650
                   ATTEN  = 1801;                                       00003660
                   NITTEN = 1901;                                       00003670
                   TYVE   = 2001;                                       00003680
                END;                                                    00003690
             ELSE                                                       00003700
                DO;                                                     00003710
                   ATTEN  = 1800;                                       00003720
                   NITTEN = 1900;                                       00003730
                   TYVE   = 2000;                                       00003740
                END;                                                    00003750
                                                                        00003760
             ALDER = F_ALDER(FNR_PIC11,F_SNU_DATO(VIRK_ÅR_MND));        00003770
                                                                        00003780
             IF ALDER < ATTEN      THEN;                                00003790
                                                                        00003800
             ELSE                                                       00003810
             IF ALDER < NITTEN  THEN                                    00003820
                DO;                                                     00003830
                   IF POENG  < 1  THEN                                  00003840
                                                                        00003850
                      DO;                                               00003860
                         POENG  = 1;                                    00003870
                         W1.YPT_KODE  = 'Y';                            00003880
                      END;                                              00003890
                END;                                                    00003900
                                                                        00003910
             ELSE                                                       00003920
             IF ALDER < TYVE  THEN                                      00003930
                DO;                                                     00003940
                   IF POENG  < 1.5  THEN                                00003950
                                                                        00003960
                      DO;                                               00003970
                         POENG  = 1.5;                                  00003980
                         W1.YPT_KODE  = 'Y';                            00003990
                      END;                                              00004000
                END;                                                    00004010
                                                                        00004020
             ELSE     /*ALDER ER NÅ OVER TYVE */                        00004030
                IF POENG  < 2  THEN                                     00004040
                   DO;                                                  00004050
                      POENG =  2;                                       00004060
                      W1.YPT_KODE = 'Y';                                00004070
                   END;                                                 00004080
          END;                                                          00004090
                                                                        00004100
       OTHERWISE;                                                       00004110
                                                                        00004120
          END;                                                          00004130
                                                                        00004140
       B02.PAÅ (BER_EK_IND,YHIST_IND) = POENG + 0.005;                  00004150
                                                                        00004160
  /* ************************************************************* */   00004170
  /* HVIS NYTT POENGTALL ER STØRST, SKAL OPPDATERING SKJE          */   00004180
  /* ************************************************************* */   00004190
                                                                        00004200
       IF B02.PAÅ(BER_EK_IND,YHIST_IND) >                               00004210
          B02.YRKEPENS.YPT(BER_EK_IND)            THEN                  00004220
          DO;                                                           00004230
             B02.YRKEPENS.YPT(BER_EK_IND) =                             00004240
                B02.PAÅ(BER_EK_IND,YHIST_IND);                          00004250
             B02.YRKEPENS.YPT_KODE (BER_EK_IND) = W1.YPT_KODE;          00004260
          END;                                                          00004270
                                                                        00004280
 RETUR:                                                                 00004290
                                                                        00004300
 END BEREGN_POENG_AÅI_AVDØD;                                            00004310
                                                                        00004320
