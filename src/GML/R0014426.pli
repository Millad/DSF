 /*       SIST ENDRET 02/06-89 15.41.58 AV   DYBVIK                   */00000000
 /*   */                                                                00000010
 /* ***************************************************************** */00000020
 /* IDENTIFIKASJON:                                                   */00000030
 /*     R0014426 - PROGRAM I PLI                                      */00000040
 /*     PROGRAMMERT AV TRUDE MAI 1989                                 */00000050
 /* HENSIKT:                                                          */00000060
 /*     PROGRAMMET SKAL KONTROLLERE AT ENGANGSBELØPET ETTER  KAP 11-7 */00000070
 /*     IKKE OVERSTIGER KAPITALVERDIEN .                              */00000080
 /*     TABELLEN OVER KAPITALISERINGSFAKTORENE LIGGER I P001KAPT.     */00000090
 /*     RETURNERER KAPITALISERINGSBELØPET,DERSOM DETTE ER STØRST.     */00000100
 /* BRUK:                                                             */00000110
 /*     FATOR_11_7   =    ANTALL GANGER ÅRSPENSJON                    */00000120
 /*     ALDER_I_MND  =    ALDER REGNET I MND TIL NÆRMESTE 1/4 ÅR      */00000130
 /*                                                                   */00000140
 /*                                                                   */00000150
 /* TRUDE, 16.05.89.                                                  */00000160
 /* ***************************************************************** */00000170
 %SKIP (2);                                                             00000180
 F_KAPFAKT:                                                             00000190
 PROC (W_FNR,W_SUM_YTELSE,W_VIRK_DATO_MÅ,W_YUG)RETURNS(FIXED DEC(7));   00000200
 %INCLUDE P0014426;                                                     00000210
      DCL                                                               00000220
         W_FAKTOR_11_7       PIC '9',                                   00000230
         W_FNR               PIC'(11)9',                                00000240
         W_SUM_YTELSE        FIXED DEC(5),                              00000250
         W_VIRK_DATO_MÅ      FIXED DEC(5),                              00000260
         W_YUG               FIXED DEC(3);                              00000270
                                                                        00000280
      DCL                                                               00000290
         W_ALDER             PIC'(4)9',        /*ÅR_MND*/               00000300
         W_KJØNN             CHAR(1);                                   00000310
                                                                        00000320
      DCL                                                               00000330
         1 W_ALDER_DEF       DEF   W_ALDER,                             00000340
           2 W_ÅR            PIC '99',                                  00000350
           2 W_MND           PIC '99';                                  00000360
                                                                        00000370
      DCL W_ANTALL_MND       FIXED DEC(5) INIT(0);                      00000380
      DCL W_KAPITALVERDI     FIXED DEC(7) INIT(0);                      00000390
                                                                        00000400
      DCL I,J                FIXED BIN(15);     /*INDEX*/               00000410
                                                                        00000420
                                                                        00000430
      /* ALDER PR VIRKNINGSDATO */                                      00000440
                                                                        00000450
      W_ALDER           = F_ALDER(W_FNR,W_VIRK_DATO_MÅ);    /*R0019905*/00000460
      W_ANTALL_MND      = (W_ÅR * 12) + W_MND;                          00000470
                                                                        00000480
      /* KJØNN. DET FINNES EN TABELL FOR KVINNER OG EN FOR MENN */      00000490
                                                                        00000500
      W_KJØNN           = F_KJØNN(W_FNR);                   /*R0019902*/00000510
                                                                        00000520
      IF (W_YUG         > 14     &                                      00000530
          W_YUG         < 20)    THEN                                   00000540
          W_FAKTOR_11_7 =  3;                                           00000550
                                                                        00000560
      IF (W_YUG         > 19     &                                      00000570
          W_YUG         < 25)    THEN                                   00000580
          W_FAKTOR_11_7 =  6;                                           00000590
                                                                        00000600
      IF (W_YUG         > 24     &                                      00000610
          W_YUG         < 30)    THEN                                   00000620
          W_FAKTOR_11_7 =  9;                                           00000630
                                                                        00000640
                                                                        00000650
      /* SKAL VI BRUKE KAPITALISERINGSFAKTOR ? */                       00000660
                                                                        00000670
      IF W_KJØNN        = 'M'     THEN                                  00000680
         DO;                                                            00000690
           IF ( W_FAKTOR_11_7     =  3   &                              00000700
                W_ANTALL_MND      > 764  !        /* 63 OG 3/4 ÅR */    00000710
                                                                        00000720
                W_FAKTOR_11_7     =  6   &                              00000730
                W_ANTALL_MND      > 707  !        /* 59        ÅR */    00000740
                                                                        00000750
                W_FAKTOR_11_7     =  9   &                              00000760
                W_ANTALL_MND      > 632 ) THEN    /* 52 OG 3/4 ÅR */    00000770
                                                                        00000780
                DO;     /* FINNE UT HVOR I TABELLEN VI SKAL BEGYNNE */  00000790
                                                                        00000800
                   IF W_FAKTOR_11_7    =   3  THEN                      00000810
                                   J    = 765 ;   /* 63 OG 3/4 ÅR */    00000820
                                                                        00000830
                   IF W_FAKTOR_11_7    =   6  THEN                      00000840
                                  J    = 708 ;   /* 59        ÅR */     00000850
                                                                        00000860
                   IF W_FAKTOR_11_7    =   9  THEN                      00000870
                                   J    = 633 ;   /* 52 OG 3/4 ÅR */    00000880
                                                                        00000890
                   DO I  = J TO 804  WHILE(W_ANTALL_MND > I);           00000900
                   END;                                                 00000910
                                                                        00000920
                   W_KAPITALVERDI = (W_SUM_YTELSE * 12                  00000930
                                     * KAP_FAKTOR_MENN(I) + 0.5);       00000940
                END;                                                    00000950
         END;                                                           00000960
                                                                        00000970
      ELSE            /* KVINNE */                                      00000980
                                                                        00000990
         DO;                                                            00001000
            IF (W_FAKTOR_11_7     =  3   &                              00001010
                W_ANTALL_MND      > 764) !        /* 63 OG 3/4 ÅR */    00001020
                                                                        00001030
               (W_FAKTOR_11_7     =  6   &                              00001040
                W_ANTALL_MND      > 713) !        /* 59 OG 1/2 ÅR */    00001050
                                                                        00001060
               (W_FAKTOR_11_7     =  9   &                              00001070
                W_ANTALL_MND      > 644) THEN     /* 53 OG 3/4 ÅR */    00001080
                                                                        00001090
                DO;     /* FINNE UT HVOR I TABELLEN VI SKAL BEGYNNE */  00001100
                                                                        00001110
                   IF W_FAKTOR_11_7    =   3  THEN                      00001120
                                   J    = 765 ;   /* 63 OG 3/4 ÅR */    00001130
                                                                        00001140
                   IF W_FAKTOR_11_7    =   6  THEN                      00001150
                                   J    = 714 ;   /* 59 OG 1/2 ÅR */    00001160
                                                                        00001170
                   IF W_FAKTOR_11_7    =   9  THEN                      00001180
                                   J    = 645 ;   /* 53 OG 3/4 ÅR */    00001190
                                                                        00001200
                   DO I  = J TO 804  WHILE(W_ANTALL_MND > I);           00001210
                   END;                                                 00001220
                                                                        00001230
                   W_KAPITALVERDI = ( W_SUM_YTELSE * 12                 00001240
                                     * KAP_FAKTOR_KVINNER(I) + 0.5);    00001250
                END;                                                    00001260
         END;                                                           00001270
      RETURN(W_KAPITALVERDI) ;                                          00001280
 END F_KAPFAKT;                                                         00001290
