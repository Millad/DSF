 /*       SIST ENDRET 26/04-91 12.38.32 AV   DYBVIK                   */00000000
 /*       SIST ENDRET 14/04-89 15.05.59 AV   DYBVIK                   */00000010
 /*       SIST ENDRET 24/10-84 10.15.49 AV   LUNDEBY                  */00000020
 /*       SIST ENDRET 16/10-84 08.09.16 AV   LUNDEBY                  */00000030
 /*       SIST ENDRET 02/10-84 13.22.13 AV   LUNDEBY                  */00000040
 /*       SIST ENDRET 15/09-83 14.47.42 AV   LUNDEBY                  */00000050
 /*       SIST ENDRET 01/09-83 11.42.06 AV   LUNDEBY                  */00000060
                                                                        00000070
                                                                        00000080
 /* ***************************************************************** */00000090
 /*IDENTIFIKASJON:                                                    */00000100
 /*    R0019928 - HOVEDPROGRAM I PLI                                  */00000110
 /*    PROGRAMMERER: KRISTENSEN, MARS 1982                            */00000120
 /*    PROGRAMMET ER TOTALT ENDRET AV HANSEN & FARVIK OKT -82.        */00000130
 /*HENSIKT:                                                           */00000140
 /*    PROGRAMMET INITIERER ET ELEMENT AV B01 OG LINKER INN DE        */00000150
 /*    AKTUELLE PROGRAM SOM LESER INFORMASJON FRA BASEN OG INN I B01, */00000160
 /*    STATUS FOR PERSON BLIR OPPRETTET.                              */00000170
 /*PROGRAMTILKNYTNING:                                                */00000180
 /*    PROGRAMMET BLIR LINKET .                                         00000190
 /*BRUK:                                                              */00000200
 /*    EXEC CICS LINK PROGRAM('R0019928') COMMAREA(KOM_OMR);          */00000210
 /* ***************************************************************** */00000220
                                                                        00000230
 R001992: PROC(COMMAREA_PEKER) OPTIONS (MAIN);                          00000240
                                                                        00000250
                                                                        00000260
 DCL                                                                    00000270
   1 B01  BASED                     (B01_PEKER),                        00000280
 %INCLUDE P0019921;                 /* B01-OMRÅDE (BASED)           */  00000290
                                                                        00000300
 %INCLUDE P0019906;                 /* TRANS_OPPL_OMR (BASED)       */  00000310
                                                                        00000320
 %INCLUDE P0019908;                 /* KOM-OMRÅDE (BASED)           */  00000330
                                                                        00000340
 DCL                                                                    00000350
   1 TRANS_LISTE_OMR  UNAL   BASED  (TRANS_LISTE_PEKER),                00000360
     3  TRANS_LISTE_LINJE (5),                                          00000370
 %INCLUDE P0019911;                 /* TRANS-LISTE-OMRÅDE UNDERNIVÅ */  00000380
                                                                        00000390
 DCL                                                                    00000400
   1 WORK_TRANS_LISTE UNAL BASED (WORK_TRANS_LISTE_PEKER),              00000410
 %INCLUDE P0019911;  /* TRANS-LISTE-OMRÅDE UNDERNIVÅ)*/                 00000420
                                                                        00000430
 %INCLUDE P0019912;                 /* DIV-PARAM-OMRÅDE (BASED)     */  00000440
                                                                        00000450
                                                                        00000460
 DCL                                                                    00000470
    (ADDR,CSTG,VERIFY)          BUILTIN;                                00000480
                                                                        00000490
 DCL                                                                    00000500
   COMMAREA_PEKER               PTR;                                    00000510
                                                                        00000520
 DCL                                                                    00000530
   W_TRANS_IKKE_FUNNET          BIT       (1)     INIT  ('1'B);         00000540
                                                                        00000550
 DCL                                                                    00000560
   (I,J,B01_IND,FORSØRGER_POS_I_B01,T_IND)                              00000570
                                                                        00000580
                                FIXED BIN (15)    INIT  ( 0 ) ;         00000590
                                                                        00000600
                                                                        00000610
                                                                        00000620
 /* *************************************************************/      00000630
 /* ****                                                  ***** */      00000640
 /* ****         EKSEKVERING  STARTER                     ***** */      00000650
 /* ****                                                  ***** */      00000660
 /* *************************************************************/      00000670
                                                                        00000680
                                                                        00000690
   PROGRAM_ID                      =   'R0019928'                 ;     00000700
                                                                        00000710
   B01.PERSON (POS_I_B01)          =   ''                         ;     00000720
   B01_IND                         =   POS_I_B01                  ;     00000730
                                                                        00000740
   STATUS_TYPE                     =   ' '                        ;     00000750
   ØNSKET_STATUS                   =   ' '                        ;     00000760
   DIV_PARAM_OMR.TRANS_VIRKDATO_ÅM =   F_SNU_DATO (TRANS_OPPL_OMR.      00000770
                                                   VIRKNINGSDATO) ;     00000780
                                                                        00000790
   IF ANTALL_TRANS_I_TRANS_LISTE   >   1    THEN                        00000800
      T_IND                        = ANTALL_TRANS_I_TRANS_LISTE + 1;    00000810
   ELSE                                                                 00000820
      T_IND                        =   2                          ;     00000830
                                                                        00000840
                                                                        00000850
   EXEC CICS LINK PROGRAM('R0013110') COMMAREA(KOM_OMR)           ;     00000860
                                                                        00000870
   IF FEIL_MELD_NR > 0 THEN                                             00000880
      GO TO L999;                                                       00000890
   ELSE                                                                 00000900
      PROGRAM_ID         =   'R0019928'        ;                        00000910
                                                                        00000920
                                                                        00000930
   SELECT  (STATUS_TYPE);                                               00000940
                                                                        00000950
      WHEN ('B')                                                        00000960
         DO;                                                            00000970
            DO      I =  3 TO 14 UNTIL (B01.RF0PERSN.FNR(I) = 0);       00000980
            END;                                                        00000990
                                                                        00001000
                                                                        00001010
            IF      I > 14 THEN                                         00001020
                    I = 14     ;                                        00001030
            POS_I_B01 =  I     ;                                        00001040
                                                                        00001050
                                                                        00001060
            /*------------------------------------------------*/        00001070
            /*  SJEKKER OM TRANSEN SOM GIR STATUS = FORSØRGET */        00001080
            /*  BARN ER INNLEST . ER DET TILFELLE BLANKES     */        00001090
            /*  B01 OG STATUS_TYPE ENDRES FRA B TIL A .       */        00001100
            /*------------------------------------------------*/        00001110
                                                                        00001120
            IF F010_FJERN_STATUS                     THEN               00001130
               DO;                                                      00001140
                  B01.RF0PERSN (B01_IND)  = ''                  ;       00001150
                  STATUS_TYPE             = 'A'                 ;       00001160
               END;                                                     00001170
            ELSE                                                        00001180
               DO J=1 TO 13 WHILE (B01.FNR_TILKN (B01_IND,J) > 0);      00001190
                                                                        00001200
                  DIV_PARAM_OMR.ØNSKET_STATUS = B01.STATUS_KODE         00001210
                                                   (B01_IND)     ;      00001220
                                                                        00001230
                  IF POS_I_B01          >   14               THEN       00001240
                     DO;                                                00001250
 L100:                  FEIL_VED_LABEL  =  'L100';                      00001260
                        FEIL_MELD_NR    =   214  ;                      00001270
                        GO   TO   L999;                                 00001280
                     END;                                               00001290
                  ELSE                                                  00001300
               IF B01.TILKN.TILKNYTNINGSKODE(B01_IND,J) = 'L'  !        00001310
  /*HL*/          B01.TILKN.TILKNYTNINGSKODE(B01_IND,J) = 'V'  !        00001320
                  B01.TILKN.TILKNYTNINGSKODE(B01_IND,J) = 'W'  THEN     00001330
                                                                        00001340
                        DO;                                             00001350
                          DIV_PARAM_OMR.SEARCH_FNR = B01.FNR_TILKN      00001360
                                                    (B01_IND,J);        00001370
                                                                        00001380
                          EXEC CICS LINK PROGRAM  ('R0013110')          00001390
                                         COMMAREA ( KOM_OMR  ) ;        00001400
                                                                        00001410
                          IF FEIL_MELD_NR > 0 THEN                      00001420
                             GO TO L999;                                00001430
                          ELSE                                          00001440
                             DO;                                        00001450
                                PROGRAM_ID  =  'R0019928'    ;          00001460
                                POS_I_B01   =   POS_I_B01 + 1;          00001470
                             END;                                       00001480
                        END;                                            00001490
                                                                        00001500
               END;     /**    DO   J = 1 TO 13   **/                   00001510
                                                                        00001520
         END;           /**    WHEN ('B')         **/                   00001530
                                                                        00001540
      WHEN ('C')                                                        00001550
                                                                        00001560
         /* ********************************************* */            00001570
         /* VTP.TRANS   <   VTP.SS       INGEN  NS        */            00001580
         /*                                               */            00001590
         /* LESER TRANS HOS SØKER .                       */            00001600
         /* ********************************************* */            00001610
         DO;                                                            00001620
            CALL P910_LES_FORRIGE_TRANS;                                00001630
                                                                        00001640
            IF   FEIL_MELD_NR >  0 THEN                                 00001650
                 GO TO L999;                                            00001660
            ELSE                                                        00001670
                 PROGRAM_ID   =  'R0019928';                            00001680
                                                                        00001690
            /******************************************** */            00001700
            /* SJEKKER OM TRANS ER FUNNET                 */            00001710
            /******************************************** */            00001720
                                                                        00001730
            IF TRANS_LISTE_LINJE.VIRKNINGSDATO(T_IND) = 0 THEN          00001740
               TRANS_LISTE_LINJE.FØDSNUMMER   (T_IND) = 0    ;          00001750
            ELSE                                                        00001760
               ANTALL_TRANS_I_TRANS_LISTE             = T_IND;          00001770
                                                                        00001780
                                                                        00001790
            /******************************************** */            00001800
            /* SORTERER RESTEN AV TRANSLISTE PÅ           */            00001810
            /* VIRKNINGSDATO OG PRIORITETSKODE            */            00001820
            /******************************************** */            00001830
                                                                        00001840
            CALL P_SORTER_TRANS_LISTE_OMR ( T_IND );                    00001850
                                                                        00001860
         END;  /* WHEN C               */                               00001870
                                                                        00001880
      WHEN ('F')                                                        00001890
         DO;                                                            00001900
            /*-------------------------------------------------*/       00001910
            /*  LESER SISTE STATUS FOR Å SJEKKE OM DET ER ET   */       00001920
            /*  FORSØRGET BARN OG OM DEN ER DANNET AV EN AV    */       00001930
            /*  TRANSENE I TRANS_LISTE                         */       00001940
            /*-------------------------------------------------*/       00001950
                                                                        00001960
            STATUS_TYPE             =   ' '                     ;       00001970
            ØNSKET_STATUS           =   'S'                     ;       00001980
                                                                        00001990
                                                                        00002000
            EXEC CICS LINK PROGRAM('R0013110') COMMAREA(KOM_OMR);       00002010
                                                                        00002020
            /*-------------------------------------------------*/       00002030
            /*  SJEKKER OM TRANSEN SOM GIR STATUS = FORSØRGET  */       00002040
            /*  BARN ER I TRANS_LISTE. ER DET TILFELLE BLANKES */       00002050
            /*  B01 OG STATUS_TYPE ENDRES FRA F TIL A .        */       00002060
            /*-------------------------------------------------*/       00002070
                                                                        00002080
            STATUS_TYPE                   = 'F'                 ;       00002090
                                                                        00002100
            IF F010_FJERN_STATUS                     THEN               00002110
               DO;                                                      00002120
                  B01.RF0PERSN (B01_IND)  = ''                  ;       00002130
                  STATUS_TYPE             = 'A'                 ;       00002140
               END;                                                     00002150
                                                                        00002160
         END;           /**    WHEN ('F')         **/                   00002170
                                                                        00002180
      OTHERWISE                                                         00002190
                                                                        00002200
         IF ^(STATUS_TYPE   =   'A'         !                           00002210
              STATUS_TYPE   =   'B'         !                           00002220
              STATUS_TYPE   =   'C'         !                           00002230
              STATUS_TYPE   =   'I'         )  THEN                     00002240
                                                                        00002250
              STATUS_TYPE   =   'F'               ;                     00002260
                                                                        00002270
                                                                        00002280
   END;    /* SELECT (STATUS_TYPE)                    */                00002290
                                                                        00002300
                                                                        00002310
 L999:                                                                  00002320
   EXEC CICS RETURN;                                                    00002330
                                                                        00002340
                                                                        00002350
                                                                        00002360
                                                                        00002370
   /* ************************************************************ */   00002380
   /*      I N T E R N E   P R O C E D U R E R                     */   00002390
   /* ************************************************************ */   00002400
                                                                        00002410
                                                                        00002420
                                                                        00002430
   /*--------------------------------------------------------------*/   00002440
   /* HER SJEKKES OM LEST PERSON ER :                              */   00002450
   /*                                                              */   00002460
   /*   FORSØRGET BARN    &  VTP I TRANS = VTP I STATUS  &         */   00002470
   /*   FLERE TRANSER LEST INN  &  SØKER = TILKN. TIL LEST PERSON  */   00002480
   /*                                                              */   00002490
   /*   HVIS STATUS_TYPE =  B / F SJEKKES OM TRANSEN, SOM GA       */   00002500
   /*   STATUS  =  FORSØRGET BARN, ER LEST INN .                   */   00002510
   /*--------------------------------------------------------------*/   00002520
                                                                        00002530
   F010_FJERN_STATUS:                                                   00002540
     PROC  RETURNS   ( BIT (1) )                                    ;   00002550
                                                                        00002560
     DCL RETUR         BIT (1)            INIT ('0'B)               ,   00002570
     W010_VTP    FIXED DEC (4)            INIT ( 0  )               ;   00002580
                                                                        00002590
                                                                        00002600
                                                                        00002610
     W010_VTP                           =   F_SNU_DATO (TRANS_OPPL_OMR. 00002620
                                                        VIRKNINGSDATO); 00002630
                                                                        00002640
     IF B01.PENSJONSTYPE1   (B01_IND  ) = 'L'                         & 00002650
                                                                        00002660
      ((STATUS_TYPE                     = 'B'                    &      00002670
        B01.VIRK_DATO_ÅM    (B01_IND  ) = W010_VTP                )!    00002680
                                                                        00002690
       (STATUS_TYPE                     = 'F'                    &      00002700
        B01.FØRSTE_GANG_REG (B01_IND)   = 'N'                    &      00002710
        B01.VIRK_DATO_ÅM    (B01_IND)   > W010_VTP                ) ) & 00002720
                                                                        00002730
        B01.FNR_TILKN       (B01_IND,1) = TRANS_OPPL_OMR.FØDSNUMMER   & 00002740
                                                                        00002750
        ANTALL_TRANS_I_TRANS_LISTE      > 1                        THEN 00002760
                                                                        00002770
                                                                        00002780
        RETUR                           = '1'B                        ; 00002790
                                                                        00002800
                                                                        00002810
     RETURN (RETUR);                                                    00002820
                                                                        00002830
   END F010_FJERN_STATUS;                                               00002840
                                                                        00002850
                                                                        00002860
                                                                        00002870
                                                                        00002880
                                                                        00002890
 % INCLUDE R0019902;   /*    FINNE KJØNN             */                 00002900
                                                                        00002910
 % INCLUDE R0019913;   /*    SNUR  DATO (PIC(4))     */                 00002920
                                                                        00002930
 % INCLUDE R0019950;   /*    P_SORTER_TRANS_LISTE_OMR (1)  */           00002940
                                                                        00002950
 % INCLUDE R0019952;   /*    P910_LES_FORRIGE_TRANS  */                 00002960
                                                                        00002970
 END R001992;                                                           00002980
