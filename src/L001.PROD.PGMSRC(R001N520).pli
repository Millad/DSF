 /*   SIST ENDRET PÅ PROD   2005.08.30  8.53.50 AV   TSB2970          */        
 /*   SIST ENDRET PÅ TEST   2005.08.16 13.43.01 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2005.05.31 12.15.33 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2004.03.02 11.38.02 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.02.11 11.25.42 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2002.10.23 13.57.47 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.07.18 12.37.46 AV   HLA2970          */        
 /*   SIST ENDRET PÅ PROD   2000.10.02 10.35.51 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.07.07 13.04.44 AV   JDA7339          */        
 /*       SIST ENDRET 07/05-98 14.13.52 AV   JDA7339                  */        
 /*       SIST ENDRET 05/05-98 14.20.07 AV   SPA7339                  */        
 /*       SIST ENDRET 11/03-98 13.56.18 AV   RFA9991                  */        
 /* **************************************************************** */         
 /*IDENTIFIKASJON:                                                   */         
 /*    R001N520 - PROSEDYRE I PLI   - HOVEDPROGRAM                   */         
 /*                                                                  */         
 /* **************************************************************** */         
 /* **************************************************************** */         
 /*HENSIKT:                                                          */         
 /*    KONTROLLERE A1TRANS MOT STATUS FAMILIEFORHOLD.                */         
 /*    RUTINEN KONTROLLERER ALLE PERSONER I TRANSAKSJONEN MOT        */         
 /*    INFORMASJON I REGISTERET. FOR HVER FAMILIEPERSON SOM GÅR      */         
 /*    FEILFRI GJENNOM KONTROLLENE BLIR DET OPPRETTET NY STATUS      */         
 /*    FOR PERSONEN, OG SØKERENS STATUS OPPDATERES.                  */         
 /*PROGRAMTILKNYTNING:                                               */         
 /*    KALLES OPP AV PROGRAM R0013520                                */         
 /*BRUK:                                                             */         
 /*    EXEC CICS XCTL PROGRAM ('R001N520') COMMAREA (KOM_OMR)        */         
 /*                                                                  */         
 /* **************************************************************** */         
 KON_A1:PROC (COMMAREA_PEKER) OPTIONS (MAIN);                                   
      %INCLUDE P0019908;                                                        
      %INCLUDE P0019906;                                                        
      %INCLUDE P001N501;                                                        
      %INCLUDE P0019910;                                                        
      %INCLUDE P0019912;                                                        
      %INCLUDE P0019924;  /*GRUNNBELØP  VEIET GJENNOMSNITT*/                    
      DCL 1 B01 BASED(B01_PEKER), %INCLUDE P0019921;                            
      DCL 1 B02 BASED(B02_PEKER), %INCLUDE P0019921;                            
      DCL  COMMAREA_PEKER PTR;                                                  
   DCL                                                                          
      BARN_IND            FIXED BIN          (15),                              
      IND                 FIXED BIN          (15),                              
      I                   FIXED BIN          (15),                              
      J                   FIXED BIN          (15),                              
      (ADDR,CSTG,MAX)     BUILTIN,                                              
      X_DATO              PIC   '(8)9',                                         
      DATO_ÅMD            PIC   '(8)9',                                         
      DAGENS_DATO_ÅMD DEF DATO_ÅMD POS(1) PIC '99999999',                       
      DAGENS_DATO_DAG DEF DATO_ÅMD POS(7) PIC '99',                             
      DAGENS_DATO_1MND DEF DATO_ÅMD POS(5) PIC'99',                             
      DAGENS_DATO_1ÅR  DEF DATO_ÅMD POS(1) PIC'9999',                           
      TILLEGG             PIC                  '9',                             
      SISTE_UTTAKSDATO_ÅMD DEC FIXED            (8),                            
      HJ_VIRK_DATO_ÅMD     PIC               '(8)9';                            
   DCL                                                                          
      FNR11               PIC              '(11)9';                             
   DCL                                                                          
      FNR13               PIC              '(13)9';                             
   DCL                                                                          
      1 NR DEF FNR13,                                                           
         2 DG             PIC                 '99',                             
         2 MN             PIC                 '99',                             
         2 ÅR             PIC                 '9999',                           
         2 PERSNR,                                                              
            3 ÅRHUNDRE    PIC                '999',                             
            3 REST        PIC                 '99';                             
   DCL                                                                          
      SEKSTISJU_ÅRSDATO_ÅMD   PIC               '(8)9';                         
   DCL                                                                          
      SEKSTISJU_ÅR DEF  SEKSTISJU_ÅRSDATO_ÅMD POS(1)  PIC     '(4)9',           
      SEKSTISJU_MN DEF  SEKSTISJU_ÅRSDATO_ÅMD POS(5)  PIC     '(2)9';           
   DCL                                                                          
      STATUS_TYPE_SØKER   CHAR(1);                                              
   DCL REGÅR                    FIXED DEC (5) INIT (0);                         
   DCL POENGÅR                  FIXED DEC (5) INIT (0);                         
                                                                                
 /* **************************************************************** */         
 /* PROGRAMMET STARTER HER.                                          */         
 /* **************************************************************** */         
      IF FEIL_MELD_NR    =   0  THEN /*AB 10.09.01 */                           
      PROGRAM_ID = 'R001N520';                                                  
      IND        = 0;                                                           
      FNR11           = A1S.FNR;                                                
      HJ_VIRK_DATO_ÅMD = (TRANS_OMR.VIRK_DATO_ÅMD);                             
                                                                                
 /* **************************************************************** */         
 /*  REGNER UT ÅR OG MND VEDKOMMENDE FYLLER 67 ÅR (ÅÅMM). SIFFER     */         
 /*  7-9 ANGIR ÅRHUNDRE: 1800-TALLET 500-749, 1900-TALLET 000-499    */         
 /* **************************************************************** */         
      FNR13 = KONV_FNR11_FNR13(FNR11);                                          
      SEKSTISJU_ÅRSDATO_ÅMD  = ((ÅR + 67) * 10000)  + (MN * 100) +  DG;         
                                                                                
      IF HJ_VIRK_DATO_ÅMD > 19910000 THEN                                       
         DO;                                                                    
            IF SEKSTISJU_MN = 12           THEN                                 
               IF HJ_VIRK_DATO_ÅMD < 19981000 THEN                              
                  DO;                                                           
                     SEKSTISJU_MN = 1;                                          
                     SEKSTISJU_ÅR = SEKSTISJU_ÅR + 1;                           
                  END;                                                          
  /*FOR NYE SAKER SKAL OPPHØRSDATO I UP IKKE VÆRE JANUAR */                     
               ELSE;                                                            
            ELSE                                                                
               SEKSTISJU_MN = SEKSTISJU_MN + 1;                                 
         END;                                                                   
      SISTE_UTTAKSDATO_ÅMD =                                                    
      MAX(B01.ALDERSP.UTTAKSDATO_ÅMD(SØKER_IND),                                
                       B01.VENTØVR.UTTAKSDATO_ÅMD_ØVRIGE(SØKER_IND,1),          
                       B01.VENTØVR.UTTAKSDATO_ÅMD_ØVRIGE(SØKER_IND,2),          
                       B01.VENTØVR.UTTAKSDATO_ÅMD_ØVRIGE(SØKER_IND,3),          
                       B01.VENTØVR.UTTAKSDATO_ÅMD_ØVRIGE(SØKER_IND,4),          
                       B01.VENTØVR.UTTAKSDATO_ÅMD_ØVRIGE(SØKER_IND,5));         
      TRANSTYPE = 1;                                                            
      DIV_PERIODE.AP_TRANSTYPE_SPART(SØKER_IND) = 1;  /*9911*/                  
      DATO_ÅMD  = DATO_2000;                                                    
      TILLEGG = 2;                                                              
                                                                                
      DAGENS_DATO_1MND = DAGENS_DATO_1MND + TILLEGG;                            
      IF DAGENS_DATO_1MND > 12 THEN                                             
         DO;                                                                    
             DAGENS_DATO_1MND = DAGENS_DATO_1MND - 12;                          
             DAGENS_DATO_1ÅR  = DAGENS_DATO_1ÅR  + 1;                           
         END;                                                                   
      IF HJ_VIRK_DATO_ÅMD > DAGENS_DATO_ÅMD THEN                                
        DO;                                                                     
  L100:                                                                         
           FEIL_VED_LABEL = '100';                                              
           FEIL_MELD_NR   = 10;                                                 
           GO TO L999;                                                          
        END;                                                                    
                                                                                
                                                                                
 /* **************************************************************** */         
 /* DERSOM SØKER HAR PENSJONSSTATUS.                                 */         
 /* **************************************************************** */         
                                                                                
                                                                                
                                                                                
      STATUS_TYPE_SØKER = STATUS_TYPE;                                          
                                                                                
      IF STATUS_TYPE_SØKER  = 'B' !                                             
         STATUS_TYPE_SØKER  = 'D' !                                             
         STATUS_TYPE_SØKER  = 'G' THEN                                          
         DO;                                                                    
            /* *********************************************** */               
            /* ETTERFØLGENDE TESTER GJELDER DISSE STATUS-TYPER */               
            /* - B - BYGGER PÅ SISTE STATUS                    */               
            /* - D - BYGGER PÅ NEST SISTE STATUS               */               
            /* - G - BYGGER PÅ NEST SISTE STATUS, ER OPPHØRT   */               
            /*       I SISTE STATUS MED X (IKKE DØD)           */               
            /* *********************************************** */               
            IF B01.TKNR(SØKER_IND) > 0 THEN                                     
               IF B01.TKNR(SØKER_IND) ^= A1S.TKNR &                             
                  B01.SIVILSTAND(SØKER_IND) ^= 'A' THEN                         
                  DO;                                                           
                  B02.TKNR(SØKER_IND) = A1S.TKNR;                               
 /*   L110: RETTA 18072001 MARTIN                                               
                     FEIL_VED_LABEL = '110';                                    
                     FEIL_MELD_NR   = 1702;                                     
                     GO TO L999; */                                             
                  END;                                                          
            IF SISTE_UTTAKSDATO_ÅMD > HJ_VIRK_DATO_ÅMD THEN                     
               DO;                                                              
  L120:                                                                         
                  FEIL_VED_LABEL = '120';                                       
                  FEIL_MELD_NR   = 1703;                                        
                  GO TO L999;                                                   
               END;                                                             
                                                                                
 /* *************************************************************** */          
 /* DERSOM DET ER EN EKTEFELLE I TRANSEN.                           */          
 /* *************************************************************** */          
                                                                                
            IF A1S.FNR_EK > 0 THEN                                              
               DO;                                                              
                  SELECT ( B01.SIVILSTAND(SØKER_IND));                          
                                                                                
 /* *************************************************************** */          
 /* DERSOM SØKERS SIVILSTAND ER UGIFT.                              */          
 /* *************************************************************** */          
                                                                                
                     WHEN ('U')                                                 
                        DO;                                                     
  L140:                                                                         
                           FEIL_VED_LABEL = '140';                              
                           FEIL_MELD_NR   = 1001;                               
                           GO TO L999;                                          
                        END;                                                    
                                                                                
 /* *************************************************************** */          
 /* DERSOM SØKER ER SKILT.                                          */          
 /* *************************************************************** */          
                                                                                
                     WHEN ('S')                                                 
                        DO;                                                     
  L150:                                                                         
                           FEIL_VED_LABEL = '150';                              
                           FEIL_MELD_NR   = 1002;                               
                           GO TO L999;                                          
                        END;                                                    
                                                                                
 /* *************************************************************** */          
 /* DERSOM SØKER ER ETTERLATT.                                      */          
 /* *************************************************************** */          
                                                                                
                     WHEN ('E')                                                 
                        DO;                                                     
  L160:                                                                         
                           FEIL_VED_LABEL = '160';                              
                           FEIL_MELD_NR   = 1003;                               
                           GO TO L999;                                          
                        END;                                                    
                                                                                
 /* *************************************************************** */          
 /* ELLERS ER SØKEREN GIFT.                                         */          
 /* *************************************************************** */          
                                                                                
                     OTHERWISE                                                  
                        DO;                                                     
                                                                                
 /* *************************************************************** */          
 /* DERSOM DET ER EN ANNEN EKTEFELLE I SØKERENS PESTATUS.           */          
 /* *************************************************************** */          
                                                                                
                           IF B01.FNR(EKTEF_IND) ^= A1S. FNR_EK                 
                           THEN                                                 
                              DO;                                               
  L170:                                                                         
                                 FEIL_VED_LABEL = '170';                        
                                 FEIL_MELD_NR   = 1004;                         
                                 GO TO L999;                                    
                              END;                                              
                           ELSE                                                 
                                                                                
 /* *************************************************************** */          
 /* NÅ FORUTSETTES DET AT EKTEFELLEN ER I SØKERS PESTATUS.          */          
 /* *************************************************************** */          
                                                                                
                              DO;                                               
                                                                                
 /* *************************************************************** */          
 /* DERSOM DET SØKES OM EKTEFELLETILLEGG.                           */          
 /* *************************************************************** */          
                                                                                
                                 IF A1S.TILL_EK = 'J' THEN                      
                                    DO;                                         
                                                                                
 /* *************************************************************** */          
 /* DERSOM EKTEFELLEN HAR PENSJON.                                  */          
 /* *************************************************************** */          
                                                                                
                                       IF (                                     
                                        B01.PENSJONSTYPE1 (EKTEF_IND)           
                                                             = 'A' !            
                                        B01.PENSJONSTYPE1 ( EKTEF_IND)          
                                                             = 'U' !            
                                        B01.PENSJONSTYPE1 ( EKTEF_IND)          
                                                             = 'Y' !            
                                        B01.PENSJONSTYPE1 ( EKTEF_IND)          
                                                             = 'K'  )           
                                                                                
                                       THEN                                     
                                       DO;                                      
  L180:                                                                         
                                          FEIL_VED_LABEL = '180';               
                                          FEIL_MELD_NR   = 1011;                
                                          GO TO L999;                           
                                       END;                                     
                                                                                
 /* **************************************************************** */         
 /* EKTEFELLEN HAR IKKE PENSJON.                                     */         
 /* **************************************************************** */         
                                                                                
                                       ELSE                                     
                                       DO;                                      
                                       CALL                                     
                                       FLYTT_B01_TIL_B02( SØKER_IND);           
                                       IF FEIL_MELD_NR > 0 THEN                 
                                          GO TO L999;                           
                                       ELSE                                     
                                          PROGRAM_ID='R001N520';                
                                       CALL                                     
                                       FLYTT_B01_TIL_B02( EKTEF_IND);           
                                       IF FEIL_MELD_NR > 0 THEN                 
                                          GO TO L999;                           
                                       ELSE                                     
                                          PROGRAM_ID='R001N520';                
                                       CALL                                     
                                            AJOURFØR_B02_MED_A1TRANS;           
                                       END;                                     
                                    END;                                        
                                                                                
 /* *************************************************************** */          
 /* DET SØKES IKKE OM EKTEFELLETILLEGG.                             */          
 /* *************************************************************** */          
                                                                                
                                 ELSE                                           
                                    DO;                                         
                                       CALL                                     
                                       FLYTT_B01_TIL_B02( SØKER_IND);           
                                       IF FEIL_MELD_NR > 0 THEN                 
                                          GO TO L999;                           
                                       ELSE                                     
                                          PROGRAM_ID='R001N520';                
                                       CALL                                     
                                       FLYTT_B01_TIL_B02( EKTEF_IND);           
                                       IF FEIL_MELD_NR > 0 THEN                 
                                          GO TO L999;                           
                                       ELSE                                     
                                          PROGRAM_ID='R001N520';                
                                       CALL                                     
                                       AJOURFØR_B02_MED_A1TRANS;                
                                    END;                                        
                              END;                                              
                        END;                                                    
                  END;                                                          
               END;                                                             
            ELSE                                                                
                                                                                
 /* ************************************************************ */             
 /* INGEN EKTEFELLE I TRANSEN.                                   */             
 /* ************************************************************ */             
                                                                                
               DO;                                                              
                                                                                
 /* *************************************************************** */          
 /* DERSOM SØKERS SIVILSTAND ER G ELLER A ELLER P                   */          
 /* *************************************************************** */          
                                                                                
                 IF  B01.SIVILSTAND(SØKER_IND) = 'P' THEN                       
                     DO;                                                        
  L185:                                                                         
                        FEIL_VED_LABEL = '185';                                 
                        FEIL_MELD_NR   = 1105;                                  
                        GO TO L999;                                             
                     END;                                                       
                                                                                
                 IF  B01.SIVILSTAND(SØKER_IND) = 'W'  !                         
                     B01.SIVILSTAND(SØKER_IND) = 'V' THEN                       
                     DO;                                                        
  L187:                                                                         
                        FEIL_VED_LABEL = '187';                                 
                        FEIL_MELD_NR   = 1106;                                  
                        GO TO L999;                                             
                     END;                                                       
                                                                                
                                                                                
                  IF (                                                          
                   B01.SIVILSTAND(SØKER_IND) = 'G' !                            
                   B01.SIVILSTAND(SØKER_IND) = 'A') THEN                        
                     DO;                                                        
  L190:                                                                         
                        FEIL_VED_LABEL = '190';                                 
                        FEIL_MELD_NR   = 1102;                                  
                        GO TO L999;                                             
                     END;                                                       
                  ELSE                                                          
                     DO;                                                        
                        CALL FLYTT_B01_TIL_B02(SØKER_IND);                      
                        IF B01.PENSJONSTYPE2(SØKER_IND) = 'E'  !                
  /*0795 HL*/              B01.PENSJONSTYPE1(SØKER_IND) = 'E' THEN              
                           B02.PERSON(EKTEF_IND) =                              
                                            B01.PERSON(EKTEF_IND);              
                        IF FEIL_MELD_NR > 0 THEN                                
                           GO TO L999;                                          
                        ELSE                                                    
                           PROGRAM_ID='R001N520';                               
                        CALL AJOURFØR_B02_MED_A1TRANS;                          
                     END;                                                       
               END;                                                             
         END;                                                                   
      ELSE                                                                      
                                                                                
 /* 9/1-85 KARIN */                                                             
 /* *************************************************************** */          
 /* SØKEREN ER IKKE I PENSJONSDELEN ELLER                           */          
 /* HAR EN OPPHØRT PENSJONSSTATUS (STATUS-KODE-HIST = O / X)        */          
 /* *************************************************************** */          
                                                                                
         DO;                                                                    
            /* 9/1-85 KARIN */                                                  
            /* ************************************************/                
            /* DET ETTERFØLGENDE GJELDER DISSE STATUS-TYPER:   */               
            /* - A - IKKE STATUS-SEGMENT ELLER STATUS-SEGMENT  */               
            /*       SOM ER OPPHØRT, STATUS_KODE-HIST = 'O',   */               
            /*       INGEN OPPLYSNINGER FRA STATUS SKAL MED I  */               
            /*       B02.                                      */               
            /* - C - IKKE STATUS-SEGMENT                       */               
            /* - I - HAR STATUS-SEGMENT (SS) SOM ER HISTORISK, */               
            /*       STATUS-KODE-HIST = 'X', UFØREHISTORIKKEN  */               
            /*       SKAL VÆRE MED OVER TIL B02.               */               
            /* - K - HAR STATUS-SEGMENT (NS) SOM ER HISTORISK, */               
            /*       STATUS-KODE-HIST = 'X', UFØREHISTORIKKEN  */               
            /*       SKAL VÆRE MED OVER TIL B02.               */               
            /* *********************************************** */               
                                                                                
 /* *************************************************************** */          
 /* BARE NÅR DET LIGGER NOE I B01.                                  */          
 /* *************************************************************** */          
                                                                                
            IF B01.FNR(SØKER_IND) > 0 THEN                                      
               DO;                                                              
                  CALL FLYTT_B01_TIL_B02(SØKER_IND);                            
                  IF FEIL_MELD_NR > 0 THEN                                      
                     GO TO L999;                                                
                  ELSE                                                          
                     PROGRAM_ID='R001N520';                                     
                  B02.NAVN(SØKER_IND)  = A1S.NAVN;                              
               END;                                                             
            ELSE                                                                
                                                                                
 /* *************************************************************** */          
 /* OPPLYSNINGER FOR OPPRETTELSE AV ROTSEGMENT.                     */          
 /* *************************************************************** */          
                                                                                
               DO;                                                              
                  B02.FNR(SØKER_IND)   = A1S.FNR;                               
                  B02.NAVN(SØKER_IND)  = A1S.NAVN;                              
                  B02.TKNR(SØKER_IND)  = A1S.TKNR;                              
                  B02.SPRÅK(SØKER_IND) = A1S.SPRÅK;                             
               END;                                                             
                                                                                
 /* *************************************************************** */          
 /* DERSOM DET ER EKTEFELLE I TRANSEN.                              */          
 /* *************************************************************** */          
                                                                                
            IF A1S.FNR_EK > 0 THEN                                              
               DO;                                                              
                  SEARCH_FNR = A1S.FNR_EK;                                      
                  POS_I_B01  = EKTEF_IND;                                       
                  EXEC CICS LINK PROGRAM ('R0019928') COMMAREA                  
                                                        (KOM_OMR);              
                  IF FEIL_MELD_NR > 0 THEN                                      
                     GO TO L999;                                                
                  ELSE                                                          
                     PROGRAM_ID='R001N520';                                     
                                                                                
 /* ************************************************************** */           
 /* DERSOM EKTEFELLEN IKKE HAR PESTATUS.                           */           
 /* ************************************************************** */           
                                                                                
                  IF STATUS_TYPE  = 'A' !                                       
                     STATUS_TYPE  = 'I' !                                       
                     STATUS_TYPE  = 'K' !   /* 9/1-85 */                        
                     STATUS_TYPE  = 'C' THEN                                    
                     DO;                                                        
                                                                                
 /* ************************************************************** */           
 /* DERSOM EKTEFELLEN ER I BASEN (MEN IKKE HAR PENSJONSSTATUS).    */           
 /* ************************************************************** */           
                                                                                
                        IF B01.FNR(EKTEF_IND) > 0 THEN                          
                           DO;                                                  
                              CALL FLYTT_B01_TIL_B02(EKTEF_IND);                
                              IF FEIL_MELD_NR > 0 THEN                          
                                 GO TO L999;                                    
                              ELSE                                              
                                 PROGRAM_ID='R001N520';                         
                              B02.NAVN(EKTEF_IND)  = A1S.NAVN_EK;               
                              B02.TKNR(EKTEF_IND)  = A1S.TKNR;                  
                           END;                                                 
                        ELSE                                                    
                                                                                
 /* *************************************************************** */          
 /* OPPLYSNINGER FOR OPPRETTELSE AV ROTSEGMENT.                     */          
 /* *************************************************************** */          
                                                                                
                           DO;                                                  
                              B02.FNR(EKTEF_IND)   = A1S.FNR_EK;                
                              B02.NAVN(EKTEF_IND)  = A1S.NAVN_EK;               
                              B02.TKNR(EKTEF_IND)  = A1S.TKNR;                  
                              B02.SPRÅK(EKTEF_IND) = A1S.SPRÅK;                 
                           END;                                                 
                           /* R0010521 ENDRET 9/1-85 AV KARIN */                
                           CALL                                                 
                               OPPRETT_STATUS_A1_SØKER; /*R001N521*/            
                           CALL KOBLE_TO_PERSONER (                             
                                                 SØKER_IND,EKTEF_IND);          
                     END;                                                       
                  ELSE                                                          
                     DO;                                                        
                                                                                
 /* *************************************************************** */          
 /* EKTEFELLEN HAR PENSJONSSTATUS.                                  */          
 /* *************************************************************** */          
                                                                                
 /* ENDRET AV KARIN 9/1-85 */                                                   
                        IF STATUS_TYPE = 'F' !                                  
                           STATUS_TYPE = 'H'  THEN                              
                           DO;                                                  
  L195:                                                                         
                              FEIL_VED_LABEL = '195';                           
                              FEIL_MELD_NR   = 1716;                            
                              GO TO L999;                                       
                           END;                                                 
                        ELSE                                                    
                           DO;                                                  
  L200:                                                                         
                              FEIL_VED_LABEL = '200';                           
                              FEIL_MELD_NR   = 1008;                            
                              GO TO L999;                                       
                           END;                                                 
                     END;                                                       
               END;                                                             
            ELSE                                                                
                                                                                
 /* ************************************************************** */           
 /* DET ER IKKE EKTEFELLE I TRANSEN.                               */           
 /* *************************************************************  */           
                                                                                
                  CALL OPPRETT_STATUS_A1_SØKER;                                 
         END;                                                                   
                                                                                
 /* *************************************************************** */          
 /*         BARNEDELEN                                              */          
 /* *************************************************************** */          
                                                                                
      DO I = 1 TO A1S.BT_ANT;                                                   
         BARN_IND = 0;                                                          
         DO J = 3 TO 14 WHILE (B02.FNR(J) > 0);                                 
            IF A1B.FNR_BARN(I) = B02.FNR(J) THEN                                
               DO;                                                              
                  BARN_IND = J;                                                 
                  J = 14;                                                       
               END;                                                             
         END;                                                                   
                                                                                
 /* ***************************************************************** */        
 /* BARNET ER IKKE I B01, IKKE TILKNYTTET SØKER ELLER SØKERS EKTEFELLE*/        
 /* ***************************************************************** */        
                                                                                
         IF BARN_IND = 0 THEN                                                   
            DO;                                                                 
               BARN_IND        = J;                                             
               SEARCH_FNR      = A1B.FNR_BARN(I);                               
               POS_I_B01       = BARN_IND;                                      
               EXEC CICS LINK PROGRAM ('R0019928') COMMAREA                     
                                                         (KOM_OMR);             
               IF FEIL_MELD_NR > 0 THEN                                         
                  GO TO L999;                                                   
               ELSE                                                             
                  PROGRAM_ID = 'R001N520';                                      
                                                                                
 /* *************************************************************** */          
 /* DERSOM BARNET IKKE HAR PENSJONSSTATUS.                          */          
 /* *************************************************************** */          
                                                                                
                /* 9/1-85 AV KARIN */                                           
               IF STATUS_TYPE = 'A' !                                           
                  STATUS_TYPE = 'I' !                                           
                  STATUS_TYPE = 'C' THEN                                        
                  DO;                                                           
                                                                                
 /* *************************************************************** */          
 /* DERSOM BARNET ER I BASEN.                                       */          
 /* *************************************************************** */          
                                                                                
                     IF B01.FNR(BARN_IND) > 0 THEN                              
                        DO;                                                     
                           CALL FLYTT_B01_TIL_B02(BARN_IND);                    
                           IF FEIL_MELD_NR > 0 THEN                             
                              GO TO L999;                                       
                           ELSE                                                 
                              PROGRAM_ID = 'R001N520';                          
                        END;                                                    
                                                                                
 /* *************************************************************** */          
 /* BARNET ER IKKE I BASEN.                                         */          
 /* *************************************************************** */          
                                                                                
                     ELSE                                                       
                                                                                
 /* *************************************************************** */          
 /* OPPLYSNINGER FOR OPPRETTELSE AV ROTSEGMENT.                     */          
 /* *************************************************************** */          
                                                                                
                         DO;                                                    
                            B02.FNR(BARN_IND)   = A1B.FNR_BARN (I);             
                            B02.NAVN(BARN_IND)  = (25)' ';                      
                            B02.TKNR(BARN_IND)  = A1S.TKNR;                     
                            B02.SPRÅK(BARN_IND) = A1S.SPRÅK;                    
                         END;                                                   
                        B02.PENSJONSTYPE2(BARN_IND) = ' ';                      
                     CALL                                                       
                     OPPRETT_STATUS_FORS_BARN(BARN_IND,SØKER_IND);              
                     CALL KOBLE_TO_PERSONER(SØKER_IND,BARN_IND);                
                  END;                                                          
               ELSE                                                             
                                                                                
 /* ************************************************************** */           
 /* BARNET HAR PESTATUS.                                           */           
 /* ************************************************************** */           
                                                                                
                  DO;                                                           
                     IF STATUS_TYPE ^= 'F'                                      
                                                      THEN                      
                        DO;                                                     
                                                                                
 /* ************************************************************** */           
 /* BARNET ER FORSØRGET AV ANDRE.                                  */           
 /* ************************************************************** */           
                                                                                
                           IF B01.                                              
                                  PENSJONSTYPE1(BARN_IND) = 'L' THEN            
                              DO;                                               
  L210:                                                                         
                                 FEIL_VED_LABEL = '210';                        
                                 FEIL_MELD_NR   = 1203;                         
                                 GO TO L999;                                    
                              END;                                              
                           ELSE                                                 
                                                                                
 /* ************************************************************** */           
 /* BARNET ER TILKNYTTET ANDRE PÅ EN ANNEN MÅTE.                   */           
 /* ************************************************************** */           
                                                                                
                              IF B01.                                           
                                     FNR_TILKN(BARN_IND,1) > 0 THEN             
                                 DO;                                            
  L230:                                                                         
                                    FEIL_VED_LABEL = '220';                     
                                    FEIL_MELD_NR   = 1206;                      
                                    GO TO L999;                                 
                                 END;                                           
                              ELSE                                              
                                                                                
 /* ************************************************************** */           
 /* BARNET HAR PENSJON.                                            */           
 /* ************************************************************** */           
                                                                                
                                 DO;                                            
  L240:                                                                         
                                    FEIL_VED_LABEL = '240';                     
                                    FEIL_MELD_NR   = 1204;                      
                                    GO TO L999;                                 
                                 END;                                           
                        END;                                                    
                     ELSE                                                       
                        DO;                                                     
  L245:                                                                         
                           FEIL_VED_LABEL = '245';                              
                           FEIL_MELD_NR   = 0601;                               
                           GO TO L999;                                          
                        END;                                                    
                  END;                                                          
            END;                                                                
                                                                                
                                                                                
 /* ************************************************************** */           
 /* BARNET ER TILKNYTTET SØKERENS FAMILIE.                         */           
 /* ************************************************************** */           
                                                                                
         ELSE                                                                   
            DO;                                                                 
                                                                                
 /* ************************************************************** */           
 /* DERSOM BARNET IKKE ER TILKNYTTET SØKER.                        */           
 /* ************************************************************** */           
                                                                                
               IF B01.FNR_TILKN(BARN_IND,1) ^= B01.FNR(SØKER_IND)               
                                                                 THEN           
                  DO;                                                           
  L250:                                                                         
                     FEIL_VED_LABEL = '250';                                    
                     FEIL_MELD_NR   = 1201;                                     
                     GO TO L999;                                                
                  END;                                                          
               ELSE                                                             
                  DO;                                                           
                                                                                
 /* ************************************************************** */           
 /* IKKE FORSØRGET BARN.                                           */           
 /* ************************************************************** */           
                                                                                
                     IF B01.PENSJONSTYPE1(BARN_IND) ^= 'L' THEN                 
                        DO;                                                     
  L260:                                                                         
                           FEIL_VED_LABEL = '260';                              
                           FEIL_MELD_NR   = 1207;                               
                           GO TO L999;                                          
                        END;                                                    
                  END;                                                          
            END;                                                                
      END;                                                                      
                                                                                
 /* ************************************************************* */            
 /* BARN TILKNYTTET SØKER MED L (FORSØRGEDE BARN)                 */            
 /* ************************************************************* */            
                                                                                
  B02.ANTALL_BARN(SØKER_IND) = 0;                                               
  DO I = 1 TO 13 WHILE                                                          
         (B02.TILKN.FNR_TILKN(SØKER_IND,I) > 0 );                               
     IF B02.TILKN.TILKNYTNINGSKODE(SØKER_IND,I) = 'L'   !                       
        B02.TILKN.TILKNYTNINGSKODE(SØKER_IND,I) = 'V'   !   /*HL*/              
        B02.TILKN.TILKNYTNINGSKODE(SØKER_IND,I) = 'W'   THEN                    
        B02.ANTALL_BARN(SØKER_IND) =                                            
                                    B02.ANTALL_BARN(SØKER_IND) + 1;             
  END;                                                                          
                                                                                
 /* **************************************************************** */         
 /*YS - VI SKAL AVSLUTTE EN EVT. UFØREPERIODE                   */              
 /* **************************************************************** */         
                                                                                
         B02.STATUS.FRYSKODE(SØKER_IND) = ' ';                                  
         B02.FRYSDATO_ÅMD(SØKER_IND) = 0;                                       
         IF B02.UFØRHIST.UFT_ÅMD(SØKER_IND,1) > 0    THEN                       
         DO;                                                                    
            DCL SISTE  FIXED BIN(15);                                           
           /*0697 HL:*/                                                         
            DO J = 7 TO 1 BY -1 UNTIL(B02.UFØRHIST.UFT_ÅMD(SØKER_IND,           
                                                          J) > 0);              
               SISTE = J;                                                       
            END;                                                                
                                                                                
            IF B02.UFØRHIST.OPPHØRSDATO_ÅMD(SØKER_IND,SISTE) = 0 THEN           
                                                                                
            DO;                                                                 
               X_DATO= SEKSTISJU_ÅRSDATO_ÅMD / 100;                             
               X_DATO = X_DATO * 100;                                           
               B02.UFØRHIST.OPPHØRSDATO_ÅMD(SØKER_IND,SISTE) = X_DATO;          
               B02.UFØRHIST.OPPHØRSKODE(SØKER_IND,SISTE) = 'A';                 
               IF B01.PENSJONSTYPE1(SØKER_IND) = 'Y'    THEN                    
 /*200010*/       B02.ALDERSP.KONV_P_KODE(SØKER_IND)        = 'Y';              
               ELSE                                                             
 /*TS*/           B02.ALDERSP.KONV_P_KODE(SØKER_IND)        = 'U';              
                                                                                
               DO J = 5 TO 1 BY -1 UNTIL(B02.YRKEHIST.YUFT_ÅMD                  
                                                (SØKER_IND,J) > 0);             
                  SISTE = J;                                                    
               END;                                                             
                                                                                
               IF B02.YRKEHIST.OPPH_DATO_ÅMD(SØKER_IND,SISTE) = 0 THEN          
                  DO;                                                           
                     B02.YRKEHIST.OPPH_DATO_ÅMD(SØKER_IND,SISTE) =              
                                     X_DATO;                                    
                     B02.YRKEHIST.OPPH_KODE(SØKER_IND,SISTE) = 'A';             
           /* JFA REMEDY 978 28.4.05 */                                         
                     IF (B01.STATUS.FRYSKODE(SØKER_IND) ^= ' ' &                
                         B01.YRKEPENS.YUG(SØKER_IND) <                          
                         B01.YRKEHIST.YUG(SØKER_IND,SISTE) ) !                  
                        (B01.ALDERSP.KONV_P_KODE(SØKER_IND) = 'Y' &             
                         B01.YRKEPENS.YUG(SØKER_IND) <                          
                         B01.YRKEHIST.YUG(SØKER_IND,SISTE) ) THEN               
                        DO;                                                     
                           B02.YRKEPENS.YUG(SØKER_IND) =                        
                               B01.YRKEHIST.YUG(SØKER_IND,SISTE);               
                        END;                                                    
               END;                                                             
            END;                                                                
         END;                                                                   
                                                                                
  L999:                                                                         
                                                                                
  EXEC CICS RETURN;                                                             
                                                                                
    /* -------------------------------------------------------------- */        
    %INCLUDE R001N521; /* OPPRETT_STATUS_A1_SØKER                     */        
    %INCLUDE R001N522; /* AJOURFØR_B02_MED_A1TRANS                    */        
    %INCLUDE R0019905; /* F_ALDER                                     */        
    %INCLUDE R0019923; /* OPPRETT_STATUS_FORS_BARN                    */        
    %INCLUDE R0019924; /* KOBLE_TO_PERSONER                           */        
    %INCLUDE R0019926; /* FLYTT_B01_TIL_B02                           */        
    %INCLUDE R0019929; /* KOBLING_ØVRIGE_BARN_OPPHØRER                */        
    %INCLUDE R0019995; /* KONV-FNR11FNR13                             */        
    %INCLUDE R0019954; /* RULL_FORVENTET                              */        
    %INCLUDE R0019967; /* RULL_FAI                                    */        
    %INCLUDE R0019968; /* F_RULL_FORSI                                */        
    /* -------------------------------------------------------------- */        
   END KON_A1;                                                                  
