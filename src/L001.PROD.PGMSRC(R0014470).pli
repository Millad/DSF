 /*   SIST ENDRET PÅ PROD   2006.10.13  9.10.10 AV   JDA2990          */        
 /*   SIST ENDRET PÅ TEST   2006.10.13  9.09.20 AV   JDA2990          */        
 /*   SIST ENDRET PÅ PROD   2006.10.10 13.37.21 AV   JDA2990          */        
 /*   SIST ENDRET PÅ TEST   2006.09.28 13.33.33 AV   JDA2990          */        
 /*   SIST ENDRET PÅ QASS   2006.09.07  8.19.47 AV   JDA2990          */        
 /*   SIST ENDRET PÅ TEST   2006.09.07  8.16.59 AV   JDA2990          */        
 /*   SIST ENDRET PÅ PROD   2004.12.17 15.31.59 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.07.13 13.26.46 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2003.11.05 13.42.39 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2003.03.14 10.40.09 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2002.04.19 12.45.11 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2000.11.13 14.51.37 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.11.03 12.23.26 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.04.17 12.53.14 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.03.22  9.51.24 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.03.16 11.43.44 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.03.01 10.45.32 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.01.14 12.41.38 AV   JDA7339          */        
 /*       SIST ENDRET 20/10-99 09.22.20 AV   JDA7339                  */        
 /*       SIST ENDRET 08/07-99 09.45.06 AV   HLA7339                  */        
 /*       SIST ENDRET 20/05-99 12.07.17 AV   JDA7339                  */        
 /*       SIST ENDRET 20/05-99 12.05.07 AV   JDA7339                  */        
 /*       SIST ENDRET 14/05-99 12.10.51 AV   JDA7339                  */        
 /*       SIST ENDRET 14/05-99 12.09.14 AV   JDA7339                  */        
 /*       SIST ENDRET 14/04-99 08.00.18 AV   JDA7339                  */        
 /*       SIST ENDRET 14/04-99 07.59.59 AV   JDA7339                  */        
 /*       SIST ENDRET 26/03-99 13.26.30 AV   JDA7339                  */        
 /*       SIST ENDRET 17/03-99 09.24.05 AV   JDA7339                  */        
 /*       SIST ENDRET 09/03-99 14.58.54 AV   HLA7339                  */        
 /*       SIST ENDRET 08/01-99 13.01.41 AV   JDA7339                  */        
 /*       SIST ENDRET 08/01-99 13.00.30 AV   JDA7339                  */        
 /*       SIST ENDRET 17/12-98 13.47.40 AV   TSB7339                  */        
 /*       SIST ENDRET 17/12-98 13.46.41 AV   TSB7339                  */        
 /*       SIST ENDRET 16/12-98 14.18.19 AV   JDA7339                  */        
 /*       SIST ENDRET 16/12-98 14.17.48 AV   JDA7339                  */        
 /*       SIST ENDRET 23/10-98 13.00.14 AV   TSB7339                  */        
 /*       SIST ENDRET 02/09-98 12.43.01 AV   SPA7339                  */        
 /* ***************************************************************** */        
 /* IDENTIFIKASJON                                                    */        
 /*     R0014470 - UP - EØS-REGLER      - HOVEDPROG                   */        
 /*     PROGRAMMERER: HERMAN LARSSEN MARS 93.                         */        
 /* HENSIKT                                                           */        
 /*     BEREGNE UFØREPENSJON ETTER PRO-RATA-REGLER                    */        
 /*     OPPDATERER UFØRPENS MED :                                     */        
 /*     OPPDATERER STATUS   MED :                                     */        
 /*     OPPDATERER EØS      MED :                                     */        
 /* PROGRAMTILKNYTNING.                                               */        
 /*     PROSEDYREN BLIR LINKET FRA  R0014001                          */        
 /* ***************************************************************** */        
 UPXEØS : PROC (COMMAREA_PEKER) OPTIONS(MAIN);                                  
    DCL   COMMAREA_PEKER PTR;                                                   
    /* -------------------------------------------------------------- */        
    /* HENTER INN FORHÅNDSDEKLARERTE "BASER"                          */        
    /* -------------------------------------------------------------- */        
    %INCLUDE P0019906;   /* DEKLARERER TRANS_OPPL_OMR                 */        
    %INCLUDE P0019908;   /* DEKLARERER KOM_OMR                        */        
    %INCLUDE P0019910;   /* DEKLARERER STYRINGS_OMR                   */        
    %INCLUDE P0019912;   /* DEKLARERER DIV_PARA_OMR                   */        
    %INCLUDE P0019924;   /* GV_TAB_RE */                                        
    %INCLUDE P0014002;   /* W_POREKKE                                 */        
    %INCLUDE P0014003;   /* TAB                                       */        
    %INCLUDE P0014009;   /* POREKKE                                   */        
    %INCLUDE P0019980;   /* DEFINERING AV FNR11/13 SØKER & EKTEFELLE  */        
    %INCLUDE P0019985;   /* VARIABLER VED EØS BEREGNING               */        
    DCL 1 B02 BASED (B02_PEKER), %INCLUDE P0019921;                             
                                                                                
    /* -------------------------------------------------------------- */        
    /* DEKLARERER EGNE VARIABLER FOR DENNE PROSEDYREN                 */        
    /* -------------------------------------------------------------- */        
    DCL MINSTEVILKÅR                   FIXED DEC (5,2) INIT (0),                
        OPPTJENINGSPERIODEN            FIXED DEC (3)   INIT (0),                
        ST_PROSENT                     FIXED DEC (5,2) INIT (0),                
        TIL_ÅR                         FIXED DEC (5)   INIT (0),                
        TT_FRAMT_MAX                   FIXED DEC (3)   INIT (0),                
        GARANTI_NORSK_ÅR               FIXED DEC (3)   INIT (0),                
        GARANTI_EØS_ÅR                 FIXED DEC (3)   INIT (0),                
        GARANTI_GP                     FIXED DEC (9,2) INIT (0),                
        GP_GRUNNLAG                    FIXED DEC (9,2) INIT (0),                
        GARANTI_GP_HELTALL             FIXED DEC (7)   INIT (0),                
        W_GP_HELTALL                   FIXED DEC (7)   INIT (0),                
        W_TT_FRAM_TIL_DATO             PIC '(6)9',                              
        W_TT_FRAM_TIL_ÅR  DEF W_TT_FRAM_TIL_DATO POS(1) PIC '9999',             
        W_TT_FRAM_TIL_MND DEF W_TT_FRAM_TIL_DATO POS(5) PIC '99',               
        W_UFG_5_2                      FIXED DEC(5,2),                          
        ALDER_EK                       FIXED DEC (5)    INIT (0),               
        /* BRUKES FOR Å REGNE UT SÆRTILLEGGET PT2 = 'F'   */                    
        W_FOLKETRYGD                   FIXED DEC (7),/* -> 4012 */              
        W_STI_MND                      PIC '(2)9' DEF W_STI_ÅM POS(5),          
        W_STI_ÅM                       PIC '(6)9',                              
        W_STI_ÅR                       PIC '(4)9' DEF W_STI_ÅM POS(1),          
                                                                                
        W_UFT_ÅM                       PIC '(6)9',                              
        W_UFT_MND                      DEF W_UFT_ÅM POS (5) PIC '99',           
        W_UFT_ÅR                       DEF W_UFT_ÅM POS (1) PIC '9999',         
        W_DATO_HÅMD                    PIC'(8)9';                               
    /* ============================================================= */         
    /*  HER STARTER HOVEDPROGRAMMET                                  */         
    /* ============================================================= */         
    COMMAREA_PEKER           = ADDR(KOM_OMR);                                   
    KOM_OMR.DIV_PARAM_PEKER  = ADDR(KOM_OMR.DIV_PARAM_OMR);                     
    IF FEIL_MELD_NR = 0 THEN /* AB 01.08.16 */                                  
    PROGRAM_ID='R0014470';                                                      
  /* CALL NULLSTILL_P0019985;        4472*/                                     
     BEREGNET(*) = '';                                                          
     CALL INITIER;                                                              
     CALL FINN_TT_FAKTISK(BER_SØ_IND); /* 4471 */                               
     CALL TELL_FAKTISKE_POENGÅR;                                                
     CALL REGN_TRYGDETID;                                                       
     CALL KONTROLL_OPPGITT_TRYGDETID;                                           
 /* IF FEIL_MELD_NR = 0 THEN */                                                 
       DO;                                                                      
          CALL REGN_POENGÅR;                                                    
          IF B02.EØS_GARANTI(BER_SØ_IND) = 'A'     !                            
             B02.EØS_GARANTI(BER_SØ_IND) = 'C'        THEN                      
             CALL SPT_GAR;            /*4475*/                                  
          ELSE                                                                  
             CALL REGN_BUP_SPT;                                                 
          CALL FINN_BELØP;                                                      
 /*       IF (B02.BEREGN_FOLKETRYGD(BER_SØ_IND) = 'N' !                         
              B02.NORDISK_KONVENSJON(BER_SØ_IND)= 'J' ) THEN  */                
          IF (B02.BEREGN_FOLKETRYGD(BER_SØ_IND) = 'N' )  THEN                   
             W_SKAL_BEREGNES(1) = '0'B;                                         
          CALL FINN_HØGSTE_YTELSE;                                              
          IF B02.PENSJONSTYPE2(BER_SØ_IND) = 'A' THEN                           
             CALL OPPDATER_ATTFØRING;                                           
          ELSE                                                                  
             CALL OPPDATER_UFØRE;                                               
          CALL OPPDATER_I_BEGGE_TILFELLER;                                      
          CALL OPPDATER_DIV_PARAM_OMR;                                          
       END;                                                                     
 /* ================================================================= */        
 INITIER : PROC;                                                                
    CALL NULLSTILL_P0019985;                                                    
    POTALL_OPPL.IND = BER_SØ_IND;  /*?*/                                        
    B01_B02_IND     = 2;                                                        
    IND             = BER_SØ_IND;                                               
                                                                                
    DO J = 2 TO HBOUND(B02.UFØRHIST,2);                                         
        IF (B02.UFT_ÅMD(BER_SØ_IND,J)   = 0 &                                   
            B02.UFT_ÅMD(BER_SØ_IND,J-1) > 0 ) THEN                              
           DO;  /* FINN GUNSTIGSTE */                                           
              UF_IND = J - 1;                                                   
              DO K = 2 TO HBOUND(B02.UFØRGRAD,3);                               
                 IF B02.UFG_DATO_ÅMD(BER_SØ_IND,J,K) >                          
                    B02.UFG_DATO_ÅMD(BER_SØ_IND,J,K-1) THEN                     
                    DO;                                                         
                       UF_IND2 = B02.UFG_DATO_ÅMD(BER_SØ_IND,J,K);              
                    END;                                                        
              END; /* DO K = 2 TIL SISTE ELEMENT I UFØRGRAD */                  
           END; /* FINN GUNSTIGSTE */                                           
     END; /* DO J = 2 TIL SISTE ELEMENT I UFØRHIST */                           
     W_UFG_5_2             = B02.UFØRPENS.UFG(BER_SØ_IND);                      
     W_FNR                 = B02.FNR(BER_SØ_IND);                               
     W_FNR_13              = KONV_FNR11_FNR13(W_FNR);                           
     W_FNR_EK              = B02.FNR(BER_EK_IND);                               
     W_FNR_EK_13           = KONV_FNR11_FNR13(W_FNR_EK);                        
  /* W_DATO_HÅMD = B02.VIRK_DATO_ÅMD(BER_SØ_IND); */                            
  /* MARTIN 13.11.2000 - SE xxxxxxxxxxx (fnr fjernet) */
     W_DATO_HÅMD = TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD;                            
     W_POREKKE = POREKKE (BER_SØ_IND);                                          
     W_UFT_ÅM  = (B02.UFT_ÅMD(BER_SØ_IND,UF_IND)/100);                          
  /* POREKKE(BER_SØ_IND)   = ''  */                                             
     SM_PÅ                 = W_UFT_ÅR - 1;                                      
     FM_PÅ =                                                                    
        MAX( W_FNR_R13.ÅR+17                                                    
           , 1967);                                                             
     IF B02.UFØRPENS.STI_ÅMD(BER_SØ_IND) > 0 THEN                               
        W_STI_ÅM = B02.UFØRPENS.STI_ÅMD(BER_SØ_IND)/100;                        
     ELSE                                                                       
        W_STI_ÅM = B02.UFT_ÅMD(BER_SØ_IND,UF_IND)/100;                          
     /* ------------------------------------------------------------- */        
     /* PENSJON REGNES ALLTID OGSÅ ETTER "NORMALE" REGLER             */        
     /* ------------------------------------------------------------- */        
     W_SKAL_BEREGNES(1) = '1'B;                                                 
     /* ------------------------------------------------------------- */        
     /* KONTROLL OM PENSJON SKAL REGNES ETTER EØS ART 46 NR 2         */        
     /* ------------------------------------------------------------- */        
     IF (B02.TT_PRO_RATA_MND(BER_SØ_IND) +                                      
         B02.TT_TEORETISK_EØS_MND(BER_SØ_IND) > 0 ) THEN                        
        DO;  /* BEREGNES ETTER ART 46 NR 2 */                                   
           W_SKAL_BEREGNES(2) = '1'B;                                           
           B02.PRO_RATA_BEREGNING(BER_SØ_IND) = 'J';                            
        END; /* BEREGNES ETTER ART 46 NR 2 */                                   
     ELSE  /*200609  HL : */                                                    
     IF B02.BEREGN_FOLKETRYGD(BER_SØ_IND) = 'N'   THEN                          
        DO;  /* BEREGNES ETTER ART 46 NR 2 */                                   
           B02.TT_TEORETISK_EØS_MND(BER_SØ_IND) =                               
           B02.TT_16_66(BER_SØ_IND);                                            
           W_SKAL_BEREGNES(2) = '1'B;                                           
           B02.PRO_RATA_BEREGNING(BER_SØ_IND) = 'J';                            
        END; /* BEREGNES ETTER ART 46 NR 2 */                                   
     /* ------------------------------------------------------------- */        
     /* KONTROLL OM PENSJON SKAL REGNES ETTER EØS ART 49 NR 1 B       */        
     /* ------------------------------------------------------------- */        
  /* IF B02.PRO_RATA_BEREGNING = 'J' THEN                                       
        W_SKAL_BEREGNES(3) = 'J'+ SEMIKOLON                                     
     /* KONTROLL OM PENSJON SKAL REGNES ETTER NORDISK KONVENSJON      */        
     IF (B02.TT_NORDISK_FAKT_MND(BER_SØ_IND) +                                  
         B02.PÅ_NORDISK_FAKT(BER_SØ_IND) > 0 ) THEN                             
        DO;                                                                     
           W_SKAL_BEREGNES(4) = '1'B;                                           
           B02.NORDISK_KONVENSJON(BER_SØ_IND) = 'J';                            
        END;                                                                    
                                                                                
     /* KONTROLL OM VILKÅR PARAGRAF 3-17A ER OPPFYLLT                 */        
 /*  IF (B02.UFØRPENS.VILKÅR_8_4_3A(BER_SØ_IND) = 'J') THEN   */                
     IF (B02.UFØRPENS.VILKÅR_8_4_3A(BER_SØ_IND) = 'J')  !                       
        (B02.UFØRHIST.BUP_GAR_KODE(BER_SØ_IND,1) = 'B')   THEN                  
        DO;                                                                     
           W_PÅ_RETT_TIL_FRAMTIDIGE(1) = '1'B;                                  
           W_PÅ_RETT_TIL_FRAMTIDIGE(2) = '1'B;                                  
           W_PÅ_RETT_TIL_FRAMTIDIGE(3) = '1'B;                                  
           W_PÅ_RETT_TIL_FRAMTIDIGE(4) = '1'B;                                  
        END;                                                                    
     ELSE                                                                       
        IF (B02.EØS_8_4_3A(BER_SØ_IND) = 'J') THEN                              
           W_PÅ_RETT_TIL_FRAMTIDIGE(2) = '1'B;                                  
                                                                                
     /* KONTROLL PARAGRAF 3-17 ETTER NASJONAL/NORDISK */                        
     IF (W_PÅ_RETT_TIL_FRAMTIDIGE(1) = '0'B &                                   
         W_SKAL_BEREGNES(1)                 ) !                                 
        (W_PÅ_RETT_TIL_FRAMTIDIGE(4) = '0'B &                                   
         W_SKAL_BEREGNES(4)                 ) THEN                              
        IF (FYLLER_VILKÅR_3_17_NASJONAL(BER_SØ_IND,FM_PÅ,SM_PÅ)) THEN           
           DO I = 1 TO 4;                                                       
              IF W_SKAL_BEREGNES(I) THEN                                        
                 W_PÅ_RETT_TIL_FRAMTIDIGE(I) = '1'B;                            
           END;                                                                 
                                                                                
     /* KONTROLL PARAGRAF 3-17 ETTER EØS-REGLENE */                             
     IF (W_PÅ_RETT_TIL_FRAMTIDIGE(2) = '0'B) &                                  
        (W_SKAL_BEREGNES(2)                  ) THEN                             
        W_PÅ_RETT_TIL_FRAMTIDIGE(2) = FYLLER_VILKÅR_3_17_EØS                    
                    (BER_SØ_IND,FM_PÅ,SM_PÅ);                                   
  END INITIER;                                                                  
  /* ************************************************************ */            
  TELL_FAKTISKE_POENGÅR: PROC;                                                  
     /* FØRST KONTROLLERER OG TELLER VI OPP FAKTISKE POENGÅR         */         
     DO I = FM_PÅ TO SM_PÅ;                                                     
        IF (POREKKE.POENG(BER_SØ_IND,I) > 0     )    THEN                       
    /*     ^(POREKKE.KODE(BER_SØ_IND,I) = 'U55' !                               
             POREKKE.KODE(BER_SØ_IND,I) = 'U56' ) THEN   */                     
  /*200310 HL: ÅR MED VERNEPLIKTPOENG ER FAKTISKE NÅR */                        
  /*           POREKKE.POENG > 0                      */                        
                                                                                
        /* TAB.INNT_POENG(I) > 0 !                                              
           TAB.VERNEPL_POENG(I) ^= POREKKE.POENG(BER_SØ_IND,I) */               
           DO; /* NORSKE POENGÅR, TELL OPP */                                   
              IF I>1991 THEN                                                    
                 W_PÅ_ETTER_91(1) =                                             
                    + W_PÅ_ETTER_91(1) + 1;                                     
              W_PÅ_FAKTISKE(1) = W_PÅ_FAKTISKE(1) + 1;                          
           END;/* NORSKE POENGÅR, TELL OPP */                                   
        ELSE                                                                    
           DO; /* IKKJE GODSKREVET PENSJONSPOENG I NORGE, EØS-ÅR? */            
              IF B02.INNTEKTSÅR_EØS(BER_SØ_IND,I) THEN                          
                 DO;                                                            
                    IF I>1991 THEN                                              
                       W_PÅ_ETTER_91(2)= + W_PÅ_ETTER_91(2) + 1;                
                    W_PÅ_FAKTISKE(2) = W_PÅ_FAKTISKE(2) + 1;                    
                 END;                                                           
     /*       IF B02.INNTEKTSÅR_EØS2(BER_SØ_IND,I) THEN                         
                 DO                                                             
                    IF I>91 THEN                                                
                       W_PÅ_ETTER_91(3)= + W_PÅ_ETTER_91(3) + 1 SEMI            
                    W_PÅ_FAKTISKE(3) = W_PÅ_FAKTISKE(3) + 1 SEMI                
                 END                                                            
     */    END;/* IKKJE GODSKREVET PENSJONSPOENG I NORGE, EØS-ÅR? */            
     END; /* TELL OPP FAKTISKE POENGÅR I NORGE/EØS OMRÅDET */                   
     IF W_SKAL_BEREGNES(4) THEN                                                 
        W_PÅ_FAKTISKE(4) = B02.PÅ_NORDISK_FAKT(BER_SØ_IND);                     
                                                                                
     IF W_PÅ_FAKTISKE(1) = 0 THEN                                               
        DO;  /* HAR INGEN IDE Å REGNE FRAMTIDIGE */                             
           W_PÅ_RETT_TIL_FRAMTIDIGE(2) = '0'B;                                  
           W_PÅ_RETT_TIL_FRAMTIDIGE(2) = '0'B;                                  
           W_PÅ_RETT_TIL_FRAMTIDIGE(2) = '0'B;                                  
           W_PÅ_RETT_TIL_FRAMTIDIGE(2) = '0'B;                                  
        END; /* HAR INGEN IDE Å REGNE FRAMTIDIGE */                             
  END TELL_FAKTISKE_POENGÅR;                                                    
 /* ***************************************************************** */        
  REGN_TRYGDETID : PROC;                                                        
     /* ------------------------------------------------------------- */        
     /* BEREGNER MULIG TRYGDETID VED FULL BOTID TIL UFØRETIDSPUNKT    */        
     /* ------------------------------------------------------------- */        
     OPPTJENINGSPERIODEN=                                                       
        + ( W_STI_ÅR - W_FNR_R13.ÅR - 16 ) * 12                                 
        + ( W_STI_MND - W_FNR_R13.MND - 1 );                                    
     /* ------------------------------------------------------------- */        
     /* BEREGNER MINSTEVILKÅR FOR FULL GODSKRIVING AV FRAMTIDIG TT  */          
     /* ------------------------------------------------------------- */        
     MINSTEVILKÅR  = OPPTJENINGSPERIODEN * .80;      /*AVRUNDES IKKE*/          
     /* ------------------------------------------------------------- */        
     /* BEREGNER HVOR LANGT FRAM TRYGDETID SKAL BEREGNES             */         
     /* ------------------------------------------------------------- */        
     SELECT;                     /* W_STI_ÅM)                         */        
        WHEN (W_STI_ÅM < 197301)                                                
           DO;                      /* NÅR STØNADSTILFELLET INNTRER   */        
              W_TT_FRAM_TIL_DATO =  /* FØR 1973 SKAL EIN REKNE UT     */        
                 ( + W_FNR_R13.ÅR   /* FRAMTIDIG TRYGDETID TIL OG MED */        
                   + 70             /* DEN MÅNADEN DEN TRYGDA FYLLER  */        
                 ) * 100            /* 70 ÅR. FRAMTIDIGE BUP ÅR MAKS  */        
                + W_FNR_R13.MND;    /* TIL VEDK. FYLLER 69 ÅR         */        
              SM_BUP_ÅR = W_FNR_R13.ÅR + 69;                                    
           END;                                                                 
        OTHERWISE                                                               
           DO;                      /* NÅR STØNADSTILFELLET INNTRER   */        
              W_TT_FRAM_TIL_DATO =  /* ETTER 1972 SKAL FRAMTIDIG      */        
                 ( + W_FNR_R13.ÅR   /* TRYGDETID REKNAST FRAM TIL OG  */        
                   + 66             /* MED DESEMBER DET ÅRET MEDLEMET */        
                 ) * 100            /* TRYGDA FYLLER 66 ÅR. FRAMTIDIGE*/        
                   + 12;           /* BUP ÅR MAKS TIL VEDK. FYLLER   */         
              SM_BUP_ÅR = W_FNR_R13.ÅR + 66; /* 66 ÅR */                        
           END;                                                                 
     END;                                                                       
     TT_FRAMT_MAX =              /* I DENNE VARIABELEN LEGGES I HVOR  */        
        (   W_TT_FRAM_TIL_ÅR     /* MANGE MÅNEDER VI MAKSIMALT KAN    */        
          - W_STI_ÅR             /* BENYTTE FOR BEREGNING AV FRAM-    */        
        ) * 12                   /* TIDIG TRYGDETID                   */        
          + W_TT_FRAM_TIL_MND                                                   
          - W_STI_MND                                                           
          + 1;                                                                  
     /* ------------------------------------------------------------- */        
     /* BEREGNER FRAMTIDIG TRYGDETID I ANTALL MÅNEDER                */         
     /* ------------------------------------------------------------- */        
     DO I = 1 TO 4;                                                             
        IF (W_TT_FAKTISK_MND(I) > 0 &                                           
            W_SKAL_BEREGNES(I)      ) !                                         
            I=1                      THEN                                       
           DO; /* BEREGN FRAMTIDIG TRYGDETID */                                 
              IF I = 4 THEN                                                     
                 J = 1;                                                         
              ELSE                                                              
                 J = I;                                                         
              IF (MINSTEVILKÅR > W_TT_FAKTISK_MND(J)) THEN /*    &              
                  B02.UFT_ÅMD(BER_SØ_IND,UF_IND) > 19910000) THEN  */           
                 DO;  /* SKAL HA REDUSERT TRYGDETID */                          
                     /* --------------------------------------------- */        
                     /* BEREGN REDUSERT FRAMTIDIG TRYGDETID           */        
                     /* ***********************************           */        
                     /* FOR DEM SOM HAR BODD I UTLANDET, VIL FRAM-    */        
                     /* TIDIG TRYGDETID KUNNE BLI BEGRENSET MED       */        
                     /* VIRKNING FRA 1.1.91. JFR. R001N824            */        
                     /* --------------------------------------------- */        
                     /* DETTE GJELDER OGSÅ DERSOM:                    */        
                     /*   - NY UFT > 91, GAMMEL UFT < 91              */        
                     /*   - GAMMEL UFT < 91 AVSLUTTET OG NY UFT > 91? */        
                     /*   KARI SANNES MÅ SE PÅ DETTE                  */        
                     /* --------------------------------------------- */        
                    IF (I ^= 4) THEN                                            
                       DO;  /* IKKE NORDISK KONVENSJON */                       
                          W_TT_FRAMTIDIG_MND(I)=                                
                             ( 480 - MINSTEVILKÅR)                              
                             + 0.5;                                             
                       END; /* IKKE NORDISK KONVENSJON */                       
                    ELSE                                                        
                       DO;  /* NORDISK KONVENSJON */                            
             /*           IF (W_TT_FAKTISK_MND(I) > 480) THEN  */               
                             /* REMEDY 153 - MAX 480 I NEVNER */                
                             W_TT_FRAMTIDIG_MND(I) =                            
                                ( 480 - MINSTEVILKÅR)                           
                                * W_TT_FAKTISK_MND(1)                           
                                / MIN(480,W_TT_FAKTISK_MND(I))                  
                                + 0.5;                                          
                             /* SLIK DET TIDLIGERE                              
                             W_TT_FRAMTIDIG_MND(I) =                            
                                ( 480 - MINSTEVILKÅR)                           
                                * W_TT_FAKTISK_MND(1)                           
                                / W_TT_FAKTISK_MND(I)                           
                                + 0.5  */                                       
                       END; /* NORDISK KONVENSJON */                            
                    IF (W_PÅ_RETT_TIL_FRAMTIDIGE(I)) THEN                       
                       W_REDUSERT_ANT_BUP_ÅR(I) =                               
                          W_TT_FRAMTIDIG_MND(I)/12+0.5;                         
                    TIL_ÅR =                                                    
                       + SM_PÅ                                                  
                       + W_REDUSERT_ANT_BUP_ÅR(I);                              
                    IF SM_BUP_ÅR = TIL_ÅR THEN                                  
                       W_REDUSERT_ANT_BUP_ÅR(I) = 0;                            
                 END; /* SKAL HA REDUSERT TRYGDETID */                          
              ELSE                                                              
                 DO;  /* BEREGN FULL FRAMTIDIG TRYGDETID */                     
                    W_TT_FRAMTIDIG_MND(I) = TT_FRAMT_MAX;                       
                    IF I = 4 THEN                                               
                       W_TT_FRAMTIDIG_MND(I) =                                  
                          + TT_FRAMT_MAX                                        
                          * W_TT_FAKTISK_MND(1)                                 
                          / W_TT_FAKTISK_MND(I)                                 
                          + 0.5;                                                
                 END;  /* BEREGN FULL FRAMTIDIG TRYGDETID */                    
              /* BEREGN ANV_MND & ANV_ÅR TRYGDETID */                           
              IF I = 2 THEN                                                     
                 W_TT_ANV_MND(I)=                                               
                    MIN( + W_TT_TEORETISK_MND(J)                                
                         + W_TT_FRAMTIDIG_MND(I)                                
                       , 480);                                                  
              ELSE                                                              
                 W_TT_ANV_MND(I) =                                              
                    MIN( + W_TT_FAKTISK_MND(J)                                  
                         + W_TT_FRAMTIDIG_MND(I)                                
                       , 480);                                                  
              W_TT_ANV_ÅR(I)=                                                   
                 W_TT_ANV_MND(I)/12+0.5;                                        
           END; /* BEREGN FRAMTIDIG TRYGDETID */                                
     END; /* I = 1 TO 4 */                                                      
  END REGN_TRYGDETID;                                                           
  /* *************************************************************** */         
  KONTROLL_OPPGITT_TRYGDETID : PROC;                                            
      /* ----------------------------------------------------------- */         
      /* KONTROLL GJENNOMFØRES, OG VED EVENTUELLE FEIL VIL DET       */         
      /* BLI LAGT INN FEILMELDIGSNUMMER I DIV_PARAM_OMR              */         
      /* ----------------------------------------------------------- */         
      DCL MIN_TT_IFLG_PÅ               FIXED DEC (3) INIT (0);                  
      /* ----------------------------------------------------------- */         
      /* VI KONTROLLERER FØRST OM OPPGITT TRYGDETID ER MINDRE ENN    */         
      /* ANTALL POENGÅR SOM ER GODSKREVET, JAMFØR 7-2 NR. 1          */         
      /* ----------------------------------------------------------- */         
      MIN_TT_IFLG_PÅ = W_PÅ_FAKTISKE(1) * 12;                                   
 /*   IF (TRANS_OPPL_OMR.TRANSTYPE ^= 27) THEN          */                      
 /*ENDRET 20040805 HL*/                                                         
      IF (TRANS_OPPL_OMR.TRANSTYPE = 2) THEN                                    
         DO;  /* VI KONTOLLERER IKKE VED SATSENDRING */                         
         /* IF (B02.PINNTEKT.PI(BER_SØ_IND,(SM_PÅ+1))> 0 &                      
                POREKKE.POENG(BER_SØ_IND,(SM_PÅ+1))> 1   )THEN                  
               MIN_TT_IFLG_PÅ = MIN_TT_IFLG_PÅ + W_UFT_MND -1                   
               UTGÅR IFLG RANDI, TLF. 20.05.98 */                               
            IF W_TT_FAKTISK_MND(1) < MIN_TT_IFLG_PÅ THEN                        
               DO;                                                              
                   FEIL_VED_LABEL = '422';                                      
                   FEIL_MELD_NR = 422;                                          
               END;                                                             
            /* ----------------------------------------------------- */         
            /* KONTROLLERER OM SUM NORSK OG EØS TRYGDETID ER STØRRE  */         
            /* ENN OPPTJEINGSPERIODEN +1 -> BLANKETTEN AVVISES       */         
            /* ----------------------------------------------------- */         
            /* 25.03.98 KOMMENTERT UT - BRUKER FØRSTE UFØRETIDSPUNKT */         
            IF (B02.PENSJONSTYPE2(BER_SØ_IND) = 'A'           &                 
                W_TT_FAKTISK_MND(2) > OPPTJENINGSPERIODEN + 2 &                 
                W_SKAL_BEREGNES(2)                            ) THEN            
               DO;                                                              
                  FEIL_VED_LABEL = '423';                                       
                  FEIL_MELD_NR = 423;                                           
               END;                                                             
            /* ----------------------------------------------------- */         
            /* KONTROLLERER OM SUM NORSK OG NORDISK TRYGDETID ER     */         
            /* STØRRE ENN OPPTJEINGSPERIODEN +2-> BLANKETTEN AVVISES */         
            /* ----------------------------------------------------- */         
            /* 25.03.98 KOMMENTERT UT - BRUKER FØRSTE UFØRETIDSPUNKT */         
            IF (W_SKAL_BEREGNES(4)) THEN                                        
               IF W_TT_FAKTISK_MND(4) > OPPTJENINGSPERIODEN+2 THEN              
                  IF TRANSTYPE = 2  /*UFØREBLANKETT */          THEN            
                     DO;                                                        
                         FEIL_VED_LABEL = '424';                                
                         FEIL_MELD_NR = 424;                                    
                     END;                                                       
         END; /* VI KONTOLLERER IKKE VED SATSENDRING */                         
  END KONTROLL_OPPGITT_TRYGDETID;                                               
  /* *************************************************************** */         
  REGN_POENGÅR : PROC;                                                          
     DO I=4 TO 1 BY -1;                                                         
        /* LOOP'N ER SNUDD 130300 MARTIN */                                     
        IF W_SKAL_BEREGNES(I) THEN                                              
           DO;                                                                  
              SELECT (I);                                                       
                 WHEN (1) /* BEREGN NORSK PENSJON */                            
                    DO;                                                         
                       IF W_PÅ_RETT_TIL_FRAMTIDIGE(I) THEN                      
                          IF (W_TT_FRAMTIDIG_MND(1) =                           
                              TT_FRAMT_MAX           ) THEN                     
                             W_PÅ_FRAMTIDIGE(1) =                               
                                SM_BUP_ÅR - SM_PÅ;                              
                          ELSE                                                  
                             W_PÅ_FRAMTIDIGE(1) =                               
                                W_TT_FRAMTIDIG_MND(1)/12+0.5;                   
                       /* REMEDY 2201 */                                        
                       ELSE                                                     
                          IF (B02.YNGSTE_BARN_FÅR(BER_SØ_IND)                   
                                    +6 > SM_PÅ) THEN                            
                             W_PÅ_FRAMTIDIGE(1) =                               
                                (B02.YNGSTE_BARN_FÅR(BER_SØ_IND)                
                                +6 - SM_PÅ);                                    
                       /* REMEDY 2201 */                                        
                                                                                
                       IF W_PÅ_FAKTISKE(1)+W_PÅ_FRAMTIDIGE(1) > 40 THEN         
                          DO;                                                   
                             W_PÅ_ANV(1) = 40;                                  
                             W_PÅ_ETTER_91(1)=                                  
                                + 40                                            
                                - W_PÅ_FAKTISKE(1)                              
                                + W_PÅ_ETTER_91(1);                             
                          END;                                                  
                       ELSE                                                     
                          DO;                                                   
                             W_PÅ_ANV(1) =                                      
                                + W_PÅ_FAKTISKE(1)                              
                                + W_PÅ_FRAMTIDIGE(1);                           
                             W_PÅ_ETTER_91(1) =                                 
                                + W_PÅ_ETTER_91(1)                              
                                + W_PÅ_FRAMTIDIGE(1);                           
                          END;                                                  
                    END;                                                        
                 WHEN (2) /* PRO RATA 1 */                                      
                    DO;                                                         
                       W_PÅ_FAKTISKE(2) =                                       
                          W_PÅ_FAKTISKE(2) + W_PÅ_FAKTISKE(1);                  
                       IF W_PÅ_RETT_TIL_FRAMTIDIGE(I) THEN                      
                          IF (W_TT_FRAMTIDIG_MND(2) =                           
                              TT_FRAMT_MAX           ) THEN                     
                             W_PÅ_FRAMTIDIGE(2) =                               
                                SM_BUP_ÅR - SM_PÅ;                              
                          ELSE                                                  
                             W_PÅ_FRAMTIDIGE(2) =                               
                                W_TT_FRAMTIDIG_MND(2)/12+0.5;                   
                       /* REMEDY 2201 */                                        
                       ELSE                                                     
                          IF (B02.YNGSTE_BARN_FÅR(BER_SØ_IND)                   
                                    +6 > SM_PÅ) THEN                            
                             W_PÅ_FRAMTIDIGE(1) =                               
                                (B02.YNGSTE_BARN_FÅR(BER_SØ_IND)                
                                +6 - SM_PÅ);                                    
                       /* REMEDY 2201 */                                        
                       IF W_PÅ_FAKTISKE(2)+W_PÅ_FRAMTIDIGE(2) > 40 THEN         
                          DO;  /* OVER 40 POENGÅR EØS */                        
                             W_PÅ_ANV(2) = 40;                                  
                             W_PÅ_ETTER_91(2) =                                 
                                + 40                                            
                                - W_PÅ_FAKTISKE(2)                              
                                + W_PÅ_ETTER_91(2)                              
                                + W_PÅ_ETTER_91(1)                              
                                - W_PÅ_FRAMTIDIGE(1);                           
                             IF (W_PÅ_FAKTISKE(1)                               
                                 + W_PÅ_FRAMTIDIGE(1) > 40) THEN                
                             /* RED GRUNNA DÅRLIG STRUKTUR */                   
                             W_PÅ_ETTER_91(2) =                                 
                                + W_PÅ_ETTER_91(2)                              
                                - 40                                            
                                + W_PÅ_FAKTISKE(1)                              
                                + W_PÅ_FRAMTIDIGE(1);                           
                          END; /* OVER 40 POENGÅR EØS */                        
                       ELSE                                                     
                          DO;  /* UNDER 40 POENGÅR EØS */                       
                             W_PÅ_ANV(2) =                                      
                                + W_PÅ_FAKTISKE(2)                              
                                + W_PÅ_FRAMTIDIGE(2);                           
                             W_PÅ_ETTER_91(2) =                                 
                                + W_PÅ_ETTER_91(2)                              
                                + W_PÅ_FRAMTIDIGE(2)                            
                                + W_PÅ_ETTER_91(1)                              
                                - W_PÅ_FRAMTIDIGE(1);                           
                          END; /* UNDER 40 POENGÅR  EØS */                      
                    END;                                                        
                 WHEN (3) /* PRO RATA 2 */                                      
                    DO;                                                         
                    END;                                                        
                 WHEN (4) /* NORDISK */                                         
                    DO;                                                         
                       IF W_PÅ_RETT_TIL_FRAMTIDIGE(I) THEN                      
                          DO;  /* HAR RETT TIL FRAMTIDIGE POENG */              
                             IF (W_TT_FRAMTIDIG_MND(1) =                        
                                 TT_FRAMT_MAX          ) THEN                   
                                W_PÅ_FRAMTIDIGE(4) =                            
                                   SM_BUP_ÅR - SM_PÅ;                           
                             ELSE                                               
                                DO;                                             
                                   W_PÅ_FRAMTIDIGE(4) =                         
                                      W_TT_FRAMTIDIG_MND(1)/12+0.5;             
                                   W_REDUSERT_ANT_BUP_ÅR(4) =                   
                                      W_PÅ_FRAMTIDIGE(4);                       
                                END;                                            
                             IF (W_PÅ_FAKTISKE(1) > 0 &                         
                                 W_PÅ_FAKTISKE(4) > 0 ) THEN                    
                                W_PÅ_FRAMTIDIGE(4) =                            
                                     W_PÅ_FRAMTIDIGE(4)                         
                                   * W_PÅ_FAKTISKE(1)                           
                                   / ( W_PÅ_FAKTISKE(4)                         
                                      +W_PÅ_FAKTISKE(1))                        
                                   + 0.5;                                       
                             W_PÅ_ANV(4) =                                      
                                + W_PÅ_FAKTISKE(1)                              
                                + W_PÅ_FRAMTIDIGE(4);                           
                          END; /* HAR RETT TIL FRAMTIDIGE POENG */              
                       ELSE                                                     
                          DO;  /* IKKE RETT TIL FRAMTIDIGE POENG */             
                             /* REMEDY 2201 */                                  
                             IF (B02.YNGSTE_BARN_FÅR(BER_SØ_IND)                
                                       +6 > SM_PÅ) THEN                         
                                W_PÅ_FRAMTIDIGE(1) =                            
                                   (B02.YNGSTE_BARN_FÅR(BER_SØ_IND)             
                                   +6 - SM_PÅ);                                 
                             ELSE                                               
                             /* REMEDY 2201 */                                  
                                W_PÅ_FRAMTIDIGE(4) = 0;                         
                             W_PÅ_ANV(4) =                                      
                                + W_PÅ_FAKTISKE(1)                              
                                + W_PÅ_FRAMTIDIGE(4);                           
                           /*W_PÅ_ANV(4) = W_PÅ_FAKTISKE(1); */                 
                          END; /* IKKE RETT TIL FRAMTIDIGE POENG */             
                       IF W_PÅ_ANV(4) > 40 THEN                                 
                          DO;                                                   
                             W_PÅ_ANV(4) = 40;                                  
                             W_PÅ_ETTER_91(4) =                                 
                                  40                                            
                                - W_PÅ_FAKTISKE(1)                              
                                + W_PÅ_ETTER_91(1);                             
                          END;                                                  
                       ELSE                                                     
                          DO;                                                   
                             W_PÅ_ETTER_91(4) =                                 
                                  W_PÅ_ANV(4)                                   
                                - W_PÅ_FAKTISKE(1)                              
                                + W_PÅ_ETTER_91(1);                             
                          END;                                                  
                    END;                                                        
                 OTHERWISE;                                                     
              END; /* SELECT I */                                               
              IF W_PÅ_ANV(I) > 40 THEN                                          
                 DO;                                                            
                    W_PÅ_ETTER_91(I) =                                          
                       + W_PÅ_ETTER_91(I)                                       
                       - W_PÅ_ANV(I)                                            
                       + 40;                                                    
                    W_PÅ_ANV(I) = 40;                                           
                 END;                                                           
              IF W_STI_ÅM < 199200 THEN                                         
                 W_PÅ_ETTER_91(I) = 0;                                          
           END; /* W_SKAL_BEREGNES(I) */                                        
     END; /* DO I = 1 TO 4 */                                                   
  END REGN_POENGÅR;                                                             
  /* *************************************************************** */         
  REGN_BUP_SPT : PROC;                                                          
     /* ------------------------------------------------------------ */         
     /*  DEKLARERER VARIABLER SOM KUN BENYTTES I DENNE PROCEDURE     */         
     /* ------------------------------------------------------------ */         
     DCL BEREGNET_BUP          FIXED DEC (03,2) INIT(0    ) ,                   
         I                     FIXED BIN (15  ) INIT(0    ) ,                   
         J                     FIXED DEC ( 5  ) INIT(0    ) ,                   
         K                     FIXED BIN (15  ) INIT(1967 ) ,                   
         L                     FIXED BIN (15  ) INIT(0    ) ,                   
         M                     FIXED BIN (15  ) INIT(0    ) ,                   
         HALVPARTEN            PIC       '(3)9' INIT(1    ) ,                   
         POENG_SISTE_3ÅR       FIXED DEC (05,3) INIT(0    ) ,                   
         RUNDING               FIXED DEC (05,3) INIT(0.005) ,                   
         SORTERTE_POENG(50)    FIXED DEC (05,3)             ,                   
         SORTERTE_POENG_92(50) FIXED DEC (05,3)             ,                   
         SUM_OPT               FIXED DEC (07,3) INIT (0   ) ,                   
         SUM_POENGTALL         FIXED DEC (07,3) INIT (0   ) ,                   
         SUM_POENGÅR           FIXED DEC (03  ) INIT (0   ) ;                   
     /* ------------------------------------------------------------ */         
     /* VI LEGGER POENGREKKA INN I ARBEIDSVARIABLENE                 */         
     /* ------------------------------------------------------------ */         
     SORTERTE_POENG(*) = 0;                                                     
     SORTERTE_POENG_92(*) = 0;                                                  
     /* ------------------------------------------------------------ */         
     /*  BEREGNER SNITTET AV DE TRE SISTE POENGÅRENE, HER ER ENNO    */         
     /*  IKKJE LAGT INN EVT. KORREKSJON FOR OMSORGSPOENG             */         
     /* ------------------------------------------------------------ */         
     POENG_SISTE_3ÅR =                                                          
        + (W_POREKKE.POENG(SM_PÅ - 2)                                           
        +  W_POREKKE.POENG(SM_PÅ - 1)                                           
        +  W_POREKKE.POENG(SM_PÅ    )   ) / 3 + RUNDING;                        
     HALVPARTEN =                                                               
        MIN((SM_PÅ - FM_PÅ + 1)/2 + 0.5                                         
           ,20);                                                                
     /* ------------------------------------------------------------ */         
     /*  SORTERER ALLE POENGÅR SLIK AT ELEMENT 1 INNEHOLDER HØYESTE  */         
     /* ------------------------------------------------------------ */         
     DO I = 1 TO 50;                                                            
        DO J = FM_PÅ TO SM_PÅ;                                                  
           IF W_POREKKE.POENG(J) > SORTERTE_POENG(I) THEN                       
              DO;                                                               
                 SORTERTE_POENG(I) = W_POREKKE.POENG(J);                        
                 K = J;                                                         
              END;                                                              
        END;                                                                    
        IF K < 1992 THEN                                                        
           IF SORTERTE_POENG(I) > 5.00 THEN                                     
              IF SORTERTE_POENG(I) > 7.00 THEN                                  
                 SORTERTE_POENG_92(I) =                                         
                    + SORTERTE_POENG(I) - 7 + 5 +2/3 + RUNDING;                 
              ELSE                                                              
                 SORTERTE_POENG_92(I) =                                         
                    + 5                                                         
                    + (SORTERTE_POENG(I)-5) * 1/3 + RUNDING;                    
           ELSE                                                                 
              SORTERTE_POENG_92(I) = SORTERTE_POENG(I);                         
        ELSE                                                                    
           SORTERTE_POENG_92(I) = SORTERTE_POENG(I);                            
        W_POREKKE.POENG(K) = 0;                                                 
     END; /*DO I=1 ANTALL MULIGE POENGÅR */                                     
     /* ------------------------------------------------------------- */        
     BEREGNET_BUP = B02.UFØRHIST.BUP(BER_SØ_IND,UF_IND);                        
     /* ------------------------------------------------------------- */        
     IF W_SKAL_BEREGNES(2) THEN                                                 
        DO;                                                                     
           /* SLUTTPOENGTALL - EØS REGNES AV FAKTISKE POENG */                  
           SUM_POENGÅR = MIN(W_PÅ_FAKTISKE(1),20);                              
           SUM_POENGTALL,                                                       
           SUM_OPT       = 0;                                                   
           IF (SUM_POENGÅR > 0) THEN                                            
              DO;                                                               
                 SUM_POENGTALL = 0;                                             
                 DO I = 1 TO SUM_POENGÅR;                                       
                    SUM_POENGTALL = SUM_POENGTALL + SORTERTE_POENG(I);          
                    IF SORTERTE_POENG(I) > 4 THEN                               
                       SUM_OPT = SUM_OPT + 4;                                   
                    ELSE                                                        
                       SUM_OPT = SUM_OPT + SORTERTE_POENG(I);                   
                 END;                                                           
                 IF B02.SPESOPPL.OVERKOMP_UTL(BER_SØ_IND) = 'J' THEN            
                    W_OPT(2) = SUM_OPT/SUM_POENGÅR + RUNDING;                   
                 W_SPT(2) = SUM_POENGTALL/SUM_POENGÅR + RUNDING;                
                 IF W_FNR_R13.ÅR > 1936        THEN                             
                    W_OPT(2) = 0;                                               
              END;                                                              
        END;                                                                    
     /* ------------------------------------------------------------- */        
     IF W_SKAL_BEREGNES(4) THEN                                                 
        DO;  /* SLUTTPOENGTALL NORDISK - FAKTISKE & FRAMTIDIGE POENG */         
           /* ------------------------------------------------------- */        
           /* SLUTTPOENG SKAL BEREGNES UTEN AT VI TAR HENSYN TIL      */        
           /* AVKORTING ETTER ARTIKKEL 15 - BRUKER DERFOR ORDINÆRE    */        
           /* ------------------------------------------------------- */        
           W_OPT(4)= B02.UFØRPENS.OPT(BER_SØ_IND);                              
           W_SPT(4)= B02.UFØRPENS.SPT(BER_SØ_IND);                              
        END; /* SLUTTPOENGTALL NORDISK - FAKTISKE & FRAMTIDIGE POENG */         
     /* ------------------------------------------------------------- */        
     IF W_SKAL_BEREGNES(1) THEN                                                 
        DO;  /* FOLKETRYGD SKAL BEREGNES */                                     
           W_OPT(1)= B02.UFØRPENS.OPT(BER_SØ_IND);                              
           W_SPT(1)= B02.UFØRPENS.SPT(BER_SØ_IND);                              
        END; /* FOLKETRYGD SKAL BEREGNES */                                     
     /* --------------------------------------------------------- */            
     /* OPPDATERER TAB MED U54 DERSOM FRAMTIDIGE POENGÅR          */            
     /* --------------------------------------------------------- */            
     DO I = 1 TO W_REDUSERT_ANT_BUP_ÅR(1);                                      
        TAB.POENG_KODE(I+SM_PÅ) = 'U54';                                        
     END;                                                                       
  END REGN_BUP_SPT;                                                             
  /* ************************************************************ */            
  FINN_BELØP : PROC;                                                            
     IF (B02.PENSJONSTYPE2(BER_SØ_IND) = 'A') THEN                              
        DO;  /* ATTFØRINGSPENGER*/                                              
           W_GP(1) = DIV_PERIODE.ATTFØRING_GP_ÅRSBELØP;                         
           W_TP(1) = DIV_PERIODE.ATTFØRING_TP_ÅRSBELØP;                         
        END; /* ATTFØRINGSPENGER*/                                              
     ELSE                                                                       
        DO;  /* UFØREPENSJON */                                                 
           W_GP(1) = B02.UFØRPENS.GP(BER_SØ_IND);                               
           W_TP(1) = B02.UFØRPENS.TP(BER_SØ_IND);                               
           W_ST(1) = B02.UFØRPENS.ST(BER_SØ_IND);                               
        END; /* UFØREPENSJON */                                                 
     /* ------------------------------------------------------------- */        
     DO I = 1 TO 4;                                                             
        IF W_SKAL_BEREGNES(I) THEN                                              
           DO;  /* SKAL BEREGNES */                                             
              IF B02.SPESOPPL.IKKE_GP(BER_SØ_IND) ^= 'J' THEN                   
                 DO;  /* BEREGN TEORETISK GP OG ST.GRL */                       
                    W_GP_TEORETISK(I),                                          
                    W_ST_TEORETISK(I),                                          
                    TEORETISK_GP           =G;                                  
                                                                                
                    /* ---------------------------------------------- */        
                    /* REDUKSJON PGA TRYGDETID UNDER 40 ÅR?          */         
                    /* ---------------------------------------------- */        
                    IF (W_TT_ANV_ÅR(I) ^= 40) THEN                              
                       TEORETISK_GP =                                           
                            TEORETISK_GP                                        
                          * W_TT_ANV_ÅR(I)                                      
                          / 40 + 0.005;                                         
                                                                                
                    /* ---------------------------------------------- */        
                    /* REDUKSJON PGA UFØREGRAD UNDER 100%?            */        
                    /* ---------------------------------------------- */        
                    IF (B02.UFØRPENS.UFG(BER_SØ_IND) ^= 100) THEN               
                       TEORETISK_GP =                                           
                         (   TEORETISK_GP                                       
                           * B02.UFØRPENS.UFG(BER_SØ_IND)                       
                         ) / 100 + 0.005;                                       
                                                                                
                    W_GP_TEORETISK(I) = TEORETISK_GP;                           
                    W_ST_TEORETISK(I) = TEORETISK_GP;                           
                                                                                
                    W_GP(I) = W_GP_TEORETISK(I);                                
                                                                                
                    /* ---------------------------------------------- */        
                    /* RED. PRORATA PGA ULIK NORSK OG FAKTISK TT?   */          
                    /* ---------------------------------------------- */        
                    IF (I=2 /* ! = = 3 */                         ) &           
                       (W_TT_FAKTISK_MND(2) ^= W_TT_FAKTISK_MND(1)) &           
                       (B02.PENSJONSTYPE2(BER_SØ_IND)     ^= 'A') THEN          
                       /* ------------------------------------------- */        
                       /* SJÅ RUNDSKRIV EØS-AVTALENS TRYGDEDEL,       */        
                       /* PUNKT 14.2.2, EKSEMPEL VII.                 */        
                       /* ------------> IKKE VED ATTFØRINGSPENGER     */        
                       /* ------------------------------------------- */        
          DO;                                                                   
                                                                                
      /*HER ER LAGT INN GARANTI FOR PERSONER SOM TIDLIGERE    */                
      /*HAR FÅTT GP/ST BEREGNET MED TT OMGJORT FRA MND TIL ÅR */                
                                                                                
                    IF B02.EØS_GARANTI(BER_SØ_IND) = 'B'  !                     
                       B02.EØS_GARANTI(BER_SØ_IND) = 'C'   THEN                 
                       DO;                                                      
                          GARANTI_NORSK_ÅR =                                    
                            ((W_TT_FAKTISK_MND(1) + 6) / 12);                   
                          W_TT_FAKTISK_MND(1) = GARANTI_NORSK_ÅR * 12;          
                          GARANTI_EØS_ÅR   =                                    
                            ((W_TT_FAKTISK_MND(2) + 6) / 12);                   
                          W_TT_FAKTISK_MND(2) = GARANTI_EØS_ÅR * 12;            
                       END;                                                     
                                                                                
                       W_ST_TEORETISK(2),                                       
                       W_GP_TEORETISK(2) =                                      
                          + W_GP_TEORETISK(2)                                   
                          * W_TT_FAKTISK_MND(1)                                 
                          / MIN(W_TT_FAKTISK_MND(2),480)                        
                          + 0.005;                                              
          END;                                                                  
                    IF B02.PENSJONSTYPE2(BER_SØ_IND)  ='S'!                     
 /* SAMBOERE 200107 HL : */                                                     
                      ((B02.RED_GP_3_2_5(BER_SØ_IND) = 'J'   !                  
                        B02.EK_INNT_OVER_2G(BER_SØ_IND)='J' ) &                 
                        B02.PENSJON_FØR_9802(BER_SØ_IND)^='J') THEN             
                  DO;                                                           
                       W_GP_TEORETISK(I) =                                      
                          W_GP_TEORETISK(I) * GP_PROSENT + 0.005;               
                  END;                                                          
                 END; /* BEREGN TEORETISK GP OG ST.GRL */                       
                                                                                
              /* ---------------------------------------------------- */        
                                                                                
      /*      IF (W_PÅ_FAKTISKE(1) > 0 &              */                        
              IF (W_PÅ_FAKTISKE(1) > 0   !                                      
                  B02.UFØRHIST.BUP_GAR_KODE(BER_SØ_IND,1) = 'B') &              
                  W_PÅ_ANV(I)      > 2   THEN                                   
                 DO; /* BEREGN TILLEGGSPENSJON */                               
                    /* ---------------------------------------- */              
                    /* DET ER ET MINSTEVILKÅR AT ANTALL POENGÅR */              
                    /* FAKTISKE & FRAMTIDIGE MINST ER 3 ÅR!!!!! */              
                    /* ---------------------------------------- */              
                    W_TP_TEORETISK(I) = F_TP_AARSBEL_M_2_DES                    
                       (W_FNR_R13.ÅR,                                           
                        G,                                                      
                        W_SPT(I),                                               
                        W_OPT(I),                                               
                        W_PÅ_ANV(I),                                            
                        W_PÅ_ETTER_91(I),                                       
                        W_UFG_5_2,                                              
                        100);       /* 4186 */                                  
                                                                                
                    IF (B02.PENSJONSTYPE2(BER_SØ_IND) = 'A') THEN               
                       /* ------------------------------------- */              
                       /* ATTFØRINGSPENGER REGNES I ÅRSBELØP OG */              
                       /* DELES DERETTER I 90'                  */              
                       /* ------------------------------------- */              
                       W_TP(I)                                                  
                          = W_TP_TEORETISK(I) + 0.5;                            
                    ELSE                                                        
                    IF (I = 1 !                                                 
                        I = 4 ) THEN                                            
                       W_TP(I) =                                                
                          W_TP_TEORETISK(I) /12 + 0.5;                          
                    ELSE                                                        
                       DO;                                                      
                          W_TP(I) =                                             
                             + W_TP_TEORETISK(I)                                
                             * W_PÅ_FAKTISKE(1)                                 
                             / W_PÅ_FAKTISKE(I)                                 
                             / 12                                               
                             + 0.5;                                             
                       END;                                                     
                 END; /* BEREGN TILLEGGSPENSJON */                              
              /* ---------------------------------------------------- */        
                                                                                
              IF B02.PENSJONSTYPE2(BER_SØ_IND) = 'A' THEN                       
                 DO;                                                            
                    W_ST(I),                                                    
                    W_ST_TEORETISK(I) = 0;                                      
                 END;                                                           
              ELSE                                                              
                 DO;  /* KAN FYLLE VILKÅRA FOR SÆRTILLEGG */                    
                    SELECT (B02.PENSJONSTYPE2(BER_SØ_IND));                     
                       WHEN ('F')                                               
                          DO;                                                   
                             ALDER_EK =                                         
                                F_ALDER(W_FNR_EK,(W_DATO_HÅMD));                
                             IF ((ALDER_EK       >= 6000 &                      
                                  W_DATO_HÅMD    >  19890300 &                  
                                  W_DATO_HÅMD    <  19910000 ) !                
                                 (ALDER_EK       >= 6001 &                      
                                  W_DATO_HÅMD    >  19910000 ) ) THEN           
                                ST_PROSENT = ST_PROSENT_ENSLIG * 2;             
                             ELSE                                               
                                ST_PROSENT = ST_PROSENT_ENSLIG;                 
                          END;                                                  
                       WHEN ('S')                                               
                          DO;                                                   
                            TEORETISK_GP=TEORETISK_GP*GP_PROSENT;               
                            IF B02.PENSJONSTYPE3(BER_EK_IND) = 'K' !            
                               B02.PENSJONSTYPE3(BER_EK_IND) = 'S' THEN         
                               ST_PROSENT = ST_PROSENT_ENSLIG;                  
                            ELSE                                                
                               ST_PROSENT = ST_PROSENT_SAMST;                   
                          END;                                                  
                       OTHERWISE                                                
                          DO;      /*9710 HL : */                               
                             ST_PROSENT = ST_PROSENT_ENSLIG;                    
 /*TILLEGG 9801 HL : */   END;                                                  
                    END; /* SELECT PENSJONSTYPE2(BER_SØ_IND) */                 
                    /* ---------------------------------------------- */        
                    IF B02.PENSJONSTYPE3(BER_EK_IND) = 'K'  &                   
                       B02.UTTAKSDATO_ÅMD(BER_EK_IND) > 19970900 &              
                       B02.SIVILSTAND (BER_SØ_IND) ^= 'A' &                     
                       ALDER_EK < 6501          THEN                            
                       DO;                                                      
                          TEORETISK_GP=TEORETISK_GP*GP_PROSENT;                 
                          DIV_PARAM_OMR                                         
                             .REDUSERT_GP_2G(BER_SØ_IND) = 'K';                 
                       END;                                                     
                    ELSE                                                        
 /* HIT 9801 HL */                                                              
                    IF B02.PENSJON_FØR_9802(BER_SØ_IND) ^= 'J' THEN             
                       DO;  /* FYLLER VILKÅRA FOR PENSJON ETTER 9801 */         
                          IF B02.EK_INNT_OVER_2G (BER_SØ_IND)  = 'J'  &         
                             (B02.SIVILSTAND(BER_SØ_IND) = 'G' !                
                              B02.SIVILSTAND(BER_SØ_IND) = 'P' !                
                              B02.SIVILSTAND(BER_SØ_IND) = 'V' !                
                              B02.SIVILSTAND(BER_SØ_IND) = 'W' )   THEN         
                             DO;                                                
                                DIV_PARAM_OMR                                   
                                   .REDUSERT_GP_2G(BER_SØ_IND) = 'J';           
                                TEORETISK_GP=TEORETISK_GP*GP_PROSENT;           
                             END;                                               
                          ELSE    /*200107 : */                                 
                          IF B02.RED_GP_3_2_5 (BER_SØ_IND)  = 'J'  &            
                             (B02.SIVILSTAND(BER_SØ_IND) = 'S' !                
                              B02.SIVILSTAND(BER_SØ_IND) = 'A' !                
                              B02.SIVILSTAND(BER_SØ_IND) = 'V' !                
                              B02.SIVILSTAND(BER_SØ_IND) = 'U' !                
                              B02.SIVILSTAND(BER_SØ_IND) = 'E' )   THEN         
                             DO;                                                
                                DIV_PARAM_OMR                                   
                                   .REDUSERT_GP_2G(BER_SØ_IND) = 'X';           
                                TEORETISK_GP=TEORETISK_GP*GP_PROSENT;           
                             END;                                               
                          ELSE                                                  
                          IF B02.SIVILSTAND(BER_SØ_IND) = 'V'  &                
                            (B02.PENSJONSTYPE2(BER_EK_IND) = 'P' !              
                             B02.PENSJONSTYPE2(BER_EK_IND) = 'S') THEN          
                             DO;                                                
                                DIV_PARAM_OMR                                   
                                   .REDUSERT_GP_2G(BER_SØ_IND) = 'V';           
                                TEORETISK_GP=TEORETISK_GP*GP_PROSENT;           
                             END;                                               
                       END; /* FYLLER VILKÅRA FOR PENSJON ETTER 9801 */         
                                                                                
                    /* TERMINBELØP BEREGNES OG AVRUNDES TIL HELE KR */          
                    W_GP(I) = W_GP_TEORETISK(I)/12+0.5;                         
                                                                                
                    W_ST_TEORETISK(I) =                                         
                       W_ST_TEORETISK(I) * ST_PROSENT / 100 + 0.005;            
                    W_ST(I)                                                     
                       = W_ST_TEORETISK(I) / 12 + 0.5;                          
                    W_ST_TEORETISK(I)                                           
                       = W_ST_TEORETISK(I)/12 + 0.5;                            
                                                                                
                    /* PRO RATA ST REDUSERES FOR TP */                          
                    IF W_ST(I) > W_TP(I) THEN                                   
                       W_ST(I) = W_ST(I) - W_TP(I);                             
                    ELSE                                                        
                       W_ST(I) = 0;                                             
                                                                                
                    /* TEORETISK ST REDUSERES FOR TP */                         
                    IF W_ST_TEORETISK(I) > W_TP_TEORETISK(I) THEN               
                       W_ST_TEORETISK(I) =                                      
                          W_ST_TEORETISK(I) - W_TP_TEORETISK(I);                
                    ELSE                                                        
                       W_ST_TEORETISK(I) = 0;                                   
                 END; /* KAN FYLLE VILKÅRA FOR SÆRTILLEGG */                    
           END; /* SKAL BEREGNES */                                             
     END; /* DO I = 1 TO 4  */                                                  
                                                                                
     DO I = 1 TO 4;                                                             
        IF (W_SKAL_BEREGNES(I)) THEN                                            
           W_SUM_PENSJON(I) =                                                   
             W_GP(I) +                                                          
             W_TP(I) +                                                          
             W_ST(I) +                                                          
             W_BT(I) +                                                          
             W_ET(I);                                                           
     END; /* DO I = 1 TO 4  */                                                  
                                                                                
  IF (B02.BEREGN_FOLKETRYGD(BER_SØ_IND) = 'J') &                                
     (B02.PENSJONSTYPE2(BER_SØ_IND) ^= 'A')    THEN                             
     DO; /* REGN UT EVT. FORSØRGINGSTILLEGG */                                  
        /* ---------------------------------------------------------- */        
        /* FORSØRGINGSTILLEGG VERT KUN BEREGNA AV UFØREPENSJON EVT.   */        
        /* FORSØRGINGSTILLEGG I ATTFØRINGSPENGER VERT UTREKNA I INFO. */        
        /* VI REKNER FORELØBIG IKKJE UT FORSØRGINGSTILLEGG UANSETT... */        
        /* ---------------------------------------------------------- */        
        W_FOLKETRYGD =                                                          
           + W_GP(1)                                                            
           + W_ST(1)                                                            
           + W_TP(1);                                                           
        /* CALL FORSØRG_PRO_RATA(BER_SØ_IND)SEMIK/* 4012 */                     
     END; /* REGN UT EVT. FORSØRGINGSTILLEGG */                                 
                                                                                
  END FINN_BELØP;                                                               
 /* ***************************************************************** */        
  FINN_HØGSTE_YTELSE : PROC;                                                    
     IF (W_SKAL_BEREGNES(4)                      !                              
         B02.BEREGN_FOLKETRYGD(BER_SØ_IND) = 'N' ) THEN                         
        W_SKAL_BEREGNES(1) = '0'B;                                              
     DO I = 1,4,2,3;                                                            
        IF W_SKAL_BEREGNES(I) THEN                                              
           IF ^(HØGSTE = 0) THEN                                                
              IF W_SUM_PENSJON(I) > W_SUM_PENSJON(HØGSTE) THEN                  
                 DO                                                             
                    NEST_HØGSTE = HØGSTE;                                       
                    HØGSTE      = I;                                            
                 END;                                                           
              ELSE                                                              
                 IF ^(NEST_HØGSTE = 0) THEN                                     
                    IF W_SUM_PENSJON(I) > W_SUM_PENSJON(NEST_HØGSTE)            
                       THEN NEST_HØGSTE = I;                                    
                    ELSE;                                                       
                 ELSE                                                           
                    NEST_HØGSTE=I;                                              
           ELSE                                                                 
              HØGSTE=I;                                                         
     END;                                                                       
     IF (W_SKAL_BEREGNES(1) &                                                   
         HØGSTE ^= 1        ) THEN                                              
        NEST_HØGSTE = 1;                                                        
  /*FOR Å UNNGÅ ABEND :                                                         
     IF HØGSTE = 0            THEN                                              
        HØGSTE = 2;        */                                                   
                                                                                
  END FINN_HØGSTE_YTELSE;                                                       
 /* ***************************************************************** */        
  OPPDATER_ATTFØRING : PROC;                                                    
                                                                                
     /* ------------------------------------------------------------- */        
     /* HØGSTE ALTERNATIV LEGGES INN I EØS OMRÅDET NB!                */        
     /*        HØGSTE GP LIGG I DIV_PERIODE.ATTFØRING_GP_ÅRSBELØP     */        
     /*        HØGSTE TP LIGG I DIV_PERIODE.ATTFØRING_TP_ÅRSBELØP     */        
     /* ------------------------------------------------------------- */        
     DIV_PERIODE.ATTFØRING_GP_ÅRSBELØP = W_GP(HØGSTE);                          
     DIV_PERIODE.ATTFØRING_TP_ÅRSBELØP = W_TP(HØGSTE);                          
     /* ------------------------------------------------------------- */        
     /* NEST HØYESTE ALTERNATIV LEGGES INN I EØS-SEGMENTET ...        */        
     /* ------------------------------------------------------------- */        
     IF (NEST_HØGSTE>0) THEN                                                    
        DO;  /* HAR ALTERNATIV BEREGNING */                                     
           B02.ALTERNATIV_GP(BER_SØ_IND) = W_GP(NEST_HØGSTE);                   
           B02.ALTERNATIV_TP(BER_SØ_IND) = W_TP(NEST_HØGSTE);                   
           B02.ALTERNATIV_SUM_PENSJON(BER_SØ_IND)                               
              = W_SUM_PENSJON(NEST_HØGSTE);                                     
        END; /* HAR ALTERNATIV BEREGNING */                                     
     ELSE                                                                       
        DO;  /* HAR IKKE ALTERNATIV BEREGNING */                                
           B02.ALTERNATIV_GP(BER_SØ_IND),                                       
           B02.ALTERNATIV_TP(BER_SØ_IND),                                       
           B02.ALTERNATIV_SUM_PENSJON(BER_SØ_IND) = 0;                          
        END; /* HAR IKKE ALTERNATIV BEREGNING */                                
                                                                                
     B02.UFØRPENS.PÅ(BER_SØ_IND)   = W_PÅ_ANV(HØGSTE);                          
     B02.UFØRPENS.PÅ_ETTER91(BER_SØ_IND) = W_PÅ_ETTER_91(HØGSTE);               
                                                                                
     DIV_PARAM_OMR.TT_NORSK_FAKTISK = W_TT_FAKTISK_MND(1);                      
  END OPPDATER_ATTFØRING;                                                       
 /* ***************************************************************** */        
  OPPDATER_UFØRE : PROC;                                                        
     IF HØGSTE = 4 THEN                                                         
        DO;                                                                     
           TAB.POENG_KODE(SM_PÅ+1) = 'U54';                                     
           TAB.POENG_ANVENDT(SM_PÅ+1)                                           
              = B02.UFØRHIST.BUP(BER_SØ_IND,UF_IND);                            
        END;                                                                    
                                                                                
     /* ------------------------------------------------------------ */         
     /* OPPDATERER I EØSINFO SEGMENTET                               */         
     /* ------------------------------------------------------------ */         
     B02.ANTALL_NORSKE_PÅ(BER_SØ_IND) =                                         
        W_PÅ_FAKTISKE(1)+W_PÅ_FRAMTIDIGE(1);                                    
     B02.ANTALL_NORSKE_PÅ_ETTER91(BER_SØ_IND) =                                 
        W_PÅ_ETTER_91(1);                                                       
     IF W_SKAL_BEREGNES(4) THEN                                                 
        DO;  /* OPPDATER NORDISK KONVENSJON */                                  
           B02.TT_NORDISK_FRAMT_BRUTTO(BER_SØ_IND)                              
              = W_TT_FRAMTIDIG_MND(1);                                          
           B02.TT_NORDISK_FRAMT_NETTO(BER_SØ_IND)                               
              = W_TT_FRAMTIDIG_MND(4);                                          
           B02.PÅ_NORDISK_FRAMT_BRUTTO(BER_SØ_IND)                              
              = W_PÅ_FRAMTIDIGE(1);                                             
           B02.PÅ_NORDISK_FRAMT_NETTO(BER_SØ_IND)                               
              = W_PÅ_FRAMTIDIGE(4);                                             
           B02.SPT_NORDISK_ART15(BER_SØ_IND)                                    
              = W_SPT(4);                                                       
           B02.OPT_NORDISK_ART15(BER_SØ_IND)                                    
              = W_OPT(4);                                                       
        END; /* OPPDATER NORDISK KONVENSJON */                                  
                                                                                
     /* ------------------------------------------------------------ */         
     /* OPPDATERER I UFØREPENS SEGMENTET                             */         
     /* ------------------------------------------------------------ */         
     B02.UFØRPENS.ST(BER_SØ_IND)          = W_ST(HØGSTE);                       
                                                                                
     /* ------------------------------------------------------------ */         
     /* OPPDATERER I STATUS SEGMENT                                  */         
     /* ------------------------------------------------------------ */         
     IF W_SKAL_BEREGNES(4) THEN                                                 
        B02.TT_FRAMT(BER_SØ_IND)      = W_TT_FRAMTIDIG_MND(4);                  
     ELSE                                                                       
        IF W_SKAL_BEREGNES(1) THEN                                              
           B02.TT_FRAMT(BER_SØ_IND)      = W_TT_FRAMTIDIG_MND(1);               
  /* B02.TT_FRAMT(BER_SØ_IND)         = W_TT_FRAMTIDIG_MND(HØGSTE) */           
     B02.TT_ANV(BER_SØ_IND)           = W_TT_ANV_ÅR(HØGSTE);                    
     B02.SUM_YTELSE(BER_SØ_IND)       = W_SUM_PENSJON(HØGSTE)                   
                          /*      + B02.UFØRPENS.ET(BER_SØ_IND)                 
                                  + B02.UFØRPENS.BT(BER_SØ_IND)                 
                            REMEDY 2101   */                                    
                                  + B02.BT_SÆRBARN(BER_SØ_IND);                 
     IF HØGSTE = 2 ! HØGSTE = 3 THEN                                            
        B02.POENGTILLEGG_KODE             = ' ';                                
     B02.GARANTI_TP(BER_SØ_IND),                                                
     B02.GARANTI_DATO_ÅMD(BER_SØ_IND),                                          
     B02.GT_LOV92(BER_SØ_IND),                                                  
     B02.GT_TILLEGG_LOV92(BER_SØ_IND)     = 0;                                  
     /* ------------------------------------------------------------- */        
     /* OPPDATERER I UFØRHIST SEGMENTET                               */        
     /* ------------------------------------------------------------- */        
  /* B02.UFØRHIST.REDUSERT_ANT_BUP_ÅR(BER_SØ_IND,UF_IND) =                      
        W_REDUSERT_ANT_BUP_ÅR(HØGSTE);   OBS*/                                  
     IF W_PÅ_RETT_TIL_FRAMTIDIGE(HØGSTE) THEN                                   
        B02.UFØRHIST.REDUSERT_ANT_BUP_ÅR(BER_SØ_IND,UF_IND) =                   
           W_REDUSERT_ANT_BUP_ÅR(HØGSTE);                                       
     ELSE                                                                       
        B02.UFØRHIST.REDUSERT_ANT_BUP_ÅR(BER_SØ_IND,UF_IND) = 0;                
                                                                                
  /* B02.UFØRHIST.REDUSERT_ANT_BUP_ÅR(BER_SØ_IND,UF_IND) =                      
        W_REDUSERT_ANT_BUP_ÅR(HØGSTE);   OBS*/                                  
  END OPPDATER_UFØRE;                                                           
 /* ***************************************************************** */        
 OPPDATER_I_BEGGE_TILFELLER : PROC;                                             
     /* ------------------------------------------------------------ */         
     /* OPPDATERER I EØS SEGMENTET - SKAL OPPDATERES UANSETT         */         
     /* ------------------------------------------------------------ */         
     SELECT (HØGSTE);                                                           
        WHEN(1)                                                                 
           B02.EØSINFO.GUNSTIGSTE_ALTERNATIV(BER_SØ_IND) = '1';                 
        WHEN(2)                                                                 
           DO;  /* ART 46 NR 2 */                                               
              B02.EØSINFO.GUNSTIGSTE_ALTERNATIV(BER_SØ_IND) = '2';              
              TAB.POENG_KODE(SM_PÅ+1)     = '   ';                              
              TAB.POENG_ANVENDT(SM_PÅ+1) = 0;                                   
              IF ^(B02.EØS_GARANTI(BER_SØ_IND) = 'A'  !                         
                   B02.EØS_GARANTI(BER_SØ_IND) = 'C'   )  THEN                  
                   POREKKE.POENG(BER_SØ_IND,SM_PÅ+1) = 0;                       
           END; /* ART 46 NR 2 */                                               
        WHEN(3)                                                                 
           B02.EØSINFO.GUNSTIGSTE_ALTERNATIV(BER_SØ_IND) = '3';                 
        WHEN(4)                                                                 
           B02.EØSINFO.GUNSTIGSTE_ALTERNATIV(BER_SØ_IND) = '4';                 
        OTHER;                                                                  
     END; /* SELECT HØGSTE */                                                   
     IF (W_SKAL_BEREGNES(2)) THEN                                               
        DO;                                                                     
           B02.EØSINFO.TT_EØS_FRAMT_MND(BER_SØ_IND) =                           
              W_TT_FRAMTIDIG_MND(2);                                            
           B02.EØSINFO.TT_EØS_ANV_ÅR(BER_SØ_IND) =                              
              W_TT_ANV_ÅR(2);                                                   
           B02.EØSINFO.TT_EØS_ANV_MND(BER_SØ_IND) =                             
              W_TT_ANV_MND(2);                                                  
   /* DISSE FELTENE BLE TIDLIGERE OPPDATERT MED NEST HØYESTE ALT:*/             
              B02.SPT_EØS(BER_SØ_IND) = W_SPT(2);                               
              B02.OPT_EØS(BER_SØ_IND) = W_OPT(2);                               
              B02.PÅ_EØS(BER_SØ_IND) = W_PÅ_ANV(2);                             
              B02.PÅ_EØS_FRAMT(BER_SØ_IND)                                      
                 = W_PÅ_FRAMTIDIGE(2);                                          
              B02.PÅ_EØS_ETTER91(BER_SØ_IND)                                    
                 = W_PÅ_ETTER_91(2);                                            
   /* HIT 9908 HL ********************************* */                          
        END;                                                                    
     IF (W_SKAL_BEREGNES(1) ! W_SKAL_BEREGNES(4) ) THEN                         
        IF ^(NEST_HØGSTE = 0) THEN                                              
           DO;  /* HAR ALTERNATIV BEREGNING */                                  
              B02.ALTERNATIV_GP(BER_SØ_IND) = W_GP(NEST_HØGSTE);                
              B02.ALTERNATIV_TP(BER_SØ_IND) = W_TP(NEST_HØGSTE);                
              B02.ALTERNATIV_ST(BER_SØ_IND) = W_ST(NEST_HØGSTE);                
              B02.ALTERNATIV_BT(BER_SØ_IND) = 0;                                
                 /* + B02.UFØRPENS.BT(BER_SØ_IND)                               
                    + B02.GARANTITILLEGG_BARN(BER_SØ_IND)                       
                    + B02.GARANTITILLEGG_SÆRBARN(BER_SØ_IND)                    
                    + B02.BT_SÆRBARN(BER_SØ_IND)SEMIKOLON */                    
              B02.ALTERNATIV_ET(BER_SØ_IND) = 0;                                
                 /* + B02.UFØRPENS.ET(BER_SØ_IND)                               
                    + B02.GARANTITILLEGG_EK(BER_SØ_IND)SEMIKOLON */             
              B02.ALTERNATIV_SUM_PENSJON(BER_SØ_IND) =                          
                 W_SUM_PENSJON(NEST_HØGSTE);                                    
           END; /* HAR ALTERNATIV BEREGNING */                                  
        ELSE                                                                    
           CALL NULLSTILL_FOLKETRYGD;                                           
     ELSE                                                                       
        CALL NULLSTILL_FOLKETRYGD;                                              
     /* ------------------------------------------------------------ */         
     /* OPPDATERER I UFØREPENS SEGMENTET - SKAL OPPDATERES UANSETT   */         
     /* ------------------------------------------------------------ */         
     B02.UFØRPENS.GP(BER_SØ_IND)          = W_GP(HØGSTE);                       
     B02.UFØRPENS.TP(BER_SØ_IND)          = W_TP(HØGSTE);                       
                                                                                
     IF W_SPT(HØGSTE) > 0 THEN                                                  
        DO;  /* HAR RETT TIL TP */                                              
           B02.UFØRPENS.SPT(BER_SØ_IND)       = W_SPT(HØGSTE);                  
           B02.UFØRPENS.OPT(BER_SØ_IND)       = W_OPT(HØGSTE);                  
           B02.UFØRPENS.PÅ(BER_SØ_IND)        = W_PÅ_ANV(HØGSTE);               
           B02.UFØRPENS.PÅ_ETTER91(BER_SØ_IND) = W_PÅ_ETTER_91(HØGSTE);         
       /*  IF B02.UFT_ÅMD(BER_SØ_IND,1) < 19920000 THEN                         
              DO      SKAL KUN FYLLES UT VED UFT FØR 1992                       
                 B02.UFØRPENS.SPT_1291(BER_SØ_IND)                              
                    = W_SPT(HØGSTE)                                             
                 B02.UFØRPENS.OPT_1291(BER_SØ_IND)                              
                    = W_OPT(HØGSTE)                                             
                 B02.UFØRPENS.PÅ_1291 (BER_SØ_IND)                              
                    = W_PÅ_ANV(HØGSTE)                                          
                    - W_PÅ_ETTER_91(HØGSTE)                                     
              END     SKAL KUN FYLLES UT VED UFT FØR 1992                       
                      DETTE BLEI FEIL IFLG HERMAN 290798 */                     
           IF HØGSTE = 2                   THEN                                 
              DO;                     /*9907*/                                  
                 B02.SPT_EØS      (BER_SØ_IND) =                                
                    W_SPT(2);                                                   
                 B02.OPT_EØS      (BER_SØ_IND) =                                
                    W_OPT(2);                                                   
              END;                                                              
        END; /* HAR RETT TIL TP */                                              
     ELSE                                                                       
        DO;  /* IKKE RETT TIL TP */                                             
           B02.UFØRPENS.SPT(BER_SØ_IND),                                        
           B02.UFØRPENS.OPT(BER_SØ_IND),                                        
           B02.UFØRPENS.PÅ(BER_SØ_IND),                                         
           B02.UFØRPENS.PÅ_ETTER91(BER_SØ_IND),                                 
           B02.UFØRPENS.PÅ_1291(BER_SØ_IND),                                    
           B02.UFØRPENS.SPT_1291(BER_SØ_IND),                                   
           B02.UFØRPENS.OPT_1291(BER_SØ_IND)    = 0;                            
        END; /* IKKE RETT TIL TP */                                             
     IF W_SKAL_BEREGNES(1) THEN                                                 
        B02.STATUS.TT_ANV(BER_SØ_IND) = W_TT_ANV_ÅR(1);                         
     ELSE                                                                       
     IF W_SKAL_BEREGNES(4) THEN                                                 
        B02.STATUS.TT_ANV(BER_SØ_IND) = W_TT_ANV_ÅR(4);                         
     ELSE                                                                       
        B02.STATUS.TT_ANV(BER_SØ_IND) = W_TT_ANV_ÅR(HØGSTE);                    
     IF (W_SKAL_BEREGNES(1) !                                                   
         W_SKAL_BEREGNES(4) !                                                   
         W_SKAL_BEREGNES(2) ) THEN                                              
        B02.TT_FAKTISK_NORSK_MND(BER_SØ_IND) = W_TT_FAKTISK_MND(1);             
 END OPPDATER_I_BEGGE_TILFELLER;                                                
 /* ----------------------------------------------------------------- */        
  NULLSTILL_FOLKETRYGD:PROC;                                                    
     /* ------------------------------------------------------------- */        
     /* BENYTTES NÅR BO2.BEREGN_FOLKETRYGD(BER_SØ_IND) ^= 'J'         */        
     /* NULLSTILLER ALLE FOLKETRYGDVARIABLER I BO2                    */        
     /* ------------------------------------------------------------- */        
     B02.ALTERNATIV_BT(BER_SØ_IND),                                             
     B02.ALTERNATIV_GP(BER_SØ_IND),                                             
     B02.ALTERNATIV_SUM_PENSJON(BER_SØ_IND),                                    
     B02.ALTERNATIV_ST(BER_SØ_IND),                                             
     B02.ALTERNATIV_TP(BER_SØ_IND),                                             
     B02.SPT_EØS(BER_SØ_IND),                                                   
     B02.OPT_EØS(BER_SØ_IND),                                                   
     B02.BT_SÆRBARN(BER_SØ_IND),                                                
     B02.GARANTITILLEGG_BARN(BER_SØ_IND),                                       
     B02.GARANTITILLEGG_SÆRBARN(BER_SØ_IND),                                    
     B02.ALTERNATIV_ET(BER_SØ_IND),                                             
   /*B02.TT_FAKTISK_NORSK_MND(BER_SØ_IND),              2006*/                  
     B02.GARANTITILLEGG_EK(BER_SØ_IND)             = 0;                         
  END NULLSTILL_FOLKETRYGD;                                                     
  /* ************************************************************ */            
  OPPDATER_DIV_PARAM_OMR : PROC;                                                
     /* ------------------------------------------------------------- */        
     /* POENGÅR VERT OPPDATERT                                        */        
     /* ------------------------------------------------------------- */        
     DIV_PARAM_OMR.PÅ_NORSK_FAKTISK(BER_SØ_IND)                                 
        = W_PÅ_FAKTISKE(1);                                                     
     DIV_PARAM_OMR.PÅ_EØS_FAKTISK(BER_SØ_IND)                                   
        = W_PÅ_FAKTISKE(2)-W_PÅ_FAKTISKE(1);                                    
     DIV_PARAM_OMR.PÅ_TEORETISK_FRAMT                                           
        = W_PÅ_FRAMTIDIGE(1);                                                   
     /* ------------------------------------------------------------- */        
     /* TRYGDETID VERT OPPDATERT                                      */        
     /* ------------------------------------------------------------- */        
  /* IF (B02.PENSJONSTYPE2(BER_SØ_IND) = 'A') THEN  */                          
        DIV_PARAM_OMR.TT_TEORETISK = W_TT_ANV_ÅR(2);                            
  /* ELSE                                                                       
        DIV_PARAM_OMR.TT_TEORETISK = W_TT_ANV_MND(2) */                         
                                                                                
    /* DENNE UTGÅR - VI BRUKER B02 - IKKE VED ATTFØRING, 130400 MARTIN          
     DIV_PARAM_OMR.TT_NORSK_FAKTISK = W_TT_FAKTISK_MND(1) */                    
     IF HØGSTE = 4             THEN  /*200308 HL */                             
        DIV_PARAM_OMR.TT_SUM_FAKTISK = W_TT_FAKTISK_MND(4);                     
     ELSE                                                                       
        DIV_PARAM_OMR.TT_SUM_FAKTISK = W_TT_TEORETISK_MND(2);                   
     /* ------------------------------------------------------------- */        
     /* TEORETISKE BELØP VERT OPPDATERT                               */        
     /* ------------------------------------------------------------- */        
     DIV_PARAM_OMR.TEORETISK_BELØP_GP = W_GP_TEORETISK(HØGSTE);                 
     DIV_PARAM_OMR.TEORETISK_BELØP_TP = W_TP_TEORETISK(HØGSTE);                 
  END OPPDATER_DIV_PARAM_OMR;                                                   
  /* =============================================================== */         
 /* % INCLUDE R0014012       FORSØRG_PRO_RATA(TYPE)                */           
   %INCLUDE R0014187;     /* F_TP_AARSBEL_M_2_DES                     */        
 /*%INCLUDE R0014186      /* F_TP92_DES                            */           
   %INCLUDE R0014471;     /* FINN_TT_FAKTISK (->W_TT_FAKTISK(*))   */           
   %INCLUDE R0014472;     /* NULLSTILL_P0019985                    */           
   %INCLUDE R0014475;     /* SPT_GAR                               */           
   %INCLUDE R0014573;     /* FYLLER_VILKÅR_3_17_NASJONAL              */        
   %INCLUDE R0014574;     /* FYLLER_VILKÅR_3_17_EØS                   */        
   %INCLUDE R0019905;     /* F_ALDER                               */           
   %INCLUDE R0019995;     /* KONV_FNR11_FNR13 BRUKES I 9905        */           
  /* =============================================================== */         
  END UPXEØS;                                                                   
