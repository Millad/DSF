 /*   SIST ENDRET PÅ PROD   2004.12.17 13.57.49 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.07.13 12.58.59 AV   JDA2970          */        
 /*       SIST ENDRET 04/05-98 12.40.37 AV   JDA7339                  */        
 /* ***************************************************************** */        
 /*IDENTIFIKASJON:                                                    */        
 /*    R0014123 _ BEREGN_APD_OVERGANG_FRA_UP - INTERN PROC I PLI.     */        
 /*    PROGRAMMERER: KRISTENSEN, APRIL 1982.                          */        
 /*HENSIKT:                                                           */        
 /*    PROGRAMMET BEREGNER APD VED OVERGANP FRA UP.                 . */        
 /*PROGRAMTILKNYTNING:                                                */        
 /*    PROGRAMMET BLIR INKLUDERT I OVERLIGGENDE PROGRAM.              */        
 /*BRUK:                                                              */        
 /*    CALL BEREGN_APD_OVERGANG_FRA_UP;                               */        
 /* ***************************************************************** */        
 BEREGN_APD_OVERGANG_FRA_UP: PROC;                                              
                                                                                
  DCL                                                                           
    I                    FIXED BIN(15),                                         
    J                    FIXED BIN(15),                                         
    K               FIXED BIN(15),                                              
    L               FIXED BIN(15),                                              
    UFG                  FIXED DEC(3),                                          
    UFT                  FIXED DEC(9),         /*2000*/                         
    W_APD_UP             FIXED DEC(3);                                          
                                                                                
    /*0697 HL:*/                                                                
    DO K = 7 TO 1 BY -1 UNTIL   (B02.UFØRHIST.UFT_ÅMD     /*2000*/              
                               (BER_SØ_IND,K) > 0);                             
      UFT =  B02.UFØRHIST.UFT_ÅMD(BER_SØ_IND,K);          /*2000*/              
      I = K;                                                                    
    END;                                                                        
                                                                                
    IF B02.UFØRHIST.UFØRGRAD.UFG_ØVRIGE(BER_SØ_IND,I,1) = 0 THEN                
     UFG = B02.UFØRHIST.UFG(BER_SØ_IND,I);                                      
    ELSE                                                                        
    /*0697 HL: + JFA 2.4.04 */                                                  
     DO L = HBOUND(B02.UFØRGRAD,3) TO 1 BY -1 UNTIL                             
  /* DO L = 12 TO 1 BY -1 */  (B02.UFØRHIST.UFØRGRAD.UFG_ØVRIGE                 
                                (BER_SØ_IND,I,L) > 0);                          
        UFG = B02.UFØRHIST.UFØRGRAD.UFG_ØVRIGE(BER_SØ_IND,I,L);                 
      END;                                                                      
                                                                                
    IF UFT > 19721200 THEN                   /*2000*/                           
      DO;                                                                       
        IF UFT < 19730700 THEN               /*2000*/                           
          DO;                                                                   
            IF UFG < 51 THEN                                                    
              W_APD_UP = 50;                                                    
                                                                                
 /*         ELSE IF UFG < 76 THEN ** FEIL-RETTET 6.10.86 HL */                  
            ELSE IF UFG < 70 THEN  /* SE FRAPP 409 */                           
              W_APD_UP = 75;                                                    
                                                                                
            ELSE                                                                
              W_APD_UP = 100;                                                   
          END;                                                                  
                                                                                
        ELSE IF UFG < 51 THEN               /* UFT > 7306  */                   
          W_APD_UP = 50;                                                        
                                                                                
        ELSE IF UFG < 76 THEN                                                   
          W_APD_UP = 75;                                                        
                                                                                
        ELSE                                                                    
          W_APD_UP = 100;                                                       
      END;                                                                      
                                                                                
          ELSE IF UFG < 51 THEN           /* UFT < 7301  */                     
      W_APD_UP = 50;                                                            
                                                                                
    ELSE IF UFG < 66 THEN                                                       
      W_APD_UP = 75;                                                            
                                                                                
    ELSE                                                                        
      W_APD_UP = 100;                                                           
                                                                                
 /* ENDRING 14.2.84 HL ********/                                                
                                                                                
 IF B02.ALDERSP.APD(BER_SØ_IND) < W_APD_UP               THEN                   
                                                                                
 /* HIT                ********/                                                
                                                                                
    DO;                                                                         
       B02.ALDERSP.APD       (BER_SØ_IND) = W_APD_UP;                           
       B02.ALDERSP.KONV_GRAD (BER_SØ_IND) = W_APD_UP;                           
       B02.ALDERSP.UTTAKS_APD(BER_SØ_IND) = W_APD_UP;                           
       B02.ALDERSP.P67_KODE  (BER_SØ_IND) = '2';    /* KONVERTERING */          
       B02.ALDERSP.UTTAKSDATO_ÅMD(BER_SØ_IND) =       /*2000*/                  
                  B02.STATUS.VIRK_DATO_ÅMD(BER_SØ_IND);/*2000*/                 
    END;                                                                        
                                                                                
 /* TILLEGG 14.2.84 HL. */                                                      
                                                                                
 ELSE                                                                           
                                                                                
    IF B02.ALDERSP.KONV_GRAD (BER_SØ_IND) < W_APD_UP     THEN                   
       B02.ALDERSP.KONV_GRAD (BER_SØ_IND) = W_APD_UP;                           
                                                                                
 /* HIT                 */                                                      
                                                                                
 END BEREGN_APD_OVERGANG_FRA_UP;                                                
