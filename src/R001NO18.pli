 /*   SIST ENDRET PÅ PROD   2000.02.18 13.05.02 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   1999.12.10  8.36.56 AV   JDA7339          */        
 /*       SIST ENDRET 28/08-98 15.57.00 AV   SPA7339                  */        
 /*       SIST ENDRET 17/06-98 14.40.58 AV   HLA7339                  */        
 /* ***************************************************************** */        
 /*IDENTIFIKASJON:                                                    */        
 /* **********************                                            */        
 /*  PROGRAM-IDENT : R001NO18  UNDERPROGRAM SOM BRUKES AV R001NO10    */        
 /*                            KONTROLL O2-BLANKETT.                  */        
 /*  PROGRAM-SPRÅK : PLI                                              */        
 /*  PROGRAMMERER  : BRITT FOSSUM                                     */        
 /*  PROGRAMMET BLE LAGET : OKTOBER 1986                              */        
 /*  ENDRINGERSDATO :                                                 */        
 /*  ENDRINGEN GJELDER:                                               */        
 /*  ENDRINGEN BLE UTFØRT AV :                                        */        
 /*                                                                   */        
 /* ***************************************************************** */        
 /*BRUK:                                                              */        
 /* ********                                                          */        
 /*                                                                   */        
 /*  %INCLUDE R001NO18                                                */        
 /*                                                                   */        
 /*  INCLUDE PÅ FØLGENDE PROSEDYRER :                                 */        
 /*                                                                   */        
 /*    R0011702   BLANK O2 TRANSOMR                                   */        
 /*    R0011703   KONTR O2 SØKER                                      */        
 /*    FUNKSJONER SOM BLIR BRUKT ER INKLUDERT I HOVEDPROGRAMMET       */        
 /*                                                                   */        
 /* ***************************************************************** */        
 /*                                                                   */        
 /*  PROSEDYREN BLANKER TRANSOMRÅDE FOR O2 OG FLYTTER DERETTER        */        
 /*  VERDIER FRA OVERFØRT RECORD TIL TRANSOMRÅDET.  TRANSEN BLIR      */        
 /*  KONTROLLERT.  DERSOM DET BLIR FUNNET FEIL, BLIR FEILMELDINGS-    */        
 /*  NUMRE TATT VARE PÅ.                                              */        
 /*                                                                   */        
 /* ***************************************************************** */        
                                                                                
 P117_BEHANDLE_O2: PROC;                                                        
                                                                                
                                                                                
 %INCLUDE P0011701;                               /* TRANSOMR O2 */             
                                                                                
                                                                                
          FEIL_I_BOSATT = '0'B;                                                 
          FEIL_BLANKETT = '0'B;                                                 
          FEIL_FUNNET   = '0'B;                                                 
          CALL BLANK_O2_OPPHØR;                  /* R0011702 */                 
          CALL BLANK_O2_MELDNR;                  /* R0011702 */                 
          CALL OVERFØR_O2_BLANKETT;                                             
          CALL KONTROLL_O2(FEIL_FUNNET);         /* R0011703 */                 
                                                                                
          IF FEIL_FUNNET     THEN                                               
             DO;                                                                
                CALL FINN_FEIL_O2;                                              
             END;                                                               
                                                                                
                                                                                
                                                                                
 /* ***************************************************************** */        
 /*   PROSEDYRE FOR Å OVERFØRE FELT FRA OVERFØRINGSRECORDEN           */        
 /*   TIL O2.TRANS_OMR I KOM_OMR FOR OPPHØRSBLANKETT 2.               */        
 /* ***************************************************************** */        
 OVERFØR_O2_BLANKETT: PROC;                                                     
                                                                                
      DCL HJELPEDATO    PIC '(8)9';                                             
                                                                                
          O2.REGDATO_ÅMD     = BLANKETT_RECORD.TRANS_DATO_ÅMD;                  
          O2.TERMID          = DIV_PARAM_OMR.TERMINAL_NR;                       
          O2.FNR             = BLANKETT_RECORD.FNR;                             
          O2.TKNR            = BLANKETT_RECORD.TKNR;                            
          HJELPEDATO         = O2_BLANKETT.VIRK_DATO_ÅMD / 100;                 
          O2.VIRK_DATO_ÅMD   = HJELPEDATO * 100;                                
          O2.EK_TILL         = O2_BLANKETT.E_TILL_STOPP;                        
                                                                                
          DO J = 1 TO 5;                                                        
             IF F_FELT_PIC_CHAR11(O2_BLANKETT.FNR_BARN(J))                      
                                                  ^= (11)' '   THEN             
                O2.FNR_BARN(J) = O2_BLANKETT.FNR_BARN(J);                       
          END;                                                                  
                                                                                
          O2.OPPHØR_KODE     = O2_BLANKETT.OPPHØR_KODE;                         
                                                                                
                                                                                
    END OVERFØR_O2_BLANKETT;                                                    
                                                                                
                                                                                
 /* ***************************************************************** */        
 /*   PROSEDYRE SOM FINNER OG TAR VARE PÅ FEILMELDINGSNUMRE DERSOM    */        
 /*   DET ER FEIL I DEN OVERFØRTE RECORDEN.                           */        
 /* ***************************************************************** */        
 FINN_FEIL_O2:PROC;                                                             
                                                                                
                                                                                
          FEIL_IND = 1;                                                         
                                                                                
          IF O2.FNRNR ^= 0   &                                                  
             O2.FNRNR ^= 999 THEN                                               
             DO;                                                                
                W01_FEIL_NR(FEIL_IND) = O2.FNRNR;                               
                FEIL_IND = FEIL_IND + 1;                                        
             END;                                                               
                                                                                
          IF O2.TKNRNR ^= 0   &                                                 
             O2.TKNRNR ^= 999 THEN                                              
             DO;                                                                
                W01_FEIL_NR(FEIL_IND) = O2.TKNRNR;                              
                FEIL_IND = FEIL_IND + 1;                                        
             END;                                                               
                                                                                
          IF O2.VIRK_DATO_ÅMDNR ^= 0  &                                         
             O2.VIRK_DATO_ÅMDNR ^= 999 THEN                                     
             DO;                                                                
                W01_FEIL_NR(FEIL_IND) = O2.VIRK_DATO_ÅMDNR;                     
                IF FEIL_IND ^= 3  THEN                                          
                   FEIL_IND  = FEIL_IND + 1;                                    
             END;                                                               
                                                                                
          IF O2.EK_TILLNR ^= 0   &                                              
             O2.EK_TILLNR ^= 999 THEN                                           
             DO;                                                                
                W01_FEIL_NR(FEIL_IND) = O2.EK_TILLNR;                           
                IF FEIL_IND ^= 3  THEN                                          
                   FEIL_IND  = FEIL_IND + 1;                                    
             END;                                                               
                                                                                
          DO J = 1 TO 5;                                                        
             IF O2.FNR_BARNNR(J) ^= 0 &                                         
                O2.FNR_BARNNR(J) ^= 999 THEN                                    
                DO;                                                             
                   W01_FEIL_NR(FEIL_IND) = O2.FNR_BARNNR(J);                    
                   IF FEIL_IND ^= 3 THEN                                        
                      FEIL_IND = FEIL_IND + 1;                                  
                END;                                                            
          END;                                                                  
                                                                                
          IF O2.OPPHØR_KODENR ^= 0   &                                          
             O2.OPPHØR_KODENR ^= 999 THEN                                       
             DO;                                                                
                W01_FEIL_NR(FEIL_IND) = O2.OPPHØR_KODENR;                       
                IF FEIL_IND ^= 3  THEN                                          
                   FEIL_IND  = FEIL_IND + 1;                                    
             END;                                                               
                                                                                
                                                                                
    END FINN_FEIL_O2;                                                           
                                                                                
                                                                                
 %PAGE;                                                                         
 /* ***************************************************************** */        
 /*          E K S T E R N E    P R O C E D U R E R                   */        
 /* ***************************************************************** */        
                                                                                
                                                                                
 %PAGE;                                                                         
 %INCLUDE R0011702;          /*     BLANK O2 TRANSOMR   */                      
                                                                                
 %PAGE;                                                                         
 %INCLUDE R0011703;          /*     KONTR O2 OPPHØR     */                      
                                                                                
                                                                                
 END P117_BEHANDLE_O2;                                                          
                                                                                
                                                                                
