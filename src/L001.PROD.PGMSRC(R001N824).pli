 /*       SIST ENDRET 10/06-98 09.48.35 AV   TSB7339                  */00000000
 /*       SIST ENDRET 09/06-98 08.55.54 AV   SPA7339                  */00000010
 /*       SIST ENDRET 25/05-98 11.40.47 AV   SPA7339                  */00000020
  /*----------------------------------------------------------------*/  00000030
  /* IDENTIFIKASJON : R001N824 - FUNKSJON  I PLI                    */  00000040
  /* PROGRAMMERER   : TRUDE SPONBERG OKTOBER 1990                   */  00000050
  /* HENSIKT        : BEREGNE FREMTIDIG TRYGDETID                   */  00000060
  /*                  ETTER 01.01.91                                */  00000070
  /* BRUKER         : CALL F_BEREGN_TT_FRAMT_UTLAND(FNR,STI_ÅMD,IND) */ 00000080
  /*                  RETURNERER FIXED DEC(3)                       */  00000090
  /* UNDERPROGRAM I : R001N620 : KONTROLL U2 TRANS MOT STATUS I B.  */  00000120
  /*                  R001N820 : ---"---- E3 --"-- -"- --"--- " "   */  00000130
  /*                  R001N920 : ---"---- E4 --"-- -"- --"--- " "   */  00000140
  /*                  R001U620 : ---"---- U3 --"-- -"- --"--- " "   */  00000150
  /*                  R001U820 : ---"---- EE --"-- -"- --"--- " "   */  00000160
  /*----------------------------------------------------------------*/  00000100
  /* NY REGEL :                                                     */  00000110
  /* FOR DEM SOM HAR BODD I UTLANDET, VIL FREMTIDIG TRYGDETID       */  00000120
  /* KUNNE BLI BEGRENSET.                                           */  00000130
  /* MED VIRKNING FRA 01.01.91                                      */  00000140
  /* DERSOM 4/5 AV TIDEN MELLOM FYLTE 16 ÅR OG STØNADSTILFELLETS    */  00000150
  /* INNTREDEN (OPPTJENINGSPERIODEN) KAN REGNES SOM TRYGDETID,      */  00000160
  /* MEDREGNES ALL FREMTIDIG TRYGDETID. DET SAMME GJELDER DERSOM    */  00000170
  /* STØNADSTILFELLET ER INNTRÅDT FØR FYLTE 16 ÅR.                  */  00000180
  /* (HVORDAN SKAL VI FANGE OPP DEN ? UFT=16ÅRSDAG??) TRUDE.        */  00000190
  /* I ANDRE TILFELLE SKAL FREMTIDIG TRYGDETID UTGJØRE 40 ÅR MED    */  00000200
  /* FRADRAG FOR 4/5 AV OPPTJENINGSPERIODEN.                        */  00000210
  /* TRUDE, 9.10.90                                                 */  00000220
  /*----------------------------------------------------------------*/  00000230
  /* ENDRET: 020591                                                 */  00000240
  /* ENDRINGEN GJELDER: FJERNE MND VI LEGGER TIL I OPPTJ.PERIODEN.  */  00000250
  /*                    MINSTEVILKÅR SKAL REGNE MED DESIMALER.      */  00000260
  /*                    TT_FRAMT_UTLAND   REGNE MED DESIMALER.      */  00000270
  /*                    ENDRINGEN ER I SAMARBEID MED KARI SANNES.   */  00000280
  /*----------------------------------------------------------------*/  00000290
                                                                        00000300
                                                                        00000310
  %SKIP(2);                                                             00000320
  F_BEREGN_TT_FRAMT_UTLAND:                                             00000330
    PROC(FNR11,STI_ÅMD,IND) RETURNS(FIXED DEC(3));                      00000340
                                                                        00000350
    DCL FNR11                  FIXED DEC (11);                          00000360
    DCL STI_ÅMD              FIXED DEC (8);                             00000370
    DCL IND                  FIXED BIN (15);                            00000380
    DCL TT_FRAMT_UTLAND      FIXED DEC (3) INIT (0);                    00000390
    DCL W_TT_FRAMT_UTLAND    FIXED DEC (5,2) INIT (0);                  00000400
                                                                        00000410
    DCL W_STI_ÅMD         PIC '(8)9';                                   00000420
    DCL W_STI_ÅR          PIC '(4)9' DEF W_STI_ÅMD POS(1),              00000430
        W_STI_MND         PIC '(2)9' DEF W_STI_ÅMD POS(5);              00000440
                                                                        00000450
    DCL OPPTJENINGSPERIODEN  FIXED DEC (3) INIT (0),                    00000460
        FAKTISK_TT           FIXED DEC (3) INIT (0),                    00000470
        MINSTEVILKÅR         FIXED DEC (5,2) INIT (0); /*TS,020591*/    00000480
                                                                        00000490
    DCL                                                                 00000500
        W_TT_FRAM_TIL_DATO_ÅM                             PIC '(6)9',   00000510
        W_TT_FRAM_TIL_ÅR  DEF W_TT_FRAM_TIL_DATO_ÅM                     00000520
                                                    POS (1) PIC '9999', 00000530
        W_TT_FRAM_TIL_MND DEF W_TT_FRAM_TIL_DATO_ÅM  POS (5) PIC '99';  00000540
                                                                        00000550
    DCL FNR13                PIC '(13)9';                               00000560
                                                                        00000570
    DCL                                                                 00000580
       1 FNR13_R             DEF      FNR13,                            00000590
         2 DAG               PIC        '99',                           00000600
         2 MND               PIC        '99',                           00000610
         2 ÅR                PIC      '9999',                           00000620
         2 ÅRHUNDRE          PIC         '9',                           00000630
         2 REST              PIC      '9999';                           00000640
                                                                        00000650
    FNR13  = KONV_FNR11_FNR13((FNR11));                                 00000660
                                                                        00000670
           /* IFLG. REFERAT FRA ARBEIDSGRUPPEN 12.9.90 SKAL  */         00000680
           /* MND 16. ÅR FYLLES IKKE TELLE MED. TRUDE, 10.90 */         00000690
                                                                        00000700
     W_STI_ÅMD = STI_ÅMD;            /*TOM. MND FØR DØD ELLER STI_ÅM */ 00000710
                                        /*TOM. MND ETTER F_MND       */ 00000720
     OPPTJENINGSPERIODEN    = ((W_STI_ÅR *12) + W_STI_MND - 1) -        00000730
             ((( FNR13_R.ÅR + 16 ) * 12) + FNR13_R.MND);                00000740
                                                                        00000750
     IF B02.TT_16_66(IND) > 0 THEN                                      00000760
        FAKTISK_TT = B02.TT_16_66(IND);                                 00000770
     ELSE                                                               00000780
           FAKTISK_TT        =  B02.TT_FØR_1967(IND) * 12 +             00000790
                                B02.TT_ETTER_1966(IND);                 00000800
                                                                        00000810
     MINSTEVILKÅR  = (OPPTJENINGSPERIODEN * 4) / 5;  /*AVRUNDES IKKE*/  00000820
                                                                        00000830
     IF MINSTEVILKÅR > FAKTISK_TT        THEN                           00000840
      DO;                                                               00000850
                                        /* TT SKAL REDUSERES         */ 00000860
                                        /* NOTAT TIL SLETNER SIDE 3  */ 00000870
                                        /* N.SISTE AVS. ANG AVRUNDING*/ 00000880
        W_TT_FRAMT_UTLAND   = 480 - MINSTEVILKÅR ;                      00000890
        TT_FRAMT_UTLAND   = W_TT_FRAMT_UTLAND + 0.5; /*TS*/             00000900
        TT_RED  = 'J';                                                  00000910
        DIV_PARAM_OMR.REDUSERT_FRAMTIDIG_TT(IND) = 'J'; /* KONS RF */   00000980
      END;                                                              00000920
     ELSE                                                               00000930
      DO;                                                               00000940
        TT_RED    = ' ';                                                00000950
                                                                        00000960
        IF STI_ÅMD < 19730100 THEN                                      00000970
           W_TT_FRAM_TIL_DATO_ÅM  =                                     00000980
                          (FNR13_R.ÅR + 70) * 100 + FNR13_R.MND;        00000990
        ELSE                                                            00001000
          IF B02.PENSJONSRETT_FØR_91(IND) = 'J' THEN                    00001010
             W_TT_FRAM_TIL_DATO_ÅM  =                                   00001020
                    (FNR13_R.ÅR + 67) * 100 + FNR13_R.MND;              00001030
          ELSE                                                          00001040
             W_TT_FRAM_TIL_DATO_ÅM  =                                   00001050
 /*HL*/             (FNR13_R.ÅR + 66) * 100 + 12 ;                      00001060
                                                                        00001070
       TT_FRAMT_UTLAND = (W_TT_FRAM_TIL_ÅR * 12 + W_TT_FRAM_TIL_MND) -  00001080
                (W_STI_ÅR  * 12 + W_STI_MND - 1);                       00001090
      END;                                                              00001100
                                                                        00001110
      IF TT_FRAMT_UTLAND < 0 THEN                                       00001120
         TT_FRAMT_UTLAND = 0;                                           00001130
                                                                        00001140
     RETURN(TT_FRAMT_UTLAND);                                           00001150
  END F_BEREGN_TT_FRAMT_UTLAND;                                         00001160
