 /*   SIST ENDRET PÅ PROD   2008.05.31 11.10.54 AV   SPA2990          */        
 /*   SIST ENDRET PÅ PROD   2007.12.21 12.20.14 AV   JDA2990          */        
 /*   SIST ENDRET PÅ PROD   2005.08.12 11.55.24 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2005.08.11  8.42.21 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2005.06.24 12.42.50 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2005.04.26 12.53.50 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2004.09.17 11.36.56 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.09.14  9.17.52 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2004.03.02 11.37.41 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.02.11 11.25.07 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2002.10.23 13.57.43 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2002.04.19 12.14.23 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2000.10.02 10.35.49 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.07.07 12.59.45 AV   JDA7339          */        
 /*       SIST ENDRET 04/05-98 12.23.20 AV   JDA7339                  */        
 /*       SIST ENDRET 11/03-98 13.53.27 AV   RFA9991                  */        
 /* ***************************************************************** */        
 /*IDENTIFIKASJON:                                                    */        
 /*    R001N522 - PROSEDYRE I PLI      KOPI AV R0010522               */        
 /*    PROGRAMMERER: GEIR, OKTOBER 1981                               */        
 /*HENSIKT:                                                           */        
 /*    PROSEDYREN AJOURFØRER SØKERS PENSJONSSTATUS MED INFO FRA TRANS */        
 /*    ALDERSBLANKETTEN.                                              */        
 /* ***************************************************************** */        
 /*SIST ENDRET 1/6-84 AV KARIN                                        */        
 /*    ENDRINGEN GJELDER REDUSERT BARNETILLEGG, ENDREDE PT2-KODER FOR */        
 /*    EKTEFELLEN                                                     */        
 /* ***************************************************************** */        
 /*PROGRAMTILKNYTNING:                                                */        
 /*    PROSEDYREN INCLUDES I R0010520.                                */        
 /*BRUK:                                                              */        
 /*    CALL AJOURFØR_B02_MED_APTRANS                                  */        
 /*                                                                   */        
 /* ***************************************************************** */        
 %SKIP(2);                                                                      
 AJOURFØR_B02_MED_A1TRANS:                                                      
   PROC;                                                                        
 %SKIP;                                                                         
   DCL                                                                          
     W_VIRK_DATO_ÅMD   FIXED DEC(9),                                            
      ALDER_Å_MND      FIXED DEC(5);                                            
 %INCLUDE P0019925;     /* GRUNNBELØPS - TABELL              */                 
 %SKIP;                                /*     */                                
 /* ***************************************************************** */        
 /* YTELSE_SEGMENTET                                                  */        
 /* ***************************************************************** */        
 %SKIP;                                                                         
      IF B02.BEREGN_FOLKETRYGD(SØKER_IND) = 'N'   THEN                          
         B02.BEREGN_FOLKETRYGD(SØKER_IND) = 'J';   /*9907*/                     
      IF HJ_VIRK_DATO_ÅMD < 19980200   THEN                                     
         B02.PENSJON_FØR_9802(SØKER_IND) = 'J';                                 
      ELSE                                                                      
      IF B01.PENSJONSTYPE2(SØKER_IND) = 'N'    THEN     /*9802 HL */            
         B02.PENSJON_FØR_9802(SØKER_IND) = ' ';                                 
      B02.GP_REDUKSJON_KODE(SØKER_IND) = ' ';                                   
      W_VIRK_DATO_ÅMD =  HJ_VIRK_DATO_ÅMD      ;                                
      B02.NAVN(SØKER_IND)             = A1S.NAVN;                               
      B02.TKNR(SØKER_IND)             = A1S.TKNR;                               
      B02.SPRÅK(SØKER_IND)            = A1S.SPRÅK;                              
      B02.PENSJONSTYPE1(SØKER_IND)    = 'A';                                    
      B02.FØRSTE_GANG_REG(SØKER_IND)  = 'N';                                    
      B02.P67_KODE       (SØKER_IND)  = ' ';           /*HL*/                   
  /* ************************************************************* */           
  /* KONTROLL AV REGISTRERT TT MOT POENGÅR - 200401 HL             */           
  /* ************************************************************* */           
                                                                                
   IF A1S.TT_16_66ÅR > 0              THEN                                      
      REGÅR = A1S.TT_16_66ÅR;                                                   
   IF REGÅR > 0 THEN                                                            
   DO;                                                                          
      SISTE = ÅR + 66;                                                          
      DO I = 1967 TO SISTE;                                                     
         IF B02.PINNTEKT.PI(SØKER_IND,I)  /  /*FJERNET *100 JD*/                
            (GV_TAB_ARRAY.G_V(I)        )  > 1.0049    THEN                     
            POENGÅR = POENGÅR + 1;                                              
      END;                                                                      
      IF POENGÅR > 40                    THEN                                   
         POENGÅR = 40;                                                          
      IF POENGÅR > REGÅR                 THEN                                   
   L270:                                                                        
          DO;                                                                   
             FEIL_VED_LABEL = '270';                                            
             FEIL_MELD_NR   = 0422;                                             
             GO TO L999;                                                        
          END;                                                                  
    END;                                                                        
      B02.TT_16_66(SØKER_IND)        = A1S.TT_16_66ÅR * 12 +                    
                                        A1S.TT_16_66MD;                         
      IF B02.TT_16_66(SØKER_IND)  > 480   THEN                                  
         B02.TT_16_66(SØKER_IND)  = 480;                                        
                                                                                
      B02.ALDERSP.APD  (SØKER_IND)    = 100;                                    
      DO I = 1 TO 13;                                                           
         IF B02.FNR(I) > 0 THEN                                                 
            B02.FØRSTE_GANG_REG(I)  = 'N';                                      
      END;                                                                      
 /* ***************************************************************** */        
 /* NYE FORS-TILL BARN SKAL IKKE REGISTRERES,BRUK F7-BL  (JD 7.91)    */        
 /* ***************************************************************** */        
                                                                                
    IF A1S.BT_ANT > B01.ANTALL_BARN (SØKER_IND) &                               
       HJ_VIRK_DATO_ÅMD  > 19910400 THEN                                        
                 DO;                                                            
  L378:                                                                         
                    FEIL_VED_LABEL = '378';                                     
                    FEIL_MELD_NR   = 1738;                                      
                    GO TO RETUR;                                                
                 END;                                                           
                                                                                
    IF A1S.TILL_EK = 'J' & HJ_VIRK_DATO_ÅMD > 19910400  &                       
       B01.PENSJONSTYPE2(SØKER_IND) ^= 'F' THEN                                 
       A1S.TILL_EK = ' ' ; /* JD 7.91*/                                         
 /* ***************************************************************** */        
 /* TIDLIGERE REGISTRERT SOM ETTERLATT EKTEFELLE.                     */        
 /* ***************************************************************** */        
     SELECT(B01.PENSJONSTYPE1(SØKER_IND));  /*200010*/                          
        WHEN('E')                                                               
           DO;                                                                  
              B02.PENSJONSTYPE2(SØKER_IND) = 'E';                               
              B02.KONV_P_KODE(SØKER_IND)   = 'E';                               
           END;                                                                 
        WHEN('Y')                                                               
           DO;                                                                  
              B02.YRKEPENS.GP  (SØKER_IND) = 0   ;                              
              B02.YRKEPENS.TP  (SØKER_IND) = 0   ;                              
              B02.YRKEPENS.BT  (SØKER_IND) = 0   ;                              
              B02.YRKEPENS.YPT_KODE (SØKER_IND) = ' ' ;                         
              B02.KONV_P_KODE(SØKER_IND)   = 'Y';                               
           END;                                                                 
        WHEN('K')                                                               
           DO;  /* OVERGANG FRA AFP */                                          
              IF B01.PENSJONSTYPE3(SØKER_IND) = 'K'  !                          
                 B01.PENSJONSTYPE3(SØKER_IND) = 'S'      THEN                   
                 B02.KONV_P_KODE(SØKER_IND) = 'O';                              
              ELSE                                                              
                 B02.KONV_P_KODE(SØKER_IND)   = 'K';                            
                                                                                
              /* NY 23.10.2002 - 'NULL' ELEMENT */                              
              IF (B01.AFPDATO_ÅMD(SØKER_IND,1) > 0) THEN                        
                 DO; /* HAR AFPHIST SEGMENT */                                  
                                                                                
                    /* FØRST BLANKER VI AFPHIST MED SENERE DATO : */            
                    DO I = 1 TO 30; /* ENDRET 20->30 SP 18.07.05 */             
                       IF (A1S.VIRK_DATO_ÅMD           <=                       
                           B02.AFPDATO_ÅMD(SØKER_IND,I)) THEN                   
                              B02.AFPHIST(SØKER_IND,I) = '';                    
                    END; /* DO I                                                
                                                                                
                    /* SÅ FINNER VI INDEX FOR NYTT NULLELEMENT */               
                    DO I = 1 TO 30 UNTIL /* ENDRET 20->30 SP 18.07.05 */        
                                   (B02.AFPDATO_ÅMD(SØKER_IND,I) = 0);          
                    END;                                                        
                                                                                
                    /*NULL ELEMENT DANNES*/                                     
                    IF I > 1 THEN                                               
                       DO;                                                      
                          B02.AFPDATO_ÅMD(SØKER_IND,I) =                        
                              A1S.VIRK_DATO_ÅMD;                                
                          B02.TEI_FAKTOR(SØKER_IND,I ) =                        
                              B02.TEI_FAKTOR(SØKER_IND,I-1);                    
                       END;                                                     
                    ELSE                                                        
                       DO;                                                      
                          B02.AFPDATO_ÅMD(SØKER_IND,I) = 0;                     
                          B02.TEI_FAKTOR(SØKER_IND,I ) = 0;                     
                       END;                                                     
                    B02.FEI_I_100KR(SØKER_IND,I) = 0;                           
                    B02.GRAD_UTBET(SØKER_IND,I)  = 0;                           
                    B02.FULL_AFP(SØKER_IND,I)    = 0;                           
                    B02.RED_AFP(SØKER_IND,I)     = 0;                           
                    B02.ET_REG(SØKER_IND,I)      = ' ';                         
                 END;/* HAR AFPHIST SEGMENT */                                  
              /* NY 23.10.2002 - 'NULL' ELEMENT */                              
                                                                                
           END; /* OVERGANG FRA AFP */                                          
        /* OVERGANG FRA UP BLIR BEHANDLET I N520 */                             
        OTHER;                                                                  
     END; /* SELECT B01.PENSJONSTYPE1  */                                       
                                                                                
   /* IF B01.PENSJONSTYPE1(SØKER_IND) = 'E' THEN                                
            B02.PENSJONSTYPE2(SØKER_IND) = 'E';     */                          
 /* ***************************************************************** */        
 /* HVIS VI REGISTRERER AP PÅ EN YP SOM IKKE ER KONVERTERT ENNÅ :     */        
 /* ***************************************************************** */        
 /*ENDRET 12.7.90 HL : */                                                       
  /*                                                                            
      ELSE                                                                      
      IF B01.PENSJONSTYPE1(SØKER_IND) = 'Y' THEN                                
         DO;                                                                    
            B02.KONV_P_KODE  (SØKER_IND) = 'Y' ;                                
            B02.YRKEPENS.GP  (SØKER_IND) = 0   ;                                
            B02.YRKEPENS.TP  (SØKER_IND) = 0   ;                                
            B02.YRKEPENS.BT  (SØKER_IND) = 0   ;                                
            B02.YRKEPENS.YPT_KODE (SØKER_IND) = ' ' ;                           
         END;                                                                   
 /* ***************************************************************** */        
 /* HVIS SØKER HAR EKTEFELLE                                          */        
 /* ***************************************************************** */        
      IF A1S.FNR_EK > 0 THEN                                                    
         DO;                                                                    
            IF  A1S.SIVILSTAND               = 'A' THEN                         
               DO;                                                              
                  B02.SIVILSTAND(SØKER_IND)      = 'A';                         
                  B02.SIVILSTAND(EKTEF_IND)      = 'A';                         
               END;                                                             
            ELSE                                                                
            IF  A1S.SIVILSTAND               = 'P' THEN                         
               DO;                                                              
                  B02.SIVILSTAND(SØKER_IND)      = 'P';                         
                  B02.SIVILSTAND(EKTEF_IND)      = 'P';                         
               END;                                                             
            ELSE                                                                
            IF  A1S.SIVILSTAND               = 'W' THEN                         
               DO;                                                              
                  B02.SIVILSTAND(SØKER_IND)      = 'W';                         
                  B02.SIVILSTAND(EKTEF_IND)      = 'W';                         
               END;                                                             
            ELSE                                                                
            IF  A1S.SIVILSTAND               = 'V' THEN                         
               DO;                                                              
                  B02.SIVILSTAND(SØKER_IND)      = 'V';                         
                  B02.SIVILSTAND(EKTEF_IND)      = 'V';                         
               END;                                                             
            ELSE                                                                
               DO;                                                              
                  B02.SIVILSTAND(SØKER_IND)      = 'G';                         
                  B02.SIVILSTAND(EKTEF_IND)      = 'G';                         
               END;                                                             
 /* ***************************************************************** */        
 /* HVIS FORSØRGINGSTILLEGG EKTEFELLE                                 */        
 /* ***************************************************************** */        
            IF  A1S.TILL_EK = 'J' ! B01.ALDERSP.ET(SØKER_IND) > 0               
                     ! B01.GARANTITILLEGG_EK(SØKER_IND) > 0    THEN             
                                                                                
               DO;                                                              
                  B02.PENSJONSTYPE2(SØKER_IND) = 'F';                           
                  B02.PENSJONSTYPE1(EKTEF_IND) = 'F';                           
                  B02.PENSJONSTYPE2(EKTEF_IND) = ' ';                           
  /* 200309 HL: PT3 MÅ BLANKES */                                               
                  B02.PENSJONSTYPE3(EKTEF_IND) = ' ';                           
                  B02.NAVN(EKTEF_IND)          = A1S.NAVN_EK;                   
               END;                                                             
 /* ***************************************************************** */        
 /* HVIS EKTEFELLEN ER ALDERS ELLER U/Y -PENSJONIST  ELLER HAR        */        
 /* AVTALEFESTET PENSJON                                              */        
 /* ***************************************************************** */        
            ELSE                                                                
               DO;                                                              
                                                                                
 /*18.9.89 HL : ENDRET FRA B01 TIL B02 I TESTER NEDENFOR :*/                    
                                                                                
                  IF B02.PENSJONSTYPE1(EKTEF_IND) = 'A' !                       
                     B02.PENSJONSTYPE1(EKTEF_IND) = 'U' !                       
                     B02.PENSJONSTYPE1(EKTEF_IND) = 'Y' !                       
 /*AFP*/             B02.PENSJONSTYPE1(EKTEF_IND) = 'K'         THEN            
 /* ***************************************************************** */        
 /* HVIS AVTALEFESTET PENSJON IKKE UTBETALES                          */        
 /* ***************************************************************** */        
 /*AFP*/             IF B02.PENSJONSTYPE1(EKTEF_IND) = 'K' &                    
 /*AFP*/                B02.PENSJONSTYPE2(EKTEF_IND) = 'N' THEN                 
                                                                                
 /*HIT 18.9.89 HL */                                                            
                                                                                
 /*AFP*/                DO;                                                     
 /*AFP*/                   B02.PENSJONSTYPE2(SØKER_IND) = 'P';                  
 /*AFP*/                   B02.PENSJONSTYPE2(EKTEF_IND) = 'N';                  
 /*AFP*/                END;                                                    
 /* ***************************************************************** */        
 /* HVIS EKTEFELLENE LEVER ADSKILT /ELLER SIVILSTAND = V              */        
 /* ***************************************************************** */        
 /*AFP*/             ELSE                                                       
                       IF  A1S.SIVILSTAND               = 'A'   !               
                           A1S.SIVILSTAND               = 'V' THEN              
                          DO;                                                   
                             B02.PENSJONSTYPE2(SØKER_IND)   = 'P';              
                             B02.PENSJONSTYPE2(EKTEF_IND)   = 'P';              
                          END;                                                  
 /* ***************************************************************** */        
 /* HVIS EKTEFELLENE LEVER SAMMEN                                     */        
 /* ***************************************************************** */        
                       ELSE                                                     
                        DO;                                                     
                          B02.PENSJONSTYPE2(SØKER_IND) = 'S';                   
                          B02.PENSJONSTYPE2(EKTEF_IND) = 'S';                   
    /* FJERNET 3.12.97 HL :                                                     
                          IF B02.PENSJONSTYPE3(EKTEF_IND)                       
                                               = 'S'  &                         
                             B02.PENSJONSTYPE2(EKTEF_IND)                       
                                              ^= 'N' THEN                       
                               B02.PENSJONSTYPE2(SØKER_IND) = 'P';              
    ***********  HIT 9712 */                                                    
  /*DET ER NÅ TILLATT MED UFG= 0 - 0797 HL : */                                 
                          IF B02.PENSJONSTYPE1(EKTEF_IND) = 'U' !               
                             B02.PENSJONSTYPE1(EKTEF_IND) = 'Y' &               
                             B02.UFØRPENS.UFG(EKTEF_IND)  = 0  THEN             
                             DO;                                                
                               B02.PENSJONSTYPE2(SØKER_IND) = 'P';              
                               B02.PENSJONSTYPE2(EKTEF_IND) = 'P';              
                             END;                                               
                        END;                                                    
 /* *************************************************************** */          
 /* EKTEFELLEN ER BARE GIFT ELLER EKTEFELLENE LEVER ATSKILT         */          
 /* *************************************************************** */          
                  ELSE                                                          
                     DO;                                                        
                        B02.PENSJONSTYPE2(SØKER_IND) = 'P';                     
                        B02.PENSJONSTYPE1(EKTEF_IND) = 'G';                     
  /* 200309 HL: PT3 MÅ BLANKES */                                               
                        B02.PENSJONSTYPE3(EKTEF_IND) = ' ';                     
                        IF A1S.MINST4G = 'J' THEN                               
                           B02.PENSJONSTYPE2(EKTEF_IND)   = 'M';                
                        ELSE                                                    
                           B02.PENSJONSTYPE2(EKTEF_IND)   = 'U';                
                     END;                                                       
                  B02.NAVN(EKTEF_IND)             = A1S.NAVN_EK;                
 /* ***************************************************************** */        
 /* TILKN-KODE TIL EKTEFELLENE OPPDATERES MED RESPEKTIVE NYE STATUSER.*/        
 /* ***************************************************************** */        
                  DO I = 1 TO 13;                                               
                     IF (B02.FNR_TILKN(SØKER_IND,I) =                           
                                        B02.FNR(EKTEF_IND))THEN                 
                         DO;                                                    
                             B02.TILKN.TILKNYTNINGSKODE(SØKER_IND,I)            
                             =  B02.PENSJONSTYPE1(EKTEF_IND);                   
                             I=13;                                              
                         END;                                                   
                  END;                                                          
                  DO I = 1 TO 13;                                               
                     IF (B02.FNR_TILKN(EKTEF_IND,I)=                            
                                        B02.FNR(SØKER_IND))THEN                 
                         DO;                                                    
                             B02.TILKNYTNINGSKODE(EKTEF_IND,I)                  
                             =  B02.PENSJONSTYPE1(SØKER_IND);                   
  /*HL*/                     B02.FT_FØR_91(EKTEF_IND,I) = ' ';                  
                             I=13;                                              
                         END;                                                   
                  END;                                                          
               END;                                                             
         END;                                                                   
 /* **************************************************************** */         
 /* SØKER HAR IKKE EKTEFELLE                                         */         
 /* **************************************************************** */         
      ELSE                                                                      
         DO;                                                                    
  /* **********************                                                     
            IF B01.PENSJONSTYPE2(SØKER_IND) ^= 'E' THEN                         
               B02.PENSJONSTYPE2(SØKER_IND)  = 'P';                             
  ************************                                                      
                                                                                
     RETTET FRA LINJENE OVER TIL DE NEDENFOR - HL 090496 */                     
                                                                                
            IF B01.PENSJONSTYPE1(SØKER_IND)  = 'E'      !                       
               B01.PENSJONSTYPE2(SØKER_IND)  = 'E' THEN                         
               B02.PENSJONSTYPE2(SØKER_IND)  = 'E';                             
            ELSE                                                                
               B02.PENSJONSTYPE2(SØKER_IND)  = 'P';                             
         END;                                                                   
      IF B02.PENSJON_FØR_9802(SØKER_IND) = ' '  THEN                            
         DO;                                                                    
            B02.EK_INNT_OVER_2G (SØKER_IND) = A1S.EK_INNT_OVER_2G;              
            B02.RED_GP_3_2_5    (SØKER_IND) = A1S.RED_GP_3_2_5   ;              
         END;                                                                   
 /* LOVENDRING - HL 200811*/                                                    
      ALDER_Å_MND = F_ALDER(A1S.FNR,A1S.VIRK_DATO_ÅMD);                         
      IF A1S.FAI ^= 9999999   &                                                 
         (ALDER_Å_MND > 6712   !                                                
          A1S.VIRK_DATO_ÅMD < 20080000)     THEN                                
        DO;                                                                     
          IF NR.ÅR  <  1925  THEN                                               
             B02.ALDERSP.FAI(SØKER_IND) = A1S.FAI / 100;                        
          ELSE                                                                  
            DO;                                                                 
             B02.ALDERSP.FAI(SØKER_IND) = A1S.FAI / 1000; /*PGA HELE */         
             B02.ALDERSP.FAI(SØKER_IND) = B02.FAI(SØKER_IND) * 10;              
            END;                                                                
          DO J = 1 TO 65;/* KONS RF */                                          
             IF W_VIRK_DATO_ÅMD >= PERIODE_START_ÅMD(J) &                       
                W_VIRK_DATO_ÅMD <= PERIODE_SLUTT_ÅMD(J) THEN                    
                DO;                                                             
                   B02.G_DATO_ÅMD(SØKER_IND) =                                  
                                   PERIODE_START_ÅMD(J)     ;                   
                   J = 99;                                                      
                END;                                                            
          END;                                                                  
      /*  FJERNET 230401 - MARTIN HER VART DET TULL !!!! */                     
      /*  B02.ALDERSP.FAI(SØKER_IND) = HJ_VIRK_DATO_ÅMD; /*200011*/             
          B02.ALDERSP.FAI_DATO_ÅMD(SØKER_IND) = HJ_VIRK_DATO_ÅMD;               
        END;                                                                    
      ELSE                                                                      
         B02.ALDERSP.FAI(SØKER_IND) = 0;                                        
                                                                                
                                                                                
         B02.ETTEPENS.FORVENTET(SØKER_IND) = 0;                                 
                                                                                
      B02.STATUS.TT_FRAMT(SØKER_IND) = 0;                                       
      IF B02.UFØRPENS.VILKÅR_8_4_3A(SØKER_IND) ^= ' '       THEN                
         B02.STATUS.VILKÅR_8_4_3A(SØKER_IND)    =                               
                             B02.UFØRPENS.VILKÅR_8_4_3A(SØKER_IND);             
                                                                                
      B02.SPESOPPL(SØKER_IND) = '';                                             
      IF A1S.NORSK_BOSATT = 'N' THEN                                            
         DO;                                                                    
                                                                                
 /*NY GRUNNBLANKETT : */                                                        
                                                                                
            B02.SPESOPPL.KONVENSJON(SØKER_IND) = A1S.FRAVIK_TT_KONV;            
            B02.KONVENSJON_TP(SØKER_IND) = A1S.FRAVIK_KONV_TP;                  
                                        /*9907*/                                
                                                                                
            B02.SPESOPPL.STATSBORGER(SØKER_IND)   = A1S.STATSBORGER;            
            B02.SPESOPPL.BOSATT(SØKER_IND)        = A1S.BOSATT;                 
                                                                                
                                                                                
            B02.SPESOPPL.FRAVIK_FLYKTN(SØKER_IND) = A1S.FRAVIK_FLYKT;           
                                                                                
            IF A1S.OVERKOMP_UTL = 'J'        THEN                               
               B02.SPESOPPL.OVERKOMP_UTL(SØKER_IND)  = 'J';                     
            ELSE                                                                
               B02.SPESOPPL.OVERKOMP_UTL(SØKER_IND)  = 'N';                     
                                                                                
            B02.SPESOPPL.IKKE_GP(SØKER_IND)     = A1S.IKKE_GP;                  
         END;                                                                   
                                                                                
   IF B02.ALDERSP.UTTAKSDATO_ÅMD(SØKER_IND) = 0  THEN                           
      B02.ALDERSP.UTTAKSDATO_ÅMD(SØKER_IND) =                                   
                                        B02.VIRK_DATO_ÅMD(SØKER_IND);           
                                                                                
  /*------------------------------------------------------------*/              
  /* ENDRING FOR GARANTERT TILLEGGSPENSJON TIL FØDTE UNGE UFØRE */              
  /* FRA 01.93. DERSOM PENSJONISTEN TAR OPPHOLD I UTLANDET ETTER*/              
  /* DENNE DATO, MISTER VEDKOMMENDE RETT TIL TP SOM UNG UFØR    */              
  /* TRUDE, 140193                                              */              
  /*------------------------------------------------------------*/              
                                                                                
  IF B02.STATUS.TP_GAR_KODE(SØKER_IND)  = 'E'    &                              
     B02.VIRK_DATO_ÅMD(SØKER_IND)       > 19930000   &                          
     B02.BOSATT(SØKER_IND)              > 0      THEN                           
        DO;                                                                     
          DO I = 1 TO 7;                    /*9709 HL*/                         
            B02.BUP_GARANTI(SØKER_IND,I)   = 0;                                 
            B02.BUP_GAR_KODE(SØKER_IND,I)  = ' ';                               
          END;                                                                  
          B02.STATUS.TP_GAR_KODE(SØKER_IND)  = ' ';                             
        END;                                                                    
 /* RULLERING AV FORVENTET            - 9802 HL */                              
    IF B02.ETTEPENS.FORVENTET(SØKER_IND) > 0   THEN                             
       CALL RULL_FORVENTET(B02.G_DATO_ÅMD(SØKER_IND),  /*2000-2*/               
                     B02.VIRK_DATO_ÅMD(SØKER_IND),     /*2000-2*/               
                     B02.ETTEPENS.FORVENTET(SØKER_IND));                        
                                                                                
 /* RULLERING AV EKTEFELLES INNTEKTER - 9802 HL */                              
    IF B02.ALDERSP.FAI(EKTEF_IND) > 0   THEN                                    
       CALL RULL_FAI(B02.G_DATO_ÅMD(EKTEF_IND),        /*2000-2*/               
                     B02.VIRK_DATO_ÅMD(EKTEF_IND),     /*2000-2*/               
                     B02.ALDERSP.FAI(EKTEF_IND));                               
                                                                                
    DO I = 1 TO 2;                                                              
       IF B02.FORSI.PENSJONSINNTEKT(I) > 0  THEN                                
          B02.FORSI.PENSJONSINNTEKT(I) =                                        
              F_RULL_FORSI(B02.G_DATO_ÅMD(I),           /*2000-2*/              
                    B02.VIRK_DATO_ÅMD(I),               /*2000-2*/              
                    B02.FORSI.PENSJONSINNTEKT(I));                              
                                                                                
       IF B02.FORSI.ARBEIDSINNTEKT(I) > 0  THEN                                 
          B02.FORSI.ARBEIDSINNTEKT(I) =                                         
              F_RULL_FORSI(B02.G_DATO_ÅMD(I),           /*2000-2*/              
                    B02.VIRK_DATO_ÅMD(I),               /*2000-2*/              
                    B02.FORSI.ARBEIDSINNTEKT(I));                               
                                                                                
       IF B02.FORSI.PENSJONSINNTEKT_EK(I) > 0  THEN                             
          B02.FORSI.PENSJONSINNTEKT_EK(I) =                                     
              F_RULL_FORSI(B02.G_DATO_ÅMD(I),           /*2000-2*/              
                     B02.VIRK_DATO_ÅMD(I),              /*2000-2*/              
                     B02.FORSI.PENSJONSINNTEKT_EK(I));                          
                                                                                
       IF B02.FORSI.ARBEIDSINNTEKT_EK(I) > 0  THEN                              
          B02.FORSI.ARBEIDSINNTEKT_EK(I) =                                      
              F_RULL_FORSI(B02.G_DATO_ÅMD(I),          /*2000-2*/               
                    B02.VIRK_DATO_ÅMD(I),              /*2000-2*/               
                    B02.FORSI.ARBEIDSINNTEKT_EK(I));                            
    END;                                                                        
  RETUR:                                                                        
   END AJOURFØR_B02_MED_A1TRANS;                                                
