 /*   SIST ENDRET PÅ PROD   2000.11.07 12.22.40 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.04.12 10.11.58 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.03.23 11.37.55 AV   JDA7339          */        
 /*       SIST ENDRET 20/07-98 10.03.47 AV   JDA7339                  */        
 /*       SIST ENDRET 04/05-98 12.59.05 AV   JDA7339                  */        
 /*       SIST ENDRET 25/02-98 09.57.15 AV   RFA9991                  */        
 /* ******************************************************************/         
 /* IDENTIFIKASJON                                                    */        
 /*     R0014180 - F_SÆRTILLEGG_UTLAND    FUNKSJON  I PLI.            */        
 /*     PROGRAMMERER: TRUDE SPONBERG JANUAR 1990                      */        
 /* HENSIKT                                                           */        
 /*     BEREGNE SÆRTILLEGG - BOSATT I UTLANDET                        */        
 /* PROGRAMTILKNYTNING                                                */        
 /*     PROSEDYREN BLIR INKLUDERT I OVERLIGGENDE PROGRAM.             */        
 /* BRUK                                                              */        
 /*     ST = F_SÆRTILLEGG(SUM_TP,ÅFT_MAX,FORH_INDIKATOR,              */        
 /*                              ST_PROSENT,GRAD,TT_ANV)              */        
 /* ENDRET :                                                          */        
 /* ***************************************************************** */        
                                                                                
 SÆRTILLEGG_UTLAND:                                                             
  PROC(SUM_TP);                                                                 
                                                                                
   DCL SUM_TP                   FIXED DEC (5),                                  
       ÅFT_MAX                  FIXED DEC (5),                                  
       FORH_INDIKATOR           CHAR      (1),                                  
       ST_PROSENT               FIXED DEC (5,2),                                
       GRAD                     FIXED DEC (3),                                  
       TT_ANV                   FIXED DEC (3);                                  
                                                                                
   DCL ST                       FIXED DEC (7,2);                                
                                                                                
   DCL W_FNR_EK                 PIC'(11)9';                                     
                                                                                
   DCL W_DATO_ÅMD               PIC'(8)9';                                      
   DCL W_HH DEF W_DATO_ÅMD POS(1)   PIC'99';     /*2000*/                       
   DCL W_ÅR DEF W_DATO_ÅMD POS(3)   PIC'99';     /*2000*/                       
   DCL W_MN DEF W_DATO_ÅMD POS(5)   PIC'99';     /*2000*/                       
   DCL W_DD DEF W_DATO_ÅMD POS(7)   PIC'99';     /*2000*/                       
                                                                                
   DCL W_DATO_MÅ                PIC'(4)9';                                      
                                                                                
   DCL TT_ETTER_1966_ÅR_EG   PIC'99';                                           
   DCL TT_ETTER_1966_ÅR_EK   PIC'99';                                           
                                                                                
   DCL W_TT_ETTER_1966_ÅR_EG   FIXED DEC (5,3);                                 
   DCL W_TT_ETTER_1966_ÅR_EK   FIXED DEC (5,3);                                 
                                                                                
   DCL W_ST_DEL1                FIXED DEC (15,2);                               
   DCL W_ST_DEL2                FIXED DEC (15,2);                               
                                                                                
   DCL W_ST                     FIXED DEC (15,4),                               
       W_GRAD                   FIXED DEC (3),                                  
       W_ST_FRADRAG             FIXED DEC (5),                                  
       W_ST_APD                 FIXED DEC (3);                                  
       W_ST_APD    = B02.ALDERSP.APD(BER_SØ_IND);                               
                                                                                
   DCL APD_GRAD                 FIXED DEC (3) INIT(0);   /*TS*/                 
                                                                                
   DCL SEKSTI                   FIXED DEC (5);       /*HL*/                     
                                                                                
  /*VI MÅ PASSE PÅ AT DET IKKE ER REGNET ST TIDLIGERE */                        
  /*TRUDE, 300591                                     */                        
  B02.ALDERSP.ST(BER_SØ_IND) = 0;  /* TESTING*/                                 
                                                                                
       /*-------------------------------------------------------*/              
       /*NYE REGLER FRA 0192 SETTER APD TIL 0. VI MÅ HER PASSE  */              
       /*PÅ AT APD BLIR 100 . TRUDE 230392                      */              
       /*-------------------------------------------------------*/              
                                                                                
  IF W_FNR_R13.ÅR       > 1924 THEN                                             
     APD_GRAD           = 100;                                                  
  ELSE                                                                          
     APD_GRAD           = B02.ALDERSP.APD(BER_SØ_IND);                          
                                                                                
  W_FNR_EK   = B02.FNR(BER_EK_IND);                                             
  /* W_DATO_ÅMD  = B02.VIRK_DATO_ÅMD(BER_SØ_IND); 200010 */                     
  W_DATO_ÅMD  = TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD; /*200010*/                    
                                                                                
  W_TT_ETTER_1966_ÅR_EG   = (B02.TT_FRAMT(BER_SØ_IND) +                         
                             B02.TT_ETTER_1966(BER_SØ_IND) + 11) / 12;          
  W_TT_ETTER_1966_ÅR_EK   = (B02.TT_FRAMT(BER_EK_IND) +                         
                             B02.TT_ETTER_1966(BER_EK_IND) + 11) / 12;          
                                                                                
  TT_ETTER_1966_ÅR_EG   = W_TT_ETTER_1966_ÅR_EG;                                
  TT_ETTER_1966_ÅR_EK   = W_TT_ETTER_1966_ÅR_EK;                                
                                                                                
                                                                                
     /*-----------------------------------------------------*/                  
     /* ETTER 0191 HAR VI IKKE 19-3 KODER .SPES FOR DEM SOM */                  
     /* BLIR KONV. FRA UP TIL AP. ELLER FEIL BLANKETT, AP   */                  
     /* ISTEDET FOR A1.                                     */                  
     /* UTTAKSDATO KAN VÆRE > 9100  NYE REGLER              */                  
     /*-----------------------------------------------------*/                  
                                                                                
                                                                                
     /*--------------------------------------*/                                 
     /* BEREGNINGSREGLENE PUNKT 3.1.5.2      */                                 
     /*--------------------------------------*/                                 
                                                                                
    IF B02.PENSJONSTYPE2(BER_SØ_IND)   ^= 'S' THEN  /*ENSLIG PENSJON */         
      DO;  /* "ENSLIG PENSJON, DVS IKKE SAMMENSTØT */                           
       IF B02.SPESOPPL.FRAVIK_19_3(BER_SØ_IND)   = '2' !                        
          B02.SPESOPPL.FRAVIK_19_3(BER_SØ_IND)   = '5' !                        
          B02.SPESOPPL.FRAVIK_19_3(BER_SØ_IND)   = '8' ! /*KONV FRA UP*/        
          B02.SPESOPPL.FRAVIK_19_3(BER_SØ_IND)   = '7' THEN                     
           DO;  /* GAMLE OVERFøRINGER AV TT MELLOM EKTEFELLER */                
             IF B02.PENSJONSTYPE2(BER_SØ_IND) =  'F' THEN                       
               DO;  /* FORSØRGER EKTEFELLE */                                   
                                                                                
                                                                                
                 IF W_DATO_ÅMD > 19910000     THEN    /*2000*/                  
                    SEKSTI = 6001;               /*2000*/                       
                 ELSE                                                           
                    SEKSTI = 6000;               /*2000*/                       
                                                                                
                 IF F_ALDER(W_FNR_EK,W_DATO_ÅMD) >= SEKSTI  THEN                
                    IF W_DATO_ÅMD          >  19940000  THEN                    
                                                                                
                     B02.ALDERSP.ST(BER_SØ_IND) = F_BEREGN_SÆRTILLEGG(          
                                           SUM_TP,0,'J',                        
                                         (ST_PROSENT_ENSLIG * 2 ),              
                                           APD_GRAD,                            
                                            (B02.TT_ANV(BER_SØ_IND)));          
                    ELSE                                                        
                    IF W_DATO_ÅMD          >  19890300  THEN                    
                                                                                
                     B02.ALDERSP.ST(BER_SØ_IND) = F_BEREGN_SÆRTILLEGG(          
                                           SUM_TP,0,'J',                        
                                           (ST_PROSENT_SAMST * 2 ),             
                                           APD_GRAD,                            
                                            (B02.TT_ANV(BER_SØ_IND)));          
                    ELSE                                                        
                     B02.ALDERSP.ST(BER_SØ_IND) = F_BEREGN_SÆRTILLEGG(          
                                           SUM_TP,0,'J',                        
                                           (ST_PROSENT_ENSLIG),                 
                                           APD_GRAD,                            
                                           (B02.TT_ANV(BER_SØ_IND)));           
                 ELSE                                                           
                     B02.ALDERSP.ST(BER_SØ_IND) = F_BEREGN_SÆRTILLEGG(          
                                           SUM_TP,0,'J',                        
                                           (ST_PROSENT_ENSLIG),                 
                                           APD_GRAD,                            
                                           (B02.TT_ANV(BER_SØ_IND)));           
               END;                                                             
             ELSE                                                               
                 B02.ALDERSP.ST(BER_SØ_IND) = F_BEREGN_SÆRTILLEGG(              
                                           SUM_TP,0,'J',                        
                                           (ST_PROSENT_ENSLIG),                 
                                           APD_GRAD,                            
                                           (B02.TT_ANV(BER_SØ_IND)));           
             GO TO RETUR;                  /*TRUDE 230392*/                     
           END;                                                                 
                                                                                
       IF (B02.TT_ANV(BER_SØ_IND)          < 40   &                             
           TT_ETTER_1966_ÅR_EG         >= 3 ) !                                 
          (B02.TT_ANV(BER_SØ_IND)             = 40  &                           
          (B02.SPESOPPL.FRAVIK_19_3(BER_SØ_IND)  ^= '5' &                       
           B02.SPESOPPL.FRAVIK_19_3(BER_SØ_IND)  ^= '7' &                       
           B02.SPESOPPL.FRAVIK_19_3(BER_SØ_IND)  ^= '8' &                       
           B02.SPESOPPL.FRAVIK_19_3(BER_SØ_IND)  ^= '2')) THEN                  
           DO;                                                                  
             IF B02.PENSJONSTYPE2(BER_SØ_IND) =  'F' THEN                       
               DO;                                                              
                                                                                
                                                                                
                 IF W_DATO_ÅMD > 19910000     THEN            /*2000*/          
                    SEKSTI = 6001;                                              
                 ELSE                                                           
                    SEKSTI = 6000;                                              
                                                                                
                IF F_ALDER(W_FNR_EK,W_DATO_ÅMD) >= SEKSTI  THEN                 
                   IF W_DATO_ÅMD               >  19940000  THEN                
                    B02.ALDERSP.ST(BER_SØ_IND) = F_BEREGN_SÆRTILLEGG(           
                                        SUM_TP,0,'J',                           
                                  (ST_PROSENT_ENSLIG * 2),                      
                                        APD_GRAD,                               
                                        (TT_ETTER_1966_ÅR_EG));                 
                   ELSE                                                         
                   IF W_DATO_ÅMD               >  19890300  THEN                
                    B02.ALDERSP.ST(BER_SØ_IND) = F_BEREGN_SÆRTILLEGG(           
                                        SUM_TP,0,'J',                           
                                        (ST_PROSENT_SAMST * 2),                 
                                        APD_GRAD,                               
                                        (TT_ETTER_1966_ÅR_EG));                 
                   ELSE                                                         
                    B02.ALDERSP.ST(BER_SØ_IND) = F_BEREGN_SÆRTILLEGG(           
                                        SUM_TP,0,'J',                           
                                        (ST_PROSENT_ENSLIG),                    
                                        APD_GRAD,                               
                                        (TT_ETTER_1966_ÅR_EG));                 
                ELSE                                                            
                    B02.ALDERSP.ST(BER_SØ_IND) = F_BEREGN_SÆRTILLEGG(           
                                        SUM_TP,0,'J',                           
                                        (ST_PROSENT_ENSLIG),                    
                                        APD_GRAD,                               
                                        (TT_ETTER_1966_ÅR_EG));                 
               END;                                                             
             ELSE                                                               
                    B02.ALDERSP.ST(BER_SØ_IND) = F_BEREGN_SÆRTILLEGG(           
                                        SUM_TP,0,'J',                           
                                        (ST_PROSENT_ENSLIG),                    
                                        APD_GRAD,                               
                                        (TT_ETTER_1966_ÅR_EG));                 
           END;                                                                 
      END;                                                                      
                                                                                
                                   /*--------------------------------*/         
                                   /* 3.1.5.3 SAMMENSTØTENDE PENSJON */         
                                   /*--------------------------------*/         
                                                                                
    IF B02.PENSJONSTYPE2(BER_SØ_IND)   = 'S' &    /* SAMMENSTØTENDE P.*/        
       (B02.SPESOPPL.BOSATT     (BER_SØ_IND)  > 0   &                           
        B02.SPESOPPL.BOSATT     (BER_EK_IND)  > 0 )  THEN                       
     DO;                                          /* 3.1.5.3          */        
       IF (B02.SPESOPPL.FRAVIK_19_3(BER_SØ_IND)  ^= '2'  !                      
           B02.SPESOPPL.FRAVIK_19_3(BER_SØ_IND)  ^= '7'  !                      
           B02.SPESOPPL.FRAVIK_19_3(BER_SØ_IND)  ^= '8'  !                      
           B02.SPESOPPL.FRAVIK_19_3(BER_SØ_IND)  ^= '5') THEN                   
        DO;                                                                     
         IF B02.TT_ETTER_1966   (BER_SØ_IND)   =  0 &                           
            B02.TT_ETTER_1966   (BER_EK_IND)   =  0  THEN                       
           DO;                            /* IKKE RETT TIL SÆRTILLEGG */        
             W_ST = 0;                                                          
             GO TO RETUR;                                                       
           END;                                                                 
         ELSE                                                                   
           IF B02.TT_ANV(BER_SØ_IND)  < 40 THEN                                 
             DO;                                                                
               IF TT_ETTER_1966_ÅR_EG   <                                       
                  TT_ETTER_1966_ÅR_EK   THEN                                    
                  TT_ETTER_1966_ÅR_EG   =                                       
                  TT_ETTER_1966_ÅR_EK;                                          
               B02.ALDERSP.ST(BER_SØ_IND) = F_BEREGN_SÆRTILLEGG(                
                                             SUM_TP,0,'J',                      
                                             (ST_PROSENT_SAMST),                
                                             APD_GRAD,                          
                                             (TT_ETTER_1966_ÅR_EG));            
             END;                                                               
                                                                                
       IF (B02.TT_FØR_1967(BER_SØ_IND)     = 0   &                              
           B02.TT_FØR_1967(BER_EK_IND)     = 0 ) &                              
          (B02.TT_ANV(BER_SØ_IND)  <  B02.TT_ANV(BER_EK_IND)) THEN              
            DO;                                                                 
        /*   STM NEDENFOR MÅ VÆRE TULL!! TRUDE 130292 ** UT !!!  */             
        /*   MEN SKAL VI BRUKE TT TIL EKTEFELLE PÅ ST ????       */             
        /*   B02.TT_ANV(BER_SØ_IND)  =  B02.TT_ANV(BER_EK_IND)   */             
             B02.ALDERSP.ST(BER_SØ_IND) = F_BEREGN_SÆRTILLEGG(                  
                                          SUM_TP,0,'J',                         
                                          (ST_PROSENT_SAMST),                   
                                          APD_GRAD,                             
                                          (B02.TT_ANV(BER_SØ_IND)));            
            END;                                                                
                                                                                
       IF (B02.TT_FØR_1967(BER_SØ_IND)     > 0   &                              
           B02.TT_ETTER_1966(BER_SØ_IND)   > 0 ) !                              
          (B02.TT_FØR_1967(BER_EK_IND)     > 0   &                              
           B02.TT_ETTER_1966(BER_EK_IND)   > 0 ) THEN                           
            DO;                                                                 
              IF (B02.SPESOPPL.FRAVIK_19_3(BER_SØ_IND)  = '2'  !                
                  B02.SPESOPPL.FRAVIK_19_3(BER_SØ_IND)  = '5'  !                
                  B02.SPESOPPL.FRAVIK_19_3(BER_SØ_IND)  = '7')   THEN           
                IF B02.TT_ANV(BER_SØ_IND) > B02.TT_ANV(BER_EK_IND) THEN         
                   DO;                                                          
                      W_ST_DEL1 = F_BEREGN_SÆRTILLEGG(                          
                                                   SUM_TP,0,'N',                
                                                   (ST_PROSENT_ENSLIG),         
                                                   APD_GRAD,                    
                    ((B02.TT_ANV(BER_SØ_IND)- B02.TT_ANV(BER_EK_IND))));        
                                                                                
                      W_ST_DEL2 = F_BEREGN_SÆRTILLEGG(                          
                                              SUM_TP,0,'N',                     
                                              (ST_PROSENT_SAMST),               
                                              APD_GRAD,                         
                                              (B02.TT_ANV(BER_EK_IND)));        
           B02.ALDERSP.ST(BER_SØ_IND) = W_ST_DEL1 + W_ST_DEL2 + 0.5;            
                    END;                                                        
                ELSE                                                            
                     B02.ALDERSP.ST(BER_SØ_IND) = F_BEREGN_SÆRTILLEGG(          
                                             SUM_TP,0,'J',                      
                                             (ST_PROSENT_SAMST),                
                                             APD_GRAD,                          
                                             (B02.TT_ANV(BER_SØ_IND)));         
            END;                                                                
     IF (B02.TT_FØR_1967(BER_SØ_IND) = 0 &                                      
         B02.TT_FØR_1967(BER_EK_IND) = 0 ) &                                    
         TT_ETTER_1966_ÅR_EG  >= 3    THEN  /* FORUTSETN. AT TTF67=0 */         
      DO;                                  /* PÅ BEGGE   ????        */         
       IF TT_ETTER_1966_ÅR_EG     >   TT_ETTER_1966_ÅR_EK                       
                                                            THEN                
             B02.ALDERSP.ST(BER_SØ_IND) = F_BEREGN_SÆRTILLEGG(                  
                                          SUM_TP,0,'J',                         
                                          (ST_PROSENT_SAMST),                   
                                          APD_GRAD,                             
                                          (TT_ETTER_1966_ÅR_EG));               
       ELSE                                                                     
             B02.ALDERSP.ST(BER_SØ_IND) = F_BEREGN_SÆRTILLEGG(                  
                                          SUM_TP,0,'J',                         
                                          (ST_PROSENT_SAMST),                   
                                          APD_GRAD,                             
                                          (TT_ETTER_1966_ÅR_EK));               
      END;                                                                      
     END;                                                                       
  END; /*??? MANGLER DET IKKE EN END  TRUDE, 140990   */                        
               /*---------------------------------------------------*/          
               /* PENSJONISTENE FLYTTER TIL UTLANDET 3.1.5.4        */          
               /*---------------------------------------------------*/          
                                                                                
                                                                                
  IF B01.SPESOPPL.BOSATT(BER_SØ_IND) = 000 &                                    
     B01.PENSJONSTYPE1  (BER_SØ_IND) = 'A' &                                    
     B02.SPESOPPL.BOSATT(BER_SØ_IND) > 000  THEN                                
     DO;                                                                        
       IF B02.PENSJONSTYPE2(BER_SØ_IND) = 'S'  THEN                             
         DO;                                                                    
            B02.ALDERSP.ST(BER_SØ_IND) = F_BEREGN_SÆRTILLEGG(                   
                                           (B02.ALDERSP.TP(BER_SØ_IND)),        
                                           0,'J',                               
                                           (ST_PROSENT_SAMST),                  
                                           APD_GRAD,                            
                                           (B02.TT_ANV(BER_SØ_IND)));           
         END;    /* HIT 23.01.90 */                                             
     END; /* HAR VÆRT BOSATT I NORGE , FLYTTER TIL UTLANDET */                  
                                                                                
          /* DEN ENE AV EKTEFELLENE BLE TILSTÅTT PENSJON MED ST */              
          /* I NORGE .DEN ANDRE TILSTÅS PENSJON UNDER BOSETTING */              
          /* I UTLANDET.   ER DENNE TESTEN GOD NOK ??????       */              
                                                                                
  IF (B01.SPESOPPL.BOSATT(BER_SØ_IND) = 000    &                                
      B01.ALDERSP.ST(BER_SØ_IND)      > 0   )  &                                
      B02.PENSJONSTYPE2(BER_SØ_IND)   = 'S'    &                                
      B02.SPESOPPL.BOSATT(BER_EK_IND) > 000    THEN                             
       DO;                                                                      
          IF B02.TT_ANV(BER_SØ_IND)  >  B02.TT_ANV(BER_EK_IND) THEN             
            DO;                                                                 
               W_ST_DEL1 = F_BEREGN_SÆRTILLEGG(                                 
                                               SUM_TP,0,'N',                    
                                               (ST_PROSENT_ENSLIG),             
                                               APD_GRAD,                        
                  ((B02.TT_ANV(BER_SØ_IND)- B02.TT_ANV(BER_EK_IND))));          
                                                                                
               W_ST_DEL2 = F_BEREGN_SÆRTILLEGG(                                 
                                          SUM_TP,0,'N',                         
                                          (ST_PROSENT_SAMST),                   
                                          APD_GRAD,                             
                                          (B02.TT_ANV(BER_EK_IND)));            
               B02.ALDERSP.ST(BER_SØ_IND) = W_ST_DEL1 + W_ST_DEL2 + 0.5;        
            END;                                                                
          ELSE                                                                  
               B02.ALDERSP.ST(BER_SØ_IND) = F_BEREGN_SÆRTILLEGG(                
                                          SUM_TP,0,'J',                         
                                          (ST_PROSENT_SAMST),                   
                                          APD_GRAD,                             
                                          (B02.TT_ANV(BER_SØ_IND)));            
       END;                                                                     
                                                                                
         /*----------------------------------------------------*/               
         /*DERSOM BARE RETTIGHETER ETTER KAP11 IKKE ST!!!!     */               
         /*----------------------------------------------------*/               
                                                                                
       IF B02.DØD_AV_YRKESSKADE(BER_SØ_IND)   = 'E' &                           
          B02.YRKEPENS.YUG(BER_EK_IND)        = 100  THEN                       
          B02.ALDERSP.ST(BER_SØ_IND)          = 0;                              
                                                                                
                                                                                
         /*----------------------------------------------------*/               
         /*KONVENSJON DERSOM TT MINDRE ENN 3 ÅR                */               
         /*----------------------------------------------------*/               
                                                                                
        IF (TT_ETTER_1966_ÅR_EG         < 3 &                                   
            TT_ETTER_1966_ÅR_EG         > 0 &                                   
            B02.KONVENSJON(BER_SØ_IND)  = 'J') THEN                             
             DO;                                                                
               IF B02.PENSJONSTYPE2(BER_SØ_IND)  ^= 'S' THEN                    
                  B02.ALDERSP.ST(BER_SØ_IND) = F_BEREGN_SÆRTILLEGG(             
                                        SUM_TP,0,'J',                           
                                        (ST_PROSENT_ENSLIG),                    
                                        APD_GRAD,                               
                                        (TT_ETTER_1966_ÅR_EG));                 
               ELSE                                                             
                IF B02.PENSJONSTYPE2(BER_SØ_IND)  = 'S' THEN                    
                   B02.ALDERSP.ST(BER_SØ_IND) = F_BEREGN_SÆRTILLEGG(            
                                        SUM_TP,0,'J',                           
                                        (ST_PROSENT_SAMST),                     
                                        APD_GRAD,                               
                                        (TT_ETTER_1966_ÅR_EG));                 
             END;       /*TT_E66 < 3 OG J I KONVENSJON */                       
                                                                                
   RETUR:                                                                       
     B02.AP_ST_NETTO(BER_SØ_IND) = B02.ALDERSP.ST(BER_SØ_IND);                  
                                                                                
  END SÆRTILLEGG_UTLAND;                                                        
