 /*   SIST ENDRET PÅ PROD   2003.12.05  8.37.43 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2003.12.05  8.26.06 AV   JDA2970          */        
 /*       SIST ENDRET 04/05-98 14.30.37 AV   JDA7339                  */        
 /*       SIST ENDRET 12/03-98 10.35.58 AV   SLA9300                  */        
 /*       SIST ENDRET 13/12-91 13.11.22 AV   DYBVIK                   */        
 /*       SIST ENDRET 14/04-89 14.59.46 AV   DYBVIK                   */        
 /*       SIST ENDRET 14/12-88 14.32.20 AV   DYBVIK                   */        
 /*       SIST ENDRET 06/10-87 10.05.12 AV   DYBVIK                   */        
 /*       SIST ENDRET 24/10-84 12.00.36 AV   FARVIK                   */        
 /*       SIST ENDRET 07/02-84 12.21.38 AV   BREIVIK                  */        
 /*       SIST ENDRET 10/10-83 08.35.05 AV   BREIVIK                  */        
 /*********************************************************************/        
 /*                                                                   */        
 /* IDENTIFIKASJON:                                                   */        
 /*    R0016201  -  DANNE TRANSAKSJON FOR FJERNING AV PERSON.         */        
 /*                                                                   */        
 /*    PROGRAMMERER: P. BREIVIK, JULI 1982.                           */        
 /*                                                                   */        
 /* HENSIKT:                                                          */        
 /*    HER GENERERES ET ROT- OG ET REGTPKT-SEGMENT TIL                */        
 /*    STAT-TRANS-BASEN SOM FORTELLER AT OPPLYSNINGENE UNDER ET       */        
 /*    FØDSELSNUMMER I DSR SKAL FJERNES (SETTES PASSIVE).             */        
 /*                                                                   */        
 /* PROGRAMTILKNYTNING:                                               */        
 /*                                                                   */        
 /* BRUK:                                                             */        
 /*                                                                   */        
 /*-------------------------------------------------------------------*/        
                                                                                
 R001620: PROC (COMMAREA_PEKER) OPTIONS (MAIN);                                 
                                                                                
 DCL COMMAREA_PEKER          POINTER;                                           
                                                                                
 %INCLUDE P0019906; /* TRANS_OPPL_OMR */                                        
 %PAGE;                                                                         
 %INCLUDE P0019908; /* KOM_OMR */                                               
                                                                                
 %PAGE;                                                                         
 %INCLUDE P0019912; /* DIV_PARAM_OMR */                                         
 %PAGE;                                                                         
 DCL 1 TR0ROT,                                                                  
 %INCLUDE P0016201; /* ARBEIDSOMRÅDE TR0ROT               */                    
 DCL 1 REGTPKT,                                                                 
 %INCLUDE P0016202; /* ARBEIDSOMRÅDE REGTPKT              */                    
 /*-------------------------------------------------------------------*/        
 /*   DL/1 PARAMETRE                                                  */        
 /*-------------------------------------------------------------------*/        
 DCL PLITDLI ENTRY;                                                             
                                                                                
 %INCLUDE DLIUIB;                                                               
                                                                                
 DCL UIB_RC_OK               BIT (8) INIT (0);     /* KONSTANT */               
                                                                                
 DCL 1 TR0_PCB_PEKER         BASED (UIBPCBAL),                                  
       2 TR0_PCB_PTR         POINTER;                                           
                                                                                
 DCL INSERT                  CHAR (4) INIT('ISRT');                             
 DCL PARAM_COUNT_3           FIXED BIN (31) INIT(3);                            
 DCL PARAM_COUNT_4           FIXED BIN (31) INIT(4);                            
 DCL PARAM_COUNT_5           FIXED BIN (31) INIT(5);                            
                                                                                
 /*-------------------------------------------------------------------*/        
 /*                                                                   */        
 /*   DEKLARASJON AV SSA                                              */        
 /*                                                                   */        
 /*-------------------------------------------------------------------*/        
                                                                                
 DCL SSA_UNQUAL              CHAR (9);                                          
                                                                                
 DCL 1 SSA_TR0ROT UNALIGNED,                                                    
       2 HOVEDDEL            CHAR (17) INIT('TR0ROT  (FNR     '),               
       2 REL_OP              CHAR (2)  INIT(' ='),                              
       2 FNR_KEY             FIXED DEC (11),                                    
       2 END_MRK             CHAR (1)  INIT(')');                               
                                                                                
 /*-------------------------------------------------------------------*/        
 /*   PCB-OMRÅDE                                                      */        
 /*-------------------------------------------------------------------*/        
 %INCLUDE P0012002;                                                             
                                                                                
 DCL 1 TR0_PCB               BASED (TR1_PCB_PEKER),                             
 %INCLUDE P0012003;                                                             
 /*-------------------------------------------------------------------*/        
 /*   HJELPEVARIABLE                                                  */        
 /*-------------------------------------------------------------------*/        
 /* 2000                                                                        
 DCL DATO_KLOKKE             PIC '(17)9',                                       
     DATO_ÅMD                PIC '(8)9' DEF DATO_KLOKKE,                        
     DATO                    PIC '(8)9' DEF DATO_KLOKKE,                        
     KLOKKE                  PIC '(9)9' DEF DATO_KLOKKE POS(9);                 
  */                                                                            
 DCL DATO                    PIC '(8)9',                       /*2000*/         
     KLOKKE_CH               CHAR (9)  ,                       /*2000*/         
     KLOKKE                  PIC '(9)9' DEF KLOKKE_CH POS(1);  /*2000*/         
                                                                                
 DCL HELP_VTP                PIC '(8)9',                                        
     HELP_DATO               PIC '(8)9' DEF HELP_VTP;                           
                                                                                
 DCL IO_PEK                  POINTER;                                           
                                                                                
 DCL IO_AREA                 CHAR (150);                                        
                                                                                
 DCL IO_HELP_AREA            CHAR (150) BASED (IO_PEK);                         
                                                                                
 DCL PBR_TEXT                CHAR (50);                                         
 DCL W01_FNR               DEC FIXED(11);                                       
                                                                                
 DCL (TIME,CSTG,                                                                
      ADDR,CHAR)             BUILTIN;                                           
                                                                                
 %PAGE;                                                                         
 /*-------------------------------------------------------------------*/        
 /*    START PÅ PROGRAM DEAKTIVISER OPPLYSNINGENE PÅ EN PERSON.       */        
 /*-------------------------------------------------------------------*/        
                                                                                
    DIV_PARAM_OMR.PROGRAM_ID = 'R0016201';                                      
                                                                                
    UIBPTR =UIB_PEKER;                                                          
                                                                                
    TR0ROT            = '';                                                     
    REGTPKT           = '';                                                     
                                                                                
    TR0ROT.FNR = TRANS_OPPL_OMR.FØDSNUMMER;                                     
                                                                                
    SSA_UNQUAL = 'TR0ROT   ';                                                   
                                                                                
    CALL PLITDLI (PARAM_COUNT_4,                                                
                  INSERT,                                                       
                  TR0_PCB,                                                      
                  TR0ROT,                                                       
                  SSA_UNQUAL);                                                  
                                                                                
    IF DLIUIB.UIBRCODE.UIBFCTR ^= UIB_RC_OK !                                   
       ^(TR0_PCB.STATUS_KODE = '  ' ! TR0_PCB.STATUS_KODE = 'II') THEN          
       DO;                                                                      
 L100:                                                                          
     /*--------------------------------------*/                                 
     /* FEIL VED INSERT-KALLET TIL TR0-BASEN */                                 
     /* SEGMENT FANTES IKKE FRA FØR OG       */                                 
     /* LOT SEG HELLER IKKE LEGGE INN        */                                 
     /*--------------------------------------*/                                 
                                                                                
          FEIL_VED_LABEL = 'L100';                                              
          FEIL_MELD_NR = 500;                                                   
          GO TO UTGANG;                                                         
       END;                                                                     
                                                       /*2000*/                 
    DATO                = DATO_2000;                   /*2000*/                 
    KLOKKE_CH           = TIME;                        /*2000*/                 
    REGTPKT.DATO        = DATO_2000     ;              /*2000*/                 
    REGTPKT.KLOKKE      = KLOKKE        ;              /*2000*/                 
    REGTPKT.VIRKDATO    = '00010100'; /* INGEN VIRKDATO  2000    */             
    REGTPKT.DELMRK_ALT  = 'F';        /* FJERNES */                             
    SSA_TR0ROT.FNR_KEY  = TR0ROT.FNR;                                           
                                                                                
    IO_PEK  = ADDR(REGTPKT);                                                    
    IO_AREA = IO_HELP_AREA;                                                     
                                                                                
                                                                                
    SSA_UNQUAL = 'REGTPKT ';                                                    
                                                                                
    CALL PLITDLI (PARAM_COUNT_5,                                                
                  INSERT,                                                       
                  TR0_PCB,                                                      
                  IO_AREA,                                                      
                  SSA_TR0ROT,                                                   
                  SSA_UNQUAL);                                                  
                                                                                
    IF DLIUIB.UIBRCODE.UIBFCTR ^= UIB_RC_OK !                                   
       TR0_PCB.STATUS_KODE ^= '  ' THEN                                         
       DO;                                                                      
 L110:                                                                          
     /*--------------------------------------*/                                 
     /* FEIL VED INSERT-KALLET TIL TR0-BASEN */                                 
     /* UNIKT SEGMENT LOT SEG IKKE LEGGE INN */                                 
     /*--------------------------------------*/                                 
                                                                                
          FEIL_VED_LABEL = 'L110';                                              
          FEIL_MELD_NR = 500;                                                   
       END;                                                                     
                                                                                
 UTGANG:                                                                        
    EXEC CICS RETURN;                                                           
                                                                                
 END;                                                                           
