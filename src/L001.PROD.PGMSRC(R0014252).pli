 /*   SIST ENDRET PÅ PROD   2006.02.13  8.15.08 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2006.02.09  8.03.32 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2005.11.29  9.54.59 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2005.11.09  8.40.25 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2004.06.18 12.23.57 AV   SPA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.05.03 12.32.17 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2004.02.03  9.49.13 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.02.03  9.48.38 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2003.10.09 11.54.30 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2003.10.09 11.52.18 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2003.08.14  8.29.59 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2003.08.14  8.28.10 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2003.06.26  8.33.50 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2003.03.14 10.49.25 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2003.03.14 10.36.43 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2002.06.03 14.35.53 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2002.04.19 12.27.52 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.06.01  8.37.17 AV   HLA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.11.14 12.38.02 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.07.07 13.00.38 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.05.03 13.11.37 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.03.20 13.23.16 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.01.18 11.47.42 AV   JDA7339          */        
 /*       SIST ENDRET 04/05-98 12.59.41 AV   JDA7339                  */        
 /*       SIST ENDRET 17/03-98 09.30.07 AV   RFA9991                  */        
 /*                                                                   */        
 /* ***************************************************************** */        
 /* IDENTIFIKASJON:                                                   */        
 /*    R0014252 - UNDERPROGRAM I PLI                                  */        
 /*    PROGRAMMERER:  TRUDE OKTOBER 1988.                             */        
 /* HENSIKT:                                                          */        
 /*    BEREGNER 100 % AVTALEFESTET FØRTIDSPENSJON                     */        
 /*    OPPDATERER      SPT, OPT, PÅ, TT.                              */        
 /*    OPPDATERER      ST,KT,BT, ET, GP OG TP                         */        
 /*    OPPDATERER      PÅ_ETTER91                                     */        
 /* PROGRAMTILKNYTNING:                                               */        
 /*    PROSEDYREN INNGÅR SOM EN DEL AV BEHANDLINGA AV AVTALE-         */        
 /*    FESTET PENSJONSBERGENINGEN.                                    */        
 /*    INCLUDES I 4251                                                */        
 /* BRUK:                                                             */        
 /*    CALL BEREGN_AFP_DEL1                                           */        
 /* ***************************************************************** */        
 BEREGN_AFP_DEL1: PROC;                                                         
    DCL SUM_TP                       FIXED DEC(5);                              
    DCL TT_EGEN                      PIC '(3)9';                                
    DCL TT_EK                        PIC '(3)9';                                
    DCL W_PT2                        CHAR (1) INIT (' ');                       
    DCL W_GP                         FIXED DEC (11,4) INIT (0);                 
    DCL W_TP                         FIXED DEC (7)   INIT (0);                  
    DCL W_TP_ALT                     FIXED DEC (7)   INIT (0);                  
                                                                                
    /* == R0014252 ================================================== */        
                                                                                
    /* -------------------------------------------------------------- */        
    /* TILLEGGSPENSJON                                                */        
    /* -------------------------------------------------------------- */        
                                                                                
    B01_B02_IND          = 2;                                                   
    POTALL_OPPL.IND      = BER_SØ_IND;                                          
    EXEC CICS LINK PROGRAM ('R0014141')  COMMAREA (KOM_OMR);                    
                                                                                
    IF FEIL_MELD_NR > 0  THEN                                                   
       GO TO RETUR;                                                             
    ELSE                                                                        
       PROGRAM_ID = 'R0014251';                                                 
                                                                                
    B02.POENGTILLEGG_DATO_ÅMD(BER_SØ_IND) = 0;                                  
    B02.POENGTILLEGG_KODE   (BER_SØ_IND) = ' ';                                 
    B02.POENGTILLEGG_DATO_ÅMD(BER_EK_IND) = 0;                                  
    B02.POENGTILLEGG_KODE   (BER_EK_IND) = ' ';                                 
    B02.GP_REDUKSJON_KODE(BER_SØ_IND) = ' ';   /*20030128 HL */                 
                                                                                
    /*LOVENDRING FRA 1.1.92 : */                                                
    /* ENDRA 030500 MARTIN... 4142 BØR UTGÅ MED TIDEN... */                     
    IF (B02.UTTAKSDATO_ÅMD(BER_SØ_IND) > 19920000)       &     /*2000*/         
       (W_FNR_R13.ÅR > 1925)            THEN  /*2000*/                          
       DO;                                                                      
          B02.ALDERSP.PÅ_ETTER91(BER_SØ_IND) =                                  
              POREKKE.PÅ_ETTER91(BER_SØ_IND);                                   
       END;                                                                     
                                                                                
    B02.ALDERSP.TP(BER_SØ_IND)=                                                 
       F_TP92                                                                   
          (                                                                     
           W_FNR_R13.ÅR,                                                        
           G,                                                                   
           POREKKE.SPT(BER_SØ_IND),                                             
           POREKKE.OPT(BER_SØ_IND),                                             
           POREKKE.PÅ(BER_SØ_IND),                                              
           POREKKE.PÅ_ETTER91(BER_SØ_IND),                                      
           100,                                                                 
           100,                                                                 
           'J'                                                                  
          );                                                                    
                                                                                
    B02.ALDERSP.SPT(BER_SØ_IND) = POREKKE.SPT(BER_SØ_IND);                      
    B02.ALDERSP.OPT(BER_SØ_IND) = POREKKE.OPT(BER_SØ_IND);                      
    B02.ALDERSP.PÅ (BER_SØ_IND) = POREKKE.PÅ (BER_SØ_IND);                      
                                                                                
    IF FEIL_MELD_NR > 0  THEN                                                   
       GO TO RETUR;                                                             
    ELSE                                                                        
       PROGRAM_ID = 'R0014251';                                                 
                                                                                
    /* -------------------------------------------------------------- */        
    /*  GRUNNPENSJON                                                  */        
    /* -------------------------------------------------------------- */        
                                                                                
    IF B02.TT_16_66(BER_SØ_IND)  > 0  THEN                                      
       TT_EGEN  = (B02.TT_16_66(BER_SØ_IND) + 6) / 12;                          
    ELSE                                                                        
       TT_EGEN = B02.STATUS.TT_FØR_1967 (BER_SØ_IND) +                          
                 (B02.TT_ETTER_1966 (BER_SØ_IND)  +                             
                  B02.STATUS.TT_FRAMT (BER_SØ_IND) + 11) / 12;                  
                                                                                
    IF TT_EGEN > 39 !                                                           
       B02.FRAVIK_FLYKTN(BER_SØ_IND) = 'J' THEN                                 
       B02.TT_ANV(BER_SØ_IND) = 40;                                             
    ELSE                                                                        
       B02.TT_ANV(BER_SØ_IND) = TT_EGEN;                                        
                                                                                
    IF B02.TT_ANV(BER_SØ_IND) < B02.ALDERSP.PÅ  (BER_SØ_IND) THEN               
       B02.TT_ANV(BER_SØ_IND) = B02.ALDERSP.PÅ  (BER_SØ_IND);                   
                                                                                
    IF (B02.PENSJONSTYPE2 (BER_SØ_IND) = 'S') !                                 
       /* SAMMENSTØTENDE PENSJON */                                             
       (B02.PENSJONSTYPE2 (BER_SØ_IND) = 'N' &            /*2000-2*/            
        /* AFP MED NULLPENSJON */                                               
        B02.PENSJONSTYPE2 (BER_EK_IND) = 'P' &            /*2000-2*/            
        /* EKTEFELLEN HAR PENSJON */                                            
        B02.SIVILSTAND    (BER_SØ_IND) ^= 'A' )     THEN                        
        /* LEVER IKKE ADSKILT */                                                
       DO;                                                                      
                                                                                
          /* TILLEGG 18.9.89 HL -                      */                       
          /* LOVENDRING 7.2 NR 2 B - 2. SETNING UTGÅR */                        
                                                                                
          IF B01.PENSJONSTYPE2(BER_SØ_IND)    = 'S'    &                        
             B01.STATUS_KODE_HIST(BER_SØ_IND) = ' '    &                        
             B01.UFØRPENS.UFG    (BER_EK_IND) < 100    &                        
             B01.UFØRPENS.UFG    (BER_EK_IND) >   0    &                        
             B01.VIRK_DATO_ÅMD   (BER_SØ_IND) < 19900100  THEN                  
                                                                                
             B02.GAMMEL_SAMMENSTØTS_REGEL(BER_SØ_IND) = 'J';                    
                                                                                
          IF  (B02.PENSJONSTYPE1(BER_EK_IND) = 'U' !                            
               B02.PENSJONSTYPE1(BER_EK_IND) = 'Y'  )  THEN                     
             DO;  /* EKTEFELLEN HAR UFØREPENSJON */                             
                IF (B02.VIRK_DATO_ÅMD   (BER_SØ_IND) < 19900100 !               
                   B02.GAMMEL_SAMMENSTØTS_REGEL(BER_SØ_IND) = 'J') THEN         
                   DO;  /* GAMMEL SAMMENSTØTSREGEL (FØR 1990) */                
     W_GP= 1 - REST_PROSENT * B02.UFØRPENS.UFG(BER_EK_IND)/100;                 
                      IF (W_GP < GP_PROSENT) THEN                               
                         W_GP = GP_PROSENT;                                     
                      B02.ALDERSP.GP(BER_SØ_IND) =  G * W_GP  *                 
                                            B02.TT_ANV(BER_SØ_IND)  /           
                                                   (40 * 12) + 0.5;             
                      IF (B02.SIVILSTAND(BER_SØ_IND) = 'G'  !                   
                          B02.SIVILSTAND(BER_SØ_IND) = 'P' ) THEN               
                          B02.GP_REDUKSJON_KODE(BER_SØ_IND) = 'J';              
                      ELSE                                                      
                          B02.GP_REDUKSJON_KODE(BER_SØ_IND) = 'Y';              
                   END; /* GAMMEL SAMMENSTØTSREGEL (FØR 1990) */                
                ELSE                                                            
                   DO;  /* IKKE GAMMEL SAMMENSTØTSREGEL */                      
                      B02.GAMMEL_SAMMENSTØTS_REGEL(BER_SØ_IND) = 'N';           
  /* R 1303 JFA       IF B02.SUM_YTELSE(BER_EK_IND) > 0 &                       
                         (B02.PENSJON_FØR_9802(BER_SØ_IND) = 'J' !              
                          B02.PENSJON_FØR_9802(BER_SØ_IND) = 'X') */            
  /*TILBAKE HL*/      IF B02.SUM_YTELSE(BER_EK_IND) > 0 THEN                    
                       DO;                                                      
                        B02.ALDERSP.GP(BER_SØ_IND) = (G * GP_PROSENT *          
                                          B02.TT_ANV(BER_SØ_IND)) /             
                                          (40 * 12) + 0.5;                      
                         IF (B02.SIVILSTAND(BER_SØ_IND) = 'G'  !                
                             B02.SIVILSTAND(BER_SØ_IND) = 'P' ) THEN            
                             B02.GP_REDUKSJON_KODE(BER_SØ_IND) = 'J';           
                         ELSE                                                   
                             B02.GP_REDUKSJON_KODE(BER_SØ_IND) = 'Y';           
                       END;                                                     
                      ELSE                                                      
                         B02.ALDERSP.GP(BER_SØ_IND) =                           
                                     (G * B02.TT_ANV(BER_SØ_IND)) /             
                                          (40 * 12) + 0.5;                      
                   END; /* IKKE GAMMEL SAMMENSTØTSREGEL */                      
             END; /* EKTEFELLEN HAR UFØREPENSJON */                             
 /*HIT 18.9.89 HL*/                                                             
                                                                                
          ELSE /*EKTEFELLEN ER ALDER/AFP*/                                      
            DO;                                                                 
   /*AVKLART I MAIL FRA BENTE SLETBAK 12.8.03:   */                             
               IF (B02.PENSJONSTYPE1(BER_EK_IND) = 'K'    &                     
 /*RETTET HL*/     B02.SUM_YTELSE(BER_EK_IND)    =  0)    &                     
                 (B02.PENSJON_FØR_9802(BER_SØ_IND) = 'J'  !                     
  /*R 1303 JFA*/  B02.PENSJON_FØR_9802(BER_SØ_IND) = 'X')  THEN                 
                  DO;                                                           
                     B02.GP_REDUKSJON_KODE(BER_SØ_IND) = ' ';                   
                                                                                
                     B02.ALDERSP.GP(BER_SØ_IND) = (G *                          
                                          B02.TT_ANV(BER_SØ_IND)) /             
                                          (40 * 12) + 0.5;                      
                  END;                                                          
               ELSE                                                             
               DO;                                                              
                     B02.ALDERSP.GP(BER_SØ_IND) = (G * GP_PROSENT *             
                                          B02.TT_ANV(BER_SØ_IND)) /             
                                          (40 * 12) + 0.5;                      
                  IF (B02.SIVILSTAND(BER_SØ_IND) = 'G'  !                       
                      B02.SIVILSTAND(BER_SØ_IND) = 'P' ) THEN                   
                      B02.GP_REDUKSJON_KODE(BER_SØ_IND) = 'J';                  
                  ELSE                                                          
                      B02.GP_REDUKSJON_KODE(BER_SØ_IND) = 'Y';                  
               END;                                                             
            END;                                                                
       END;                                                                     
    ELSE                                                                        
       DO;                                                                      
   /*     B02.TT_ANV(BER_SØ_IND) = TT_EGEN    */                                
                                                                                
          IF B02.TT_GARANTI(BER_SØ_IND) >                                       
             B02.TT_ANV(BER_SØ_IND)            THEN                             
             B02.TT_ANV(BER_SØ_IND) = B02.TT_GARANTI(BER_SØ_IND);               
                                                                                
          IF B02.PENSJONSTYPE1(BER_EK_IND) = 'K'   &                            
             B02.SUM_YTELSE   (BER_EK_IND) > 0     & /*9802 HL*/                
             B02.SIVILSTAND    (BER_SØ_IND) ^= 'A'  &                           
             B02.SIVILSTAND    (BER_SØ_IND) ^= 'S'  & /*200310 HL*/             
             ALDER_EK < 6501              THEN                                  
            DO;                                                                 
             IF B02.ALDERSP.UTTAKSDATO_ÅMD(BER_EK_IND) > 19970900   THEN        
                DO;  /*REGLER FRA 1.10.1997 */                                  
                   B02.ALDERSP.GP(BER_SØ_IND) = (G * GP_PROSENT *               
                                    B02.TT_ANV(BER_SØ_IND)) /                   
                                    (40 * 12) + 0.5;                            
                    DIV_PARAM_OMR.REDUSERT_GP_2G(BER_SØ_IND) = 'K';             
                    IF (B02.SIVILSTAND(BER_SØ_IND) = 'G'  !                     
                        B02.SIVILSTAND(BER_SØ_IND) = 'P' ) THEN                 
                        B02.GP_REDUKSJON_KODE(BER_SØ_IND) = 'J';                
                    ELSE                                                        
                        B02.GP_REDUKSJON_KODE(BER_SØ_IND) = 'Y';                
                END; /* REGLER FRA 1.10.1997 */                                 
             ELSE /*NÅ BLIR DET REGLER FØR 9710 : */                            
                  /* SKAL PT3 FOR EKTEFELLE = K OGSÅ VÆR MED?*/                 
                                             /* TRUDE 020698  */                
             IF (B02.PENSJONSTYPE3(BER_EK_IND) = 'S'   !                        
                 B02.PENSJONSTYPE3(BER_EK_IND) = 'K')   &                       
                (B02.PENSJONSTYPE3(BER_SØ_IND) = 'S'  !                         
                 (B02.PENSJONSTYPE3(BER_SØ_IND) = 'K'  &                        
                  ALDER_ÅM > 6500))               THEN                          
                DO;  /* SAMMENSTØT ETTER GAMLE REGLER */                        
                   B02.ALDERSP.GP(BER_SØ_IND) = (G * GP_PROSENT *               
                                    B02.TT_ANV(BER_SØ_IND)) /                   
                                    (40 * 12) + 0.5;                            
                    DIV_PARAM_OMR.REDUSERT_GP_2G(BER_SØ_IND) = 'K';             
                    IF (B02.SIVILSTAND(BER_SØ_IND) = 'G'  !                     
                        B02.SIVILSTAND(BER_SØ_IND) = 'P' ) THEN                 
                        B02.GP_REDUKSJON_KODE(BER_SØ_IND) = 'J';                
                    ELSE                                                        
                        B02.GP_REDUKSJON_KODE(BER_SØ_IND) = 'Y';                
                END; /* SAMMENSTØT ETTER GAMLE REGLER */                        
                /* REMEDY #64, SE LOGG - NYE STMT FRA HER */                    
             ELSE                                                               
                DO;  /* IKKE SAMMENSTØT ETTER GAMLE REGLER */                   
                   DIV_PARAM_OMR.REDUSERT_GP_2G(BER_SØ_IND) = ' ';              
                   B02.GP_REDUKSJON_KODE(BER_SØ_IND) = ' ';                     
                   B02.ALDERSP.GP(BER_SØ_IND) = (G *                            
                                    B02.TT_ANV(BER_SØ_IND)) /                   
                                    (40 * 12) + 0.5;                            
                END; /* IKKE SAMMENSTØT ETTER GAMLE REGLER */                   
                /* REMEDY #64, SE LOGG - NYE STMT TIL HIT */                    
            END;                                                                
          ELSE                                                                  
          IF B02.PENSJON_FØR_9802(BER_SØ_IND) ^= 'J'    THEN                    
                                                                                
            DO;                                                                 
              IF (B02.SIVILSTAND(BER_SØ_IND) = 'G'  !                           
                  B02.SIVILSTAND(BER_SØ_IND) = 'P'  !                           
                  B02.SIVILSTAND(BER_SØ_IND) = 'W'  !                           
                  B02.SIVILSTAND(BER_SØ_IND) = 'V'   )      &                   
                                                                                
                  B02.EK_INNT_OVER_2G(BER_SØ_IND) = 'J' THEN                    
                 DO;                                                            
                                                                                
                    B02.ALDERSP.GP(BER_SØ_IND) = (G * GP_PROSENT *              
                                          B02.TT_ANV(BER_SØ_IND)) /             
                                          (40 * 12) + 0.5;                      
                    DIV_PARAM_OMR.REDUSERT_GP_2G(BER_SØ_IND) = 'J';             
                    IF (B02.SIVILSTAND(BER_SØ_IND) = 'G'  !                     
                        B02.SIVILSTAND(BER_SØ_IND) = 'P' ) THEN                 
                        B02.GP_REDUKSJON_KODE(BER_SØ_IND) = 'I';                
                    /* MARTIN 151002 - FRA HER */                               
                    ELSE                                                        
                    IF (B02.SIVILSTAND(BER_SØ_IND) = 'V' !                      
                        B02.SIVILSTAND(BER_SØ_IND) = 'W' ) &                    
                       (B02.PENSJONSTYPE2(BER_EK_IND) = 'P' !                   
                        B02.PENSJONSTYPE2(BER_EK_IND) = 'S' ) THEN              
                        B02.GP_REDUKSJON_KODE(BER_SØ_IND) = 'Y';                
                    /* MARTIN 151002 - TIL HIT */                               
                    ELSE                                                        
                       B02.GP_REDUKSJON_KODE(BER_SØ_IND) = 'Z';                 
                 END;                                                           
              ELSE  /*NYTT FELT 200107 : */                                     
              IF (B02.SIVILSTAND(BER_SØ_IND) = 'S'  !                           
                  B02.SIVILSTAND(BER_SØ_IND) = 'A'  !                           
                  B02.SIVILSTAND(BER_SØ_IND) = 'V'  !                           
                  B02.SIVILSTAND(BER_SØ_IND) = 'U'  !                           
                  B02.SIVILSTAND(BER_SØ_IND) = 'E'   )      &                   
                                                                                
                  B02.RED_GP_3_2_5(BER_SØ_IND) = 'J' THEN                       
                 DO;                                                            
                                                                                
                    B02.ALDERSP.GP(BER_SØ_IND) = (G * GP_PROSENT *              
                                          B02.TT_ANV(BER_SØ_IND)) /             
                                          (40 * 12) + 0.5;                      
                    DIV_PARAM_OMR.REDUSERT_GP_2G(BER_SØ_IND) = 'X';             
                    B02.GP_REDUKSJON_KODE(BER_SØ_IND) = 'X';                    
                 END;                                                           
              ELSE                                                              
              IF  B02.SIVILSTAND(BER_SØ_IND) = 'V'   &                          
                 (B02.PENSJONSTYPE2(BER_EK_IND) = 'P'  !                        
                  B02.PENSJONSTYPE2(BER_EK_IND) = 'S'  )   THEN                 
                 DO;                                                            
                    B02.ALDERSP.GP(BER_SØ_IND) = (G * GP_PROSENT *              
                                          B02.TT_ANV(BER_SØ_IND)) /             
                                          (40 * 12) + 0.5;                      
                    DIV_PARAM_OMR.REDUSERT_GP_2G(BER_SØ_IND) = 'V';             
                    B02.GP_REDUKSJON_KODE(BER_SØ_IND) = 'Y';                    
                 END;                                                           
              ELSE                                                              
                 B02.ALDERSP.GP(BER_SØ_IND) = (G * B02.TT_ANV(                  
                                       BER_SØ_IND)) /                           
                                       (40 * 12) + 0.5;                         
            END;                                                                
          ELSE                                                                  
          B02.ALDERSP.GP(BER_SØ_IND) = (G * B02.TT_ANV(                         
                                       BER_SØ_IND)) /                           
                                       (40 * 12) + 0.5;                         
       END;                                                                     
                                                                                
 /*NÅR FAMILIEN ER SPLITTET SKAL MAN ALLTID BEHANDLES SOM ENSLIG:*/             
 /*  FJERNET 200310 HL :                                                        
   IF (FAMILIE_SPLITTET_IND) THEN                                               
      DO;                *********************** */                             
         /* --------------------------------------------------------- */        
         /* DIV_PARAM_OMR.FAMILIE_SPLITTET_IND SETTES ENTEN I         */        
         /*    1440 : MELDING OM SKILSMISSE - ELLER                   */        
         /*    4901 : AUTOHENDELSE BEREGNING                          */        
         /* --------------------------------------------------------- */        
  /*     B02.ALDERSP.GP(BER_SØ_IND) = (G * B02.TT_ANV(                          
                                       BER_SØ_IND)) /                           
                                       (40 * 12) + 0.5;                         
         DIV_PARAM_OMR.REDUSERT_GP_2G(BER_SØ_IND) = ' ';                        
         B02.GP_REDUKSJON_KODE(BER_SØ_IND) = ' ';                               
      END; ********************************************** */                    
    SUM_TP = B02.ALDERSP.TP(BER_SØ_IND) + B02.TP_BRUTTO(BER_SØ_IND);            
                                                                                
 /* ***************************************************************** */        
 /* KALL FUNKSJON SOM BEREGNER SÆRTILLEGG.     4133                   */        
 /* ***************************************************************** */        
                                                                                
 /*14.1.00 : EKTEFELLER TIL AFP STAT/KOMM SKAL HA FULLT ST */                   
    IF ((B02.PENSJONSTYPE2 (BER_SØ_IND) = 'S')  &                               
       ^(B02.PENSJONSTYPE3 (BER_EK_IND) = 'K')  &                               
       ^(B02.PENSJONSTYPE3 (BER_EK_IND) = 'S')     )    !                       
       (B02.PENSJONSTYPE2 (BER_SØ_IND) = 'N' &                                  
        B02.PENSJONSTYPE2 (BER_EK_IND) = 'P' &                                  
        B02.SIVILSTAND    (BER_EK_IND) = 'G') THEN                              
                                                                                
       DO;                                                                      
          B02.ALDERSP.ST(BER_SØ_IND) =                                          
             F_BEREGN_SÆRTILLEGG                                                
                (                                                               
                 SUM_TP,                                                        
                 0,                                                             
                 'J',                                                           
                 (ST_PROSENT_SAMST),                                            
                 100,                                                           
                 (B02.TT_ANV(BER_SØ_IND))                                       
                );                                                              
          B02.ALDERSP.ET(BER_SØ_IND)   = 0;                                     
       END;                                                                     
    ELSE                                                                        
                                                                                
  /*IF B02.PENSJONSTYPE2 (BER_SØ_IND) = 'F' THEN                                
  /* STMT OVER VAR FEIL 14.11.2000 - MARTIN.... */                              
    IF (B02.PENSJONSTYPE2 (BER_SØ_IND) = 'F' ) !                                
       (B02.PENSJONSTYPE2 (BER_SØ_IND) = 'N' &                                  
        B02.PENSJONSTYPE1 (BER_EK_IND) = 'F' ) THEN                             
       DO;                                                                      
  /*LOVENDRING OM BEHOVSPRØVET FORSØRGINGSTI_ÅMDLLEGG FRA 1.5.91 :*/            
  /* SÅ SNART DET SKAL BEREGNES MED HØYERE GRUNNBELØP ENN     */                
  /* PR 30.4.91, SÅ SKAL ALL BEREGNING SKJE I RXX14011        */                
                                                                                
          IF G > 34100 THEN                                                     
             DO;                                                                
                B02.ALDERSP.ET(BER_SØ_IND)   = 0;                               
                IF B02.FT_SKAL_BEREGNES(BER_SØ_IND) = ' '     THEN              
                   B02.FT_SKAL_BEREGNES(BER_SØ_IND) = 'N';                      
             END;                                                               
          ELSE                                                                  
             B02.ALDERSP.ET(BER_SØ_IND)   = 0.5 * G *                           
                      B02.TT_ANV (BER_SØ_IND)                                   
                      /   (12 * 40) + 0.5;                                      
                                                                                
                                                                                
 /*HL*/    IF (ALDER_EK        >= 6000    &                                     
               W_DATO_ÅMD      >  19890300    &          /*2000*/               
               W_DATO_ÅMD      <  19910000)        !     /*2000*/               
              (ALDER_EK        >  6000    &                                     
               W_DATO_ÅMD      >  19910000)      THEN   /*2000*/                
                                                                                
              B02.ALDERSP.ST(BER_SØ_IND) =                                      
                 F_BEREGN_SÆRTILLEGG                                            
                    (                                                           
                     SUM_TP,                                                    
                     0,                                                         
                     'J',                                                       
                     (ST_PROSENT_SAMST * 2),                                    
                     100,                                                       
                     (B02.TT_ANV(BER_SØ_IND))                                   
                    );                                                          
           ELSE                                                                 
                                                                                
              B02.ALDERSP.ST(BER_SØ_IND) =                                      
                 F_BEREGN_SÆRTILLEGG                                            
                    (                                                           
                     SUM_TP,                                                    
                     0,                                                         
                     'J',                                                       
                     (ST_PROSENT_ENSLIG),                                       
                     100,                                                       
                     (B02.TT_ANV(BER_SØ_IND))                                   
                    );                                                          
                                                                                
           /* TILLEGGSTEST 21.03.94. REGELENDR. 0194 BLE TUDLIGERE*/            
           /* TAKLET I R0014010, MEN BLIR FEIL VED 'TAK' OG 'FAI' */            
           /* TRUDE                                               */            
                                                                                
           IF  ALDER_EK        >= 6001    &                                     
               W_DATO_ÅMD      >  19931200    THEN   /*2000*/                   
               B02.ALDERSP.ST(BER_SØ_IND) =                                     
                 F_BEREGN_SÆRTILLEGG                                            
                    (                                                           
                     SUM_TP,                                                    
                     0,                                                         
                     'J',                                                       
                     (ST_PROSENT_ENSLIG*2),                                     
                     100,                                                       
                     (B02.TT_ANV(BER_SØ_IND))                                   
                    );                                                          
                                                                                
       END;                                                                     
                                                                                
    ELSE                                                                        
                                                                                
       DO;                                                                      
                                                                                
          B02.ALDERSP.ET(BER_SØ_IND)   = 0;                                     
                                                                                
          B02.ALDERSP.ST(BER_SØ_IND) =                                          
                 F_BEREGN_SÆRTILLEGG                                            
                    (                                                           
                     SUM_TP,                                                    
                     0,                                                         
                     'J',                                                       
                     (ST_PROSENT_ENSLIG),                                       
                     100,                                                       
                     (B02.TT_ANV(BER_SØ_IND))                                   
                    );                                                          
       END;                                                                     
                                                                                
 /* ***************************************************************** */        
 /* KALL FUNKSJON SOM BEREGNER KOMPENSASJONSTI_ÅMDLLEGG. 4134         */        
 /* ***************************************************************** */        
    IF B02.SPESOPPL.BOSATT(BER_SØ_IND) = 0 THEN                                 
       DO;  /* KAN HA RETT PÅ KOMPENSASJONSTILLEGG */                           
          IF (B02.PENSJONSTYPE2 (BER_SØ_IND) = 'S') !                           
             (B02.PENSJONSTYPE2 (BER_SØ_IND) = 'N' &                            
              B02.PENSJONSTYPE2 (BER_EK_IND) = 'P' &                            
              B02.SIVILSTAND    (BER_EK_IND) = 'G') THEN                        
             W_PT2 = 'S';                                                       
          ELSE                                                                  
             W_PT2 = B02.PENSJONSTYPE2(BER_SØ_IND);                             
                                                                                
          B02.ALDERSP.KT(BER_SØ_IND) =                                          
             F_BEREGN_KOMPENSASJONSTILLEGG                                      
                ( /* 4134 */                                                    
                 B02.PENSJONSTYPE1(BER_SØ_IND),                                 
                 W_PT2,                                                         
                 B02.TT_ANV(BER_SØ_IND),                                        
                 0,                                                             
                 1,                                                             
                 'J'                                                            
                ); /* 4134 */                                                   
       END; /* KAN HA RETT PÅ KOMPENSASJONSTILLEGG */                           
    ELSE                                                                        
       /* KOMPENSASJONSTILLEGG KUN AKTUELT FOR DE SOM ER BOSATT */              
       B02.ALDERSP.KT(BER_SØ_IND) = 0 ;                                         
                                                                                
     /* DETTE  FOR Å FÅ MED ET FOR AFP-NULL.TRUDE0996*/                         
     IF B02.PENSJONSTYPE1(BER_EK_IND)  = 'F' &                                  
        B02.PENSJONSTYPE2(BER_SØ_IND)  = 'N' THEN                               
       DO;                                                                      
          B02.PENSJONSTYPE2(3)    = B02.PENSJONSTYPE2(BER_SØ_IND);              
          B02.PENSJONSTYPE2(BER_SØ_IND)    =  'F';                              
          B02.PENSJONSTYPE2(BER_EK_IND)     =  ' ';                             
          B02.FT_SKAL_BEREGNES(BER_SØ_IND)  = 'J';                              
       END;                                                                     
                                                                                
    RETUR:                                                                      
                                                                                
                                                                                
   END BEREGN_AFP_DEL1;                                                         
