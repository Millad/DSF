 /*   SIST ENDRET PÅ PROD   2005.01.13  8.42.06 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2005.01.10 12.50.24 AV   JDA2970          */        
 /* *************************************************************** */          
 /*IDENTIFIKASJON:                                                  */          
 /*    R001U825 - UNDERPROGRAM I PLI                                */          
 /*    PROGRAMMERER: MARTIN           JAN 2005                      */          
 /* *************************************************************** */          
 /*HENSIKT:                                                         */          
 /*    OPPDATERE BLANKETT OG STATUS MED OPPLYSNINGER FRA            */          
 /*    BLANKETTENS PUNKT 6.                                         */          
 /*  - SKILT UT DA VI HAR FÅTT NY SIDE FOR DETTE IFBM ENDRINGER     */          
 /*    DESEMBER 2004.                                               */          
 /*  - SE REMEDY 729.                                               */          
 /*PROGRAMTILKNYTNING:                                              */          
 /*    INCLUDES I U820 - ETTERLATTEPENSJON                          */          
 /*BRUK:                                                            */          
 /*    CALL AJOURFØR_B02_MED_EE_EØS                                 */          
 /* *************************************************************** */          
 AJOURFØR_B02_MED_EE_EØS:PROC;                                                  
                                                                                
    /* 6.1 ER FOLKETRYGDENS VILKÅR OPPFYLT                      */              
    B02.BEREGN_FOLKETRYGD(AVDØD_IND)                                            
       = EEEØS.BEREGN_FOLKETRYGD;                                               
                                                                                
    /* 6.2 ARBEIDSINNTEKT I EØS-LAND MINST 1G PÅ DØDSTIDSPUNKTET*/              
    B02.EØS_8_4_3A(AVDØD_IND)                                                   
       = EEEØS.EØS_3_17A;                                                       
                                                                                
    /* 6.3 POENGÅR I ANDRE EØS-LAND FOR PRO RATA 1              */              
    DO TELLER = LBOUND(B02.INNTEKTSÅR_EØS,2) TO                                 
                HBOUND(B02.INNTEKTSÅR_EØS,2);                                   
       B02.INNTEKTSÅR_EØS(AVDØD_IND,TELLER) = '0'B;                             
    END;                                                                        
    DO TELLER = 1 TO HBOUND(EEEØS.EØS_ÅR);                                      
    IF (EEEØS.EØS_ÅR(TELLER) > 0) THEN                                          
       B02.INNTEKTSÅR_EØS(AVDØD_IND,EEEØS.EØS_ÅR(TELLER)) = '1'B;               
    END;                                                                        
                                                                                
    /* 6.4 EØS ÅR IKKE PRO RATA                                */               
    DO TELLER = LBOUND(B02.EØS_ÅR_IKKE_PRO_RATA,2) TO                           
                HBOUND(B02.EØS_ÅR_IKKE_PRO_RATA,2);                             
       B02.EØS_ÅR_IKKE_PRO_RATA(AVDØD_IND,TELLER) = '0'B;                       
    END;                                                                        
    DO TELLER = LBOUND(EEEØS.IKKE_PRO_TAB) TO                                   
                HBOUND(EEEØS.IKKE_PRO_TAB);                                     
       IF (EEEØS.EØS_ÅR_IKKE_PRO_RATA(TELLER) > 0) THEN                         
          B02.EØS_ÅR_IKKE_PRO_RATA(AVDØD_IND,                                   
                       EEEØS.EØS_ÅR_IKKE_PRO_RATA(TELLER)) = '1'B;              
    END;                                                                        
                                                                                
    /* 6.5 SAMLET TRYGDETID I ANDRE EØS-LAND FOR TEORETISK B.   */              
    B02.TT_TEORETISK_EØS_MND(AVDØD_IND)                                         
       = EEEØS.TT_TEORETISK_MND                                                 
       + EEEØS.TT_TEORETISK_ÅR * 12;                                            
    IF (B02.TT_TEORETISK_EØS_MND(AVDØD_IND) > 0 !                               
        EEEØS.EØS_ÅR(TELLER)                 > 0 ) THEN                         
       B02.PRO_RATA_BEREGNING(AVDØD_IND) = 'J';                                 
                                                                                
    /* 6.6 SAMLET TRYGDETID I ANDRE EØS-LAND FOR PRO RATA B.    */              
    B02.TT_PRO_RATA_MND(AVDØD_IND)                                              
       = EEEØS.TT_PRORATA_MND                                                   
       + (EEEØS.TT_PRORATA_ÅR * 12);                                            
                                                                                
    /* 6.7 SUM PENSJON FRA ANDRE EØS-LAND FOR TILLEGG ART 50.   */              
    B02.PENSJON_EØS (AVDØD_IND)                                                 
       = EEEØS.PENSJON_EØS;                                                     
                                                                                
    /* 6.8 EØS ÅR 2 - PRO RATA 2 TAS IKKE VARE PÅ */                            
    DO TELLER = LBOUND(B02.PRO_RATA2,2) TO                                      
                HBOUND(B02.PRO_RATA2,2);                                        
       B02.PRO_RATA2(AVDØD_IND,TELLER) = '0'B;                                  
    END;                                                                        
    DO TELLER = LBOUND(EEEØS.PRO_TAB) TO                                        
                HBOUND(EEEØS.PRO_TAB);                                          
       IF (EEEØS.EØS_ÅR_PRO_RATA2(TELLER) > 0) THEN                             
          B02.PRO_RATA2(AVDØD_IND,                                              
                        EEEØS.EØS_ÅR_PRO_RATA2(TELLER)) = '1'B;                 
    END;                                                                        
                                                                                
    /* 6.10 FAKTISK TRYGDETID I NORDEN */                                       
    B02.TT_NORDISK_FAKT_MND(AVDØD_IND)                                          
       = EEEØS.TT_FAKTISK_NORDISK_ÅR * 12                                       
       + EEEØS.TT_FAKTISK_NORDISK_MND;                                          
                                                                                
    /* 6.11 FAKTISKE POENGÅR I NORDEN */                                        
    B02.PÅ_NORDISK_FAKT(AVDØD_IND)                                              
       = EEEØS.PÅ_FAKTISK_NORDISK;                                              
                                                                                
    /* 6.11 INNTEKTSPRØVET EØS PENSJON */                                       
    B02.INNT_PRØVET_EØS_PENS(AVDØD_IND)                                         
       = EEEØS.INNT_PRØVET_EØS_PENS;                                            
                                                                                
    /* OPPDATERER OM RETT TIL NORDISK KONVENSJON */                             
    IF (B02.TT_NORDISK_FAKT_MND(AVDØD_IND)                                      
       +B02.PÅ_NORDISK_FAKT(AVDØD_IND)) > 0 THEN                                
       B02.NORDISK_KONVENSJON(AVDØD_IND) = 'J';                                 
 END AJOURFØR_B02_MED_EE_EØS;                                                   
