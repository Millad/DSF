 /*   SIST ENDRET PÅ PROD   2008.05.31 11.26.55 AV   SPA2990          */        
 /*       SIST ENDRET 02/09-98 10.52.16 AV   SPA7339                  */        
 /*IDENTIFIKASJON:                                                    */        
 /************************                                            */        
 /*  PROGRAM-IDENT : R001I902-KONTROLL AV BRUKER_ID                   */        
 /*  PROGRAM-SPRÅK : PLI/CICS                                         */        
 /*  PROGRAMMERER  : BRITT                                            */        
 /*  PROGRAMMET BLE LAGET  070289                                     */        
 /*                                                                   */        
 /*  ENDRINGSDATO :            090389                                 */        
 /*  ENDRINGEN GJELDER:        DERSON DET KRYSSES I FELTET LES        */        
 /*                            PÅ PÅLOGGINGSBILDET, BLIR KONTROLLEN   */        
 /*                            GITT TIL PROGRAM R001I904 FOR BROWSE   */        
 /*                            OG FJERNING FRA FILEN.                 */        
 /*  ENDRINGEN BLE UTFØRT AV : BRITT                                  */        
 /*                                                                   */        
 /*  ENDRINGSDATO : 08.06.1998.                                       */        
 /*  ENDRINGEN GJELDER: ÅR2000 - DEF. AV MAPS + KOMPILERING.          */        
 /*  ENDRINGEN BLE UTFØRT AV : O.GØYTIL.                              */        
 /******************************************************************* */        
 /*HENSIKT:                                                           */        
 /************                                                        */        
 /*  PROGRAMMET AKTIVISERES NÅR OPERATØREN                            */        
 /*  HAR TASTET INN BRUKER-IDENTEN, MAP S001011 - MAPSET S001I93.     */        
 /*  PROGRAMMET LESER INN BRUKER-IDENTEN OG KONTROLLERER DEN.         */        
 /*  ER DEN OK, SKRIVES REGISTRERINGSBILDE FOR INNTEKTER -            */        
 /*  MAP S001I09 - MAPSET R001I93, OG TRANSKODEN SETTES TIL RI9B.     */        
 /*  PROGRAMMET GIR BRUKEREN 3 SJANSER TIL Å REGISTRERE EN AKSEPTABEL */        
 /*  BRUKER-ID. VED FORTSATT FEIL, SKRIVES PAUSEBILDE UT OG           */        
 /*  OPPSTARTING MÅ SKJE PÅ NYTT (MAP S001012 - MAPSET S001I93)       */        
 /*                                                                   */        
 /******************************************************************* */        
 /*PROGRAMTILKNYTTING:                                                */        
 /**********************                                              */        
 /*  PROGRAMMET BLIR AKTIVISERT FRA CICS MED TRANS-ID = RI9A.         */        
 /*  DENNE TRANSID SETTES I R001I901.                                 */        
 /*  KONTROLLEN GÅR VIDERE VIA CICS TIL R001I903 FOR REGISTRERING.    */        
 /*  TRANSAKSJONSKODEN ER RI9A                                        */        
 /******************************************************************* */        
 /*DATASETTOPPLYSNINGER:                                              */        
 /*************************                                           */        
 /*  DET BLIR IKKE BRUKT NOEN DATASETT I DETTE PROGRAMMET             */        
 /*                                                                   */        
 /******************************************************************* */        
 /*FEILMELDINGER:                                                     */        
 /*********************                                               */        
 /* 'UGYLDIG BRUKERIDENT' SKRIVES PÅ SKJERMBILDE                      */        
 /*                                                                   */        
 /* ***************************************************************** */        
 R001I92:                                                                       
    PROC   (COMMAREA_PEKER) OPTIONS  (MAIN);                                    
                                                                                
                                                                                
    %PAGE;                                                                      
    %INCLUDE S001I9;                    /* REG AV INNT. MAPSET        */        
    %INCLUDE P0019906;                  /* TRANS_OPPL_OMR(BASED)      */        
    %INCLUDE P0019908;                  /* KOM_OMR(BASED)             */        
    %INCLUDE P0019910;                  /* STYRINGS_OMRÅRDE (BASED)   */        
    %INCLUDE P0019912;                  /* DIV_OMRÅDE       (BASED)   */        
    %INCLUDE P0019959;                  /* COPYOMRÅDET FOR FUNKTAB    */        
    %INCLUDE DFHBMSCA;                                                          
 %PAGE;                                                                         
                                                                                
                                                                                
                                                                                
    DCL GRUNN_OMR1                  CHAR (1440),                                
        GRUNN_CICS  DEF GRUNN_OMR1  CHAR (   4) POS  (5),                       
        GRUNN_IDENT                 CHAR ( 8  );                                
                                                                                
    DCL                                                                         
        W01_CICSINFO    CHAR(16)    ,                                           
        W01_BRUKERID    CHAR(04)    ;                                           
    DCL                                                                         
        (COMMAREA_PEKER , W01_CWA_PTR , IDENT_PEKER,                            
         BMSMAPBR)                                         PTR     ;            
    DCL                                                                         
        IDENT_BIT       BIT (32) BASED (IDENT_PEKER);                           
                                                                                
    DCL                                                                         
        (STG,LOW,CSTG,ADDR,SUBSTR,VERIFY) BUILTIN;                              
                                                                                
    DCL W01_STARTREC  PIC '( 6)9';                                              
    DCL W01_SØK_FNR   PIC '(11)9';                                              
                                                                                
   /* *************************************************************** */        
   /* ****************                         ********************** */        
   /* **************     EKSEKVERING STARTER      ******************  */        
   /* ****************                         ********************** */        
   /* *************************************************************** */        
                                                                                
        EXEC CICS HANDLE CONDITION ERROR(FEILBEH);                              
                                                                                
        GRUNN_IDENT         = 'P001CICS';                                       
        CALL P9956_BER_G_CICS(GRUNN_IDENT,GRUNN_OMR1);                          
                                                                                
        EXEC CICS RECEIVE MAP ('S001011') MAPSET ('S001I93')                    
                              SET(BMSMAPBR)              ;                      
                                                                                
        STYRINGS_PEKER      = ADDR  (KOM_OMR.STYRINGS_OMR)  ;                   
        TRANS_OPPL_PEKER    = ADDR  (KOM_OMR.TRANS_OPPL_OMR);                   
        DIV_PARAM_PEKER     = ADDR  (KOM_OMR.DIV_PARAM_OMR) ;                   
        KOM_OMR.DIV_PARAM_OMR = ''                          ;                   
                                                                                
        W01_BRUKERID        = S001011I.BRUKER_IDI;                              
        IDENT_PEKER         = ADDR (W01_BRUKERID);                              
                                                                                
        EXEC CICS HANDLE CONDITION NOTFND(IKKEFUNNET);                          
                                                                                
        IF W01_BRUKERID    ^= 'TEST' THEN                                       
           DO;                                                                  
              EXEC CICS READ DATASET ('FUNKTAD')                                
                  INTO (FUNKTAD)                                                
                  RIDFLD (IDENT_BIT);                                           
           END;                                                                 
                                                                                
        IF W01_BRUKERID           = 'TEST'     &                                
           GRUNN_CICS            ^= 'R001'     THEN                             
           BRUKERID = W01_BRUKERID;                                             
        ELSE                                                                    
           DO;                                                                  
              BRUKERID = DIV_PARAM_OMR.BRUKER_ID;                               
              IF SUBSTR(FUNKTAD.FUNKSJON,5,1) ^= 'R'  !                         
                 SUBSTR(STYRING,1,2)          ^= 'NI' THEN                      
                 DO;                                                            
                                                                                
                    S001011O.MELDINGO = 'DU HAR IKKE TILGANG.';                 
                                                                                
                    /* SKRIVER FEILMELDING */                                   
                                                                                
                    EXEC CICS SEND MAP('S001011') MAPSET('S001I93');            
                                                                                
                    EXEC CICS DELAY INTERVAL (005);                             
                                                                                
                    /* SKRIVER PAUSEBILDET */                                   
                                                                                
                    EXEC CICS SEND MAP('S001012') MAPSET('S001I93')             
                                                         MAPONLY ERASE;         
                    EXEC CICS RETURN;                                           
                 END;                                                           
           END;                                                                 
                                                                                
        IF S001011I.LESI = 'X' THEN                                             
           DO;                                                                  
              TRANS_LISTE_INDEX = 0;                                            
              SEARCH_FNR        = 0;                                            
              IF S001011I.STARTRECL ^= 0 THEN                                   
                 DO;                                                            
                    W01_STARTREC = F_FELT_CHAR_PIC6 (STARTRECI);                
                    IF ^F_NUMERISK(F_FELT_PIC_CHAR6 (W01_STARTREC)) THEN        
                       DO;                                                      
                           S001011O.MELDINGO = 'UGYLDIG REKORD NR';             
                           EXEC CICS SEND MAP('S001011')                        
                                          MAPSET('S001I93');                    
                                                                                
                           /* SKRIVER FEILMELDING */                            
                                                                                
                           TRANSKODE = 'RI9A';                                  
                                                                                
                           EXEC CICS RETURN TRANSID(TRANSKODE)                  
                                            COMMAREA(KOM_OMR);                  
                       END;                                                     
                    TRANS_LISTE_INDEX = W01_STARTREC;                           
                 END;                                                           
              ELSE                                                              
                 IF S001011I.FNRL ^= 0 THEN                                     
                    DO;                                                         
                       W01_SØK_FNR = F_FELT_CHAR_PIC11(S001011I.FNRI);          
                       IF ^F_NUMERISK(F_FELT_PIC_CHAR11(W01_SØK_FNR))           
                                                                THEN            
                          DO;                                                   
                              S001011O.MELDINGO = 'UGYLDIG FNR';                
                              EXEC CICS SEND MAP('S001011')                     
                                             MAPSET('S001I93');                 
                                                                                
                              /* SKRIVER FEILMELDING */                         
                                                                                
                              TRANSKODE = 'RI9A';                               
                                                                                
                              EXEC CICS RETURN TRANSID(TRANSKODE)               
                                               COMMAREA(KOM_OMR);               
                          END;                                                  
                       SEARCH_FNR = W01_SØK_FNR;                                
                    END;                                                        
              FRA_CICS  = '1'B;                                                 
              TRANSKODE = 'RI9D';                                               
              EXEC CICS XCTL PROGRAM ('R001I904') COMMAREA (KOM_OMR);           
           END;                                                                 
                                                                                
        FRA_CICS = '1'B;                                                        
                                                                                
        TRANSKODE = 'RI9B';                                                     
                                                                                
        /* SKRIVER SKJERMBILDE FOR REG AV INNTEKT    */                         
                                                                                
        EXEC CICS SEND MAP('S001I09') MAPSET ('S001I93') MAPONLY ERASE;         
                                                                                
        EXEC CICS RETURN TRANSID(TRANSKODE) COMMAREA(KOM_OMR);                  
                                                                                
                                                                                
 IKKEFUNNET:                                                                    
                                                                                
        IF ANT_FORSØK = 2 THEN                                                  
           DO;                                                                  
              EXEC CICS SEND MAP('S001012') MAPSET('S001I93')                   
                                                      MAPONLY ERASE;            
              /* SKRIVER PAUSEBILDET */                                         
                                                                                
              EXEC CICS RETURN;                                                 
           END;                                                                 
        ELSE                                                                    
           DO;                                                                  
              ANT_FORSØK = ANT_FORSØK + 1;                                      
              S001011O.MELDINGO = 'UGYLDIG BRUKERIDENT';                        
              EXEC CICS SEND MAP('S001011') MAPSET('S001I93');                  
                                                                                
              /* SKRIVER FEILMELDING */                                         
                                                                                
              TRANSKODE = 'RI9A';                                               
           END;                                                                 
                                                                                
                                                                                
        EXEC CICS RETURN TRANSID(TRANSKODE) COMMAREA(KOM_OMR);                  
                                                                                
                                                                                
                                                                                
 FEILBEH:                                                                       
        EXEC CICS SEND MAP('S001012') MAPSET('S001I93')                         
                                                      MAPONLY ERASE;            
        /* SKRIVER PAUSEBILDET */                                               
                                                                                
        EXEC CICS RETURN;                                                       
                                                                                
                                                                                
                                                                                
                                                                                
    %INCLUDE R0019910;    /* F_NUMERISK                           */            
                                                                                
    %INCLUDE R0019912;    /* KONV. CHAR-PIC OG PIC-CHAR           */            
                                                                                
    %INCLUDE R0019956;    /* P9956_BER_G_CICS                     */            
                                                                                
                                                                                
   END R001I92;                                                                 
