 /*   SIST ENDRET PÅ PROD   2004.12.17 14.03.00 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.07.14 13.07.55 AV   JDA2970          */        
 /*       SIST ENDRET 16/09-99 08.44.05 AV   JDA7339                  */        
 /*       SIST ENDRET 16/09-99 08.43.13 AV   JDA7339                  */        
 /*       SIST ENDRET 09/06-98 08.49.02 AV   SPA7339                  */        
 /*       SIST ENDRET 27/05-98 09.51.44 AV   RFA9991                  */        
 /*       SIST ENDRET 02/06-89 15.41.58 AV   DYBVIK                   */        
 /*   */                                                                        
 /* ***************************************************************** */        
 /* IDENTIFIKASJON:                                                   */        
 /*     R0014426 - PROGRAM I PLI                                      */        
 /*     PROGRAMMERT AV TRUDE MAI 1989                                 */        
 /* HENSIKT:                                                          */        
 /*     PROGRAMMET SKAL KONTROLLERE AT ENGANGSBELØPET ETTER  KAP 11-7 */        
 /*     IKKE OVERSTIGER KAPITALVERDIEN .                              */        
 /*     TABELLEN OVER KAPITALISERINGSFAKTORENE LIGGER I P001KAPT.     */        
 /*     RETURNERER KAPITALISERINGSBELØPET,DERSOM DETTE ER STØRST.     */        
 /* BRUK:                                                             */        
 /*     FATOR_11_7   =    ANTALL GANGER ÅRSPENSJON                    */        
 /*     ALDER_I_MND  =    ALDER REGNET I MND TIL NÆRMESTE 1/4 ÅR      */        
 /*                                                                   */        
 /*                                                                   */        
 /* TRUDE, 16.05.89.                                                  */        
 /* ***************************************************************** */        
 %SKIP (2);                                                                     
 F_KAPFAKT:                                                                     
 PROC (W_FNR,W_SUM_YTELSE,W_VIRK_DATO_ÅMD,W_YUG)RETURNS(FIXED DEC(7));          
 %INCLUDE P0014426;                                                             
      DCL                                                                       
         W_FAKTOR_11_7       PIC '9',                                           
         W_FNR               PIC'(11)9',                                        
         W_SUM_YTELSE        FIXED DEC(5),                                      
         W_VIRK_DATO_ÅMD      FIXED DEC(8),    /*Y2K*/                          
         W_YUG               FIXED DEC(3);                                      
                                                                                
      DCL                                                                       
         W_ALDER             PIC'(4)9',        /*ÅR_MND*/                       
         W_KJØNN             CHAR(1);                                           
                                                                                
      DCL                                                                       
         1 W_ALDER_DEF       DEF   W_ALDER,                                     
           2 W_ÅR            PIC '99',                                          
           2 W_MND           PIC '99';                                          
                                                                                
      DCL W_ANTALL_MND       FIXED DEC(5) INIT(0);                              
      DCL W_KAPITALVERDI     FIXED DEC(7) INIT(0);                              
                                                                                
      DCL (I,J)                FIXED BIN(15);     /*INDEX*/                     
                                                                                
                                                                                
      /* ALDER PR VIRKNINGSDATO */                                              
                                                                                
      W_ALDER           = F_ALDER(W_FNR,(W_VIRK_DATO_ÅMD));   /*Y2K*/           
      W_ANTALL_MND      = (W_ÅR * 12) + W_MND;                                  
                                                                                
      /* KJØNN. DET FINNES EN TABELL FOR KVINNER OG EN FOR MENN */              
                                                                                
      W_KJØNN           = F_KJØNN(W_FNR);                   /*R0019902*/        
                                                                                
      IF (W_YUG         > 14     &                                              
          W_YUG         < 20)    THEN                                           
          W_FAKTOR_11_7 =  3;                                                   
                                                                                
      IF (W_YUG         > 19     &                                              
          W_YUG         < 25)    THEN                                           
          W_FAKTOR_11_7 =  6;                                                   
                                                                                
      IF (W_YUG         > 24     &                                              
          W_YUG         < 30)    THEN                                           
          W_FAKTOR_11_7 =  9;                                                   
                                                                                
                                                                                
      /* SKAL VI BRUKE KAPITALISERINGSFAKTOR ? */                               
                                                                                
      IF W_KJØNN        = 'M'     THEN                                          
         DO;                                                                    
           IF ( W_FAKTOR_11_7     =  3   &                                      
                W_ANTALL_MND      > 764  !        /* 63 OG 3/4 ÅR */            
                                                                                
                W_FAKTOR_11_7     =  6   &                                      
                W_ANTALL_MND      > 707  !        /* 59        ÅR */            
                                                                                
                W_FAKTOR_11_7     =  9   &                                      
                W_ANTALL_MND      > 632 ) THEN    /* 52 OG 3/4 ÅR */            
                                                                                
                DO;     /* FINNE UT HVOR I TABELLEN VI SKAL BEGYNNE */          
                                                                                
                   IF W_FAKTOR_11_7    =   3  THEN                              
                                   J    = 765 ;   /* 63 OG 3/4 ÅR */            
                                                                                
                   IF W_FAKTOR_11_7    =   6  THEN                              
                                  J    = 708 ;   /* 59        ÅR */             
                                                                                
                   IF W_FAKTOR_11_7    =   9  THEN                              
                                   J    = 633 ;   /* 52 OG 3/4 ÅR */            
                                                                                
                   DO I  = J TO 804  WHILE(W_ANTALL_MND > I);                   
                   END;                                                         
                                                                                
                   W_KAPITALVERDI = (W_SUM_YTELSE * 12                          
                                     * KAP_FAKTOR_MENN(I) + 0.5);               
                END;                                                            
         END;                                                                   
                                                                                
      ELSE            /* KVINNE */                                              
                                                                                
         DO;                                                                    
            IF (W_FAKTOR_11_7     =  3   &                                      
                W_ANTALL_MND      > 764) !        /* 63 OG 3/4 ÅR */            
                                                                                
               (W_FAKTOR_11_7     =  6   &                                      
                W_ANTALL_MND      > 713) !        /* 59 OG 1/2 ÅR */            
                                                                                
               (W_FAKTOR_11_7     =  9   &                                      
                W_ANTALL_MND      > 644) THEN     /* 53 OG 3/4 ÅR */            
                                                                                
                DO;     /* FINNE UT HVOR I TABELLEN VI SKAL BEGYNNE */          
                                                                                
                   IF W_FAKTOR_11_7    =   3  THEN                              
                                   J    = 765 ;   /* 63 OG 3/4 ÅR */            
                                                                                
                   IF W_FAKTOR_11_7    =   6  THEN                              
                                   J    = 714 ;   /* 59 OG 1/2 ÅR */            
                                                                                
                   IF W_FAKTOR_11_7    =   9  THEN                              
                                   J    = 645 ;   /* 53 OG 3/4 ÅR */            
                                                                                
                   DO I  = J TO 804  WHILE(W_ANTALL_MND > I);                   
                   END;                                                         
                                                                                
                   W_KAPITALVERDI = ( W_SUM_YTELSE * 12                         
                                     * KAP_FAKTOR_KVINNER(I) + 0.5);            
                END;                                                            
         END;                                                                   
      RETURN(W_KAPITALVERDI) ;                                                  
 END F_KAPFAKT;                                                                 
