 /*       SIST ENDRET 02/09-98 10.26.36 AV   SPA7339                  */00000000
 /*       SIST ENDRET 20/07-98 09.10.28 AV   SPA7339                  */00000010
   R0019947: PROC(BUFF) RETURNS(CHAR (8));                              00000020
 /*-------------------------------------------------------------------*/00000030
 /*      HJELPEPROGRAM FOR OMGJØRING FRA ÅR/MND/DAG TIL ÅR/DAGNR      */00000040
 /*      OG OMVENDT.                                                  */00000050
 /*                                                                   */00000060
 /*      BUFF ER KOMMUNIKASJONEN MELLOM DET KALLENDE PROGRAM OG       */00000070
 /*      R0019947.                                                    */00000080
 /*                                                                   */00000090
 /*      HVIS R0019947 KALLES MED EN VERDI AV TYPEN ÅRMDDG, VIL       */00000100
 /*      HJELPEPROGRAMMET GI TILBAKE VERDIEN ÅRDAGNR. HVIS R0019947   */00000110
 /*      KALLES MED EN VERDI AV TYPEN ÅRDAGNR, VIL VERDIEN ÅRMDDG     */00000120
 /*      BLI SENDT TILBAKE. FORUTSETNINGEN FOR Å FÅ UTFØRT DET        */00000130
 /*      SISTNEVNTE ER AT 6. SIFFER I INPUT ER BLANK.                 */00000140
 /*      KOMMUNIKASJONEN FOREGÅR I UPAKKET FELT.                      */00000150
 /*-------------------------------------------------------------------*/00000160
         DCL BUFF CHAR (8);                                             00000170
         DCL DGNR  DEF BUFF PIC '(7)9',                                 00000180
             BÅR   DEF BUFF PIC '9999',                                 00000190
             TESTB DEF BUFF POS (8) CHAR(1),                            00000200
             DAT   DEF BUFF PIC '(8)9';                                 00000210
         DCL 1 DATUM,                                                   00000220
               2 ÅR    PIC'9999',                                       00000230
               2 MD    PIC'99',                                         00000240
               2 DG    PIC'99',                                         00000250
             1 DATO DEF DATUM PIC'(8)9';                                00000260
         DCL 1 DAGNUM,                                                  00000270
               2 ÅR    PIC'9999',                                       00000280
               2 DNR   PIC'999',                                        00000290
             1 DAGNR DEF DAGNUM PIC'(7)9';                              00000300
         DCL DNR_X   FIXED BIN(15) INIT(0);                             00000310
         DCL 1 MDTAB,                                                   00000320
               2 MD01  PIC '99' INIT(31),                               00000330
               2 MD02  PIC '99' INIT(00),                               00000340
               2 MD03  PIC '99' INIT(31),                               00000350
               2 MD04  PIC '99' INIT(30),                               00000360
               2 MD05  PIC '99' INIT(31),                               00000370
               2 MD06  PIC '99' INIT(30),                               00000380
               2 MD07  PIC '99' INIT(31),                               00000390
               2 MD08  PIC '99' INIT(31),                               00000400
               2 MD09  PIC '99' INIT(30),                               00000410
               2 MD10  PIC '99' INIT(31),                               00000420
               2 MD11  PIC '99' INIT(30),                               00000430
               2 MD12  PIC '99' INIT(31);                               00000440
         DCL 1 MDTALL(12) POS(1) DEF MDTAB,                             00000450
               2 TALL  PIC '99';                                        00000460
         DCL INDEX  FIXED BIN(15);                                      00000470
         DCL FEIL   BIT(1) INIT ('0'B),                                 00000480
             DIV    FIXED BIN(31),                                      00000490
             REST   FIXED BIN(15);                                      00000500
         DCL SUM BUILTIN;                                               00000510
 START:                                                                 00000520
         IF TESTB = ' ' THEN                                            00000530
            DO;                                                         00000540
               IF TESTB = ' ' &  VERIFY(DGNR, '0123456789') > 0 THEN    00000550
                  FEIL = '1'B;                                          00000560
            END;                                                        00000570
          ELSE                                                          00000580
            IF VERIFY(DAT, '0123456789') > 0 THEN                       00000590
               FEIL = '1'B;                                             00000600
         IF ^ FEIL THEN                                                 00000610
            DO;                                                         00000620
 /* FORTSETT: */                                                        00000630
               DIV = BÅR / 4;                                           00000640
               REST = BÅR - (4 * DIV);                                  00000650
               IF REST > 0 THEN                                         00000660
                  MD02 = 28;                                            00000670
                ELSE                                                    00000680
                  MD02 = 29;                                            00000690
               IF TESTB ^= ' ' THEN                                     00000700
                  DO;                                                   00000710
 /* GJØR_OM: */                                                         00000720
                     DATO = DAT;                                        00000730
                     IF MD > 12 ! MD = 0 THEN                           00000740
                        FEIL = '1'B;                                    00000750
                      ELSE                                              00000760
                        IF DG = 0 ! DG > TALL(MD) THEN                  00000770
                           FEIL = '1'B;                                 00000780
                     IF ^ FEIL THEN                                     00000790
                        DO;                                             00000800
                           INDEX     = MD - 1;                          00000810
                           DAGNUM.ÅR = DATUM.ÅR;                        00000820
                           DNR       = 0;                               00000830
                           DO WHILE (INDEX > 0);                        00000840
                              DNR   = DNR + TALL(INDEX);                00000850
                              INDEX = INDEX - 1;                        00000860
                           END;                                         00000870
                           DNR  = DNR + DG;                             00000880
                           DGNR = DAGNR;                                00000890
                        END;                                            00000900
                  END;                                                  00000910
 /* END GJØR_OM */                                                      00000920
                ELSE                                                    00000930
                  DO;                                                   00000940
 /* GJØR_TILBAKE: */                                                    00000950
                     DAGNR = DGNR;                                      00000960
                     IF DNR = 0 ! DNR > SUM(TALL) THEN                  00000970
                        FEIL = '1'B;                                    00000980
                      ELSE                                              00000990
                        DO;                                             00001000
                           DNR_X    = DNR;                              00001010
                           DATUM.ÅR = DAGNUM.ÅR;                        00001020
                           INDEX    = 1;                                00001030
                           DO WHILE (DNR_X > 0);                        00001040
                              DNR_X = DNR_X - TALL(INDEX);              00001050
                              IF DNR_X > 0 THEN                         00001060
                                 INDEX = INDEX + 1;                     00001070
                           END;                                         00001080
                           DNR       = DNR_X + TALL(INDEX);             00001090
                           DATUM.MD  = INDEX;                           00001100
                           DATUM.DG  = DNR;                             00001110
                           DAT = DATO;                                  00001120
                        END;                                            00001130
                  END;                                                  00001140
   /*    END GJØR_TILBAKE */                                            00001150
            END;                                                        00001160
   /* END FORTSETT        */                                            00001170
 SLUTT:                                                                 00001180
   /*    IF FEIL THEN                                                   00001190
            PUT SKIP LIST('FEIL DATO INPUT ', BUFF);                    00001200
   */                                                                           
         RETURN (BUFF);                                                 00001210
   END R0019947;                                                        00001220
