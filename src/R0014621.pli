 /*   SIST ENDRET PÅ PROD   2003.06.19 15.34.08 AV   SPA2970          */        
 /*   SIST ENDRET PÅ TEST   2003.03.14 10.37.00 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2002.04.19 12.28.04 AV   JDA2970          */        
 /*       SIST ENDRET 22/06-98 09.08.45 AV   JDA7339                  */        
 /*       SIST ENDRET 03/06-98 08.53.59 AV   JDA7339                  */        
 /*       SIST ENDRET 14/03-91 13.46.30 AV   DYBVIK                   */        
 /*       SIST ENDRET 06/03-91 12.16.49 AV   DYBVIK                   */        
 /*       SIST ENDRET 01/03-83 09.46.57 AV   OLAV                     */        
 /*       SIST ENDRET 23/11-82 13.57.42 AV   FARVIK                   */        
 /*       SIST ENDRET 01/10-82 14.16.08 AV   FARVIK                   */        
 /* ***************************************************************** */        
 /*IDENTIFIKASJON:                                                    */        
 /*    R0014621 - BEREGN_GP_FAMILIEPLEIER- INTERN PROC I PLI.         */        
 /*    PROGRAMMERER: KARIN SVENDSEN AUGUST 1982                       */        
 /*HENSIKT:                                                           */        
 /*    PROGRAMMET BEREGNER GRUNNPENSJON BRUTTO OG OPPDATERER TT_ANV.  */        
 /*PROGRAMTILKNYTNING:                                                */        
 /*    PROGRAMMET BLIR INKLUDERT I R0014601 - BEREGN_ETTERLATT_FAMILIE*/        
 /*                                           PLEIER                  */        
 /*BRUK:                                                              */        
 /*    CALL BEREGN_GP_ETTERLATT_AP;                                   */        
 /* ***************************************************************** */        
 BEREGN_GP_FAMILIEPLEIER: PROC;                                                 
                                                                                
 DCL                                                                            
    TT_EGEN               FIXED DEC(4);                                         
                                                                                
  TT_EGEN  = 0;                                                                 
                                                                                
                                                                                
  TT_EGEN = B02.STATUS.TT_FØR_1967 (BER_SØ_IND)                                 
          +(B02.STATUS.TT_ETTER_1966(BER_SØ_IND)                                
          + B02.STATUS.TT_FRAMT(BER_SØ_IND) + 11) / 12;                         
                                                                                
  IF B02.TT_16_66(BER_SØ_IND)  > 0 THEN                  /*NY0191*/             
     TT_EGEN = ( B02.TT_16_66(BER_SØ_IND)                                       
               + B02.TT_FRAMT(BER_SØ_IND) + 6 ) / 12;                           
                                                                                
  IF TT_EGEN > 39 THEN                                                          
     TT_EGEN = 40;                                                              
  B02.TT_ANV(BER_SØ_IND) = TT_EGEN;                                             
                                                                                
  IF W_TT_KONV_ÅR  > 0 THEN                                                     
    DO;                                                                         
     B02.TT_16_66(BER_SØ_IND) = W_TT_KONV_ÅR * 12;                              
                                                                                
     IF W_TT_KONV_ÅR > 40 THEN                                                  
        W_TT_KONV_ÅR = 40;                                                      
                                                                                
     B02.TT_ANV(BER_SØ_IND) = W_TT_KONV_ÅR ;                                    
    END;                                                                        
                                                                                
  IF B02.FRAVIK_FLYKTN(BER_SØ_IND) = 'J' THEN                                   
     B02.TT_ANV(BER_SØ_IND) = 40;                                               
                                                                                
  IF B02.TT_GARANTI(BER_SØ_IND) >                                               
     B02.TT_ANV    (BER_SØ_IND)             THEN                                
     B02.TT_ANV    (BER_SØ_IND) = B02.TT_GARANTI(BER_SØ_IND);                   
                                                                                
  B02.GP_BRUTTO(BER_SØ_IND) = G * B02.STATUS.TT_ANV(BER_SØ_IND) /               
                                 (40 * 12) + 0.5;                               
                                                                                
  /* NYTT FELT 200107 : */                                                      
  IF B02.PENSJON_FØR_9802(BER_SØ_IND) ^= 'J'  &                                 
     B02.RED_GP_3_2_5(BER_SØ_IND) = 'J' THEN                                    
     DO;                                                                        
         B02.GP_BRUTTO(BER_SØ_IND) = (G * GP_PROSENT *                          
                           B02.TT_ANV(BER_SØ_IND)) /                            
                             (40 * 12) + 0.5;                                   
         DIV_PARAM_OMR.REDUSERT_GP_2G(BER_SØ_IND) = 'X';                        
         B02.GP_REDUKSJON_KODE       (BER_SØ_IND) = 'X';                        
     END;                                                                       
  ELSE                                                                          
      B02.GP_REDUKSJON_KODE       (BER_SØ_IND) = ' ';                           
                                                                                
 END BEREGN_GP_FAMILIEPLEIER;                                                   
