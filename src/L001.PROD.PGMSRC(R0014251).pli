 /*   SIST ENDRET PÅ PROD   2004.12.17 15.30.55 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.07.13 13.33.27 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2003.03.12 10.39.36 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2002.11.28  9.07.21 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.06.01  8.37.21 AV   HLA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.11.07 12.26.03 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.07.07 13.05.58 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.05.03 13.11.39 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.04.17 12.52.43 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.03.22  9.41.40 AV   JDA7339          */        
 /*       SIST ENDRET 03/05-99 14.01.19 AV   JDA7339                  */        
 /*       SIST ENDRET 03/05-99 14.00.20 AV   JDA7339                  */        
 /*       SIST ENDRET 26/04-99 14.08.52 AV   JDA7339                  */        
 /*       SIST ENDRET 26/04-99 14.05.54 AV   JDA7339                  */        
 /*       SIST ENDRET 29/03-99 08.07.30 AV   JDA7339                  */        
 /*       SIST ENDRET 29/03-99 08.06.28 AV   JDA7339                  */        
 /*       SIST ENDRET 23/02-99 13.39.37 AV   JDA7339                  */        
 /*       SIST ENDRET 23/02-99 13.38.29 AV   JDA7339                  */        
 /*       SIST ENDRET 04/05-98 14.49.27 AV   JDA7339                  */        
 /*       SIST ENDRET 17/03-98 09.30.27 AV   RFA9991                  */        
 /* **************************************************************** */         
 /*IDENTIFIKASJON:                                                   */         
 /*    R0014251 - PROSEDYRE I PLI      - HOVEDPROG                   */         
 /*    PROGRAMMERER: TRUDE OKTOBER 1988                              */         
 /*HENSIKT:                                                          */         
 /*    BEREGNE AFP - AVTALEFESTET PENSJON                            */         
 /*                                                                  */         
 /*PROGRAMTILKNYTNING:                                               */         
 /*    KALLES OPP AV PROGRAM R0013520                                */         
 /*BRUK:                                                             */         
 /*    EXEC CICS XCTL PROGRAM ('R0014251') COMMAREA (KOM_OMR)        */         
 /*                                                                  */         
 /*ENDRING:                                                          */         
 /* **************************************************************** */         
 R001425:PROC (COMMAREA_PEKER) OPTIONS (MAIN);                                  
    %INCLUDE P0014002; /* W_POREKKE                       */                    
    %INCLUDE P0014003; /* TAB                             */                    
    %INCLUDE P0014009; /* POREKKE(14)                     */                    
    %INCLUDE P0019906; /* TRANS_OPPL_OMR                  */                    
    %INCLUDE P0019908; /* KOM_OMR                         */                    
    %INCLUDE P0019910; /* STYRINGS_OMR                    */                    
    %INCLUDE P0019912; /* DIV_PARAM_OMR                   */                    
    %INCLUDE P0019924; /* GV_TAB_RE                       */                    
    %INCLUDE P0019980; /* DEFINERING AV FNR11/13 SØKER & EKTEFELLE */           
    DCL 1 B01 BASED(B01_PEKER), %INCLUDE P0019921;                              
    DCL 1 B02 BASED(B02_PEKER), %INCLUDE P0019921;                              
    DCL                                                                         
      (ADDR,CSTG,ROUND,VERIFY) BUILTIN,                                         
      ALDER_EK              FIXED DEC(5) INIT (0),                              
      HJ_DATO                             PIC     '(8)9',                       
      HJ_DATO_ÅR DEF HJ_DATO      POS (1) PIC      '(4)9',                      
      ALDER_ÅM                            PIC      '(5)9',                      
      ALDER_ÅM_IDAG                      PIC      '(5)9',                       
      ÅR_ALDER_IDAG DEF ALDER_ÅM_IDAG POS (1) PIC '(3)9';                       
    DCL (                                                                       
         I,                                                                     
         J,                                                                     
         K                                                                      
        )                   FIXED BIN(15) INIT (0);                             
                                                                                
   DCL                                                                          
    1 DATO                    PIC '(8)9',                                       
    1 HJ_ÅMD                  PIC '(8)9';                                       
   DCL                                                                          
    1 DAGENS_DATO_ÅMD DEF DATO POS(1),                                          
      2 ÅR                    PIC '(4)9',                                       
      2 MND                   PIC '(2)9',                                       
      2 DAG                   PIC '(2)9';                                       
   DCL                                                                          
    1 COMMAREA_PEKER          POINTER;                                          
                                                                                
   DCL  FØRSTE_UTTAKSDATO_ÅMD FIXED DEC (9)  INIT (0);                          
                                                                                
    /* -------------------------------------------------------------- */        
    /* DCL'S SOM KUN BENYTTES TIL TULL & TØYS                         */        
    /* -------------------------------------------------------------- */        
    DCL  GARANTITILLEGG        FIXED DEC (5)  INIT (0);                         
                                                                                
    /* -------------------------------------------------------------- */        
    /* DCL'S SOM KUN BENYTTES I UNDERMODULER                          */        
    /* -------------------------------------------------------------- */        
    DCL                                                                         
        AP_TEI                FIXED DEC (5)  INIT (0), /* 4139 */               
        W_SUM_YTELSE          FIXED DEC (5)  INIT (0); /* 4139 */               
                                                                                
    /* -------------------------------------------------------------- */        
    /* DCL'S SOM BENYTTES I HOVEDPROGRAMET OG UNDERMODULER            */        
    /* -------------------------------------------------------------- */        
    DCL                                                                         
         VIRK_LOV92_ÅM         PIC      '(6)9'; /* 4161 */                      
                                                                                
    /* -------------------------------------------------------------- */        
    /* DCL'S SOM KUN BENYTTES I HOVEDPROGRAMMET                       */        
    /* -------------------------------------------------------------- */        
    DCL                                                                         
         APD_ØNSKET            FIXED DEC (3)    INIT (0),                       
         GP_GAMMEL_NETTO       FIXED DEC (5)    INIT (0),                       
         MAX_PENSJON           FIXED DEC (7)    INIT (0),                       
         REDUKSJONSBELØP       FIXED DEC (5)    INIT (0),                       
         R_FRADRAG             FIXED DEC (5)    INIT (0),                       
         ST_GAMMEL_NETTO       FIXED DEC (5)    INIT (0),                       
         TP_GAMMEL_NETTO       FIXED DEC (5)    INIT (0),                       
         W_AFP_TILLEGG         FIXED DEC (7)    INIT (0),                       
         W_DATO_ÅMD            PIC'99999999'    INIT (0),                       
         W_GARANTI             FIXED DEC (5)    INIT (0),                       
         W_GT                  FIXED DEC (5)    INIT (0),                       
         W_MAX_PENSJON         FIXED DEC (9,2)  INIT (0);                       
                                                                                
    /* ============================================================== */        
    IF FEIL_MELD_NR = 0  THEN                                                   
    PROGRAM_ID = 'R0014251';                                                    
    W_POREKKE = '';                                                             
    FØRSTE_UTTAKSDATO_ÅMD = B02.ALDERSP.UTTAKSDATO_ÅMD(BER_SØ_IND);             
    DIV_PARAM_OMR.TAI(BER_SØ_IND) = 0 ;                                         
    DIV_PARAM_OMR.GARANTITILLEGG_TP(BER_SØ_IND) = 0;                            
    DIV_PARAM_OMR.AP_KODE_AEY (BER_SØ_IND) = ' ';                               
    DIV_PARAM_OMR.REDUSERT_GP_2G (BER_SØ_IND) = ' ';   /*200402 HL*/            
    DATO  = DATO_2000;                                                          
    W_FNR = B02.FNR(BER_SØ_IND);                                                
    W_FNR_EK = B02.FNR(BER_EK_IND);                                             
    W_FNR_13    = KONV_FNR11_FNR13(W_FNR);                                      
    W_FNR_EK_13 = KONV_FNR11_FNR13(W_FNR_EK);    /*9903*/                       
    ALDER_EK = F_ALDER(W_FNR_EK,TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD);              
    W_DATO_ÅMD = B02.VIRK_DATO_ÅMD(BER_SØ_IND);                                 
  /*HJ_ÅMD = DAGENS_DATO_ÅMD   */                                               
                                                                                
    /* -------------------------------------------------------------- */        
    /* BEREGN DEL1 AV AFP                                             */        
    /* -------------------------------------------------------------- */        
    IF (B02.PENSJONSTYPE1(BER_EK_IND) = 'A' !                                   
        B02.PENSJONSTYPE1(BER_EK_IND) = 'K' ) &                                 
       (B02.SIVILSTAND   (BER_EK_IND) = 'G' !                                   
        B02.SIVILSTAND   (BER_EK_IND) = 'P' !       /*2000-2*/                  
        B02.SIVILSTAND   (BER_EK_IND) = 'W' ) &     /*2000-2*/                  
       B02.PENSJONSTYPE2(BER_SØ_IND)  = 'P'   &                                 
       B02.PENSJONSTYPE2(BER_EK_IND)  = 'P'       THEN                          
       DO;  /* SAMMENSTØT */                                                    
          B02.PENSJONSTYPE2(BER_SØ_IND)  = 'S';                                 
          B02.PENSJONSTYPE2(BER_EK_IND)  = 'S';                                 
       END; /* SAMMENSTØT */                                                    
    ELSE                                                                        
    IF (B02.PENSJONSTYPE1(BER_EK_IND) = 'U' !                                   
        B02.PENSJONSTYPE1(BER_EK_IND) = 'Y' ) &                                 
       (B02.SIVILSTAND   (BER_EK_IND) = 'G' !                                   
        B02.SIVILSTAND   (BER_EK_IND) = 'P' !       /*2000-2*/                  
        B02.SIVILSTAND   (BER_EK_IND) = 'W' ) &     /*2000-2*/                  
       B02.PENSJONSTYPE2(BER_SØ_IND)  = 'P'   &                                 
       B02.PENSJONSTYPE2(BER_EK_IND)  = 'P'       THEN                          
       DO;  /* SAMMENSTØT */                                                    
          IF B02.YRKEPENS.YUG(BER_EK_IND) = 0   &    /*200103*/                 
             B02.UFØRPENS.UFG(BER_EK_IND) = 0      THEN                         
             DO;                                                                
                B02.PENSJONSTYPE2(BER_SØ_IND)  = 'P';                           
                B02.PENSJONSTYPE2(BER_EK_IND)  = 'P';                           
             END;                                                               
          ELSE                                                                  
             DO;                                                                
                B02.PENSJONSTYPE2(BER_SØ_IND)  = 'S';                           
                B02.PENSJONSTYPE2(BER_EK_IND)  = 'S';                           
             END;                                                               
       END; /* SAMMENSTØT */                                                    
                                                                                
    /* REMEDY 252 */                                                            
    IF (FØRSTE_UTTAKSDATO_ÅMD > TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD) THEN          
       TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD = FØRSTE_UTTAKSDATO_ÅMD;                
                                                                                
    VIRK_LOV92_ÅM = TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD/100;                       
                                                                                
                                                                                
    ALDER_ÅM = F_ALDER(W_FNR,TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD);                 
                                                                                
    IF (ALDER_ÅM > 6700) THEN                                                   
       /* DERSOM ALDER ER 67 ELLER STØRRE FALLER AFP BORT */                    
       GOTO L999;                                                               
                                                                                
    /* -------------------------------------------------------------- */        
    /* BEREGN SØKERS ALDER IDAG.                                      */        
    /* -------------------------------------------------------------- */        
    HJ_DATO = DATO_2000;                                                        
    ALDER_ÅM_IDAG = F_ALDER(W_FNR,HJ_DATO);                                     
                                                                                
    CALL BEREGN_AFP_DEL1;   /* BEREGNER GP,TP,ST,KT,ET * 4252 */                
                                                                                
    DIV_PARAM_OMR.FULLT_SÆRTILLEGG(BER_SØ_IND) =                                
                                          B02.ALDERSP.ST(BER_SØ_IND);           
                                                                                
    IF FEIL_MELD_NR > 0  THEN                                                   
       GO TO L999;                                                              
    ELSE                                                                        
       PROGRAM_ID = 'R0014251';                                                 
                                                                                
    IF (FØRSTE_UTTAKSDATO_ÅMD < 19920100) THEN                                  
       DO; /* AFP FØR 1992 */                                                   
          SELECT (TRANSTYPE);                                                   
             WHEN (40)                                                          
                DO;  /* GRUNNBLANKETT AFP */                                    
                   CALL HENT_FRA_AFP;                                           
                   CALL BEREGN_APD(APD_ØNSKET); /* 4122 */                      
                   CALL KONTROLL_AFP_RETTIGHET; /* 4253 */                      
                   IF FEIL_MELD_NR > 0  THEN                                    
                      GO TO L999;                                               
                   ELSE                                                         
                      PROGRAM_ID = 'R0014251';                                  
                END; /* GRUNNBLANKETT AFP */                                    
             WHEN (8)                                                           
                DO;  /* ENDRINGSBLANKETT */                                     
                   CALL HENT_FRA_E1;                                            
                   CALL BEREGN_APD(APD_ØNSKET); /* 4122 */                      
                END; /* ENDRINGSBLANKETT */                                     
             OTHERWISE;                                                         
          END; /* SELECT TRANSTYPE */                                           
                                                                                
          /* -------------------------------------------------------- */        
          /* DERSOM AFP GRAD ER MINDRE ENN 100 ÅMD VI JUSTERE MED APD */        
          /* GJELDER BARE FOR TILFELLER MED FØRSTE UTTAK FØR 1992     */        
          /* -------------------------------------------------------- */        
          IF (B02.ALDERSP.APD(BER_SØ_IND) < 100) THEN                           
             DO;                                                                
                CALL JUSTER_MED_APD; /* 4137 */                                 
                B02.ALDERSP.AP_GP_NETTO(BER_SØ_IND)  =                          
                                      B01.ALDERSP.GP(BER_SØ_IND);               
                B02.ALDERSP.AP_TP_NETTO(BER_SØ_IND)  =                          
                                      B01.ALDERSP.TP(BER_SØ_IND);               
             END;                                                               
       END; /* AFP FØR 1992 */                                                  
                                                                                
    /*KOMP.TILLEGGET ENDRET FRA 1.5.91 ( G > 34100 ) */                         
    IF G > 34100 THEN                                                           
       IF B02.ALDERSP.KT(BER_SØ_IND) > B02.ALDERSP.ST(BER_SØ_IND) THEN          
          B02.ALDERSP.KT(BER_SØ_IND) =                                          
             B02.ALDERSP.KT(BER_SØ_IND) - B02.ALDERSP.ST(BER_SØ_IND);           
       ELSE                                                                     
          B02.ALDERSP.KT(BER_SØ_IND) = 0;                                       
                                                                                
    /* OPPDATERER B02.SUM_YTELSE HER */                                         
    B02.SUM_YTELSE(BER_SØ_IND) = B02.ALDERSP.AP_GP_NETTO(BER_SØ_IND)            
    /* MARTIN 131000           + B02.ALDERSP.ST  (BER_SØ_IND) */                
                               + B02.AP_ST_NETTO (BER_SØ_IND)                   
                               + B02.ALDERSP.KT  (BER_SØ_IND)                   
                               + B02.ALDERSP.ET  (BER_SØ_IND)                   
                               + B02.ALDERSP.AP_TP_NETTO(BER_SØ_IND);           
                                                                                
    IF (B02.GARANTI_TP(BER_SØ_IND) > 0) THEN                                    
       IF (G > 34100)                      !                                    
          ((G = 34100) & (TRANSTYPE ^= 27) ) THEN                               
          DO;                                                                   
             W_GARANTI = B02.GARANTI_TP(BER_SØ_IND)                             
     /* MARTIN 131000  HER SKAL VI BRUKE BRUTTOBELØPA */                        
                       - B02.ALDERSP.ST  (BER_SØ_IND)                           
                       - B02.ALDERSP.TP  (BER_SØ_IND);                          
             IF W_GARANTI > 0 THEN                                              
                DO;                                                             
                   B02.SUM_YTELSE(BER_SØ_IND) =                                 
                         B02.SUM_YTELSE(BER_SØ_IND)                             
                          + W_GARANTI;                                          
                   DIV_PARAM_OMR.GARANTITILLEGG_TP(BER_SØ_IND) =                
                            W_GARANTI;                                          
                END;                                                            
          END;                                                                  
                                                                                
    IF (B01.GT_LOV92(BER_SØ_IND) > 0 &                                          
        B02.GT_LOV92(BER_SØ_IND) = 0 ) THEN                                     
       DO;                                                                      
         B02.ALDERSP.SPT_1291(BER_SØ_IND) =                                     
                                   B01.ALDERSP.SPT_1291(BER_SØ_IND);            
         B02.ALDERSP.OPT_1291(BER_SØ_IND) =                                     
                                   B01.ALDERSP.OPT_1291(BER_SØ_IND);            
         B02.ALDERSP.PÅ_1291(BER_SØ_IND)  =                                     
                                   B01.ALDERSP.PÅ_1291(BER_SØ_IND);             
         B02.GT_LOV92(BER_SØ_IND)    = B01.GT_LOV92(BER_SØ_IND);                
       END;                                                                     
                                                                                
   IF B01.GT_LOV92(BER_SØ_IND) > 0            &                                 
     TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD  > 19911200     THEN /*2000*/             
      DO;                                                                       
         B02.GT_LOV92(BER_SØ_IND) =                                             
         /* F_TILLEGGSPENSJON */                                                
            F_TP92                                                              
               (                                                                
                W_FNR_R13.ÅR,                                                   
                35500,                                                          
                (B02.ALDERSP.SPT_1291(BER_SØ_IND)),                             
                (B02.ALDERSP.OPT_1291(BER_SØ_IND)),                             
                (B02.ALDERSP.PÅ_1291(BER_SØ_IND)),                              
                0, /* NYTT ARGUMENT 4142 -> 4148 */                             
                (B02.ALDERSP.APD(BER_SØ_IND)),                                  
                100,                                                            
                'J'                                                             
               );                                                               
                                                                                
                                                                                
                                                                                
      END;                                                                      
                                                                                
   ELSE                                                                         
      IF B01.GT_LOV92(BER_SØ_IND) > 0                        &                  
         B01.PENSJONSTYPE1(BER_SØ_IND) = 'K'                THEN                
         B02.GT_LOV92(BER_SØ_IND) = B01.GT_LOV92(BER_SØ_IND);                   
                                                                                
   REDUKSJONSBELØP             =  B02.ALDERSP.TP(BER_SØ_IND) +                  
           /* MARTIN 131000 - HER SKAL VI BRUKE BRUTTOBELØPA */                 
                                  B02.ALDERSP.ST(BER_SØ_IND) +                  
                /* ALLTID 0 */    GARANTITILLEGG ;                              
   IF B02.GT_LOV92(BER_SØ_IND) > REDUKSJONSBELØP      THEN                      
      DO;                                                                       
         B02.GT_TILLEGG_LOV92(BER_SØ_IND) = B02.GT_LOV92(BER_SØ_IND)            
                                            - REDUKSJONSBELØP;                  
         B02.SUM_YTELSE(BER_SØ_IND) = B02.SUM_YTELSE(BER_SØ_IND)                
                            + B02.GT_TILLEGG_LOV92(BER_SØ_IND);                 
      END;                                                                      
   ELSE                                                                         
      B02.GT_TILLEGG_LOV92(BER_SØ_IND) = 0;                                     
                                                                                
    IF (FØRSTE_UTTAKSDATO_ÅMD > 19920000) THEN                                  
       DO;  /* REGLER PERIODEN 01.01.1992 - 31.07.2000 */                       
          CALL KONTROLL_AFP_RETTIGHET; /*4253*/                                 
  /*      IF (FØRSTE_UTTAKSDATO_ÅMD < 31072000) THEN     */                     
          IF (FØRSTE_UTTAKSDATO_ÅMD < 20000800) THEN                            
             /* REGLER PERIODEN 01.01.1992 - 31.07.2000 */                      
             CALL JUSTER_FORVENTET                                              
               ((B02.ALDERSP.KT(BER_SØ_IND)),BER_SØ_IND); /*200010*/            
          ELSE                                                                  
          DO;                                                                   
          B02.ALDERSP.AP_GP_NETTO(BER_SØ_IND) =                                 
             B02.ALDERSP.GP(BER_SØ_IND);                                        
          B02.ALDERSP.AP_TP_NETTO(BER_SØ_IND) =                                 
             B02.ALDERSP.TP(BER_SØ_IND);                                        
          END;                                                                  
       END; /* REGLER PERIODEN 01.01.1992 - 31.07.2000 */                       
    ELSE                                                                        
       DO;  /* FØRSTE UTTAKSDATO FØR 1992 - IKKE INNTEKTSJUSTERT */             
          B02.ALDERSP.AP_GP_NETTO(BER_SØ_IND) =                                 
             B02.ALDERSP.GP(BER_SØ_IND);                                        
          B02.ALDERSP.AP_TP_NETTO(BER_SØ_IND) =                                 
             B02.ALDERSP.TP(BER_SØ_IND);                                        
       END; /* FØRSTE UTTAKSDATO FØR 1992 - IKKE INNTEKTSJUSTERT */             
                                                                                
    B02.SUM_YTELSE(BER_SØ_IND) = B02.ALDERSP.AP_GP_NETTO(BER_SØ_IND)            
           /* MARTIN 131000 - NYTT FELT INNFØRT                                 
                               + B02.ALDERSP.ST  (BER_SØ_IND)*/                 
                               + B02.ALDERSP.AP_ST_NETTO(BER_SØ_IND)            
                               + B02.ALDERSP.KT  (BER_SØ_IND)                   
                               + B02.ALDERSP.ET  (BER_SØ_IND)                   
                               + B02.GT_TILLEGG_LOV92(BER_SØ_IND)               
                               + B02.ALDERSP.AP_TP_NETTO(BER_SØ_IND);           
                                                                                
   /*----------------------------------------------------------*/               
   /* FRA 01.10.92 AFP-TILLEGG FAST MND SUM SOM IKKE           */               
   /* SKAL G-REGULERES.                                        */               
   /*----------------------------------------------------------*/               
 /*IF B02.VIRK_DATO_ÅMD(BER_SØ_IND) > 9209  THEN TESTI_ÅMDNG */                 
  /*       TS LOVENDRING 10.93   TS           */                                
                                                                                
   IF B02.PENSJONSTYPE3(BER_SØ_IND)  = 'N' THEN                                 
      B02.AFP_TILLEGG(BER_SØ_IND) =  0  ;                                       
   ELSE                                                                         
      IF B02.AFP_TILLEGG (BER_SØ_IND)  > 0   !                                  
         B02.VIRK_DATO_ÅMD(BER_SØ_IND) > 19920900  THEN  /*2000*/               
         DO;  /* SKAL HA AFP TILLEGG */                                         
            IF (B02.PENSJONSTYPE3(BER_SØ_IND) = 'B' !                           
                B02.PENSJONSTYPE3(BER_SØ_IND) = 'K' !                           
                B02.PENSJONSTYPE3(BER_SØ_IND) = 'V' !                           
                B02.PENSJONSTYPE3(BER_SØ_IND) = 'S' )       !                   
               (B02.VIRK_DATO_ÅMD(BER_SØ_IND) > 19930900  &                     
                B02.PENSJONSTYPE2(BER_SØ_IND) = 'N'       ) THEN                
               B02.AFP_TILLEGG(BER_SØ_IND) =  1700;                             
            ELSE                                                                
               B02.AFP_TILLEGG(BER_SØ_IND) =  950;                              
                                                                                
            IF (B02.UTTAKSDATO_ÅMD(BER_SØ_IND) < 20000800) THEN                 
               DO;  /* ELLERS REDUKSJON I 4254 */                               
                 IF B02.ALDERSP.APD(BER_SØ_IND) > 0 THEN                        
                    W_AFP_TILLEGG = B02.AFP_TILLEGG(BER_SØ_IND)                 
                                  * B02.ALDERSP.APD(BER_SØ_IND) / 100;          
                 ELSE                                                           
                 IF B02.ALDERSP.GP(BER_SØ_IND) > 0 THEN                         
                    W_AFP_TILLEGG = B02.AFP_TILLEGG(BER_SØ_IND) *               
                           B02.ALDERSP.AP_GP_NETTO(BER_SØ_IND)                  
                          / B02.ALDERSP.GP(BER_SØ_IND);                         
                 ELSE                                                           
                   W_AFP_TILLEGG = 0;                                           
                                                                                
                 B02.AFP_TILLEGG_NETTO(BER_SØ_IND) =                            
                     W_AFP_TILLEGG + 0.5;                                       
               END; /* ELLERS REDUKSJON I 4254 */                               
                                                                                
            B02.SUM_YTELSE(BER_SØ_IND) = B02.SUM_YTELSE(BER_SØ_IND)             
                                 + B02.AFP_TILLEGG_NETTO(BER_SØ_IND);           
         END; /* SKAL HA AFP TILLEGG */                                         
                                                                                
                                                                                
   /* NY REGEL FRA 01.10.97.TRUDE - HVILKEN MARTIN ??? */                       
                                                                                
   /* --------------------------------------------------------------- */        
   /* HER KONTROLLERES 70% REGELEN                                    */        
   /* --------------------------------------------------------------- */        
   DCL W_TAI   FIXED DEC (7) INIT (0);                                          
   DCL W_BRP   FIXED DEC (7,2) INIT (0);                                        
   CALL TIDLIGERE_ARBEIDSINNTEKT(W_TAI,W_BRP);                                  
   W_MAX_PENSJON    =   DIV_PARAM_OMR.TAI(BER_SØ_IND) / 100;                    
   MAX_PENSJON      =   ((W_MAX_PENSJON * 70) / 12 + 0.5);                      
                                                                                
   IF (B02.SUM_YTELSE(BER_SØ_IND) > MAX_PENSJON &                               
       FØRSTE_UTTAKSDATO_ÅMD      > 19970900    ) THEN  /*9905*/                
      DO;  /* 70% REGELENE FRA OKTOBER 1997 */                                  
         DIV_PARAM_OMR.AFP_REDUSERT_70(BER_SØ_IND) = 'J'; /*9904*/              
         R_FRADRAG   = B02.SUM_YTELSE(BER_SØ_IND) - MAX_PENSJON;                
         /* --------------------------------------------------------- */        
         IF (B02.AP_GP_NETTO(BER_SØ_IND) > 0) THEN                              
            DO;  /* REDUKSJON AV GRUNNPENSJON */                                
               IF ( B02.GT_TILLEGG_LOV92(BER_SØ_IND)                            
                  + B02.ALDERSP.TP(BER_SØ_IND)                                  
                  + B02.ALDERSP.ST(BER_SØ_IND)                                  
                  + B02.AFP_TILLEGG(BER_SØ_IND) > 0) THEN                       
                  DO;  /* VI SKAL REDUSERE FLERE DELYTELSER */                  
                     GP_GAMMEL_NETTO = B02.AP_GP_NETTO(BER_SØ_IND);             
                     B02.AP_GP_NETTO(BER_SØ_IND) =                              
                        ROUND(  GP_GAMMEL_NETTO                                 
                              * MAX_PENSJON                                     
                              / B02.SUM_YTELSE(BER_SØ_IND)                      
                             ,0);                                               
                     R_FRADRAG = R_FRADRAG                                      
                               - GP_GAMMEL_NETTO                                
                               + B02.AP_GP_NETTO(BER_SØ_IND);                   
                  END; /* VI SKAL REDUSERE FLERE DELYTELSER */                  
               ELSE                                                             
                  DO;  /* DETTE ER SISTE DELYTELSE SOM SKAL RED. */             
                     B02.AP_GP_NETTO(BER_SØ_IND) =                              
                        B02.AP_GP_NETTO(BER_SØ_IND)                             
                        - R_FRADRAG;                                            
                     R_FRADRAG = 0;                                             
                  END; /* DETTE ER SISTE DELYTELSE SOM SKAL RED. */             
            END; /* REDUKSJON AV GRUNNPENSJON */                                
         /* --------------------------------------------------------- */        
         IF B02.GT_TILLEGG_LOV92(BER_SØ_IND)  > 0 THEN                          
            DO;  /* REDUKSJON AV GARANTITILLEGG */                              
               IF ( B02.ALDERSP.TP(BER_SØ_IND)                                  
                  + B02.ALDERSP.ST(BER_SØ_IND)                                  
                  + B02.AFP_TILLEGG(BER_SØ_IND) > 0) THEN                       
                  DO;  /* VI SKAL REDUSERE FLERE DELYTELSER */                  
                     W_GT  = B02.GT_TILLEGG_LOV92(BER_SØ_IND);                  
                     B02.GT_TILLEGG_LOV92(BER_SØ_IND)  =                        
                        ROUND(  B02.GT_TILLEGG_LOV92(BER_SØ_IND)                
                              * MAX_PENSJON                                     
                              / B02.SUM_YTELSE(BER_SØ_IND)                      
                             ,0);                                               
                     R_FRADRAG = R_FRADRAG                                      
                               + W_GT                                           
                               - B02.GT_TILLEGG_LOV92(BER_SØ_IND);              
                  END; /* VI SKAL REDUSERE FLERE DELYTELSER */                  
               ELSE                                                             
                  DO;  /* DETTE ER SISTE DELYTELSE SOM SKAL RED. */             
                     B02.GT_TILLEGG_LOV92(BER_SØ_IND)  =                        
                          B02.GT_TILLEGG_LOV92(BER_SØ_IND)                      
                        - R_FRADRAG;                                            
                     R_FRADRAG = 0;                                             
                  END; /* DETTE ER SISTE DELYTELSE SOM SKAL RED. */             
            END; /* REDUKSJON AV GARANTITILLEGG */                              
         /* --------------------------------------------------------- */        
         IF B02.ALDERSP.TP(BER_SØ_IND)  > 0 THEN                                
            DO;  /* REDUKSJON AV TILLEGGSPENSJON */                             
               IF ( B02.ALDERSP.ST(BER_SØ_IND)                                  
                  + B02.AFP_TILLEGG(BER_SØ_IND) > 0) THEN                       
                  DO;  /* VI SKAL REDUSERE FLERE DELYTELSER */                  
                     TP_GAMMEL_NETTO =                                          
                        B02.AP_TP_NETTO(BER_SØ_IND);                            
                     B02.AP_TP_NETTO(BER_SØ_IND) =                              
                        ROUND(  TP_GAMMEL_NETTO                                 
                              * MAX_PENSJON                                     
                              / B02.SUM_YTELSE(BER_SØ_IND)                      
                             ,0);                                               
                     R_FRADRAG = R_FRADRAG                                      
                               - TP_GAMMEL_NETTO                                
                               + B02.AP_TP_NETTO(BER_SØ_IND);                   
                  END; /* VI SKAL REDUSERE FLERE DELYTELSER */                  
               ELSE                                                             
                  DO;  /* DETTE ER SISTE DELYTELSE SOM SKAL RED. */             
                     B02.AP_TP_NETTO(BER_SØ_IND) =                              
                          B02.AP_TP_NETTO(BER_SØ_IND)                           
                        - R_FRADRAG;                                            
                     R_FRADRAG = 0;                                             
                  END; /* DETTE ER SISTE DELYTELSE SOM SKAL RED. */             
            END; /* REDUKSJON AV TILLEGGSPENSJON */                             
         /* --------------------------------------------------------- */        
          IF B02.ALDERSP.ST(BER_SØ_IND)   > 0 THEN                              
             DO;  /* REDUKSJON AV SÆRTILLEGG GRUNNA 70%-REGELEN */              
               IF (B02.AFP_TILLEGG(BER_SØ_IND) > 0) THEN                        
                  DO;                                                           
                     ST_GAMMEL_NETTO = B02.ALDERSP.ST(BER_SØ_IND);              
            /*       B02.ALDERSP.ST(BER_SØ_IND) = */                            
                     B02.AP_ST_NETTO(BER_SØ_IND) =                              
                        ROUND(  ST_GAMMEL_NETTO                                 
                              * MAX_PENSJON                                     
                              / B02.SUM_YTELSE(BER_SØ_IND)                      
                             ,0);                                               
                     R_FRADRAG = R_FRADRAG                                      
                               - ST_GAMMEL_NETTO                                
                               + B02.AP_ST_NETTO(BER_SØ_IND);                   
            /*                 + B02.ALDERSP.ST(BER_SØ_IND)  */                 
                  END;                                                          
               ELSE                                                             
                  DO;  /* DETTE ER SISTE DELYTELSE SOM SKAL RED. */             
                  /* B02.ALDERSP.ST(BER_SØ_IND) = */                            
                     B02.AP_ST_NETTO(BER_SØ_IND) =                              
                          B02.ALDERSP.ST(BER_SØ_IND)                            
                        - R_FRADRAG;                                            
                     R_FRADRAG = 0;                                             
                  END; /* DETTE ER SISTE DELYTELSE SOM SKAL RED. */             
                                                                                
             END; /* REDUKSJON AV SÆRTILLEGG GRUNNA 70%-REGELEN */              
         /* --------------------------------------------------------- */        
          IF (B02.AFP_TILLEGG(BER_SØ_IND) > 0) THEN                             
             DO;  /* AFP-TILLEGGET ER SISTE DELYTELSE SOM VERT RED. */          
                B02.AFP_TILLEGG_NETTO(BER_SØ_IND) =                             
                     B02.AFP_TILLEGG(BER_SØ_IND)                                
                   - R_FRADRAG;                                                 
             END; /* AFP-TILLEGGET ER SISTE DELYTELSE SOM VERT RED. */          
         /* --------------------------------------------------------- */        
                                                                                
          B02.SUM_YTELSE(BER_SØ_IND) =                                          
                        ( B02.AP_GP_NETTO(BER_SØ_IND)                           
                 /*     + B02.ALDERSP.ST  (BER_SØ_IND) */                       
                        + B02.AP_ST_NETTO (BER_SØ_IND)                          
                        + B02.ALDERSP.KT  (BER_SØ_IND)                          
                        + B02.ALDERSP.ET  (BER_SØ_IND)                          
                        + B02.GT_TILLEGG_LOV92(BER_SØ_IND)                      
                        + B02.AP_TP_NETTO(BER_SØ_IND)                           
                 /*     + B02.AFP_TILLEGG(BER_SØ_IND)) */                       
                        + B02.AFP_TILLEGG_NETTO(BER_SØ_IND));                   
                                                                                
                                                                                
      END; /* 70% REGELENE FRA OKTOBER 1997 */                                  
                                                                                
    IF (FØRSTE_UTTAKSDATO_ÅMD > 20000731) THEN                                  
       CALL PRO_RATA_INNTEKT_AFP; /*4254*/                                      
                                                                                
                                                                                
   /* --------------------------------------------------------------- */        
   HENT_FRA_AFP: PROC;                                                          
     %INCLUDE P0011901;            /* AFP-OMRÅDE  */                            
       APD_ØNSKET = AFP.APD;                                                    
   END HENT_FRA_AFP;                                                            
   /* --------------------------------------------------------------- */        
   HENT_FRA_E1: PROC;                                                           
     %INCLUDE P0011401;            /* E1-OMRÅDE   */                            
       APD_ØNSKET = E1K7.APD;                                                   
   END HENT_FRA_E1;                                                             
                                                                                
   /* --------------------------------------------------------------- */        
   /* INCLUDE'S SORTERT I STIGENDE REKKEFØLGE                         */        
   /* --------------------------------------------------------------- */        
      %INCLUDE R0014022;      /* JUSTER_FORVENTET            */                 
      %INCLUDE R0014122;      /* BEREGN_APD                  */                 
      %INCLUDE R0014133;      /* F_BEREGN_SÆRTILLEGG         */                 
      %INCLUDE R0014134;      /* F_BEREGN_KOMPTILLEGG        */                 
      %INCLUDE R0014135;      /* TIDLIGERE_ARBEIDSINNTEKT    */                 
      %INCLUDE R0014136;      /* ÅTTI_PROSENT_REGELEN        */                 
      %INCLUDE R0014137;      /* JUSTER_MED_APD              */                 
      %INCLUDE R0014140;      /* F_OVERGANG_FRA_YP           */                 
      %INCLUDE R0014146;      /* FAI_GRENSER                 */                 
      %INCLUDE R0014147;      /* FRAVIK_80%                  */                 
      %INCLUDE R0014148;      /* F_TP92                      */                 
      %INCLUDE R0014149;      /* NY TP FUNKSJON              */                 
      %INCLUDE R0014151;      /* KLARGJØR_TAB                */                 
   /* %INCLUDE R0014153          BEREGN_SPT_OPT_PÅ           */                 
      %INCLUDE R0014155;      /* FAI_GRENSER_FRAVIK          */                 
   /* %INCLUDE R0014156       /* BEREGN_SPT_OPT_PÅ_ALT       */                 
      %INCLUDE R0014161;      /* LEGG_UFØREHIST_I_TAB        */                 
      %INCLUDE R0014180;      /* SÆRTILLEGG_UTLAND           */                 
      %INCLUDE R0014233;      /* BEREGN_BT                   */                 
      %INCLUDE R0014241;      /* BEREGN_AP_OVERGANG_FRA_YP   */                 
      %INCLUDE R0014252;      /* BEREGN_AFP_DEL1             */                 
      %INCLUDE R0014253;      /* KONTROLL_AFP_RETTIGHET      */                 
      %INCLUDE R0014254;      /* PRO_RATA_INNTEKT_AFP        */                 
      %INCLUDE R0019905;      /* F_ALDER                     */                 
      %INCLUDE R0019995;      /* FNR11 - FNR13               */                 
   /* %INCLUDE RXX14142       /* F_TILLEGGSPENSJON           */                 
   /* %INCLUDE R0019913          F_SNU_DATO                  */                 
    /* -------------------------------------------------------------- */        
    L999:                                                                       
                                                                                
 END R001425;                                                                   
