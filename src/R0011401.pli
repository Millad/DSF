 /*   SIST ENDRET PÅ PROD   2003.02.27 12.40.52 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2002.11.21 13.32.49 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.10.22 13.06.14 AV   JDA2970          */        
 /*       SIST ENDRET 22/06-98 12.25.00 AV   JDA7339                  */        
 /*       SIST ENDRET 11/06-98 11.05.50 AV   RFA9991                  */        
 R00114:                                                                        
   PROC(COMMAREA_PEKER) OPTIONS(MAIN);                                          
 %PAGE;                                                                         
 %INCLUDE S00114;    /*  ENDRINGSBLANKETT-1 MAPSETTET   */                      
 %PAGE;                                                                         
 %INCLUDE P0011401;  /*  TRANS-OMR FOR ENDRINGSBLANKETT-1        */             
 %PAGE;                                                                         
 %INCLUDE P0019906;  /*  TRANS-OPPL-OMRÅDE (BASED)        */                    
 %PAGE;                                                                         
 %INCLUDE P0019908;  /*  KOM-OMRÅDE (BASED)               */                    
 %PAGE;                                                                         
 %INCLUDE P0019910;  /*  STYRINGS-OMRÅDE (BASED)          */                    
 %PAGE;                                                                         
 %INCLUDE P0019912;  /*  DIV_OMRÅDE (BASED)      */                             
 %PAGE;                                                                         
 %INCLUDE DFHBMSCA;                                                             
 %PAGE;                                                                         
   DCL                                                                          
      (BMSMAPBR,COMMAREA_PEKER) PTR;                                            
   DCL                                                                          
      (ADDR,UNSPEC,CSTG,ONCODE,VERIFY,SUBSTR) BUILTIN;                          
   DCL                                                                          
      FEIL_FUNNET  BIT (1),                                                     
      ONKODE PIC'9999',                                                         
      CURSOR_POS FIXED BIN(15) INIT(-1),                                        
      ONK DEF ONKODE CHAR(4),                                                   
      FEILKODE       CHAR(4),                                                   
      DSNAVN         CHAR(8),                                                   
      ANT_FEIL_SKREVET FIXED DEC (3);                                           
 %SKIP(1);                                                                      
 %PAGE;                                                                         
      ON ERROR SNAP BEGIN       ;                                               
         ON ERROR SYSTEM        ;                                               
         ONKODE=ONCODE          ;                                               
         FEILKODE = ONK         ;                                               
         DSNAVN   = EIBDS       ;                                               
         GO TO FEILBEH          ;                                               
      END;                                                                      
                                                                                
 FEILKODE = 'FEIL'      ;                                                       
 DSNAVN   = '        '  ;                                                       
                                                                                
 EXEC CICS HANDLE CONDITION ERROR(FEILBEH);                                     
                                                                                
 EXEC CICS HANDLE AID PF1(PF1) PF2(PF2);                                        
                                                                                
 KOM_OMR.PEKER_LISTE.DIV_PARAM_PEKER  = ADDR(KOM_OMR.DIV_PARAM_OMR);            
                                                                                
 IF FRA_CICS THEN                                                               
  DO;                                                                           
    KOM_OMR.PEKER_LISTE.TRANS_OPPL_PEKER = ADDR(KOM_OMR.TRANS_OPPL_OMR);        
    KOM_OMR.PEKER_LISTE.TRANS_PEKER      = ADDR(KOM_OMR.TRANS_OMR);             
    KOM_OMR.PEKER_LISTE.STYRINGS_PEKER   = ADDR(KOM_OMR.STYRINGS_OMR);          
    ATK_KOM_PTR           = ADDR  (KOM_OMR.ATK_COM_OMR  ) ;                     
  END;                                                                          
                                                                                
 IF HENT_FRAM_MAP  THEN                                                         
   DO;                                                                          
     EXEC CICS SEND MAP('S001141') MAPSET('S001143') MAPONLY                    
     SET (BMSMAPBR);                                                            
     ALLOCATE S001141O;                                                         
     S001141O.CICS_INFOO = CICS_NAVN;                                           
   END;                                                                         
                                                                                
 RECEIVE_MAP:                                                                   
                                                                                
                                                                                
 IF ^HENT_FRAM_MAP THEN                                                         
    DO;                                                                         
       EXEC CICS RECEIVE MAP('S001141')                                         
                                    MAPSET('S001143') SET(BMSMAPBR);            
       CALL BLANK_E1_MELDNR;                                                    
       FEIL_MELD_NR = 0;                                                        
    END;                                                                        
                                                                                
 IF FUNKSJONSKODEL > 0 THEN                                                     
    DO;                                                                         
       FUNKSJONSKODE = FUNKSJONSKODEI;                                          
       EXEC CICS XCTL PROGRAM('R0010301') COMMAREA(KOM_OMR);                    
    END;                                                                        
                                                                                
 IF STYRE_KODEL > 0 THEN                                                        
    DO;                                                                         
       STYREKODE = STYRE_KODEI;                                                 
       TRANSKODE = 'R041';                                                      
       EXEC CICS XCTL PROGRAM('R0010401') COMMAREA(KOM_OMR);                    
    END;                                                                        
                                                                                
                                                                                
                                                                                
 IF STYREKODE ^= 'E1'  &  FRA_CICS THEN                                         
    TRANSKODE = 'R041';                                                         
                                                                                
 ELSE                                                                           
  DO;                                                                           
   TRANS_RETURKODE = TRANSKODE;                                                 
                                                                                
   SELECT (TRANSKODE);                                                          
    WHEN('R140')                                                                
       DO;                                                                      
         CALL BLANK_E1_SØKER;                                                   
         CALL BLANK_E1_KAP7;                                                    
         CALL BLANK_E1_KAP8;                                                    
         CALL BLANK_E1_KAP10;                                                   
         CALL BLANK_E1_KAP11;                                                   
         CALL BLANK_E1_MELDNR;                                                  
         CALL OVERFØR_E1_SØKER;                                                 
         CALL OVERFØR_E1_KAP7;                                                  
         CALL OVERFØR_E1_KAP8;                                                  
         CALL OVERFØR_E1_KAP10;                                                 
         CALL OVERFØR_E1_KAP11;                                                 
         CALL KONTROLL_E1_SØKER(FEIL_FUNNET);                                   
         CALL KONTROLL_E1_KAP7(FEIL_FUNNET);                                    
         CALL KONTROLL_E1_KAP8(FEIL_FUNNET);                                    
         CALL KONTROLL_E1_KAP10(FEIL_FUNNET);                                   
         CALL KONTROLL_E1_KAP11(FEIL_FUNNET);                                   
       END;                                                                     
    WHEN('R141')                                                                
       DO;                                                                      
         CALL BLANK_E1_MELDNR;                                                  
         CALL OVERFØR_E1_SØKER;                                                 
         CALL OVERFØR_E1_KAP7;                                                  
         CALL OVERFØR_E1_KAP8;                                                  
         CALL OVERFØR_E1_KAP10;                                                 
         CALL OVERFØR_E1_KAP11;                                                 
         CALL KONTROLL_E1_SØKER(FEIL_FUNNET);                                   
         CALL KONTROLL_E1_KAP7(FEIL_FUNNET);                                    
         CALL KONTROLL_E1_KAP8(FEIL_FUNNET);                                    
         CALL KONTROLL_E1_KAP10(FEIL_FUNNET);                                   
         CALL KONTROLL_E1_KAP11(FEIL_FUNNET);                                   
       END;                                                                     
    OTHERWISE;                                                                  
                                                                                
       /* INGEN UTGANG DA CICS IKKE GODTAR UGYLDIGE TRANSAKSJONSKODER */        
                                                                                
   END;                                                                         
                                                                                
 IF FEIL_FUNNET ! HENT_FRAM_MAP ! FRA_UTEN_DIALOG !                             
    TRANS_RETURKODE ^= TRANSKODE THEN                                           
                                                                                
    DO;                                                                         
       IF TRANSKODE = 'R140' THEN                                               
          TRANSKODE = 'R141';                                                   
       CALL BLANK_E1_MAP;                                                       
       CALL OVERFØR_E1SØKER_MAP;                                                
       CALL OVERFØR_E1KAP7_MAP;                                                 
       CALL OVERFØR_E1KAP8_MAP;                                                 
       CALL OVERFØR_E1KAP10_MAP;                                                
       CALL OVERFØR_E1KAP11_MAP;                                                
                                                                                
      IF HENT_FRAM_MAP THEN                                                     
        EXEC CICS SEND MAP('S001141') MAPSET('S001143') ERASE CURSOR;           
      ELSE                                                                      
        EXEC CICS SEND MAP('S001141') MAPSET('S001143') ERASEAUP                
                                                    DATAONLY CURSOR;            
      FEIL_MELD_NR  = 666;/* => FEIL_FUNNET ER SATT. */                         
      HENT_FRAM_MAP = '0'B;                                                     
                                                                                
      IF  FRA_CICS THEN                                                         
        EXEC CICS RETURN TRANSID(TRANSKODE) COMMAREA(KOM_OMR);                  
      ELSE                                                                      
        GOTO RECEIVE_MAP;                                                       
                                                                                
    END;                                                                        
                                                                                
   ELSE                                                                         
                                                                                
    DO;                                                                         
                                                                                
      IF TRANSKODE = 'R140' THEN                                                
        TRANS_RETURKODE = 'R141';                                               
      ELSE                                                                      
        TRANS_RETURKODE = TRANSKODE;                                            
      IF  FRA_CICS  THEN                                                        
        DO;                                                                     
          BLANKETTYPE = STYREKODE;                                              
          TRANSKODE = 'R042';                                                   
        END;                                                                    
    END;                                                                        
  END;                                                                          
                                                                                
  CALL UTGANG;                                                                  
                                                                                
  UTGANG: PROC;                                                                 
    IF  FRA_CICS THEN                                                           
      EXEC CICS XCTL PROGRAM('R0010401') COMMAREA(KOM_OMR);                     
    ELSE                                                                        
      EXEC CICS RETURN;                                                         
  END UTGANG;                                                                   
                                                                                
                                                                                
  FEILBEH:                                                                      
                                                                                
     EXEC CICS HANDLE CONDITION ERROR(ABEND);                                   
     IF FEIL_MELD_NR = 0 THEN /* AB 01.08.16 */                                 
        PROGRAM_ID = 'R0011401';                                                
                                                                                
     S001141O.MELDING2O =                                                       
              'F E I L  H A R  O P P S T Å T T ! ! !.';                         
                                                                                
     S001141O.MELDING3O =                                                       
              'EIBRCODE : ' !! UNSPEC(EIBRCODE);                                
                                                                                
     S001141O.MELDING4O =                                                       
          'EIBFN    : ' !! UNSPEC(EIBFN) !! '. FEILKODE : ' !! FEILKODE         
                                         !! '. DATASETT : ' !! DSNAVN;          
                                                                                
     S001141O.MELDING5O =                                                       
                   'PROGRAMNAVN :  ' !! PROGRAM_ID    !!                        
                   '.INGEN OPPDATERING ER BLITT UTFØRT.';                       
                                                                                
     S001141O.MELDING6O =                                                       
                   'TA UTSKRIFT!!!. TRYKK DERETTER "ENTER".';                   
                                                                                
                                                                                
     EXEC CICS SEND MAP('S001141')                                              
                                                 MAPSET('S001143');             
                                                                                
     EXEC CICS RECEIVE MAP('S001141')                                           
                                    MAPSET('S001143') SET(BMSMAPBR);            
                                                                                
                                                                                
     EXEC CICS SYNCPOINT ROLLBACK;                                              
                                                                                
                                                                                
     TERMINERINGS_IND = 'F';                                                    
                                                                                
     TRANSKODE  = 'R041';                                                       
                                                                                
     CALL UTGANG;                                                               
                                                                                
  ABEND:                                                                        
     EXEC CICS ABEND ABCODE(FEIL);                                              
                                                                                
  PF1:                                                                          
                                                                                
                                                                                
 /* LEGGER UNDERKJENT TRANS UT PÅ VENTEDATABASE HVIS GYLDIG     */              
 /* FNR OG VIRKNINGSDATO,ELLERS FORKASTES TRANSAKSJONEN.        */              
                                                                                
     IF ^(E1S.FNRNR = 1          !                                              
          E1S.FNRNR = 200        !                                              
          E1S.VIRK_DATO_ÅMDNR = 9 !                                             
          E1S.VIRK_DATO_ÅMDNR = 200) THEN                                       
        DO;                                                                     
                                                                                
           IF FØDSNUMMER    = 0   THEN                                          
                                                                                
 /* SKRIVER MELDING OM AT TRANSAKSJONEN ER FORKASTET.           */              
                                                                                
              DO;                                                               
                 FEIL_MELD_NR  = 0;                                             
                 S001141O.MELDING6O =                                           
                         'REGISTERINGEN ER IKKE AVSLUTTET MED "ENTER"'!!        
                         '.TRANSAKSJONEN ER FORKASTET.';                        
                                                                                
              END;                                                              
           ELSE                                                                 
              DO;                                                               
                                                                                
                 SEARCH_FNR  = TRANS_OPPL_OMR.FØDSNUMMER;                       
                 IF FRA_CICS THEN                                               
                    BLANKETTYPE = STYREKODE;                                    
                                                                                
                                                                                
                 IF FEIL_MELD_NR = 666 &      /*BLANKETTKONTROLLFEIL*/          
                    ^FRA_MED_DIALOG     THEN  /*IKKE BASEKONTROLLFEIL*/         
                    FUNKSJONSKODE = 'B';                                        
                                                                                
                 EXEC CICS LINK PROGRAM('R0010490') COMMAREA(KOM_OMR);          
                                                                                
 /* SKRIVER MELDING OM AT TRANSAKSJONEN ER LAGT UT PÅ VENTEBASE.*/              
                                                                                
                 S001141O.MELDING6O =                                           
                         'TRANSAKSJONEN ER LAGT UT PÅ VENTEREGISTER.';          
              END;                                                              
                                                                                
                                                                                
           EXEC CICS SEND MAP('S001141') MAPSET('S001143')                      
                                                           DATAONLY;            
     /* DUMMY SEND-KALL.                              */                        
                                                                                
           EXEC CICS SEND MAP('S001141')                                        
                                                  MAPSET('S001143');            
           EXEC CICS DELAY INTERVAL(005);                                       
                                                                                
           TERMINERINGS_IND = 'F';                                              
                                                                                
           TRANSKODE  = 'R041';                                                 
                                                                                
           CALL UTGANG;                                                         
                                                                                
        END;                                                                    
     ELSE                                                                       
        DO;                                                                     
           TERMINERINGS_IND = 'F';                                              
                                                                                
           TRANSKODE = 'R041';                                                  
                                                                                
           S001141O.MELDING6O =                                                 
                                                                                
   'NB** TRANSAKSJONEN ER FORKASTET P.G.A. UGYLDIG FNR EL. VDATO **NB';         
                                                                                
           EXEC CICS SEND MAP('S001141') MAPSET('S001143')                      
                                                           DATAONLY;            
     /* DUMMY SEND-KALL.                            */                          
                                                                                
           EXEC CICS SEND MAP('S001141')                                        
                                    MAPSET('S001143');                          
                                                                                
           EXEC CICS DELAY INTERVAL(005);                                       
           CALL UTGANG;                                                         
        END;                                                                    
                                                                                
                                                                                
  PF2:                                                                          
     TERMINERINGS_IND   = 'F';                                                  
                                                                                
     S001141O.MELDING6O =                                                       
      'NBNBNBNBNB***    TRANSAKSJONEN ER FORKASTET.    ***NBNBNBNBNB';          
                                                                                
     EXEC CICS SEND MAP('S001141') MAPSET('S001143')                            
                                                      DATAONLY ;                
     /* DUMMY SEND-KALL.                                     */                 
                                                                                
     EXEC CICS SEND MAP('S001141')                                              
                                    MAPSET('S001143');                          
     EXEC CICS DELAY INTERVAL(005);                                             
                                                                                
                                                                                
     TRANSKODE = 'R041';                                                        
                                                                                
     CALL UTGANG;                                                               
 %PAGE;                                                                         
      %INCLUDE R0011402;               /* BLANK  ARBEIDSOMRÅDE */               
 %PAGE;                                                                         
      %INCLUDE R0011403;               /* KONTROLL AV SØKERDELEN */             
 %PAGE;                                                                         
      %INCLUDE R0011405;               /* KONTROLL AV KAP 7,8,10 */             
 %PAGE;                                                                         
      %INCLUDE R0011406;               /* ARBEIDSOMRÅDE -> MAP */               
 %PAGE;                                                                         
      %INCLUDE R0011409;               /* MAP -> ARBEIDSOMRÅDE */               
 %PAGE;                                                                         
      %INCLUDE R0019901;               /*  TESTER OM DATO ER GYLDIG */          
 %PAGE;                                                                         
      %INCLUDE R0019902;               /*  FINNE KJØNN              */          
 %PAGE;                                                                         
      %INCLUDE R0019904;               /* FØDSELSNUMMERKONTROLL */              
 %PAGE;                                                                         
      %INCLUDE R0019905;               /* BEREGNER ALDER */                     
 %PAGE;                                                                         
      %INCLUDE R0019907;               /* TABELLSØKING */                       
 %PAGE;                                                                         
      %INCLUDE R0019908;               /* DAGENS DATO PLUSS 1 */                
 %PAGE;                                                                         
      %INCLUDE R0019910;               /* NUMERISK KONTROLL */                  
 %PAGE;                                                                         
      %INCLUDE R0019912;  /* KONVERTERING CHAR ==> PIC / PIC ==> CHAR */        
 %PAGE;                                                                         
      %INCLUDE R0019944;               /* SKRIV_FEIL      */                    
 %PAGE;                                                                         
      %INCLUDE R0019964;               /* F_6MDR          */                    
 %PAGE;                                                                         
      %INCLUDE R0019988;               /* KONV_HÅMD_ÅMD 2000-3 */               
 %PAGE;                                                                         
      %INCLUDE R0019983;               /* KONV_ÅMD_HDMÅ2000-3 */                
 %PAGE;                                                                         
      %INCLUDE R0019989;               /* KONV_HÅMD_ MÅ2000-3 */                
 %PAGE;                                                                         
      %INCLUDE R0019984;               /* KONV_MÅ_HÅMD 2000-3 */                
 %PAGE;                                                                         
      %INCLUDE R0019995;               /* KONV_FNR11_FNR13 2000-3 */            
      %INCLUDE R0019945;               /* ATK_TILLGANG_KONTROLL   */            
      %INCLUDE R0019999;               /* KONTROLL_ACF2           */            
   END R00114;                                                                  
