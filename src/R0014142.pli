 /*   SIST ENDRET PÅ PROD   2002.12.20  8.56.29 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2000.07.07 13.00.18 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   1999.11.19  8.19.00 AV   JDA7339          */        
 /*       SIST ENDRET 04/05-98 12.41.17 AV   JDA7339                  */        
 /* ***************************************************************** */        
 /* IDENTIFIKASJON:                                                   */        
 /*    R0014142 - F_TILLEGGSPENSJON - INTERN FUNKSJON I PLI           */        
 /*    PROGRAMMERER: KRISTENSEN, APRIL 1982                           */        
 /*    ENDRET JULI 82 AV KARIN                                        */        
 /* HENSIKT:                                                          */        
 /*    FUNKSJONEN REGNER UT TILLEGGSPENSJON SOM BLIR RETURNERT.       */        
 /*    DETTE GJØRES PÅ GRUNNLAG AV MOTTATT FØDSELSÅR (2 SIFF),        */        
 /*    SLUTTPOENGTALL,OVERKOMP.SLUTTPOENGTALL , ANTALL POENGÅR OG     */        
 /*    PENSJONSGRAD.                                                  */        
 /* PROGRAMTILKNYTNING:                                               */        
 /*    FUNKSJONEN BLIR INKLUDERT I OVERLIGGENDE PROGRAM.              */        
 /* BRUK:                                                             */        
 /*    TP=F_TILLEGGSPENSJON(FÅ,SPT,OPT,PÅ,UFG,TP_PROSENT,FORHØY_IND)  */        
 /* ***************************************************************** */        
 F_TILLEGGSPENSJON:                                                             
  PROC(FÅ,G,SPT,OPT,PÅ,UFG,TP_PROSENT,FORHØY_IND) RETURNS(DEC FIXED(5));        
  DCL                                                                           
     FÅ                 PIC '9999',            /*2000*/                         
     G                  FIXED DEC(5),                                           
     SPT                FIXED DEC(3,2),                                         
     OPT                FIXED DEC(3,2),                                         
     PÅ                 FIXED DEC(3),                                           
     UFG                FIXED DEC(5,2),                                         
     TP_PROSENT         FIXED DEC(3),                                           
     OÅ                 FIXED DEC(3),                                           
     FORHØY_IND         CHAR(1),                                                
     TP_WORK            FIXED DEC(11,4),                                        
     TP_WORK_2          FIXED DEC(11,4),                                        
     TP                 FIXED DEC(5);                                           
                                                                                
   DCL PGNAVN         CHAR (8);                                                 
   PGNAVN = 'R0014142';                                                         
                                                                                
  IF ( PÅ < 3               &                  /* PENSJON UTLAND  */            
       B02.KONVENSJON(BER_SØ_IND)  = 'J') !       /* PENSJON UTLAND  */         
     ( PÅ > 2               !                                                   
     (FÅ = 1899 & PÅ = 2)   !                    /*2000*/                       
     (FÅ = 1898 & PÅ = 1))        THEN          /*2000*/                        
     DO;                                                                        
        IF FÅ > 1936 /* & FÅ < 98*/        THEN  /*2000*/                       
           DO;                                                                  
              TP_WORK = G * 45 /100;                                            
              TP_WORK = TP_WORK * SPT;                                          
              TP_WORK = TP_WORK * PÅ / 40;                                      
              TP_WORK = TP_WORK * UFG /100;                                     
              TP_WORK = TP_WORK * TP_PROSENT / 100;                             
              IF B02.PENSJONSTYPE2(BER_SØ_IND) = 'A' THEN                       
 /*KONSTS*/      DIV_PERIODE.ATTFØRING_TP_ÅRSBELØP = TP_WORK + 0.5;             
              TP_WORK = TP_WORK / 12;                                           
           END;                                                                 
        ELSE                                                                    
           DO;                                                                  
              IF FÅ > 1917  /* & FÅ < 98 */    THEN   /*2000*/                  
                 OÅ = 20 + FÅ - 1917; /*2000*/                                  
              ELSE                                                              
                 OÅ = 20;                                                       
              IF SPT > OPT THEN                                                 
                 DO;                                                            
                    TP_WORK = G * 45 /100;                                      
                    TP_WORK = TP_WORK * OPT;                                    
                    TP_WORK = TP_WORK * PÅ / OÅ;                                
                    TP_WORK_2 = G * 45 / 100;                                   
                    TP_WORK_2 = TP_WORK_2 * (SPT - OPT);                        
                    TP_WORK_2 = TP_WORK_2 * PÅ / 40;                            
                    TP_WORK = (TP_WORK + TP_WORK_2) * UFG /100;                 
                    TP_WORK = TP_WORK * TP_PROSENT / 100;                       
              IF B02.PENSJONSTYPE2(BER_SØ_IND) = 'A' THEN                       
 /*KONSTS*/      DIV_PERIODE.ATTFØRING_TP_ÅRSBELØP = TP_WORK + 0.5;             
                    TP_WORK = TP_WORK / 12;                                     
                 END;                                                           
              ELSE                                                              
                 DO;                                                            
                                                                                
                                                                                
                    TP_WORK = G * 45 / 100;                                     
                    TP_WORK = TP_WORK * SPT;                                    
                    TP_WORK = TP_WORK * PÅ / OÅ;                                
                    TP_WORK = TP_WORK * UFG /100;                               
                    TP_WORK = TP_WORK * TP_PROSENT / 100;                       
              IF B02.PENSJONSTYPE2(BER_SØ_IND) = 'A' THEN                       
 /*KONSTS*/      DIV_PERIODE.ATTFØRING_TP_ÅRSBELØP = TP_WORK + 0.5;             
                    TP_WORK = TP_WORK / 12;                                     
                 END;                                                           
           END;                                                                 
        IF FORHØY_IND ^= 'N' THEN                                               
           TP_WORK = TP_WORK + 0.5;                                             
     END;                                                                       
  ELSE                                                                          
     TP_WORK = 0;                                                               
  TP = TP_WORK;                                                                 
                                                                                
   /* TEST IKKE GI KONV 'F' TILLEGGSPENSJON, TRUDE 060192  */                   
   IF B02.BOSATT(BER_SØ_IND)  = 000  &                                          
      B02.TT_16_66(BER_SØ_IND) = 0     &                                        
      B02.TT_ETTER_1966(BER_SØ_IND) = 0 &                                       
      B02.FRAVIK_FLYKTN(BER_SØ_IND) = ' ' &  /*TS130902*/                       
      B02.TT_FØR_1967(BER_SØ_IND)   = 0 &                                       
      B02.ALDERSP.KONV_P_KODE(BER_SØ_IND)  = 'F'     &                          
      B02.ALDERSP.KONV_GRAD(BER_SØ_IND)  = 0     THEN                           
        TP  = 0 ;                                                               
                                                                                
  RETURN(TP);                                                                   
                                                                                
 END F_TILLEGGSPENSJON;                                                         
