 /*   SIST ENDRET PÅ PROD   2003.02.27 12.27.37 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2002.11.21 13.15.18 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.10.22 12.35.19 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2000.02.18 14.34.52 AV   JDA7339          */        
 /*       SIST ENDRET 22/06-98 12.24.28 AV   JDA7339                  */        
 /*       SIST ENDRET 11/06-98 11.02.17 AV   RFA9991                  */        
                                                                                
                                                                                
 /* ***************************************************************** */        
 /*IDENTIFIKASJON:                                                    */        
 /*    R0011403 - PROSEDYRE I PLI                                     */        
 /*    PROGRAMMERER: JAN, SEPTEMBER 1981                              */        
 /*HENSIKT:                                                           */        
 /*    PROSEDYREN FORETAR KONTROLL AV DATA FRA ENDRINGSBLANKETTEN     */        
 /*    FELTENE 1,6 OG 7                                               */        
 /*PROGRAMTILKNYTNING:                                                */        
 /*    PROSEDYREN INNGÅR SOM EN DEL AV BEHANDLING AV ENDRINGS-        */        
 /*    BLANKETTEN.                                                    */        
 /*    INCLUDES I  R0011401.                                          */        
 /*BRUK:                                                              */        
 /* CALL KONRTOLL_E1_SØKER(FEIL_I_E1_SØKER);                          */        
 /*                                                                   */        
 /* ***************************************************************** */        
 /*                                                                   */        
 /*ENDRET: 14.04.88 AV SVERRE LUNDEBY, DATA-UTVIKLING A/S             */        
 /* ******                                                            */        
 /*    INITIERER BRUKERID I FNR_REG MED CICS_IND FØR KONTROLL PÅ      */        
 /*    OM FNR ER ENDRET (R0019906).                                   */        
 /* ***************************************************************** */        
 /*                                                                   */        
 /*   MODULEN KONTROLLERER E1_SØKER, OG RETURNERER                    */        
 /*   MED FEIL_FUNNET = '1'B HVIS FEIL, '0'B HVIS RIKTIG              */        
 /*                                                                   */        
 /* ***************************************************************** */        
                                                                                
                                                                                
 KONTROLL_E1_SØKER:                                                             
   PROC (FEIL_FUNNET);                                                          
                                                                                
                                                                                
   DCL                                                                          
      FEIL_FUNNET BIT (1);                                                      
                                                                                
 /* ***************************************************************** */        
 /* DIVERSE DEKLARASJONER                                             */        
 /* DCL'ENE ER UTVIDET I FORB. MED FEILM.NR. 290.  21.2.86.  ERIK S.  */        
 /* ***************************************************************** */        
                                                                                
   DCL                                                                          
      DAGENS_DATO_ÅMD                                  PIC '99999999'  ,        
      DAGENS_DATO_ÅM DEF DAGENS_DATO_ÅMD               PIC '999999'    ,        
      DAGENS_DATO_Å4 DEF DAGENS_DATO_ÅMD               PIC '9999'      ,        
      DAGENS_DATO_Å2 DEF DAGENS_DATO_ÅMD POS(3)        PIC '99'      ,          
      DAGENS_DATO_M  DEF DAGENS_DATO_ÅMD POS (5)       PIC '99'        ,        
      DAGENS_DATO_D  DEF DAGENS_DATO_ÅMD POS (7)       PIC '99'        ;        
                                                                                
                                                                                
   DCL                                                                          
      KEY_BIT                    BIT(32) BASED (KEY_PEKER),                     
      KEY_PEKER                  POINTER,                                       
      TK_RECL                    CHAR (101);                                    
                                                                                
 /* ***************************************************************** */        
 /*  HJELPE DCL FOR Å SNU VIRK.DATO                                   */        
 /* ***************************************************************** */        
                                                                                
   DCL                                                                          
      HJ_VIRK_DATO_ÅMD PIC '99999999',                                          
      VIRK_DATO_ÅM DEF HJ_VIRK_DATO_ÅMD POS(1) PIC '999999',                    
      VIRK_DATO_Å4 DEF HJ_VIRK_DATO_ÅMD POS(1) PIC '9999',                      
      VIRK_DATO_Å2 DEF HJ_VIRK_DATO_ÅMD POS(3) PIC '99',                        
      VIRK_DATO_M DEF HJ_VIRK_DATO_ÅMD POS(5) PIC '99';                         
                                                                                
                                                                                
   DCL                                                                          
      1 FNR_REG,                                                                
        2 FNR1        FIXED DEC(11),                                            
        2 FNR2        FIXED DEC(11),                                            
        2 BRUKERID    CHAR     ( 4);                                            
                                                                                
                                                                                
                                                                                
      FNR_REG.BRUKERID =  DIV_PARAM_OMR.CICS_IND;                               
      ANT_FEIL_SKREVET =  0;                                                    
      FEIL_FUNNET      = '0'B;                                                  
      KEY_PEKER        =  ADDR(E1S.TKNR);                                       
                                                                                
 /* ***************************************************************** */        
 /* DATE () ER EN BUILT-IN-FUNCTION I PLI SOM RETURNERER DAGENS DATO  */        
 /* I FORMEN ÅR/MÅNED/DAG.                                            */        
 /* ***************************************************************** */        
                                                                                
      DAGENS_DATO_ÅMD = DATO_2000;                                              
                                                                                
                                                                                
    HJ_VIRK_DATO_ÅMD = E1S.VIRK_DATO_ÅMD;                                       
                                                                                
                                                                                
   /*ENDRING 9711 HL*/                                                          
   IF HJ_VIRK_DATO_ÅMD > 19980100  THEN                                         
      DO;                                                                       
         FEIL_FUNNET = '1'B;                                                    
         E1S.VIRK_DATO_ÅMDNR = 1543;                                            
      END;                                                                      
                                                                                
                                                                                
 /* ***************************************************************** */        
 /* KONTROLL AV FELT 1 (OPPLYSNINGER OM STØNADSMOTTAKEREN)            */        
 /*                                                                   */        
 /* F_NUMERISK KONTROLLERER OM ET OVERFØRT CHAR FELT ER NUMERISK.     */        
 /* DERSOM FELTET ER NUMERISK RETURNERES '1' B, HVIS IKKE '0'B.       */        
 /* ***************************************************************** */        
                                                                                
      IF ^ F_NUMERISK(F_FELT_PIC_CHAR11(E1S.FNR)) THEN                          
                                                                                
 /* ***************************************************************** */        
 /* F_FELT_PIC_CHAR ER EN FUNKSJON SOM KONVERTERER PIC-FELT TIL CHAR  */        
 /* ***************************************************************** */        
                                                                                
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
                                                                                
 /* ***************************************************************** */        
 /* FEILMELDING 200: TEGN SOM IKKE ER TALL FOREKOMMER I TALLFELTET    */        
 /* ***************************************************************** */        
                                                                                
            E1S.FNRNR   = 200;                                                  
         END;                                                                   
      ELSE                                                                      
                                                                                
 /* ***************************************************************** */        
 /* F_GYLDIG_FNR ER EN FUNKSJON SOM KONTROLLERER OM FNR ER GYLDIG.    */        
 /* HVIS DET IKKE ER NUMERISK ELLER INNEHOLDER FEIL RETURNERES '0'B.  */        
 /* HVIS FØDSELSNUMMERET BLIR AKSEPTERT RETURNESES '1'B               */        
 /* ***************************************************************** */        
                                                                                
      IF ^ F_GYLDIG_FNR(E1S.FNR) THEN                                           
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
                                                                                
 /* ***************************************************************** */        
 /* FEILMELDING 1: FEIL I FØDSELSNUMMER                               */        
 /* ***************************************************************** */        
                                                                                
            E1S.FNRNR   = 1;                                                    
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
            FNR_REG.FNR1 = E1S.FNR;                                             
            EXEC CICS LINK PROGRAM ('R0019906') COMMAREA (FNR_REG);             
            IF FNR_REG.FNR2 > 0 THEN                                            
              DO;                                                               
                                                                                
 /* ***************************************************************** */        
 /* FEILMELDING 203: DETTE FØDSELSNUMMERER ER ENDRET                  */        
 /* ***************************************************************** */        
                                                                                
                E1S.FNRNR   = 203;                                              
                E1S.FNR_GML = E1S.FNR;                                          
                E1S.FNR     = FNR_REG.FNR2;                                     
              END;                                                              
         END;                                                                   
                                                                                
      IF ^ F_NUMERISK(F_FELT_PIC_CHAR4(E1S.TKNR)) THEN                          
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            E1S.TKNRNR  = 200;                                                  
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
            EXEC CICS HANDLE CONDITION NOTFND(NOTFND);                          
            EXEC CICS READ DATASET ('TKNRTAB') RIDFLD(KEY_BIT)                  
                                                     INTO(TK_RECL);             
                  LEAVE;                                                        
   NOTFND:                                                                      
            FEIL_FUNNET = '1'B;                                                 
                                                                                
                  E1S.TKNRNR  = 3;                                              
         END;                                                                   
                                                                                
                                                                                
    /* ************************************************** */                    
    /* KONTROLL AV ACF2 FOR TILLGANGE TIL TKNR            */                    
    /*                                                    */                    
          /* ************************************************** */              
    BO_TKNR  = TKNR;                                                            
    CALL ATK_TILGANG_KONTROLL;                                                  
                                                                                
                                                                                
      IF ^ F_NUMERISK(F_FELT_PIC_CHAR8(E1S.VIRK_DATO_ÅMD)) THEN                 
         DO;                                                                    
            FEIL_FUNNET        = '1'B;                                          
            E1S.VIRK_DATO_ÅMDNR = 200;                                          
         END;                                                                   
      ELSE                                                                      
                                                                                
 /* **************************************************************** */         
 /* FUNKSJONEN F_GYLDIG_DATO KONTROLLERER OM EN DATO(ÅR/MND) ER      */         
 /* GYLDIG. HVIS GYLDIG RETURNERES '1'B, HVIS IKKE GYLDIG '0'B.      */         
 /* **************************************************************** */         
                                                                                
      IF ^ F_GYLDIG_DATO(E1S.VIRK_DATO_ÅMD) THEN                                
         DO;                                                                    
            FEIL_FUNNET        = '1'B;                                          
                                                                                
 /* **************************************************************** */         
 /* FEILMELD 9: UGYLDIG VIRKNINGSDATO                                */         
 /* **************************************************************** */         
                                                                                
            E1S.VIRK_DATO_ÅMDNR = 9;                                            
         END;                                                                   
                                                                                
      IF VERIFY(E1S.SKILSMISSE,'X ') ^= 0 THEN                                  
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
                                                                                
 /* ***************************************************************** */        
 /* FEILMELD 264: FEIL I SKILSMISSE                                   */        
 /* ***************************************************************** */        
                                                                                
            E1S.SKILSMISSENR = 264;                                             
         END;                                                                   
                                                                                
                                                                                
      IF VERIFY(E1S.FLYTTET_SAMMEN,'X ') ^= 0 THEN                              
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
                                                                                
 /* ***************************************************************** */        
 /* FEILMELD 264: FEIL I FLYTTET SAMMEN.                              */        
 /* ***************************************************************** */        
                                                                                
            E1S.FLYTTET_SAMMENNR = 264;                                         
         END;                                                                   
                                                                                
                                                                                
                                                                                
      IF VERIFY(E1S.FLYTTET_FRA,'X ') ^= 0 THEN                                 
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
                                                                                
 /* ***************************************************************** */        
 /* FEILMELD 264: FEIL I FLYTTET FRA.                                 */        
 /* ***************************************************************** */        
                                                                                
            E1S.FLYTTET_SAMMENNR = 264;                                         
         END;                                                                   
                                                                                
                                                                                
                                                                                
      IF VERIFY(E1S.EK_INNTU,'X ') ^= 0 THEN                                    
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
                                                                                
 /* ***************************************************************** */        
 /* FEILMELD 275: UGYLDIG KODE.                                       */        
 /* ***************************************************************** */        
                                                                                
            E1S.EK_INNTUNR = 275;                                               
         END;                                                                   
                                                                                
                                                                                
      IF VERIFY(E1S.EK_INNTM,'X ') ^= 0 THEN                                    
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
                                                                                
 /* ***************************************************************** */        
 /* FEILMELD 275: UGYLDIG KODE.                                       */        
 /* ***************************************************************** */        
                                                                                
            E1S.EK_INNTMNR = 275;                                               
         END;                                                                   
                                                                                
 /* **************************************************************** */         
 /* KONTROLL AV FELT 6 (ENDRING I EKTESKAPSFORHOLD)                  */         
 /* **************************************************************** */         
                                                                                
      IF E1S.FNR_EK  > 0 THEN                                                   
         DO;                                                                    
            IF    E1S.SKILSMISSE     = 'X' !                                    
                  E1S.FLYTTET_FRA    = 'X' !                                    
                  E1S.EK_INNTU       = 'X' !                                    
                  E1S.EK_INNTM       = 'X' !                                    
                  E1S.FLYTTET_SAMMEN = 'X'                                      
                                            THEN                                
               DO;                                                              
                  FEIL_FUNNET          = '1'B;                                  
                                                                                
 /* **************************************************************** */         
 /* FEILMELD 69: FLERE ENN ET FELT ER UTFYLT                         */         
 /* **************************************************************** */         
                                                                                
                  E1S.SKILSMISSENR     = 69;                                    
                  E1S.FLYTTET_FRANR    = 999;                                   
                  E1S.FLYTTET_SAMMENNR = 999;                                   
                  E1S.EK_INNTUNR       = 999;                                   
                  E1S.EK_INNTMNR       = 999;                                   
               END;                                                             
                                                                                
            IF ^ F_NUMERISK(F_FELT_PIC_CHAR11(E1S.FNR_EK)) THEN                 
              DO;                                                               
                 FEIL_FUNNET  = '1'B;                                           
                 E1S.FNR_EKNR = 200;                                            
              END;                                                              
            ELSE                                                                
               IF ^ F_GYLDIG_FNR(E1S.FNR_EK) THEN                               
                  DO;                                                           
                     FEIL_FUNNET  = '1'B;                                       
                                                                                
 /* **************************************************************** */         
 /* FEILMELD 15: FEIL I EKTEFELLENS FØDSELSNUMMER                    */         
 /* **************************************************************** */         
                                                                                
                     E1S.FNR_EKNR = 15;                                         
                  END;                                                          
               ELSE                                                             
               IF E1S.FNR_EK > 0  & E1S.FNR                                     
                                  = E1S.FNR_EK  THEN                            
                  DO;                                                           
                     FEIL_FUNNET  = '1'B;                                       
                     E1S.FNR_EKNR = 16;                                         
                  END;                                                          
               ELSE                                                             
                  DO;                                                           
                     FNR_REG.FNR1 = E1S.FNR_EK;                                 
                     EXEC CICS LINK PROGRAM ('R0019906')                        
                     COMMAREA (FNR_REG);                                        
                     IF FNR_REG.FNR2 > 0 THEN                                   
                       DO;                                                      
                         E1S.FNR_EKNR   = 203;                                  
                         E1S.FNR_GML_EK = E1S.FNR_EK;                           
                         E1S.FNR_EK     = FNR_REG.FNR2;                         
                       END;                                                     
                  END;                                                          
            IF E1S.NAVN_EK = (25)' ' THEN                                       
               DO;                                                              
                  FEIL_FUNNET = '1'B;                                           
                                                                                
 /* **************************************************************** */         
 /* FEILMELD 154: EKTEFELLENS NAVN ER IKKE UTFYLT                    */         
 /* **************************************************************** */         
                                                                                
                  E1S.NAVN_EK = 154;                                            
               END;                                                             
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
            IF (E1S.SKILSMISSE  = 'X' & E1S.FLYTTET_FRA    = 'X') !             
               (E1S.SKILSMISSE  = 'X' & E1S.FLYTTET_SAMMEN = 'X') !             
               (E1S.SKILSMISSE  = 'X' & E1S.EK_INNTU       = 'X') !             
               (E1S.SKILSMISSE  = 'X' & E1S.EK_INNTM       = 'X') !             
               (E1S.FLYTTET_FRA = 'X' & E1S.EK_INNTU       = 'X') !             
               (E1S.FLYTTET_FRA = 'X' & E1S.EK_INNTM       = 'X') !             
               (E1S.FLYTTET_SAMMEN = 'X' & E1S.EK_INNTU    = 'X') !             
               (E1S.FLYTTET_SAMMEN = 'X' & E1S.EK_INNTM    = 'X') !             
               (E1S.EK_INNTU       = 'X' & E1S.EK_INNTM    = 'X') !             
               (E1S.FLYTTET_FRA = 'X' & E1S.FLYTTET_SAMMEN = 'X') THEN          
               DO;                                                              
                  FEIL_FUNNET          = '1'B;                                  
                  E1S.SKILSMISSENR     = 69;                                    
                  E1S.FLYTTET_FRANR    = 999;                                   
                  E1S.FLYTTET_SAMMENNR = 999;                                   
                  E1S.EK_INNTUNR       = 999;                                   
                  E1S.EK_INNTMNR       = 999;                                   
               END;                                                             
         END;                                                                   
                                                                                
 /* ***************************************************************** */        
 /* KONTROLL AV FELT 7 (ENDRING I SPRÅKFORM)                          */        
 /* ***************************************************************** */        
                                                                                
      IF VERIFY(E1S.SPRÅK,'NB ') ^= 0 THEN                                      
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
                                                                                
 /* ***************************************************************** */        
 /* FEILMELD 6: FEIL I SPRÅK                                          */        
 /* ***************************************************************** */        
                                                                                
            E1S.SPRÅKNR = 6;                                                    
         END;                                                                   
                                                                                
 /* ***************************************************************** */        
 /*   KRYSSKONTROLLER .  TESTEN ER UTVIDET TIL Å GODTA VIRK.DATO      */        
 /*   40 DAGER FØR DAGENS DATO.  FEILMELDING 290.  24.4.86.  ERIK.    */        
 /* ***************************************************************** */        
                                                                                
      IF ^FEIL_FUNNET THEN                                                      
                                                                                
          DO;                                                                   
                                                                                
              HJ_VIRK_DATO_ÅMD   =  E1S.VIRK_DATO_ÅMD   ;                       
                                                                                
 /*           IF HJ_VIRK_DATO_ÅM > DAGENS_DATO_ÅM THEN           */             
 /*                                                              */             
 /*              DO;                                             */             
 /*                                                              */             
 /*                 IF DAGENS_DATO_D > 19 THEN                   */             
 /*                                                              */             
 /*                    DO;                                       */             
 /*                                                                */           
 /*                       HJ_DAGENS_DATO_ÅM = DAGENS_DATO_ÅM + 2 ; */           
 /*                                                                */           
 /*                    END;                                        */           
 /*                                                                */           
 /*                 ELSE                                           */           
 /*                                                                */           
 /*                    HJ_DAGENS_DATO_ÅM = DAGENS_DATO_ÅM + 1 ;    */           
 /*                                                                */           
 /*                 IF HJ_DAGENS_DATO_M > 12 THEN                  */           
 /*                                                                */           
 /*                    DO;                                         */           
 /*                                                                */           
 /*                       HJ_DAGENS_DATO_Å = HJ_DAGENS_DATO_Å + 1 ;*/           
 /*                       HJ_DAGENS_DATO_M = HJ_DAGENS_DATO_M - 12;*/           
 /*                                                                */           
 /*                    END;                                        */           
 /*                                                                */           
 /*                                                                */           
 /*                 IF HJ_VIRK_DATO_ÅM >  HJ_DAGENS_DATO_ÅM THEN   */           
 /*                                                                */           
 /*                                                                */           
 /*                    DO;                                         */           
 /*                                                                */           
 /* ***************************************************************** */        
 /* FEILMELD 185: VIRKNINGSDATO STØRRE ENN 40 DAGER FRA DAGS DATO.    */        
 /* ***************************************************************** */        
 /*                                                                */           
 /*                                                                */           
 /*                        E1S.VIRK_DATO_MÅNR = 9;                 */           
 /*                        FEIL_FUNNET        = '1'B;              */           
 /*                                                                */           
 /*                    END;                                        */           
 /*               END;                                             */           
 /*                                                                */           
 /* ENDRET 22.6.88 HL - VI AKSEPTERER VIRKDATO 6 MNDR FREM I TID   */           
                                                                                
                  IF ^ F_6MDR(HJ_VIRK_DATO_ÅMD)     THEN                        
                                                                                
                     DO;                                                        
                        FEIL_FUNNET           = '1'B;                           
                        E1S.VIRK_DATO_ÅMDNR   = 205;                            
                     END;                                                       
                                                                                
                                                                                
          END;                                                                  
                                                                                
                                                                                
    IF E1S.EK_INNTU = 'X' !                                                     
       E1S.EK_INNTM = 'X' THEN                                                  
       DO;                                                                      
          IF E1S.VIRK_DATO_ÅMD  < 19840700 THEN                                 
             DO;                                                                
                FEIL_FUNNET = '1'B;                                             
                E1S.VIRK_DATO_ÅMDNR = 273;                                      
             END;                                                               
       END;                                                                     
                                                                                
 /* ***************************************************************** */        
 /* LEGGER FNR OG VIRK_DATO INN I MENY_OMR I KOM_OMR.                 */        
 /* ***************************************************************** */        
                                                                                
     FØDSNUMMER    = E1S.FNR;                                                   
     VIRKNINGSDATO_ÅMD = E1S.VIRK_DATO_ÅMD;                                     
   END KONTROLL_E1_SØKER;                                                       
