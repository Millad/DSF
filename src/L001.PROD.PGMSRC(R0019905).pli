 /*   SIST ENDRET PÅ PROD   2005.09.28  8.07.40 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2005.04.12  8.35.01 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2004.03.16 14.00.10 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.03.12 14.34.00 AV   JDA2970          */        
 /*       SIST ENDRET 06/07-98 11.22.30 AV   JDA7339                  */        
 /*       SIST ENDRET 23/01-98 09.56.46 AV   SPA7339                  */        
 /* ***************************************************************** */        
 /*IDENTIFIKASJON:                                                    */        
 /*    R0019905 - PROGRAM I PLI                                       */        
 /*    PROGRAMMERT AV GEIR MARS 1981                                  */        
 /*HENSIKT:                                                           */        
 /*    FUNKSJONEN ANGIR TIDA FRA FØDSELSTIDSPUNKT TIL VIRKNINGSDATO   */        
 /*BRUK:                                                              */        
 /*    ALDERSBEREGNING PERSONER FØDT 1900 TIL 1999                    */        
 /*    ALDERSBEREGNING PERSONER FØDT 1800 TIL 1899                    */        
 /*HISTORIKK:                                                         */        
 /*    1998-06-25 MEA99991                                            */        
 /*       ENDRING MERKET *19980625* NEDENFOR ER GJORT FOR Å KUNNE     */        
 /*       RETURNERE NEGATIV ALDER.  BRUK AV F_ALDER VISER AT DET      */        
 /*       FORVENTES MULIGHET FOR NEGATIV ALDER.                       */        
 /* ***************************************************************** */        
 F_ALDER:PROC (FNR,VIRK_DATO)RETURNS(FIXED DEC(5));                             
                                                                                
   DCL                                                                          
      ALDER_Å_MND FIXED DEC(5),                                                 
      VIRK_DATO   PIC '99999999',                                               
                                                                                
      1 VIRK_DAT DEF VIRK_DATO,                                                 
         2 VHÅR   PIC '(4)9',                                                   
         2 VMND   PIC '(2)9',                                                   
         2 VDD    PIC '(2)9';                                                   
                                                                                
   DCL  FNR         PIC'(11)9';                                                 
   DCL  FNR13               PIC '(13)9';                                        
   DCL 1 FNR13_UP DEF FNR13,                                                    
         2 FDAG   PIC '(2)9',                                                   
         2 FMND   PIC '(2)9',                                                   
         2 FÅR    PIC '(4)9',                                                   
         2 POS_7  PIC '9',                                                      
         2 FELT_3 PIC '(4)9';                                                   
                                                                                
   /* PGA DECIMAL OVERFLOW I NY PL1/ENTERPRISE 3.2.0ENDRET 2004.06              
   DCL VIRK_ANT_MND   PIC '(5)9',                                               
       FNR_ANT_MND    PIC '(5)9',                                               
       ALD_ANT_MND    PIC '(5)9';                                               
   */                                                                           
                                                                                
   DCL VIRK_ANT_MND   PIC '(6)9',                                               
       FNR_ANT_MND    PIC '(6)9',                                               
       ALD_ANT_MND    PIC '(6)9';                          /*19980625*/         
                                                                                
   DCL 1 H_ALD,                                                                 
         2 ALD_ÅR     PIC '999',                           /*19980625*/         
         2 ALD_MND    PIC '99' ,                                                
       1 H_ALD_XX     DEF H_ALD PIC '(5)9';                /*19980625*/         
                                                                                
    /* ==9905 START ================================================= */        
                                                                                
    IF (FNR       = 0 !                                                         
        VIRK_DATO = 0 ) THEN                                                    
       ALDER_Å_MND = 0; /* GIGO */                                              
    ELSE                                                                        
       DO;                                                                      
          FNR13 = KONV_FNR11_FNR13(FNR);                                        
                                                                                
          VIRK_ANT_MND = VHÅR * 12 + VMND;                                      
          FNR_ANT_MND = FÅR * 12  + FMND;                                       
                                                                                
          ALD_ANT_MND  = VIRK_ANT_MND  - FNR_ANT_MND;                           
          ALD_MND      = MOD(ALD_ANT_MND,12);                                   
          ALD_ÅR       = FLOOR(ALD_ANT_MND/12);                                 
                                                                                
          ALDER_Å_MND = H_ALD_XX        ;                                       
                                                                                
          /* REMEDY 905 - VI SKAL RETURNERE NEGATIV ALDER */                    
          IF (FNR_ANT_MND > VIRK_ANT_MND &                                      
              ALDER_Å_MND > 0            ) THEN                                 
             ALDER_Å_MND = -ALDER_Å_MND;                                        
       END;                                                                     
    RETURN (ALDER_Å_MND);                                                       
 END F_ALDER;                                                                   
