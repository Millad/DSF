 /*   SIST ENDRET PÅ PROD   2001.07.18 12.52.08 AV   HLA2970          */        
 /*       SIST ENDRET 23/06-98 08.33.07 AV   JDA7339                  */        
 /*       SIST ENDRET 20/05-98 14.18.30 AV   HLA7339                  */        
 /*IDENTIFIKASJON:                                                    */        
 /*    R0011220 - PROSEDYRE I PLI                                     */        
 /*    PROGRAMMERER: GEIR, JANUAR 1982                                */        
 /*HENSIKT:                                                           */        
 /*    FORSØRGINGSTILLEGG EKTEFELLE / BARN                            */        
 /*    RUTINEN KONTROLLERER ALLE PERSONER I TRANSAKSJONEN MOT         */        
 /*    INFORMASJON I REGISTERET.  NÅR DET OPPDAGES UOVERENSSTEMMELSER */        
 /*    BLIR FEIL-MELD_NR SATT OG VI GÅR UT AV PGR.FOR HVER FAMILIE-   */        
 /*    PERSON SOM GÅR FEILFRI GJENNOM KONTROLLENE BLIR DET OPPRETTET  */        
 /*    NY STATUS FOR PERSONEN, OG SØKERENS STATUS OPPDATERES.         */        
 /* ****************************************************************  */        
 /*PROGRAMTILKNYTNING:                                                */        
 /*    KALLES OPP AV PROGRAM R0013520                                 */        
 /*BRUK:                                                              */        
 /*    EXEC CICS XCTL PROGRAM ('R0011220') COMMAREA (KOM_OMR)         */        
 /*                                                                   */        
 /* ***************************************************************** */        
 KON_FT:                                                                        
   PROC (COMMAREA_PEKER) OPTIONS (MAIN);                                        
      %INCLUDE P0019908;   /* KOM_OMR  BASED(COMMAREA_PEKER)        */          
      %INCLUDE P0019906;   /* TRANS_OPPL_OMR BASED(TRANS_OPPL_PEKER)*/          
      %INCLUDE P0011201;                                                        
      %INCLUDE P0019910;   /* STYRINGS_OMR  BASED(STYRINGS_PEKER)   */          
      %INCLUDE P0019912;   /* DIV_PARAM_OMR  BASED(DIV_PARAM_PEKER) */          
                                                                                
      DCL 1 B01 BASED(B01_PEKER), %INCLUDE P0019921;                            
      DCL 1 B02 BASED(B02_PEKER), %INCLUDE P0019921;                            
      DCL COMMAREA_PEKER PTR;                                                   
                                                                                
   DCL                                                                          
      BARN_IND        FIXED    BIN (15),                                        
      I               FIXED    BIN (15),                                        
      J               FIXED    BIN (15),                                        
      ADDR            BUILTIN,                                                  
      CSTG            BUILTIN;                                                  
                                                                                
    /* ============================================================== */        
     PROGRAM_ID = 'R0011220';                                                   
     BARN_IND   = 0;                                                            
      TRANSTYPE = '6';                                                          
      IF B01.TKNR(SØKER_IND) > 0  THEN                                          
         IF B01.TKNR(SØKER_IND) ^= FTS.TKNR THEN                                
            B02.TKNR(SØKER_IND) = FTS.TKNR;                                     
 /*         DO                                                                  
 L100: FJERNA 170701 MARTIN                                                     
               FEIL_VED_LABEL = '100'                                           
               FEIL_MELD_NR   = 1702                                            
               GO TO L999                                                       
            END  */                                                             
                                                                                
 /* ***************************************************************** */        
 /* SØKEREN STÅR REGISTRERT SOM ALDERS- ELLER UFØREPENSJONIST         */        
 /* ELLER AFP-PENSJONIST                                              */        
 /* ***************************************************************** */        
                                                                                
      IF (B01.PENSJONSTYPE1(SØKER_IND) = 'A' !                                  
 /*AFP*/ (B01.PENSJONSTYPE1(SØKER_IND) = 'K' &                                  
 /*AFP*/  B01.PENSJONSTYPE2(SØKER_IND)^= 'N') !                                 
          B01.PENSJONSTYPE1(SØKER_IND) = 'U' !                                  
          B01.PENSJONSTYPE1(SØKER_IND) = 'Y') THEN                              
         DO;                                                                    
                                                                                
 /* ***************************************************************** */        
 /* DERSOM DET ER EKTEFELLE I TRANSEN.                                */        
 /* ***************************************************************** */        
                                                                                
            IF FTS.FNR_EK > 0 THEN                                              
               DO;                                                              
                                                                                
 /* ***************************************************************** */        
 /* DERSOM SØKERS SIVILSTAND ER UGIFT.                                */        
 /* ***************************************************************** */        
                                                                                
                  SELECT (B01.SIVILSTAND(SØKER_IND));                           
                     WHEN ('U')                                                 
                        DO;                                                     
 L120:                                                                          
                           FEIL_VED_LABEL = '120';                              
                           FEIL_MELD_NR   = 1001;                               
                           GO TO L999;                                          
                        END;                                                    
                                                                                
 /* ***************************************************************** */        
 /* DERSOM SØKER ER SKILT.                                            */        
 /* ***************************************************************** */        
                                                                                
                     WHEN ('S')                                                 
                        DO;                                                     
 L130:                                                                          
                           FEIL_VED_LABEL = '130';                              
                           FEIL_MELD_NR   = 1002;                               
                           GO TO L999;                                          
                        END;                                                    
                                                                                
 /* ***************************************************************** */        
 /* DERSOM SØKER ER ETTERLATT.                                        */        
 /* ***************************************************************** */        
                                                                                
                     WHEN ('E')                                                 
                        DO;                                                     
 L140:                                                                          
                           FEIL_VED_LABEL = '140';                              
                           FEIL_MELD_NR   = 1003;                               
                           GO TO L999;                                          
                        END;                                                    
                                                                                
 /* ***************************************************************** */        
 /* ELLERS ER SØKEREN GIFT.                                           */        
 /* ***************************************************************** */        
                                                                                
                     OTHERWISE                                                  
                        DO;                                                     
                                                                                
 /* ***************************************************************** */        
 /* DERSOM DET ER SAMME EKTEFELLE SOM I SØKERENS PESTATUS.            */        
 /* ***************************************************************** */        
                                                                                
                           IF B01.FNR(EKTEF_IND) = FTS.FNR_EK THEN              
                              DO;                                               
                                                                                
 /* 9926 */                      CALL FLYTT_B01_TIL_B02(SØKER_IND);             
                                                                                
                                 IF FEIL_MELD_NR > 0 THEN                       
                                    GO TO L999;                                 
                                 ELSE                                           
                                    PROGRAM_ID = 'R0011220';                    
                                                                                
 /* 9926 */                      CALL FLYTT_B01_TIL_B02(EKTEF_IND);             
                                                                                
                                 IF FEIL_MELD_NR > 0 THEN                       
                                    GO TO L999;                                 
                                 ELSE                                           
                                    PROGRAM_ID = 'R0011220';                    
                                                                                
 /* ***************************************************************** */        
 /* DERSOM DET SØKES OM TILLEGG FOR EKTEFELLE.                        */        
 /* ***************************************************************** */        
                                                                                
                                 IF FTS.TILL_EK = 'J' THEN                      
                                                                                
 /* ***************************************************************** */        
 /* DERSOM EKTEFELLE HAR PENSJON.                                     */        
 /* ***************************************************************** */        
                                                                                
                                    IF B01.                                     
                                       PENSJONSTYPE1(EKTEF_IND) = 'A' !         
                                       B01.PENSJONSTYPE1(EKTEF_IND) =           
                                                              'U'     !         
 /*AFP*/                               B01.PENSJONSTYPE1(EKTEF_IND) =           
 /*AFP*/                                                      'K'     !         
                                       B01.PENSJONSTYPE1(EKTEF_IND) =           
                                                              'Y' THEN          
                                       DO;                                      
 L150:                                                                          
                                          FEIL_VED_LABEL = '150';               
                                          FEIL_MELD_NR   = 1011;                
                                          GO TO L999;                           
                                       END;                                     
                                                                                
                                 CALL                                           
 /* 1222 */                          AJOURFØR_B02_MED_FT_TRANS;                 
                                                                                
                              END;                                              
                                                                                
 /* ***************************************************************** */        
 /* IKKE SAMME EKTEFELLE.                                             */        
 /* ***************************************************************** */        
                                                                                
                           ELSE                                                 
                              DO;                                               
 L160:                                                                          
                                 FEIL_VED_LABEL = '160';                        
                                 FEIL_MELD_NR   = 1004;                         
                                 GO TO L999;                                    
                              END;                                              
                        END;                                                    
                  END;                                                          
               END;                                                             
                                                                                
 /* ***************************************************************** */        
 /* INGEN EKTEFELLE I TRANS.                                          */        
 /* ***************************************************************** */        
                                                                                
            ELSE                                                                
               DO;                                                              
                                                                                
 /* ***************************************************************** */        
 /* DERSOM SØKEREN ER REGISTRERT SOM GIFT.                            */        
 /* ***************************************************************** */        
                                                                                
                                                                                
 /* ENDRET 16.7.87 BF:  HVIS EKTEFELLE IKKE ER MED I TRANSEN, MEN ER  */        
 /*                     REG I STATUS, LEGGES EKTEFELLES FØDSELSNUMMER */        
 /*                     INN I TRANSEN.  DETTE GJØRES FOR Å UNNGÅ AT   */        
 /*                     EKTEFELLES STATUS SKAL BLI FJERNET I R0015401.*/        
 /*                     FEILRAPP. 517.                                */        
                                                                                
                  IF B01.SIVILSTAND(SØKER_IND) = 'G' !                          
                     B01.SIVILSTAND(SØKER_IND) = 'A' THEN                       
                                                                                
 /* FJERNET 18.7.85 HL  ! B01.SIVILSTAND(SØKER_IND) = 'A' THEN        */        
                                                                                
                     DO;                                                        
                        IF B01.FNR(EKTEF_IND)  >  0  &                          
                           FTS.TILL_EK        ^= 'J' THEN                       
                           DO;                                                  
                              FTS.FNR_EK = B01.FNR(EKTEF_IND);                  
 /* 9926 */                   CALL FLYTT_B01_TIL_B02(SØKER_IND);                
                                                                                
                              IF FEIL_MELD_NR > 0 THEN                          
                                 GO TO L999;                                    
                              ELSE                                              
                                 PROGRAM_ID = 'R0011220';                       
                                                                                
 /* 9926 */                   CALL FLYTT_B01_TIL_B02(EKTEF_IND);                
                                                                                
                              IF FEIL_MELD_NR > 0 THEN                          
                                 GO TO L999;                                    
                              ELSE                                              
                                 PROGRAM_ID = 'R0011220';                       
                           END;                                                 
                        ELSE                                                    
                           DO;                                                  
 L170:                                                                          
                              FEIL_VED_LABEL = '170';                           
                              FEIL_MELD_NR = 1102;                              
                              GO TO L999;                                       
                           END;                                                 
                     END;                                                       
                                                                                
                                                                                
 /* HIT    16.7.87 BF                                                 */        
                                                                                
                  ELSE                                                          
                     DO;                                                        
                                                                                
 /* 9926 */             CALL FLYTT_B01_TIL_B02(SØKER_IND);                      
                                                                                
                        IF FEIL_MELD_NR > 0 THEN                                
                           GO TO L999;                                          
                        ELSE                                                    
                           PROGRAM_ID = 'R0011220';                             
                                                                                
                        IF B01.PENSJONSTYPE2 (SØKER_IND) =  'E'    THEN         
                          DO;                                                   
                           B02.PERSON(EKTEF_IND)= B01.PERSON(EKTEF_IND);        
                                                                                
 /* RULLERING AV FORVENTET            - 9802 HL */                              
        IF B02.ETTEPENS.FORVENTET(SØKER_IND) > 0   THEN                         
           CALL RULL_FORVENTET(B02.G_DATO_ÅMD(SØKER_IND),  /*2000*/             
                    B02.VIRK_DATO_ÅMD(SØKER_IND),          /*2000*/             
                    B02.ETTEPENS.FORVENTET(SØKER_IND));                         
                                                                                
                          END;                                                  
                                                                                
 /* 1222 */             CALL AJOURFØR_B02_MED_FT_TRANS;                         
                                                                                
                     END;                                                       
                END;                                                            
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
 L175:                                                                          
            FEIL_VED_LABEL = '175';                                             
            FEIL_MELD_NR   = 1010;                                              
            GO TO L999;                                                         
         END;                                                                   
                                                                                
 /* ***************************************************************** */        
 /* BARNEDELEN                                                        */        
 /* ***************************************************************** */        
                                                                                
 /*AFP*/IF FTS.BT_ANT > 0 &                                                     
 /*AFP*/   B01.PENSJONSTYPE1(SØKER_IND) = 'K'  THEN                             
                                                                                
 /* ***************************************************************** */        
 /* SØKER HAR AVTALEFESTET PENSJON - BARNETILLEGG KAN IKKE TILSTÅS    */        
 /* ***************************************************************** */        
                                                                                
 /*AFP*/   DO;                                                                  
 /*AFP*/L177:                                                                   
 /*AFP*/      FEIL_VED_LABEL = '177';                                           
 /*AFP*/      FEIL_MELD_NR   = 1010;                                            
 /*AFP*/      GO TO L999;                                                       
 /*AFP*/   END;                                                                 
                                                                                
      DO I = 1 TO FTS.BT_ANT;                                                   
         BARN_IND = 0;                                                          
                                                                                
 /* ENDRET TESTEN NEDENFOR FRA B01 TIL B02 28.8.84    HL             */         
                                                                                
         DO J = 3 TO 14 WHILE (B02.FNR(J) > 0);                                 
                                                                                
 /* HIT 28.8.84                                                      */         
                                                                                
            IF FTB.FNR_BARN(I) = B01.FNR(J) THEN                                
               DO;                                                              
                  BARN_IND = J;                                                 
                  J        = 14;                                                
               END;                                                             
         END;                                                                   
                                                                                
 /* **************************************************************** */         
 /*  DERSOM BARNET ER I B01(TILKNYTTET SØKERENS FAMILIE).            */         
 /* **************************************************************** */         
                                                                                
         IF BARN_IND > 0 THEN                                                   
            DO;                                                                 
                                                                                
 /* **************************************************************** */         
 /* DERSOM BARNET ER TILKNYTTET EKTEFELLE.                           */         
 /* **************************************************************** */         
                                                                                
               IF B01.FNR_TILKN(BARN_IND,1) = B01.                              
                                                  FNR(EKTEF_IND) THEN           
                  DO;                                                           
                     IF B01.PENSJONSTYPE1(BARN_IND) = 'L' THEN                  
                        DO;                                                     
 L180:                                                                          
                           FEIL_VED_LABEL = '180';                              
                           FEIL_MELD_NR   = 1201;                               
                           GO TO L999;                                          
                        END;                                                    
                     ELSE                                                       
                                                                                
 /* **************************************************************** */         
 /* TILKNYTNINGSKODE = NOE ANNET.                                    */         
 /* **************************************************************** */         
                                                                                
                        DO;                                                     
 L190:                                                                          
                           FEIL_VED_LABEL = '190';                              
                           FEIL_MELD_NR   = 1208;                               
                           GO TO L999;                                          
                        END;                                                    
                  END;                                                          
               ELSE                                                             
                                                                                
 /* **************************************************************** */         
 /* BARNET ER TILKNYTTET SØKER.                                      */         
 /* **************************************************************** */         
                                                                                
                  DO;                                                           
                     IF B01.PENSJONSTYPE1(BARN_IND) ^= 'L'                      
                     THEN                                                       
                        DO;                                                     
 L200:                                                                          
                           FEIL_VED_LABEL = '200';                              
                           FEIL_MELD_NR   = 1207;                               
                           GO TO L999;                                          
                        END;                                                    
                                                                                
 /* ENDRET 280187 BF FRAPP 384  */                                              
                                                                                
                     IF FTB.RBT_BARN (I) = 'J' THEN                             
                        B02.PENSJONSTYPE2(BARN_IND) = 'R';                      
                     ELSE                                                       
                        B02.PENSJONSTYPE2(BARN_IND) = ' ';                      
                                                                                
 /* HIT    280187 BF            */                                              
                                                                                
                  END;                                                          
            END;                                                                
         ELSE                                                                   
                                                                                
 /* *************************************************************** */          
 /* BARNET ER IKKE I B01.                                           */          
 /* *************************************************************** */          
                                                                                
            DO;                                                                 
               BARN_IND        = J;                                             
               SEARCH_FNR      = FTB.FNR_BARN(I);                               
               POS_I_B01       = BARN_IND;                                      
                                                                                
               EXEC CICS LINK PROGRAM ('R0019928') COMMAREA                     
                                                     (KOM_OMR);                 
                                      /* OPPRETT_STATUS_FOR_PERSON */           
                                                                                
               IF FEIL_MELD_NR > 0 THEN                                         
                  GO TO L999;                                                   
               ELSE                                                             
                  PROGRAM_ID = 'R0011220';                                      
                                                                                
 /* ************************************************************** */           
 /* DERSOM BARNET ER I BASEN.                                      */           
 /* ************************************************************** */           
                                                                                
               IF B01.FNR(BARN_IND) > 0 THEN                                    
                  DO;                                                           
                                                                                
 /* ************************************************************** */           
 /* BARNET HAR IKKE PESTATUS.                                      */           
 /* ************************************************************** */           
                                                                                
                     IF STATUS_TYPE = 'A' !                                     
                        STATUS_TYPE = 'C' !                                     
                        STATUS_TYPE = 'I' !     /* TILLEGGSTEST I,K */          
                        STATUS_TYPE = 'K' THEN  /* 14.2.85  HL      */          
                        DO;                                                     
                                                                                
  /* 9926 */               CALL FLYTT_B01_TIL_B02(BARN_IND);                    
  /* KS 6/6-84   */                                                             
                           IF FTB.RBT_BARN (I) = 'J' THEN                       
                              B02.PENSJONSTYPE2(BARN_IND) = 'R';                
                           ELSE                                                 
                              B02.PENSJONSTYPE2(BARN_IND) = ' ';                
                                                                                
                           CALL                                                 
  /* 9923 */                   OPPRETT_STATUS_FORS_BARN(BARN_IND,               
                                                       SØKER_IND);              
                                                                                
                           CALL                                                 
  /* 9924 */                   KOBLE_TO_PERSONER(SØKER_IND,BARN_IND);           
                        END;                                                    
                     ELSE                                                       
                                                                                
 /* ************************************************************** */           
 /* BARNET HAR PESTATUS.                                           */           
 /* ************************************************************** */           
                                                                                
                        DO;                                                     
                           IF STATUS_TYPE ^= 'F'                                
                                                           THEN                 
                              DO;                                               
                                                                                
 /* ************************************************************** */           
 /* BARNET ER FORSØRGET AV ANDRE.                                  */           
 /* ************************************************************** */           
                                                                                
                                 IF B01.                                        
                                    PENSJONSTYPE1(BARN_IND) = 'L' THEN          
                                    DO;                                         
  L210:                                                                         
                                       FEIL_VED_LABEL = '210';                  
                                       FEIL_MELD_NR   = 1203;                   
                                       GO TO L999;                              
                                    END;                                        
                                 ELSE                                           
                                                                                
 /* ************************************************************** */           
 /* BARNET ER TILKNYTTET ANDRE PÅ EN ANNEN MÅTE.                   */           
 /* ************************************************************** */           
                                                                                
                                    IF B01.                                     
                                       FNR_TILKN(BARN_IND,1) > 0 THEN           
                                       DO;                                      
  L220:                                                                         
                                          FEIL_VED_LABEL = '220';               
                                          FEIL_MELD_NR   = 1206;                
                                          GO TO L999;                           
                                       END;                                     
                                    ELSE                                        
                                                                                
 /* ************************************************************** */           
 /* BARNET HAR PENSJON.                                            */           
 /* ************************************************************** */           
                                                                                
                                       DO;                                      
  L240:                                                                         
                                          FEIL_VED_LABEL = '240';               
                                          FEIL_MELD_NR   = 1204;                
                                          GO TO L999;                           
                                       END;                                     
                              END;                                              
                           ELSE                                                 
                              DO;                                               
  L250:                                                                         
                                 FEIL_VED_LABEL = '250';                        
                                 FEIL_MELD_NR   = 0601;                         
                                 GO TO L999;                                    
                              END;                                              
                        END;                                                    
                  END;                                                          
               ELSE                                                             
                                                                                
 /* *********************************************************** */              
 /* BARNET ER IKKE I BASEN.                                     */              
 /* *********************************************************** */              
                                                                                
                  DO;                                                           
                     B02.FNR   (BARN_IND) = FTB.FNR_BARN(I);                    
                     B02.NAVN  (BARN_IND) = (25)' ';                            
                     B02.TKNR  (BARN_IND) = FTS.TKNR;                           
  /* KS 6/6-84   */                                                             
                     IF FTB.RBT_BARN (I) = 'J' THEN                             
                        B02.PENSJONSTYPE2(BARN_IND) = 'R';                      
                     ELSE                                                       
                        B02.PENSJONSTYPE2(BARN_IND) = ' ';                      
                                                                                
                     CALL                                                       
                          OPPRETT_STATUS_FORS_BARN(BARN_IND,                    
                                                  SØKER_IND);                   
                          /* 9923 */                                            
                                                                                
  /* 9924 */         CALL KOBLE_TO_PERSONER(SØKER_IND,BARN_IND);                
                                                                                
                  END;                                                          
            END;                                                                
      END;                                                                      
                                                                                
 /* *********************************************************** */              
 /* BARN TILKNYTTET SØKER MED L (FORSØRGEDE BARN)               */              
 /* *********************************************************** */              
                                                                                
      B02.ANTALL_BARN(SØKER_IND) = 0;                                           
      DO I = 1 TO 13 WHILE                                                      
                       (B02.TILKN.FNR_TILKN(SØKER_IND,I) > 0);                  
         IF B02.TILKN.TILKNYTNINGSKODE(SØKER_IND,I) = 'L'   !                   
            B02.TILKN.TILKNYTNINGSKODE(SØKER_IND,I) = 'V'   !  /*HL*/           
            B02.TILKN.TILKNYTNINGSKODE(SØKER_IND,I) = 'W'   THEN                
            B02.ANTALL_BARN(SØKER_IND) =                                        
                                   B02.ANTALL_BARN(SØKER_IND) + 1;              
      END;                                                                      
  L999:                                                                         
      EXEC CICS RETURN;                                                         
 %PAGE;                                                                         
      %INCLUDE R0011222;  /* AJOURFØR_B02_MED_FT_TRANS          */              
 %PAGE;                                                                         
      %INCLUDE R0019905;  /* F_ALDER                            */              
 %PAGE;                                                                         
      %INCLUDE R0019923;  /* OPPRETT_STATUS_FORS_BARN           */              
 %PAGE;                                                                         
      %INCLUDE R0019924;  /* KOBLE_TO_PERSONER                  */              
 %PAGE;                                                                         
      %INCLUDE R0019926;  /* FLYTT_B01_TIL_B02                  */              
      %INCLUDE R0019954;   /*RULL_FORVENTET*/                                   
      %INCLUDE R0019995;   /*KONV_FNR11_FNR13*/                                 
   END KON_FT;                                                                  
