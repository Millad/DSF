 /*   SIST ENDRET PÅ PROD   2004.12.17 14.33.10 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.07.13 12.58.46 AV   JDA2970          */        
 /* ================================================================= */        
 /* R001U624 FKS_REAKTIVISERING(TIDSBERGR_ATTF)                       */        
 /* ----------------------------------------------------------------- */        
 /* INKLUDERES I : R001U622                                           */        
 /* T_KODE = KODE FOR REAKTIVISERING PÅ U3_BLANKETTEN (S,X,BLANK)     */        
 /* V_KODE = KODE FOR UNNTAK_VENTETID PÅ U3_BLANKETTEN (S,X,BLANK)    */        
 /* ================================================================= */        
 FKS_REAKTIVISERING : PROC (T_KODE,V_KODE) RETURNS (FIXED DEC (5));             
    /* -------------------------------------------------------------- */        
    DCL T_KODE           CHAR(1),                                               
        V_KODE           CHAR(1);                                               
    DCL FEILMELDING      FIXED DEC (5) INIT (0);                                
    /* -------------------------------------------------------------- */        
    SELECT (T_KODE);                                                            
       WHEN ('S') /* START TIDSBEGR ATTFØRING */                                
          DO;                                                                   
             SELECT (B01.REAKTIVISERING(BER_SØ_IND));                           
                WHEN (' ')                                                      
                   DO;                                                          
                      IF V_KODE = 'S' THEN                                      
                         /* KAN IKKJE STARTE BEGGE SAMSTUNDES */                
                         FEILMELDING = 442;                                     
                      IF B01.UNNTAK_VENTETID(BER_SØ_IND) = 'J' &                
                         V_KODE                      ^= 'X' THEN                
                         /* KAN IKKE STARTE TIDSBEGR UTEN STANS VARIG */        
                         FEILMELDING = 439;                                     
                   END;                                                         
                WHEN ('J')                                                      
                   FEILMELDING = 438;                                           
                   /* MANGLER FORRIGE OPPHØRSMELDING */                         
                OTHER;                                                          
             END; /* SELECT TIDSBEGR */                                         
          END; /* START REAKTIVISERING */                                       
       WHEN ('X') /* SLUTT TIDSBEGR ATTFØRING */                                
          IF B01.UFØRPENS.REAKTIVISERING(BER_SØ_IND) ^= 'J' THEN                
             DO;                                                                
                /* ----------------------------------------------- */           
                /* VI AVVISER ALLE SOM SLUTTER FØR DE HAR STARTET  */           
                /* ----------------------------------------------- */           
                FEILMELDING = 430;                                              
             END;                                                               
       OTHER;     /* STANSES I INPUTKONTROLL 9803 */                            
    END; /* SELECT T_KODE */                                                    
    /* ----------------------------------------------------------- */           
    /* OPPDATERER B02. DERSOM ALT ER OK                            */           
    /* ----------------------------------------------------------- */           
    IF FEILMELDING = 0 THEN                                                     
       IF T_KODE = 'S' THEN                                                     
          B02.UFØRPENS.REAKTIVISERING(BER_SØ_IND) = 'J';                        
       ELSE                                                                     
          IF T_KODE = 'X' THEN                                                  
             B02.UFØRPENS.REAKTIVISERING(BER_SØ_IND) = ' ';                     
    /* ----------------------------------------------------------- */           
    /* RETURNERER FEILKODE. 0 VISER AT ALT ER OK SÅ LANGT          */           
    /* ----------------------------------------------------------- */           
    RETURN (FEILMELDING);                                                       
                                                                                
 END FKS_REAKTIVISERING;                                                        
