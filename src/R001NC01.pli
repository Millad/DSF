 /*   SIST ENDRET PÅ PROD   2003.02.27 12.36.28 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2002.11.21 13.28.44 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2002.01.15 11.51.34 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.10.22 13.02.21 AV   JDA2970          */        
 /*       SIST ENDRET 09/06-98 08.40.17 AV   SPA7339                  */        
 /*       SIST ENDRET 25/05-98 10.43.41 AV   HLA7339                  */        
 /*IDENTIFIKASJON:                                                    */        
 /* **********************                                            */        
 /*  PROGRAM-IDENT : R001NC01 KONTROLL_GRUNNBLANKETT_F7  HOVEDPROG    */        
 /*  PROGRAM-SPRÅK : PLI - CICS                                       */        
 /*  PROGRAMMERER  : SATISH PATHAK                                    */        
 /*  PROGRAMMET BLE LAGET : MARS-APRIL 1991                           */        
 /* ***************************************************************** */        
 /*HENSIKT:                                                           */        
 /* **********                                                        */        
 /*  PROGRAMMET KONTROLLERER GRUNNBLANKETT FORSØRGINGSTILLEGG.        */        
 /*  BLANKETTEN KONTROLLERES ISOLERT - IKKE MOT STATUS.               */        
 /*  OPERATØREN KAN BRUKE PF-TASTENE :                                */        
 /*    PF1  FØRER TIL OPPBEVARING AV TRANSEN I VENTEREGISTERET.       */        
 /*    PF2  FØRER TIL AT TRANSAKSJONEN FORKASTES -                    */        
 /*    I BEGGE TILFELLER SKRIVES DET UT MELDING PÅ SKJERM, OG         */        
 /*    PROGRAM R0010401 SØRGER FOR Å LEGGE UT NYTT BILDE FOR          */        
 /*    REGISTRERING AV A1 - TRANSKODE RN41.                           */        
 /*  NÅR INGEN FEIL MER FINNES SETTES TRANSKODE = R042, OG DET        */        
 /*  RETURNERES TIL R0010401.                                         */        
 /*                                                                   */        
 /* ***************************************************************** */        
 /*PROGRAMTILKNYTTING:                                                */        
 /* ********************                                              */        
 /*  PROGRAMMET KALLES FRA R0010401                                   */        
 /*  PROGRAMMET HAR EXEC CICS XCTL TIL :                              */        
 /*             R0010301 - VALG_AV_FUNKSJON                           */        
 /*             R0010401 - REGISTRERING                               */        
 /*  PROGRAMMET HAR EXEC CICS LINK TIL :                              */        
 /*             R0010490 - SKRIV_UT_PÅ_VENTEREG                       */        
 /* ***************************************************************** */        
 R001NC:                                                                        
   PROC(COMMAREA_PEKER) OPTIONS(MAIN);                                          
 %PAGE;                                                                         
 %INCLUDE S001NC;    /*  F7-MAPSETTET   */                                      
 %PAGE;                                                                         
 %INCLUDE P001NC01;  /*  TRANSOMR FOR FORSØRGINGSTILL.EKTEF/BARN */             
 %PAGE;                                                                         
 %INCLUDE P0019906;  /*  TRANS-OPPL-OMRÅDE (BASED)      */                      
 %PAGE;                                                                         
 %INCLUDE P0019908;  /*  KOM-OMRÅDE (BASED)             */                      
 %PAGE;                                                                         
 %INCLUDE P0019910;  /*  STYRINGS-OMRÅDE (BASED)        */                      
 %PAGE;                                                                         
 %INCLUDE P0019912;  /*  DIV-PARAM-OMRÅDE (BASED)       */                      
 %PAGE;                                                                         
 %INCLUDE DFHBMSCA;                                                             
 %PAGE;                                                                         
   DCL                                                                          
      (BMSMAPBR,COMMAREA_PEKER) PTR;                                            
   DCL                                                                          
      (ADDR,UNSPEC,CSTG,ONCODE,VERIFY,SUBSTR) BUILTIN;                          
   DCL                                                                          
      FEIL_FUNNET  BIT (1),                                                     
      ONKODE PIC'9999',                                                         
      CURSOR_POS FIXED BIN(15) INIT(-1),                                        
      ONK DEF ONKODE CHAR(4),                                                   
      FEILKODE       CHAR(4),                                                   
      DSNAVN         CHAR(8),                                                   
      ANT_FEIL_SKREVET FIXED DEC (3);                                           
 %SKIP(1);                                                                      
 %PAGE;                                                                         
      ON ERROR SNAP BEGIN       ;                                               
         ON ERROR SYSTEM        ;                                               
         ONKODE=ONCODE          ;                                               
         FEILKODE = ONK         ;                                               
         DSNAVN   = EIBDS       ;                                               
         GO TO FEILBEH          ;                                               
      END;                                                                      
                                                                                
 FEILKODE   = 'FEIL'    ;                                                       
 DSNAVN     = '        ';                                                       
                                                                                
 EXEC CICS HANDLE CONDITION ERROR(FEILBEH);                                     
                                                                                
 EXEC CICS HANDLE AID PF1(PF1) PF2(PF2);                                        
                                                                                
 KOM_OMR.PEKER_LISTE.DIV_PARAM_PEKER  = ADDR(KOM_OMR.DIV_PARAM_OMR);            
                                                                                
 IF FRA_CICS THEN                                                               
  DO;                                                                           
    KOM_OMR.PEKER_LISTE.TRANS_OPPL_PEKER = ADDR(KOM_OMR.TRANS_OPPL_OMR);        
    KOM_OMR.PEKER_LISTE.TRANS_PEKER      = ADDR(KOM_OMR.TRANS_OMR);             
    KOM_OMR.PEKER_LISTE.STYRINGS_PEKER   = ADDR(KOM_OMR.STYRINGS_OMR);          
    ATK_KOM_PTR  =  ADDR(KOM_OMR.ATK_COM_OMR);                                  
  END;                                                                          
                                                                                
 IF HENT_FRAM_MAP  THEN                                                         
   DO;                                                                          
     EXEC CICS SEND MAP('S001NC1') MAPSET('S001NC3') MAPONLY                    
                                                   SET (BMSMAPBR);              
     ALLOCATE S001NC1O;                                                         
     S001NC1O.CICS_INFOO = DIV_PARAM_OMR.CICS_NAVN;                             
   END;                                                                         
                                                                                
 RECEIVE_MAP:                                                                   
                                                                                
                                                                                
 IF ^HENT_FRAM_MAP THEN                                                         
    DO;                                                                         
       EXEC CICS RECEIVE MAP('S001NC1')                                         
                                    MAPSET('S001NC3') SET(BMSMAPBR);            
       CALL BLANK_F7_MELDNR;                                                    
       FEIL_MELD_NR = 0;                                                        
    END;                                                                        
 IF FUNKSJONSKODEL > 0 THEN                                                     
    DO;                                                                         
       FUNKSJONSKODE = FUNKSJONSKODEI;                                          
       EXEC CICS XCTL PROGRAM('R0010301') COMMAREA(KOM_OMR);                    
    END;                                                                        
                                                                                
 IF STYRE_KODEL > 0 THEN                                                        
    DO;                                                                         
       STYREKODE = STYRE_KODEI;                                                 
       TRANSKODE = 'R041';                                                      
       EXEC CICS XCTL PROGRAM('R0010401') COMMAREA(KOM_OMR);                    
    END;                                                                        
                                                                                
                                                                                
 IF STYREKODE ^= 'F7'  &  FRA_CICS THEN                                         
    TRANSKODE = 'R041';                                                         
                                                                                
 ELSE                                                                           
  DO;                                                                           
   TRANS_RETURKODE = TRANSKODE;                                                 
   ANT_FEIL_SKREVET = 0;                                                        
   FEIL_FUNNET = '0'B;                                                          
                                                                                
   SELECT (TRANSKODE);                                                          
    WHEN('RNC0')                                                                
       DO;                                                                      
          CALL BLANK_F7_SØKER;                           /*NC02*/               
          CALL BLANK_F7_SØKER2;                          /*NC02*/               
          CALL BLANK_F7_MELDNR;                          /*NC02*/               
          CALL BLANK_F7_BARN;                           /*NC02*/                
          CALL OVERFØR_F7_SØKER;                                                
          CALL KONTROLL_F7_SØKER(FEIL_FUNNET);          /*NC03*/                
       END;                                                                     
    WHEN('RNC1')                                                                
       DO;                                                                      
         CALL BLANK_F7_MELDNR;                           /*NC02*/               
         IF FUNKSJONSKODE ^= 'V' THEN                                           
            CALL BLANK_F7_BARN;                          /*NC02*/               
         CALL OVERFØR_F7_SØKER;                         /*NC07*/                
         CALL KONTROLL_F7_SØKER(FEIL_FUNNET);                                   
       END;                                                                     
    WHEN('RNC2')                                                                
       DO;                                                                      
          CALL BLANK_F7_MELDNR;                                                 
          CALL OVERFØR_F7_SØKER;                                                
          CALL OVERFØR_F7_SØKER2;                     /*NC09*/                  
          CALL BLANK_F7_BARN_REST;                                              
          CALL OVERFØR_F7_BARN;                       /*NC10*/                  
          CALL KONTROLL_F7_SØKER(FEIL_FUNNET);                                  
          IF F_FELT_PIC_CHAR2(F7S.BT_ANT) = '00' THEN                           
             CALL BLANK_F7_BARN;                                                
          ELSE                                                                  
             CALL KONTROLL_F7_BARN(FEIL_FUNNET);      /*NC04*/                  
                                                                                
                                                                                
          IF F7S.TILL_EK = 'J'                      THEN                        
          CALL KONTROLL_ARBEIDSINNTEKT (F7S.VIRK_DATO_ÅMD); /*2000*/            
                                                                                
       END;                                                                     
    WHEN('RNC3')                                                                
       DO;                                                                      
          CALL BLANK_F7_MELDNR;                                                 
          CALL OVERFØR_F7_SØKER;                                                
          CALL OVERFØR_F7_SØKER2;                                               
          CALL KONTROLL_F7_SØKER(FEIL_FUNNET);                                  
                                                                                
          IF F7S.TILL_EK = 'J'                      THEN                        
          CALL KONTROLL_ARBEIDSINNTEKT (F7S.VIRK_DATO_ÅMD); /*2000*/            
       END;                                                                     
    OTHERWISE;                                                                  
       /* INGEN UTGANG DA CICS IKKE GODTAR UGYLDIGE TRANSAKSJONSKODER */        
   END;  /*   SELECT   */                                                       
                                                                                
    IF F_FELT_PIC_CHAR2(F7S.BT_ANT) ^= '00' & ^FEIL_FUNNET THEN                 
       TRANSKODE = 'RNC2';                                                      
    IF F_FELT_PIC_CHAR2(F7S.BT_ANT) = '00' & ^FEIL_FUNNET THEN                  
       TRANSKODE = 'RNC3';                                                      
    IF TRANSKODE = 'RNC2' & F_FELT_PIC_CHAR2(F7S.BT_ANT) = '00' THEN            
       TRANSKODE = 'RNC1';                                                      
                                                                                
                                                                                
 IF FEIL_FUNNET ! HENT_FRAM_MAP ! FRA_UTEN_DIALOG !                             
    TRANS_RETURKODE ^= TRANSKODE THEN                                           
    DO;                                                                         
       IF TRANSKODE = 'RNC0' THEN                                               
          TRANSKODE = 'RNC1';                                                   
       CALL BLANK_F7_MAP;                                                       
       CALL OVERFØR_F7_SØKER_MAP;                                               
       SELECT (TRANSKODE);                                                      
          WHEN ('RNC1')                                                         
             DO;                                                                
                CALL FJERN_TEKST_BARN;                                          
                CALL FJERN_TEKST_SØKER2;                                        
                CALL BLOKKERE_BARN;                                             
             END;                                                               
          WHEN ('RNC2')                                                         
             DO;                                                                
                CALL TEKST_BARN;                                                
                CALL BLANK_F7_BARN_REST;                                        
                CALL OVERFØR_F7_BARN_MAP;                                       
                CALL TEKST_SØKER2;                                              
                CALL OVERFØR_F7_SØKER2_MAP;                                     
             END;                                                               
          WHEN ('RNC3')                                                         
             DO;                                                                
                CALL FJERN_TEKST_BARN;                                          
                CALL BLOKKERE_BARN;                                             
                CALL TEKST_SØKER2;                                              
                CALL OVERFØR_F7_SØKER2_MAP;                                     
             END;                                                               
          OTHERWISE;                                                            
       END;                                                                     
                                                                                
      IF HENT_FRAM_MAP THEN                                                     
        EXEC CICS SEND MAP('S001NC1') MAPSET('S001NC3') ERASE CURSOR;           
      ELSE                                                                      
        EXEC CICS SEND MAP('S001NC1') MAPSET('S001NC3') ERASEAUP                
                                                     DATAONLY CURSOR;           
      HENT_FRAM_MAP = '0'B;                                                     
      FEIL_MELD_NR  = 666;                                                      
                                                                                
      IF  FRA_CICS THEN                                                         
        EXEC CICS RETURN TRANSID(TRANSKODE) COMMAREA(KOM_OMR);                  
      ELSE                                                                      
        GOTO RECEIVE_MAP;                                                       
    END;                                                                        
                                                                                
   ELSE                                                                         
                                                                                
    DO;                                                                         
                                                                                
      IF TRANSKODE = 'RNC0' THEN                                                
        TRANS_RETURKODE = 'RNC1';                                               
      ELSE                                                                      
        TRANS_RETURKODE = TRANSKODE;                                            
      IF  FRA_CICS  THEN                                                        
        DO;                                                                     
          BLANKETTYPE = STYREKODE;                                              
          TRANSKODE = 'R042';                                                   
        END;                                                                    
    END;                                                                        
  END;                                                                          
                                                                                
  CALL UTGANG;                                                                  
                                                                                
  UTGANG: PROC;                                                                 
    IF  FRA_CICS THEN                                                           
      EXEC CICS XCTL PROGRAM('R0010401') COMMAREA(KOM_OMR);                     
    ELSE                                                                        
      EXEC CICS RETURN;                                                         
  END UTGANG;                                                                   
                                                                                
  FEILBEH:                                                                      
                                                                                
     EXEC CICS HANDLE CONDITION ERROR(ABEND);                                   
     IF FEIL_MELD_NR = 0 THEN                                                   
        PROGRAM_ID = 'R001NC01';                                                
     S001NC1O.CICS_INFOO = DIV_PARAM_OMR.CICS_NAVN;                             
                                                                                
     S001NC1O.MELDING2O =                                                       
              'F E I L  H A R  O P P S T Å T T ! ! !.';                         
                                                                                
     S001NC1O.MELDING3O =                                                       
              'EIBRCODE : ' !! UNSPEC(EIBRCODE);                                
                                                                                
     S001NC1O.MELDING4O =                                                       
          'EIBFN    : ' !! UNSPEC(EIBFN) !! '. FEILKODE : ' !! FEILKODE         
                                         !! '. DATASETT : ' !! DSNAVN;          
                                                                                
     S001NC1O.MELDING5O =                                                       
                   'PROGRAMNAVN :  ' !! PROGRAM_ID    !!                        
                   '.INGEN OPPDATERING ER BLITT UTFØRT.';                       
                                                                                
     S001NC1O.MELDING6O =                                                       
                   'TA UTSKRIF7!!!. TRYKK DERETTER "ENTER".';                   
                                                                                
                                                                                
     EXEC CICS SEND MAP('S001NC1')                                              
                                                 MAPSET('S001NC3');             
                                                                                
     EXEC CICS RECEIVE MAP('S001NC1')                                           
                                    MAPSET('S001NC3') SET(BMSMAPBR);            
                                                                                
                                                                                
     EXEC CICS SYNCPOINT ROLLBACK;                                              
                                                                                
                                                                                
     TERMINERINGS_IND = 'F';                                                    
                                                                                
     TRANSKODE  = 'R041';                                                       
                                                                                
     CALL UTGANG;                                                               
                                                                                
  ABEND:                                                                        
     EXEC CICS ABEND ABCODE(FEIL);                                              
                                                                                
  PF1:                                                                          
                                                                                
 /* LEGGER UNDERKJENT TRANS UT PÅ VENTEDATABASE HVIS GYLDIG     */              
 /* FNR OG VIRKNINGSDATO,ELLERS FORKASTES TRANSAKSJONEN.        */              
                                                                                
     IF ^(F7S.FNRNR = 1          !                                              
          F7S.FNRNR = 200        !                                              
          F7S.VIRK_DATO_ÅMDNR = 9 !               /*2000*/                      
          F7S.VIRK_DATO_ÅMDNR = 200) THEN         /*2000*/                      
        DO;                                                                     
                                                                                
           IF FØDSNUMMER    = 0   THEN                                          
                                                                                
 /* SKRIVER MELDING OM AT TRANSAKSJONEN ER FORKASTET.           */              
                                                                                
              DO;                                                               
                 FEIL_MELD_NR  = 0;                                             
                 S001NC1O.MELDING6O =                                           
                         'REGISTERINGEN ER IKKE AVSLUTTET MED "ENTER"'!!        
                         '.TRANSAKSJONEN ER FORKASTET.';                        
                                                                                
              END;                                                              
           ELSE                                                                 
              DO;                                                               
                                                                                
                 SEARCH_FNR  = TRANS_OPPL_OMR.FØDSNUMMER;                       
                 IF FRA_CICS THEN                                               
                    BLANKETTYPE = STYREKODE;                                    
                                                                                
                                                                                
                 IF FEIL_MELD_NR = 666 &      /*BLANKETTKONTROLLFEIL*/          
                    ^FRA_MED_DIALOG     THEN  /*IKKE BASEKONTROLLFEIL*/         
                    FUNKSJONSKODE = 'B';                                        
                                                                                
                 IF F_FELT_PIC_CHAR2(F7S.BT_ANT) ^= '00'  THEN                  
                    DO;                                                         
                       CALL BLANK_F7_MELDNR;                                    
                       CALL BLANK_F7_BARN_REST;                                 
                       CALL OVERFØR_F7_BARN_MAP;                                
                    END;                                                        
                                                                                
                 FEIL_MELD_NR  = 0;                                             
                                                                                
                 EXEC CICS LINK PROGRAM('R0010490') COMMAREA(KOM_OMR);          
                                                                                
                 IF FEIL_MELD_NR > 0 THEN                                       
                    DO;                                                         
                       DSNAVN   = 'R0010490';                                   
                       FEILKODE = DB_STATUS_KODE;                               
                       GO TO FEILBEH;                                           
                    END;                                                        
                                                                                
 /* SKRIVER MELDING OM AT TRANSAKSJONEN ER LAGT UT PÅ VENTEBASE.*/              
                                                                                
                                                                                
                 S001NC1O.MELDING6O =                                           
                         'TRANSAKSJONEN ER LAGT UT PÅ VENTEREGISTER.';          
              END;                                                              
                                                                                
                                                                                
           EXEC CICS SEND MAP('S001NC1') MAPSET('S001NC3')                      
                                                           DATAONLY;            
     /* DUMMY SEND-KALL.                              */                        
                                                                                
           EXEC CICS SEND MAP('S001NC1')                                        
                                                  MAPSET('S001NC3');            
           EXEC CICS DELAY INTERVAL(005);                                       
                                                                                
           TERMINERINGS_IND = 'F';                                              
                                                                                
           TRANSKODE  = 'R041';                                                 
                                                                                
           CALL UTGANG;                                                         
                                                                                
        END;                                                                    
     ELSE                                                                       
        DO;                                                                     
           TERMINERINGS_IND = 'F';                                              
                                                                                
           TRANSKODE = 'R041';                                                  
                                                                                
           S001NC1O.MELDING6O =                                                 
                                                                                
   'NB** TRANSAKSJONEN ER FORKASTET P.G.A. UGYLDIG FNR EL. VDATO **NB';         
                                                                                
           EXEC CICS SEND MAP('S001NC1') MAPSET('S001NC3')                      
                                                           DATAONLY;            
     /* DUMMY SEND-KALL.                            */                          
                                                                                
           EXEC CICS SEND MAP('S001NC1')                                        
                                    MAPSET('S001NC3');                          
                                                                                
           EXEC CICS DELAY INTERVAL(005);                                       
           CALL UTGANG;                                                         
        END;                                                                    
                                                                                
                                                                                
  PF2:                                                                          
     TERMINERINGS_IND   = 'F';                                                  
                                                                                
     S001NC1O.MELDING6O =                                                       
      'NBNBNBNBNB***    TRANSAKSJONEN ER FORKASTET.    ***NBNBNBNBNB';          
                                                                                
     EXEC CICS SEND MAP('S001NC1') MAPSET('S001NC3')                            
                                                      DATAONLY ;                
     /* DUMMY SEND-KALL.                                     */                 
                                                                                
     EXEC CICS SEND MAP('S001NC1')                                              
                                    MAPSET('S001NC3');                          
     EXEC CICS DELAY INTERVAL(005);                                             
                                                                                
                                                                                
     TRANSKODE = 'R041';                                                        
                                                                                
     CALL UTGANG;                                                               
 %PAGE;                                                                         
      %INCLUDE R001NC02;               /* BLANK  ARBEIDSOMRÅDE */               
 %PAGE;                                                                         
      %INCLUDE R001NC03;               /* KONTROLL AV SØKERDELEN */             
 %PAGE;                                                                         
      %INCLUDE R001NC04;               /* KONTROLL AV BARNEDATA */              
 %PAGE;                                                                         
      %INCLUDE R001NC05;               /* KONTROLL AV EK_INNTEKT*/              
 %PAGE;                                                                         
      %INCLUDE R001NC06;               /* ARBEIDSOMRÅDE -> MAP */               
 %PAGE;                                                                         
      %INCLUDE R001NC07;               /* FORTEKSTER OG ATTRIBUTTER*/           
                                       /* SØKER-DEL2       */                   
 %PAGE;                                                                         
      %INCLUDE R001NC09;               /* MAP -> ARBEIDSOMRÅDE */               
 %PAGE;                                                                         
      %INCLUDE R001NC10;               /* FLYTTING AV BARNEDATA */              
 %PAGE;                                                                         
      %INCLUDE R0019901;               /*  TESTER OM DATO ER GYLDIG */          
      %INCLUDE R0019902;               /*  FINNE KJØNN     */                   
      %INCLUDE R0019904;               /* FØDSELSNUMMERKONTROLL */              
      %INCLUDE R0019905;               /* F_ALDER               */              
      %INCLUDE R0019910;               /* NUMERISK KONTROLL */                  
      %INCLUDE R0019912;  /* KONVERTERING CHAR ==> PIC / PIC ==> CHAR */        
      %INCLUDE R0019944;               /* SKRIV_FEIL      */                    
      %INCLUDE R0019964;               /* DAGENS DATO + 6 MDR */                
      %INCLUDE R0019956;          /*     P9956_BER_G_CICS    */                 
      %INCLUDE R0019983;          /*     KONV_ÅMD_HÅMD       */                 
      %INCLUDE R0019984;          /*     KONV_MÅ_HÅMD       */                  
      %INCLUDE R0019988;          /*     KONV_HÅMD_ÅMD      */                  
      %INCLUDE R0019989;          /*     KONV_HÅMD_MÅ       */                  
      %INCLUDE R0019995;          /*     KONV_FNR11_FNR13(FNR)*/                
      %INCLUDE R0019945;               /* ATK_TILLGANG_KONTROLL   */            
    %INCLUDE R0019999;               /* KONTROLL_ACF2           */              
   END R001NC;                                                                  
