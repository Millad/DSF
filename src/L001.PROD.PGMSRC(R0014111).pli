 /*   SIST ENDRET PÅ PROD   2000.04.12 10.12.08 AV   JDA7339          */        
 /*       SIST ENDRET 08/01-99 14.39.05 AV   JDA7339                  */        
 /*       SIST ENDRET 08/01-99 14.25.10 AV   JDA7339                  */        
 /*       SIST ENDRET 20/07-98 10.03.16 AV   JDA7339                  */        
 /*       SIST ENDRET 22/06-98 09.06.22 AV   RFA9991                  */        
 /*       SIST ENDRET 24/07-92 10.10.44 AV   DYBVIK                   */        
 /*       SIST ENDRET 23/03-92 14.59.18 AV   DYBVIK                   */        
 /*       SIST ENDRET 08/01-92 15.45.05 AV   HERMAN                   */        
 /*       SIST ENDRET 13/12-91 12.20.48 AV   DYBVIK                   */        
 /* ***************************************************************** */        
 /*IDENTIFIKASJON:                                                    */        
 /*    R0014111 - BEREGN_GTLOV92_AE      - UNDERPROGRAM I PLI         */        
 /*    PROGRAMMERER: HERMAN        DES 1991                           */        
 /*HENSIKT:                                                           */        
 /*    REGNER B02.GT_LOV92                                            */        
 /*          (GAR92_EGEN, GAR92_EGEN_55 OG GAR92_AVD)                 */        
 /*PROGRAMTILKNYTNING:                                                */        
 /*    PROGRAMMET BLIR INKLUDERT I 4101-(BEREGN_ETTERLATT_AP)         */        
 /*    OG CALLET FRA               4126-(BEREGN_ETTERLATT_AP_DEL3)    */        
 /* ***************************************************************** */        
 BEREGN_GTLOV92_AE: PROC;                                                       
    DCL  W_KAP11_TP_BRUTTO     FIXED DEC (11,4)  INIT (0);                      
    DCL  KAP11_TP_BRUTTO       FIXED DEC (5)     INIT (0);                      
    DCL  VANLIG_TP_BRUTTO      FIXED DEC (5)     INIT(0);                       
    DCL  W_APD                 FIXED DEC (3)     INIT(0);                       
    DCL (MINUS,                                                                 
         SPT_UTEN_PT,                                                           
         OPT_UTEN_PT)       FIXED DEC (3,2) INIT (0);                           
                                                                                
    /* ============================================================== */        
                                                                                
    IF (B02.ALDERSP.APD(BER_SØ_IND) = 0 &                                       
        W_FNR_R13.ÅR > 1924             ) THEN                                  
       W_APD = 100;                                                             
    ELSE                                                                        
       W_APD = B02.ALDERSP.APD(BER_SØ_IND);                                     
                                                                                
 /*HVIS GJENLEVENDE HAR POENGTILLEGG, SÅ MÅ DETTE FJERNES I EGENPENSJ.*/        
                                                                                
    IF B01.POENGTILLEGG_KODE(BER_SØ_IND) = 'A'      THEN                        
       MINUS = 1;                                                               
    ELSE                                                                        
       IF B01.POENGTILLEGG_KODE(BER_SØ_IND) = 'B'      THEN                     
          MINUS = 0.5;                                                          
                                                                                
    IF MINUS > 0       THEN                                                     
       DO;                                                                      
          SPT_UTEN_PT = B02.ALDERSP.SPT_1291(BER_SØ_IND) - MINUS;               
          IF B02.ALDERSP.OPT_1291(BER_SØ_IND) > SPT_UTEN_PT THEN                
             OPT_UTEN_PT = SPT_UTEN_PT;                                         
          ELSE                                                                  
             OPT_UTEN_PT = B02.ALDERSP.OPT_1291(BER_SØ_IND);                    
       END;                                                                     
    ELSE                                                                        
       DO;                                                                      
          SPT_UTEN_PT = B02.ALDERSP.SPT_1291(BER_SØ_IND);                       
          OPT_UTEN_PT = B02.ALDERSP.OPT_1291(BER_SØ_IND);                       
       END;                                                                     
                                                                                
                                                                                
   GAR92_EGEN        =  F_TILLEGGSPENSJON                                       
                          ((W_FNR_R13.ÅR),                                      
                           35500,                                               
                           SPT_UTEN_PT,                                         
                           OPT_UTEN_PT,                                         
                           (B02.ALDERSP.PÅ_1291(BER_SØ_IND)),                   
                           (W_APD),                                             
                           100,                                                 
                           'J'                                                  
                          );                                                    
                                                                                
   GAR92_EGEN_55     =  F_TILLEGGSPENSJON                                       
                          ((W_FNR_R13.ÅR),                                      
                           35500,                                               
                           (B02.ALDERSP.SPT_1291(BER_SØ_IND)),                  
                           (B02.ALDERSP.OPT_1291(BER_SØ_IND)),                  
                           (B02.ALDERSP.PÅ_1291(BER_SØ_IND)),                   
                           (W_APD),                                             
                           55,                                                  
                           'J'                                                  
                          );                                                    
                                                                                
   GAR92_AVD         =  F_TILLEGGSPENSJON                                       
                          ((FNR_EK_R13.ÅR_EK),                                  
                           35500,                                               
                           (B02.ETTEPENS.SPT_1291(BER_SØ_IND)),                 
                           (B02.ETTEPENS.OPT_1291(BER_SØ_IND)),                 
                           (B02.ETTEPENS.PÅ_1291(BER_SØ_IND)),                  
                           (W_APD),                                             
                           (B02.TP_PROSENT(BER_SØ_IND)),                        
                           'J'                                                  
                          );                                                    
                                                                                
   IF B02.YRKEPENS.YUG(BER_EK_IND) > 0       THEN                               
      DO;                                                                       
         IF B02.YRKEPENS.YPT(BER_EK_IND) <                                      
            B02.YRKE_POENG_1291(BER_EK_IND)      THEN                           
            B02.YRKEPENS.YPT(BER_EK_IND) =                                      
            B02.YRKE_POENG_1291(BER_EK_IND);                                    
         W_KAP11_TP_BRUTTO =  G * 0.45;                                         
         W_KAP11_TP_BRUTTO =  W_KAP11_TP_BRUTTO / 12;                           
         W_KAP11_TP_BRUTTO =  W_KAP11_TP_BRUTTO *                               
                               B02.YRKEPENS.YPT(BER_EK_IND);                    
         W_KAP11_TP_BRUTTO =  W_KAP11_TP_BRUTTO *                               
                               B02.YRKEPENS.YUG(BER_EK_IND);                    
         W_KAP11_TP_BRUTTO =  W_KAP11_TP_BRUTTO / 100;                          
         W_KAP11_TP_BRUTTO =  W_KAP11_TP_BRUTTO *                               
                               B02.TP_PROSENT(BER_SØ_IND);                      
         W_KAP11_TP_BRUTTO =  W_KAP11_TP_BRUTTO / 100;                          
         KAP11_TP_BRUTTO   =  W_KAP11_TP_BRUTTO + 0.5;                          
                                                                                
         VANLIG_TP_BRUTTO = GAR92_AVD *                                         
                     B02.YRKEPENS.YUG(BER_EK_IND) / 100 + 0.5;                  
                                                                                
         IF KAP11_TP_BRUTTO > VANLIG_TP_BRUTTO       THEN                       
                     GAR92_AVD = GAR92_AVD + KAP11_TP_BRUTTO -                  
                                VANLIG_TP_BRUTTO;                               
      END;                                                                      
                                                                                
   IF GAR92_EGEN > (GAR92_EGEN_55 + GAR92_AVD)         THEN                     
      B02.GT_LOV92(BER_SØ_IND) = GAR92_EGEN;                                    
    ELSE                                                                        
       B02.GT_LOV92(BER_SØ_IND) = GAR92_EGEN_55 + GAR92_AVD;                    
                                                                                
                                                                                
 RETUR:                                                                         
                                                                                
 END BEREGN_GTLOV92_AE;                                                         
