 /*   SIST ENDRET PÅ PROD   2003.06.19 15.33.47 AV   SPA2970          */        
 /*   SIST ENDRET PÅ TEST   2003.03.14 10.36.50 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2002.06.05  9.52.34 AV   HLA2970          */        
 /*   SIST ENDRET PÅ PROD   2002.04.19 12.27.58 AV   JDA2970          */        
 /*       SIST ENDRET 22/06-98 10.22.36 AV   JDA7339                  */        
 /*       SIST ENDRET 18/06-98 15.49.51 AV   RFA9991                  */        
 /* ***************************************************************** */        
 /*IDENTIFIKASJON:                                                    */        
 /*    R0014381 - PROSEDYRE I PLI                                     */        
 /*    PROGRAMMERER: HERMAN JULI 95                                   */        
 /*HENSIKT:                                                           */        
 /*    BEREGNE GRUNNPENSJON ETTERLATT - ETTER 9300                    */        
 /*PROGRAMTILKNYTNING:                                                */        
 /*    INCLUDES I R0014301                                            */        
 /*BRUK:                                                              */        
 /*    CALL GP_TT_AVD                                                 */        
 /*                                                                   */        
 /* ***************************************************************** */        
 %SKIP(2);                                                                      
 GP_TT_AVD :                                                                    
   PROC;                                                                        
                                                                                
 /* ***************************************************************** */        
 /* AVDØDES TRYGDETIDER REGNES ALLTID PÅ NYTT HER, SELV OM DET IKKE   */        
 /* ER NØDVENDIG                                                      */        
 /* ***************************************************************** */        
                                                                                
      IF DØDSTP.ÅR - W_FNR_EK13_ÅR >= 66 THEN                                   
         CALL REGN_TT_67_TIL_70_DØD;                                            
                                                                                
                                                                                
      W_TT_AVDØD = B02.TT_FØR_1967    (BER_EK_IND)                              
                   +(B02.TT_ETTER_1966(BER_EK_IND)                              
                   + B02.TT_FRAMT     (BER_EK_IND) + 11) /12                    
                   + B02.TT_67_TIL_70 (BER_EK_IND);                             
                                                                                
      IF B02.TT_16_66(BER_EK_IND) > 0 THEN                                      
         W_TT_AVDØD  = ( B02.TT_16_66(BER_EK_IND)                               
                       + B02.TT_FRAMT(BER_EK_IND) + 6 ) / 12                    
                       + B02.TT_67_TIL_70 (BER_EK_IND);                         
                                                                                
                                                                                
      IF W_TT_AVDØD  < B01.TT_ANV(BER_EK_IND)   THEN                            
         W_TT_AVDØD  = B01.TT_ANV(BER_EK_IND);                                  
                                                                                
      IF W_TT_AVDØD > 39 THEN                                                   
         W_TT_AVDØD = 40;                                                       
      B02.TT_ANV (BER_EK_IND) = W_TT_AVDØD;                                     
                                                                                
      IF B02.FRAVIK_FLYKTN(BER_EK_IND) = 'J' THEN                               
         B02.TT_ANV(BER_EK_IND)        = 40;                                    
                                                                                
      IF B02.TT_ANV(BER_EK_IND) < B02.PÅ_AVD(BER_SØ_IND) THEN                   
         B02.TT_ANV(BER_EK_IND) = B02.PÅ_AVD(BER_SØ_IND);                       
                                                                                
 /*TILLEGG 2.5.96 - ETTER MELDING FRA RANDI OHNA, FFU */                        
                                                                                
      IF TRYGDETID_KONV       > 0      THEN                                     
         DO;                                                                    
            IF TRYGDETID_KONV > 40 THEN                                         
               TRYGDETID_KONV = 40;                                             
            B02.TT_16_66(BER_EK_IND) = TRYGDETID_KONV * 12;                     
            B02.TT_ANV  (BER_EK_IND) = TRYGDETID_KONV;                          
            B02.TT_FRAMT(BER_EK_IND) = 0;                                       
         END;                                                                   
                                                                                
 /* NYTT FELT 200107 : */                                                       
      IF B02.PENSJON_FØR_9802(BER_SØ_IND) ^= 'J'   &                            
         B02.RED_GP_3_2_5(BER_SØ_IND)      = 'J'   THEN                         
         DO;                                                                    
            B02.GP_BRUTTO(BER_SØ_IND)  = G * B02.TT_ANV(BER_EK_IND)             
                                * GP_PROSENT / (40 * 12) + 0.5;                 
            DIV_PARAM_OMR.REDUSERT_GP_2G(BER_SØ_IND) = 'X';                     
            B02.GP_REDUKSJON_KODE(BER_SØ_IND) = 'X';                            
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
            B02.GP_BRUTTO(BER_SØ_IND)  = G * B02.TT_ANV(BER_EK_IND)             
                                       / (40 * 12) + 0.5;                       
            B02.GP_REDUKSJON_KODE(BER_SØ_IND) = ' ';                            
         END;                                                                   
                                                                                
      IF B02.SPESOPPL.IKKE_GP(BER_SØ_IND)  = 'J' THEN                           
      B02.GP_BRUTTO(BER_SØ_IND)        = 0;                                     
                                                                                
                                                                                
  %PAGE;                                                                        
   %INCLUDE R0014322;                                                           
   END GP_TT_AVD;                                                               
