 /*   SIST ENDRET PÅ PROD   2006.07.18 11.37.08 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2006.04.24  9.43.47 AV   SPA2970          */        
 /*   SIST ENDRET PÅ QASS   2006.04.19 12.11.04 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2006.04.19 10.14.20 AV   SPA2970          */        
 /*   SIST ENDRET PÅ PROD   2005.05.06 12.03.09 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2005.04.29  8.30.29 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2005.04.01  8.43.23 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2003.11.17  9.36.08 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2003.10.30 10.16.44 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2003.05.09 12.24.43 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2003.05.09 12.21.52 AV   JDA2970          */        
 /*   SIST ENDRET PÅ QASS   2003.01.21 15.11.41 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2002.02.18 12.32.10 AV   HLA2970          */        
 /*       SIST ENDRET 16/06-99 09.04.04 AV   JDA7339                  */        
 /*       SIST ENDRET 16/06-99 09.03.33 AV   JDA7339                  */        
 /*       SIST ENDRET 20/05-99 10.54.38 AV   JDA7339                  */        
 /*       SIST ENDRET 20/05-99 10.54.00 AV   JDA7339                  */        
 /*       SIST ENDRET 15/04-99 09.35.30 AV   JDA7339                  */        
 /*       SIST ENDRET 15/04-99 09.14.28 AV   JDA7339                  */        
 /*       SIST ENDRET 04/05-98 13.31.51 AV   JDA7339                  */        
 /*       SIST ENDRET 23/01-98 09.57.20 AV   SPA7339                  */        
 /* ***************************************************************** */        
 /* IDENTIFIKASJON:                                                   */        
 /*     R0019940 - PROSEDYRE I PLI                                    */        
 /*     PROGRAMMERER: JAN, APRIL 1982                                 */        
 /* HENSIKT:                                                          */        
 /*     DENNE RUTINEN OPPHØRER UFØREPENSJON.                          */        
 /*     DERSOM SØKER HAR EKTEFELLE SKJER FØLGENDE:                    */        
 /*     A) EKTEFELLEN HAR EN OPPHØRT UFØREPENSJON                     */        
 /*      SØKERS STATUS AVSLUTTES                                      */        
 /*      EKTEFELLEN FÅR STATUS_KODE_HIST = 'X' OG                     */        
 /*                     PENSJONSTYPE1    = 'U' OG                     */        
 /*                     PENSJONSTYPE2    = ' '                        */        
 /*     B) EKTEFELLEN HAR PENSJONSKODE1 = G/F OG IKKE UFØREHISTORIKK  */        
 /*      SØKERS OG EKTEFELLENS STATUS AVSLUTTES.                      */        
 /*     C) EKTEFELLEN HAR EGEN PENSJON:                               */        
 /*      SØKER BEHOLDER STATUS SEGMENT MED NYE PENSJONSKODER,         */        
 /*      EKTEFELLEN FÅR NY PENSJONSKODE2 OG NY TILKNYTNINGSKODE.      */        
 /*     DERSOM SØKER IKKE HAR EKTEFELLE:                              */        
 /*      SØKERS STATUS AVSLUTTES.                                     */        
 /*     DET BLIR SPESIELL BEHANDLING FOR:                             */        
 /*      UFØR ETTERLATT EKTEFELLE OG UFØR ETTERLATT BARN              */        
 /*     DERSOM SøKER FORSØRGER BARN BLIR OGSÅ DISSE SLETTET           */        
 /*      FRA STATUS.                                                  */        
 /* PROGRAMTILKNYTNING:                                               */        
 /*     KALLES OPP AV PROGRAM R0011720.                               */        
 /* BRUK:                                                             */        
 /*     UFØREPENSJON OPPHØRER                                         */        
 /* ***************************************************************** */        
 %SKIP;                                                                         
 UFØREPENSJON_OPPHØRER:                                                         
   PROC;                                                                        
 %SKIP;                                                                         
   DCL                                                                          
      I         FIXED BIN(15),                                                  
      J         FIXED BIN(15);                                                  
   DCL OPPHØR_ÅMD             PIC '(8)9';                                       
   DCL OPPHØR_ÅR DEF OPPHØR_ÅMD   PIC '9999',                                   
       OPPHØR_MN DEF OPPHØR_ÅMD POS(5)  PIC '99';                               
 %SKIP;                                                                         
                                                                                
  OPPHØR_ÅMD = HJ_VIRK_DATO_ÅMD;                                                
  IF OPPHØR_ÅR > 1998    &              /*9807*/                                
     OPPHØR_MN = 01 THEN                                                        
     DO;                                                                        
        OPPHØR_MN = 12;                                                         
        OPPHØR_ÅR = OPPHØR_ÅR - 1;                                              
     END;                                                                       
  B02.PENSJONSRETT_FØR_91(SØKER_IND) = ' ';                                     
  B02.GT_TILLEGG_LOV92   (SØKER_IND) = 0;       /*0496 HL*/                     
  B02.GT_LOV92           (SØKER_IND) = 0;       /*0496 HL*/                     
  B02.EØSINFO            (SØKER_IND) = '';      /*9902 HL*/                     
  B02.SPESOPPL           (SØKER_IND) = '';      /*9902 HL*/                     
  B02.FRYSDATO_ÅMD       (SØKER_IND) = '';      /*9902 HL*/                     
  B02.FRYSKODE           (SØKER_IND) = '';      /*9902 HL*/                     
 /* B02.PENSJON_FØR_9802   (SØKER_IND) = '';     9902 HL*/                      
 /* B02.PENSJON_FØR_9802(SØKER_IND) ^= '';        9906 HL*/                     
 IF B02.PENSJON_FØR_9802(SØKER_IND) ^= 'X' THEN /*R 927 JFA 17.3.05*/           
    /* 'X' BETYR OPPHØR I UP I PERIODE MED PENSJON FØR 9802 JFA */              
    /* (SETTES I PROGRAM 1720 */                                                
    B02.PENSJON_FØR_9802(SØKER_IND) = '';   /*9906 HL*/                         
                                                                                
 /* ***************************************************************** */        
 /* DERSOM SØKEREN ER EN VOKSEN.                                      */        
 /* ***************************************************************** */        
                                                                                
      IF SØKER_IND < 3 THEN                                                     
         DO;                                                                    
            EKTEF_IND = 3 - SØKER_IND;   /*200305 HL */                         
                                                                                
 /* ***************************************************************** */        
 /* DERSOM SØKEREN FORSØRGER BARN.                                    */        
 /* ***************************************************************** */        
                                                                                
            DO I = 1 TO 13 WHILE (B01.FNR_TILKN(SØKER_IND,I) > 0);              
               IF B01.TILKN.TILKNYTNINGSKODE(SØKER_IND,I) = 'L'  !              
  /*HL*/          B01.TILKN.TILKNYTNINGSKODE(SØKER_IND,I) = 'V'  !              
                  B01.TILKN.TILKNYTNINGSKODE(SØKER_IND,I) = 'W'                 
                                                              THEN              
                DO;                                                             
                  B01.TILKN.FT_FØR_91       (SØKER_IND,I) = ' ';                
                  DO J = 3 TO 14 WHILE (B01.FNR(J) > 0);                        
 /* 9934 */          CALL OPPHØR_KOBLING_TO_PERSONER(SØKER_IND,J);              
                     B02.VIRK_DATO_ÅMD(J)             =                         
                                                 HJ_VIRK_DATO_ÅMD;              
                     B02.PENSJONSRETT_FØR_91(J)      = ' ';                     
                     B02.STATUS_KODE_HIST(J)         = 'O';                     
                  END;                                                          
                END;                                                            
            END;                                                                
            B02.ANTALL_BARN(SØKER_IND)               = 0  ;                     
                                                                                
 /* ***************************************************************** */        
 /* DERSOM UFØR ETTERLATT EKTEFELLE.                                  */        
 /* ***************************************************************** */        
                                                                                
            IF B01.PENSJONSTYPE2(SØKER_IND) = 'E' THEN                          
               DO;                                                              
                                                                                
 /* ***************************************************************** */        
 /* NÅ SKAL ETTERLATTEPENSJON LØPE VIDERE                             */        
 /* ***************************************************************** */        
                                                                                
                  B02.PENSJONSTYPE1(SØKER_IND) = 'E';                           
                  B02.PENSJONSTYPE2(SØKER_IND) = 'P';                           
                                                                                
 /* ***************************************************************** */        
 /* ENDRINGER I SPESIELLE OPPLYSNINGER.                               */        
 /* ***************************************************************** */        
                                                                                
                  B02.SPESOPPL.UFØR_UNDER_BOTID(SØKER_IND) = ' ';               
                  B02.SPESOPPL.TT_FRAMT_UFT(SØKER_IND)     = 0;                 
                  B02.SPESOPPL.FRAVIK_8_1(SØKER_IND)       = ' ';               
                  TRANS_OPPL_OMR.TRANSTYPE                 = 21;                
               END;                                                             
            ELSE                                                                
                                                                                
 /* ***************************************************************** */        
 /* DERSOM SØKER ER UGIFT, SKILT ELLER ETTERLATT.                     */        
 /* ***************************************************************** */        
                                                                                
               IF B01.SIVILSTAND       (SØKER_IND) = 'U' !                      
                                                                                
                                                                                
                  (B01.SIVILSTAND       (SØKER_IND) = 'A' &                     
                   B01.FNR              (EKTEF_IND) = 0 ) !                     
                                                                                
                                                                                
                  B01.SIVILSTAND       (SØKER_IND) = 'E' !                      
                  B01.SIVILSTAND       (SØKER_IND) = 'S' THEN                   
                  DO;                                                           
                     B02.VIRK_DATO_ÅMD (SØKER_IND) = HJ_VIRK_DATO_ÅMD;          
                     B02.STATUS_KODE_HIST(SØKER_IND) = 'X';                     
                     B02.ANTALL_BARN     (SØKER_IND) = 0; /*0599RF*/            
                     TRANS_OPPL_OMR.TRANSTYPE        = 55;                      
                        B02.TT_FØR_1967(SØKER_IND)   = 0;                       
                        B02.TT_ETTER_1966(SØKER_IND) = 0;                       
                        B02.TT_16_66     (SØKER_IND) = 0;                       
                        B02.TT_GARANTI   (SØKER_IND) = 0;                       
                        B02.TT_ANV(SØKER_IND)        = 0;                       
                        B02.TT_FRAMT(SØKER_IND)      = 0;                       
                        B02.TP_GAR_KODE(SØKER_IND)   = ' ';                     
                  END;                                                          
               ELSE                                                             
                                                                                
 /* ***************************************************************** */        
 /* DERSOM SØKER HAR EKTEFELLE,EKTEFELLEN IKKE HAR EGEN YTELSE.       */        
 /* ***************************************************************** */        
                                                                                
                  IF B01.PENSJONSTYPE1(EKTEF_IND) = 'G' ! B01.                  
                                    PENSJONSTYPE1(EKTEF_IND) = 'F' THEN         
                     DO;                                                        
                                                                                
 /* ***************************************************************** */        
 /* B02.STATUS SETTES TIL HISTORISK.                                  */        
 /* ***************************************************************** */        
                                                                                
                        B02.VIRK_DATO_ÅMD(SØKER_IND)     =                      
                                                   HJ_VIRK_DATO_ÅMD;            
                        B02.STATUS_KODE_HIST(SØKER_IND) = 'X';                  
                        B02.ANTALL_BARN     (SØKER_IND) = 0;/*0599RF*/          
                        B02.TT_FØR_1967(SØKER_IND)   = 0;                       
                        B02.TT_ETTER_1966(SØKER_IND) = 0;                       
                        B02.TT_16_66     (SØKER_IND) = 0;                       
                        B02.TT_GARANTI   (SØKER_IND) = 0;                       
                        B02.TT_ANV(SØKER_IND)        = 0;                       
                        B02.TT_FRAMT(SØKER_IND)      = 0;                       
                        B02.TP_GAR_KODE(SØKER_IND)   = ' ';                     
                        B02.VIRK_DATO_ÅMD(EKTEF_IND)     =                      
                                                   HJ_VIRK_DATO_ÅMD;            
                                                                                
                                                                                
                        IF B01.UFT_ÅMD(EKTEF_IND,1)      > 0      THEN          
                                                                                
                           DO;                                                  
                              B02.STATUS_KODE_HIST(EKTEF_IND) = 'X';            
                              B02.ANTALL_BARN(EKTEF_IND) = 0; /*0599RF*/        
                              B02.PENSJONSTYPE1(EKTEF_IND) = 'U';               
                              B02.PENSJONSTYPE2(EKTEF_IND) = ' ';               
                           END;                                                 
                                                                                
                        ELSE                                                    
                           DO;                                                  
                              B02.STATUS_KODE_HIST(EKTEF_IND) = 'O';            
                              B02.ANTALL_BARN(EKTEF_IND) = 0; /*0599RF*/        
                           END;                                                 
                                                                                
                        TRANS_OPPL_OMR.TRANSTYPE        = 55;                   
 /* 9934 */             CALL OPPHØR_KOBLING_TO_PERSONER                         
                                                (SØKER_IND,EKTEF_IND);          
                     END;                                                       
                  ELSE                                                          
                     DO;                                                        
                                                                                
 /* ***************************************************************** */        
 /* EKTEFELLEN HAR EGEN PENSJON.                                      */        
 /* ***************************************************************** */        
                        IF B01.PENSJONSTYPE2(EKTEF_IND)   ^= 'N' THEN           
                           B02.PENSJONSTYPE2(EKTEF_IND)    = 'P';               
                                                                                
                        B02.PENSJONSTYPE1(SØKER_IND) = 'G';                     
                        B02.PENSJONSTYPE2(SØKER_IND) = ' ';                     
                        B02.TT_FØR_1967(SØKER_IND)   = 0;                       
                        B02.TT_ETTER_1966(SØKER_IND) = 0;                       
                        B02.TT_16_66     (SØKER_IND) = 0;                       
                        B02.TT_GARANTI   (SØKER_IND) = 0;                       
                        B02.TT_ANV(SØKER_IND)        = 0;                       
                        B02.TT_FRAMT(SØKER_IND)      = 0;                       
                        B02.TP_GAR_KODE(SØKER_IND)   = ' ';                     
                        B02.GAMMEL_SAMMENSTØTS_REGEL(SØKER_IND)= ' ';           
                        B02.SUM_YTELSE(SØKER_IND)    = 0;                       
                        B02.VIRK_DATO_ÅMD(SØKER_IND)  =                         
                                                   HJ_VIRK_DATO_ÅMD;            
                        B02.VIRK_DATO_ÅMD(EKTEF_IND)  =                         
                                                   HJ_VIRK_DATO_ÅMD;            
                        B02.GAMMEL_SAMMENSTØTS_REGEL(EKTEF_IND)= ' ';           
                        B02.GP_REDUKSJON_KODE(SØKER_IND)= ' ';                  
                        B02.EK_INNT_OVER_2G(SØKER_IND) = ' ';                   
 /*TRANSTYPE SETTES TIL 91 I 4901 DERSOM DETTE ER OPPHØR AV FRYS*/              
                        IF TRANS_OPPL_OMR.TRANSTYPE   = 91   THEN               
                           B02.EK_INNT_OVER_2G(EKTEF_IND) = 'J';                
                        TRANS_OPPL_OMR.TRANSTYPE        = 36;                   
                                                                                
 /* ***************************************************************** */        
 /* B02.SPESOPPL-SEGMENTET SLETTES                                 */           
 /* ***************************************************************** */        
                                                                                
                        B02.SPESOPPL(SØKER_IND)   = '';                         
                                                                                
 /* ***************************************************************** */        
 /* FINNER SØKERS FNR I EKTEFELLENS TILKNYTNINGSSEGMENT               */        
 /* ***************************************************************** */        
                                                                                
                        DO I = 1 TO 13 WHILE (                                  
                                 B02.FNR_TILKN(EKTEF_IND,I) > 0);               
                           IF    B02.FNR_TILKN(EKTEF_IND,I) =                   
                                       B02.FNR(SØKER_IND)       THEN            
                              DO;                                               
                                 B02.TILKNYTNINGSKODE(                          
                                                    EKTEF_IND,I) = 'G';         
                              END;                                              
                        END;                                                    
                     END;                                                       
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
                                                                                
 /* ***************************************************************** */        
 /* VI VET NÅ AT DETTE ER ET BARN (LIGGER PÅ PLASS 3-14 I B01).       */        
 /* DERMED ER DET ET BARN  MED BARNEPENSJON OG UFØREPENSJON.          */        
 /* UFØREPENSJONEN OPPHØRER, MENS BARNEPENSJONEN LØPER VIDERE         */        
 /* ***************************************************************** */        
                                                                                
                     B02.PENSJONSTYPE3(SØKER_IND)    = ' ';                     
                                                                                
 /* ***************************************************************** */        
 /* ENDRINGER I SPESIELLE OPPLYSNINGER                                */        
 /* ***************************************************************** */        
                                                                                
            B02.SPESOPPL.UFØR_UNDER_BOTID(SØKER_IND) = ' ';                     
            B02.SPESOPPL.TT_FRAMT_UFT(SØKER_IND)     = 0;                       
            B02.SPESOPPL.FRAVIK_8_1(SØKER_IND)       = ' ';                     
            TRANS_OPPL_OMR.TRANSTYPE                 = 21;                      
         END;                                                                   
                                                                                
 /* ***************************************************************** */        
 /* UFØRPENS OG YRKEPENS SKAL BLANKES                                 */        
 /* ***************************************************************** */        
                                                                                
         B02.UFØRPENS(SØKER_IND) = '';                                          
         B02.YRKEPENS(SØKER_IND) = '';                                          
         B02.FORSI   (SØKER_IND) = '';   /* TRUDE 300894*/                      
                                                                                
 /* ***************************************************************** */        
 /* OPPHØRSDATO OG KODE SETTES I UFØRHIST-SEGMENTET                   */        
 /* ***************************************************************** */        
                                                                                
      DO I = 1 TO 7;                                                            
         IF B01.UFØRHIST.OPPHØRSDATO_ÅMD( SØKER_IND,I)   = 0 THEN               
            DO;                                                                 
               B02.UFØRHIST.OPPHØRSDATO_ÅMD(SØKER_IND,I) =                      
                                     OPPHØR_ÅMD;     /*9807*/                   
               B02.UFØRHIST.OPPHØRSKODE(SØKER_IND,I)    = 'J';                  
               I                                        = 7;                    
            END;                                                                
      END;                                                                      
                                                                                
 /* ***************************************************************** */        
 /*YS   OPPHØRSDATO OG KODE SETTES I YRKEHIST-SEGMENTET               */        
 /* ***************************************************************** */        
                                                                                
            DO I = 1 TO 10;                                                     
               IF B01.YRKEHIST.YUFT_ÅMD     ( SØKER_IND,I)   > 0  &             
                  B01.YRKEHIST.OPPH_DATO_ÅMD( SØKER_IND,I)   = 0 THEN           
                  DO;                                                           
                     B02.YRKEHIST.OPPH_DATO_ÅMD(SØKER_IND,I) =                  
                                     OPPHØR_ÅMD;     /*9807*/                   
                     B02.YRKEHIST.OPPH_KODE(SØKER_IND,I)    = 'J';              
                     I                                        = 10;             
                  END;                                                          
            END;                                                                
   END UFØREPENSJON_OPPHØRER;                                                   
