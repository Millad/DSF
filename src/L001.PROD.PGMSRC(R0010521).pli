 /*   SIST ENDRET PÅ PROD   2006.07.18 11.35.56 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2006.04.24  9.41.47 AV   SPA2970          */        
 /*   SIST ENDRET PÅ QASS   2006.04.19 12.10.26 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2006.03.24  8.43.36 AV   SPA2970          */        
 /*       SIST ENDRET 11/05-98 15.51.50 AV   SPA7339                  */        
 /*       SIST ENDRET 04/05-98 12.24.31 AV   JDA7339                  */        
 /*       SIST ENDRET 27/01-98 11.47.36 AV   SPA7339                  */        
 /* *************************************************************** */          
 /*IDENTIFIKASJON:                                                  */          
 /*    R0010521 - PROSEDYRE I PLI                                   */          
 /*    PROGRAMMERER:  GEIR, OKTOBER 1981                            */          
 /*HENSIKT:                                                         */          
 /*    OPPRETTE PENSJONSSTATUS FOR SØKER FRA TRANSEN                */          
 /* *************************************************************** */          
 /*SIST ENDRET AV KARIN 1/6-84                                      */          
 /*    ENDRINGEN GJELDER REDUSERT BARNETILLEGG INNFØRT 1/7-84:      */          
 /*    DET INNFØRES OPPLYSNINGER OM EKTEFELLENS INNTEKT I           */          
 /*    KODEN FOR PENSJONSTYPE2(EKTEFELLENS)                         */          
 /*    PENSJONSTYPE2(EKTEFELLENS) = 'M' NÅR INNT >= 4G              */          
 /*    PENSJONSTYPE2(EKTEFELLENS) = 'U' NÅR INNT >  4G              */          
 /*  ************************************************************** */          
 /*SIST ENDRET 18/10-84 AV KARIN                                    */          
 /*    ENDRINGEN GJELDER FELTET FØRSTE-GANG-REG I B02               */          
 /*    SOM TIDLIGERE ALLTID BLE SATT LIK J I DETTE PROGRAMMET.      */          
 /*    NÅ BLIR DET SATT TIL N NÅR VEDKOMMENDE FRA FØR HAR HISTORISK */          
 /*    STATUS (STATUS-TYPE = I) , DVS OPPHØRT UFØRETILFELLE.        */          
 /*    VI TESTER På STATUS-TYPE = I OG UTFYLT UFØREHISTORIKK.       */          
 /*  ************************************************************** */          
 /*SIST ENDRET 22/10-84 AV KARIN                                    */          
 /*    ENDRINGEN GJELDER FELTET FØRSTE-GANG-REG FOR EKTEFELLEN.     */          
 /*    DET BLIR NÅ SATT LIK DET SØKER HAR FÅTT.                     */          
 /*  ************************************************************** */          
 /*SIST ENDRET 9/1-85 AV KARIN                                      */          
 /*    ENDRINGEN GJELDER NY STAUS-TYPE 'K' SOM BEHANDLES PÅ SAMME   */          
 /*    MÅTE SOM STATUS-TYPE = 'I'.                                  */          
 /*    NYTT ER DET OGSÅ AT STATUS-TYPE = 'A' KAN HA STATUS I B01    */          
 /*    MED STATUS-KODE-HIST = 'O' (OPPHØRT). VI MÅ DERFOR BLANKE    */          
 /*    SEGMENTENE I B02.STATUS.                                     */          
 /* *************************************************************** */          
 /*PROGRAMTILKNYTNING:                                              */          
 /*    PROSEDYREN INNGÅR SOM EN DEL AV BEHANDLINGA AV ALDERSPENSJON */          
 /*    INCLUDES I R0010520 .                                        */          
 /*BRUK:                                                            */          
 /*    CALL OPPRETT_STATUS_AP_SØKER                                 */          
  /* *************************************************************** */         
 /*                                                                 */          
 /* *************************************************************** */          
 %PAGE;                                /*  */                                   
 /* *************************************************************** */          
 /*   MODULEN OPPRETTER PENSJONSSTATUS  FOR SØKER PÅ GRUNNLAG AV    */          
 /*   DATA FRA TRANSEN.                                             */          
 /*                                                                 */          
 /* *************************************************************** */          
 %SKIP(1);                                                                      
 OPPRETT_STATUS_AP_SØKER:                                                       
   PROC;                                                                        
 %SKIP;                                                                         
   DCL                                                                          
      W_DATO_ÅMD    FIXED DEC(9) INIT (9),                                      
      ALDER_Å_MND   DEC (5);                                                    
 %SKIP;                                                                         
      HJ_VIRK_DATO_ÅMD  = TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD;                     
                                                                                
      IF HJ_VIRK_DATO_ÅMD < 19980200   THEN         /*2000-2*/                  
         B02.PENSJON_FØR_9802(SØKER_IND) = 'J';                                 
                                                                                
 /* HL : PERSONER FØDT FØR 1924 SKAL HA GAMMEL BESTEÅRSREGEL : */               
                                                                                
      IF NR.ÅR < 1924        THEN                                               
         B02.PENSJONSRETT_FØR_91 (SØKER_IND) = 'J';                             
                                                                                
                                                                                
      /* ******************************************************** */            
      /* ENDRET AV KARIN 9/1-85                                   */            
      /* NÅR STATUS-TYPE_SØKER = A OG STATUS-KODE-HIST = 'O'      */            
      /* MÅ VI FØRST BLANKE FELTENE SOM LIGGER I STATUS_SEGMENTET */            
      /* B01 ER FLYTTET OVER TIL B02 I R0010520                   */            
      /* ******************************************************** */            
                                                                                
      IF STATUS_TYPE_SØKER                = 'A' &                               
         B01.STATUS_KODE_HIST (SØKER_IND) = 'O' THEN                            
         DO;                                                                    
            B02.STATUS   (SØKER_IND)    = '';                                   
            B02.ALDERSP  (SØKER_IND)    = '';                                   
            B02.UFØRPENS (SØKER_IND)    = '';                                   
            B02.UFØRHIST (SØKER_IND, 1) = '';                                   
            B02.UFØRHIST (SØKER_IND, 2) = '';                                   
            B02.UFØRHIST (SØKER_IND, 3) = '';                                   
            B02.UFØRHIST (SØKER_IND, 4) = '';                                   
            B02.UFØRHIST (SØKER_IND, 5) = '';                                   
            B02.UFØRHIST (SØKER_IND, 6) = '';                                   
            B02.UFØRHIST (SØKER_IND, 7) = '';                                   
            B02.ETTEPENS (SØKER_IND)    = '';                                   
            B02.SPESOPPL (SØKER_IND)    = '';                                   
         END;                                                                   
                                                                                
 %SKIP;                                                                         
      IF APS.TILL_EK = 'J' & HJ_VIRK_DATO_ÅMD > 19910400  THEN                  
         APS.TILL_EK = ' ';     /* 7.91 JD */                                   
      IF APS.BT_ANT > 0    & HJ_VIRK_DATO_ÅMD > 19910400 THEN                   
         APS.BT_ANT = 0 ;       /* 7.91 JD */                                   
      B02.PENSJONSTYPE1(SØKER_IND)          = 'A';                              
      B02.TKNR         (SØKER_IND)          = APS.TKNR;                         
      B02.NAVN         (SØKER_IND)          = APS.NAVN;                         
      B02.SPRÅK        (SØKER_IND)          = APS.SPRÅK;                        
      IF APS.TILL_EK = 'J' THEN                                                 
         B02.PENSJONSTYPE2(SØKER_IND)       = 'F';                              
      ELSE                                                                      
         B02.PENSJONSTYPE2(SØKER_IND)       = 'P';                              
      B02.STATUS.SIVILSTAND(SØKER_IND)      = APS.SIVILSTAND;                   
      B02.STATUS.TT_FØR_1967(SØKER_IND)     = APSP.TT_F67;                      
      B02.STATUS.TT_ETTER_1966(SØKER_IND)   = APSP.TT_E66_M + (                 
                                                 APSP.TT_E66_Å * 12);           
      B02.STATUS.VIRK_DATO_ÅMD(SØKER_IND)   = HJ_VIRK_DATO_ÅMD;                 
                                                                                
      /*SETTE UTTAKSDATO FOR ALDERSPENSJON FOR NYE ALDERSPENSJONISTER*/         
      /*ETTER 0192. DVS FØDT 1925 ELLER SENERE . TRUDE ,200292       */         
                                                                                
      IF NR.ÅR                 > 1924   THEN                                    
    /*   NR.PERSNR.ÅRHUNDRE    < 500  THEN  */                                  
         B02.ALDERSP.UTTAKSDATO_ÅMD(SØKER_IND) = HJ_VIRK_DATO_ÅMD;              
                                                                                
      /* TRUDE TIL HIT 200292  */                                               
                                                                                
                                                                                
      /* ENDRET 18/10-84 AV KARIN  */                                           
      /* ENDRET  9/1 -85 AV KARIN  */                                           
      IF (STATUS_TYPE_SØKER = 'I' ! STATUS_TYPE_SØKER = 'K') &                  
         B02.UFT_ÅMD (SØKER_IND, 1) > 0 THEN                                    
         DO;                                                                    
                                                                                
 /* ***************************************************************** */        
 /* OPPHØRSDATO OG KODE SETTES I UFØRHIST-SEGMENTET                   */        
 /* DETTE ER BARE AKTUELT FOR TIDLIGERE ENGANGSUTLØSNING              */        
 /* ***************************************************************** */        
                                                                                
         DO I = 1 TO 7;                                                         
            IF B01.UFØRHIST.UFT_ÅMD( SØKER_IND,I)          > 0   &              
               B01.UFØRHIST.OPPHØRSDATO_ÅMD( SØKER_IND,I)  = 0 THEN             
               DO;                                                              
                  B02.UFØRHIST.OPPHØRSDATO_ÅMD(SØKER_IND,I) =                   
                                        SEKSTISJU_ÅRSDATO_ÅMD;                  
                  B02.UFØRHIST.OPPHØRSKODE(SØKER_IND,I)    = 'A';               
                  I                                        = 7;                 
               END;                                                             
         END;                                                                   
                                                                                
 /* ***************************************************************** */        
 /*YS   OPPHØRSDATO OG KODE SETTES I YRKEHIST-SEGMENTET               */        
 /*YS   BARE AKTUELT FOR TIDLIGERE ENGANGSUTLØSNING                   */        
 /* ***************************************************************** */        
                                                                                
        DO I = LBOUND(B01.YRKEHIST,2) TO HBOUND(B01.YRKEHIST,2);                
               IF B01.YRKEHIST.YUFT_ÅMD    ( SØKER_IND,I)   > 0  &              
                  B01.YRKEHIST.OPPH_DATO_ÅMD( SØKER_IND,I)  = 0 THEN            
                  DO;                                                           
                     W_DATO_ÅMD   =  SEKSTISJU_ÅRSDATO_ÅMD;                     
                     W_DATO_ÅMD   =  W_DATO_ÅMD / 100;                          
                     W_DATO_ÅMD   =  (W_DATO_ÅMD * 100);                        
                     B02.YRKEHIST.OPPH_DATO_ÅMD(SØKER_IND,I) =                  
                                                        W_DATO_ÅMD;             
                     B02.YRKEHIST.OPPH_KODE(SØKER_IND,I)    = 'A';              
                     I = HBOUND(B01.YRKEHIST,2);                                
                                                                                
                  END;                                                          
            END;                                                                
                                                                                
            B02.FØRSTE_GANG_REG    (SØKER_IND) = 'N';                           
         END;                                                                   
      ELSE                                                                      
         B02.FØRSTE_GANG_REG    (SØKER_IND) = 'J';                              
      B02.ALDERSP.APD(SØKER_IND)            = 100;                              
                                                                                
      ALDER_Å_MND = F_ALDER(APS.FNR,APS.VIRK_DATO_ÅMD);                         
                                                                                
      IF APS.FAI > 0  &                                                         
         APS.FAI ^= 9999999    THEN                                             
         DO;                                                                    
          IF NR.ÅR > 1924  THEN                                                 
           DO;            /* SKAL VÆRE I HELE TUSEN KR */                       
            B02.FAI_DATO_ÅMD(SØKER_IND) = HJ_VIRK_DATO_ÅMD;                     
            B02.FAI(SØKER_IND) = APS.FAI / 1000;                                
            B02.FAI(SØKER_IND) = B02.FAI(SØKER_IND) * 10; /*FEIL IND*/          
           END;                                                                 
          ELSE                                                                  
           DO;                                                                  
            B02.FAI_DATO_ÅMD(SØKER_IND) = HJ_VIRK_DATO_ÅMD;                     
            B02.FAI(SØKER_IND) = APS.FAI / 100;                                 
           END;                                                                 
         END;                                                                   
      ELSE                                                                      
        B02.FAI(SØKER_IND) = 0;     /*F.MELD.137.  ERIK 7.3.86*/                
                                                                                
      IF APS.NORSK_BOSATT = 'N' THEN                                            
         DO;                                                                    
            B02.SPESOPPL.STATSBORGER(SØKER_IND)   = APSP.STATSBORGER;           
            B02.SPESOPPL.BOSATT(SØKER_IND)        = APSP.BOSATT;                
            B02.SPESOPPL.FRAVIK_FLYKTN(SØKER_IND) = APSP.                       
                                                       FRAVIK_FLYKT;            
            B02.SPESOPPL.TT_VENT_UTL(SØKER_IND)  = APSP.TT_VENT_UTL;            
            B02.SPESOPPL.OVERKOMP_UTL(SØKER_IND) = APSP.                        
                                                      OVERKOMP_UTL;             
            B02.SPESOPPL.IKKE_GP(SØKER_IND)     = APSP.IKKE_GP;                 
                                                                                
            IF APSP.FRAVIK_19_3 ^= 'J' THEN                                     
               B02.SPESOPPL.FRAVIK_19_3(SØKER_IND) = APSP.FRAVIK_19_3;          
            ELSE     /*PGA IKKE FELT FOR KONV. PÅ AP_BLANKETT. TRUDE*/          
              IF APSP.FRAVIK_19_3  = 'J' THEN                                   
                B02.SPESOPPL.KONVENSJON(SØKER_IND) = APSP.FRAVIK_19_3;          
         END;                                                                   
 /* ***************************************************************** */        
 /* EKTEFELLES STATUS                                                 */        
 /* ***************************************************************** */        
      IF B02.FNR(EKTEF_IND) > 0 THEN                                            
         DO;                                                                    
            B02.STATUS.SIVILSTAND(EKTEF_IND)      =                             
                            B02.STATUS.SIVILSTAND(SØKER_IND);                   
            B02.STATUS.VIRK_DATO_ÅMD(EKTEF_IND)   = HJ_VIRK_DATO_ÅMD;           
            /* ENDRET 22/10-84 AV KARIN */                                      
            B02.STATUS.FØRSTE_GANG_REG(EKTEF_IND) =                             
            B02.STATUS.FØRSTE_GANG_REG(SØKER_IND);                              
                                                                                
            B02.STATUS.PENSJONSTYPE3  (EKTEF_IND) = ' ';                        
            B02.STATUS.SUM_YTELSE     (EKTEF_IND) = 0;                          
                                                                                
            IF APS.TILL_EK = 'J' THEN                                           
               DO;                                                              
                  B02.STATUS.PENSJONSTYPE1(EKTEF_IND)       = 'F';              
                  B02.STATUS.PENSJONSTYPE2(EKTEF_IND)       = ' ';              
                  IF APSP.TT_F67_EK ^= 0 THEN                                   
                     DO;                                                        
                        B02.STATUS.TT_FØR_1967(EKTEF_IND)   = APSP.             
                        TT_F67_EK;                                              
                        B02.STATUS.TT_ETTER_1966(EKTEF_IND) =                   
                        APSP. TT_E66_M_EK + (APSP.TT_E66_Å_EK * 12);            
                     END;                                                       
               END;                                                             
            ELSE                                                                
               B02.STATUS.PENSJONSTYPE1(EKTEF_IND) = 'G';                       
               IF APS.EK_INNT = 'J' THEN                                        
                  B02.STATUS.PENSJONSTYPE2  (EKTEF_IND) = 'M';                  
               ELSE                                                             
                  B02.STATUS.PENSJONSTYPE2  (EKTEF_IND) = 'U';                  
         END;                                                                   
   END OPPRETT_STATUS_AP_SØKER;                                                 
