 /*   SIST ENDRET PÅ PROD   2000.07.07 13.00.22 AV   JDA7339          */        
 /*       SIST ENDRET 04/05-98 12.41.38 AV   JDA7339                  */        
 /*       SIST ENDRET 20/09-94 09.42.37 AV   HERMAN                   */        
 /* ***************************************************************** */        
 /*IDENTIFIKASJON:                                                    */        
 /*    R0014149 - F_TP92            - INTERN FUNKSJON I PLI           */        
 /*    PROGRAMMERER: HERMAN -DES. 91                                  */        
 /*HENSIKT:                                                           */        
 /*    REGNER TP MED POENGÅR FØR 1992 OG ETTER 1991  (45 OG 42 %)     */        
 /*PROGRAMTILKNYTNING:                                                */        
 /*                                                                   */        
 /*BRUK:                                                              */        
 /*    TP=F_TP92(FÅ,SPT,OPT,PÅ,PÅ_ETTER91,UFG,TP_PROSENT,FORHØY_IND)  */        
 /* ***************************************************************** */        
 F_TP: PROC                                                                     
          (                                                                     
           FÅ,                                                                  
           G,                                                                   
           SPT,                                                                 
           OPT,                                                                 
           PÅ_45,                                                               
           PÅ_42,                                                               
           UFG,                                                                 
           TP_PROSENT,                                                          
           FORHØY_IND,                                                          
           PERIODE,                                                             
           DESIMALER                                                            
          )                                                                     
       RETURNS(DEC FIXED(9,2));                                                 
                                                                                
    DCL /* PARAMETERE INN */                                                    
        FÅ                 PIC '9999',                                          
        G                  FIXED DEC(5),                                        
        SPT                FIXED DEC(3,2),                                      
        OPT                FIXED DEC(3,2),                                      
        PÅ_42              FIXED DEC(3),                                        
        PÅ_45              FIXED DEC(3),                                        
        UFG                FIXED DEC(5,2),                                      
        TP_PROSENT         FIXED DEC(3),                                        
        FORHØY_IND         CHAR(1),                                             
        PERIODE            CHAR(1),                                             
        DESIMALER          FIXED DEC(3);                                        
                                                                                
    DCL /* INTERNE VARIABELER */                                                
        OÅ                 FIXED DEC(3),                                        
        TP_M_0_DES         FIXED DEC(5),                                        
        TP_M_2_DES         FIXED DEC(9,2),                                      
        TP_W               FIXED DEC(11,4),                                     
        TP_W1              FIXED DEC(11,4),                                     
        TP_W2              FIXED DEC(11,4),                                     
        TP_UT              FIXED DEC(9,2),                                      
        FAKTOR             FIXED DEC(11,4),                                     
        FAKTOR_45          FIXED DEC(11,4),                                     
        FAKTOR_42          FIXED DEC(11,4);                                     
                                                                                
    /* -------------------------------------------------------------- */        
    /* FØRST INITIERES ALLE INTERNE VARIABLER                         */        
    /* -------------------------------------------------------------- */        
                                                                                
    OÅ           = 20;                                                          
    TP_M_0_DES   =  0;                                                          
    TP_M_2_DES   =  0;                                                          
    TP_W         =  0;                                                          
    TP_W1        =  0;                                                          
    TP_W2        =  0;                                                          
    TP_UT        =  0;                                                          
    FAKTOR       =  0;                                                          
    FAKTOR_45    =  0;                                                          
    FAKTOR_42    =  0;                                                          
                                                                                
    /* START PROGRAM MHF1 =========================================== */        
                                                                                
    IF (B02.BOSATT(BER_SØ_IND)  = 000              &                            
        B02.TT_16_66(BER_SØ_IND) = 0               &                            
        B02.TT_ETTER_1966(BER_SØ_IND) = 0          &                            
        B02.TT_FØR_1967(BER_SØ_IND)   = 0          &                            
        B02.ALDERSP.KONV_P_KODE(BER_SØ_IND)  = 'F' &                            
        B02.ALDERSP.KONV_GRAD(BER_SØ_IND)  = 0     ) THEN                       
       ; /* KONV_P_KODE 'F' OMREGNES TIL NULLPENSJON */                         
    ELSE                                                                        
    IF (B02.FRAVIK_BOSATT_KONVENSJON(BER_SØ_IND) = 'J' !                        
        B02.KONVENSJON_TP(BER_SØ_IND)  = 'J'           !                        
        B02.KONVENSJON(BER_SØ_IND)  = 'J'              !                        
       (PÅ_45 + PÅ_42) > 2                             !                        
       (FÅ = 1899 & PÅ_42 = 2)                         !                        
       (FÅ = 1898 & PÅ_42 = 1)                         ) THEN                   
       DO;  /* FYLLLER VILKÅRA FOR TILLEGGSPENSJON */                           
                                                                                
          FAKTOR    = G * UFG / 100 * TP_PROSENT / 100 / 100;                   
          FAKTOR_45 = 45 * FAKTOR;                                              
          FAKTOR_42 = 42 * FAKTOR;                                              
                                                                                
          IF (OPT > 0) THEN                                                     
             DO;  /* HAR OVERKOMPENSASJON */                                    
                IF (FÅ < 1937) THEN                                             
                   DO;  /* FØDT FØR 1937 */                                     
                      IF (FÅ > 1917) THEN                                       
                         OÅ = 20 + FÅ - 1917;                                   
                      TP_W1 = FAKTOR_45 * PÅ_45 / OÅ * OPT;                     
                      TP_W2 = FAKTOR_42 * PÅ_42 / OÅ * OPT;                     
                      TP_W  = TP_W1 + TP_W2;                                    
                   END; /* FØDT FØR 1937 */                                     
                ELSE                                                            
                   DO;  /* FØDT 1937 ELLER SENERE - FEILMELDING */              
                   END; /* FØDT 1937 ELLER SENERE - FEILMELDING */              
             END; /* HAR OVERKOMPENSASJON */                                    
                                                                                
          IF (SPT > OPT) THEN                                                   
             DO;                                                                
                TP_W1 = FAKTOR_45 * PÅ_45 / 40 * (SPT-OPT);                     
                TP_W2 = FAKTOR_42 * PÅ_42 / 40 * (SPT-OPT);                     
                TP_W  = TP_W  + TP_W1 + TP_W2;                                  
             END;                                                               
                                                                                
          IF (B02.PENSJONSTYPE2(BER_SØ_IND) = 'A') THEN                         
             DIV_PERIODE.ATTFØRING_TP_ÅRSBELØP = TP_W + 0.5;                    
                                                                                
          SELECT (PERIODE);                                                     
             WHEN ('M') TP_W = TP_W / 12;                                       
             WHEN ('Å');                                                        
             OTHER                                                              
                DO;  /* FEILMELDING */                                          
                END; /* FEILMELDING */                                          
          END; /* SELECT PERIODE */                                             
                                                                                
          IF (FORHØY_IND ^= 'N') THEN                                           
             SELECT (DESIMALER);                                                
                WHEN (0)                                                        
                   DO;                                                          
                      TP_W = TP_W + 0.5;                                        
                      TP_M_0_DES = TP_W;                                        
                      TP_UT = TP_M_0_DES;                                       
                   END;                                                         
                WHEN (2)                                                        
                   DO;                                                          
                      TP_W = TP_W + 0.005;                                      
                      TP_M_2_DES = TP_W;                                        
                      TP_UT = TP_M_2_DES;                                       
                   END;                                                         
                OTHER                                                           
                   DO;  /* FEILMELDING */                                       
                   END; /* FEILMELDING */                                       
             END; /* SELECT DESIMALER */                                        
       END; /* FYLLLER VILKÅRA FOR TILLEGGSPENSJON */                           
                                                                                
    RETURN(TP_UT);                                                              
                                                                                
 END F_TP;                                                                      
