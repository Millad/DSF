 /*   SIST ENDRET PÅ PROD   2002.12.20  8.56.28 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2000.07.07 13.00.20 AV   JDA7339          */        
 /*       SIST ENDRET 04/05-98 12.41.38 AV   JDA7339                  */        
 /*       SIST ENDRET 20/09-94 09.42.37 AV   HERMAN                   */        
 /* ***************************************************************** */        
 /*IDENTIFIKASJON:                                                    */        
 /*    R0014148 - F_TP92            - INTERN FUNKSJON I PLI           */        
 /*    PROGRAMMERER: HERMAN -DES. 91                                  */        
 /*HENSIKT:                                                           */        
 /*    REGNER TP MED POENGÅR FØR 1992 OG ETTER 1991  (45 OG 42 %)     */        
 /*PROGRAMTILKNYTNING:                                                */        
 /*                                                                   */        
 /*BRUK:                                                              */        
 /*    TP=F_TP92(FÅ,SPT,OPT,PÅ,PÅ_ETTER91,UFG,TP_PROSENT,FORHØY_IND)  */        
 /* ***************************************************************** */        
 F_TP92 :                                                                       
  PROC(FÅ,G,SPT,OPT,PÅ,PÅ_ETTER91,UFG,TP_PROSENT,FORHØY_IND)                    
        RETURNS(DEC FIXED(5));                                                  
  DCL                                                                           
     FÅ                 PIC '9999',  /*2000*/                                   
     G                  FIXED DEC(5),                                           
     SPT                FIXED DEC(3,2),                                         
     OPT                FIXED DEC(3,2),                                         
     PÅ                 FIXED DEC(3),                                           
     PÅ_FØR92           FIXED DEC(3),                                           
     PÅ_ETTER91         FIXED DEC(3),                                           
     UFG                FIXED DEC(5,2),                                         
     TP_PROSENT         FIXED DEC(3),                                           
     OÅ                 FIXED DEC(3),                                           
     FORHØY_IND         CHAR(1),                                                
     TP_WORK            FIXED DEC(11,4),                                        
     TP_WORK_2          FIXED DEC(11,4),                                        
     TP2_WORK           FIXED DEC(11,4),                                        
     TP_WORK_92         FIXED DEC(11,4),                                        
     GAMMEL             FIXED DEC(11,4),                                        
     NY                 FIXED DEC(11,4),                                        
     TP                 FIXED DEC(5);                                           
                                                                                
   DCL PGNAVN         CHAR (8);                                                 
   PGNAVN = 'R0014148';                                                         
                                                                                
   IF PÅ_ETTER91 = 0           THEN                                             
      DO;                                                                       
         CALL GAMLE_REGLER;                                                     
         GOTO SLUTT;                                                            
      END;                                                                      
                                                                                
   PÅ_FØR92 = PÅ - PÅ_ETTER91;                                                  
   GAMMEL = G * 45 /100;                                                        
   NY     = G * 42 /100;                                                        
                                                                                
                                                                                
  IF   B02.FRAVIK_BOSATT_KONVENSJON(BER_SØ_IND) = 'J'      !                    
       B02.KONVENSJON_TP(BER_SØ_IND)  = 'J'  !    /*9909*/                      
       B02.KONVENSJON(BER_SØ_IND)  = 'J'  !       /* PENSJON UTLAND  */         
       PÅ > 2               !                                                   
     (FÅ = 1899 & PÅ = 2)   !                     /*2000*/                      
     (FÅ = 1898 & PÅ = 1)         THEN            /*2000*/                      
     DO;                                                                        
        IF FÅ > 1936  /* & FÅ < 98 */       THEN     /*2000*/                   
           DO;                                                                  
                                                                                
              TP_WORK = GAMMEL * PÅ_FØR92 / 40;                                 
              TP_WORK_92 = NY * PÅ_ETTER91 / 40;                                
              TP_WORK = (TP_WORK + TP_WORK_92) * SPT;                           
              TP_WORK = TP_WORK * UFG /100;                                     
              TP_WORK = TP_WORK * TP_PROSENT / 100;                             
              IF B02.PENSJONSTYPE2(BER_SØ_IND) = 'A' THEN                       
 /*KONSTS*/      DIV_PERIODE.ATTFØRING_TP_ÅRSBELØP = TP_WORK + 0.5;             
              TP_WORK = TP_WORK / 12;                                           
           END;                                                                 
        ELSE                                                                    
           DO;                                                                  
              IF FÅ > 1917   /* & FÅ < 98*/     THEN     /*2000*/               
                 OÅ = 20 + FÅ - 1917;             /*2000*/                      
              ELSE                                                              
                 OÅ = 20;                                                       
              IF SPT > OPT THEN                                                 
                 DO;                                                            
                                                                                
                    TP_WORK = GAMMEL * PÅ_FØR92 / OÅ;                           
                    TP_WORK_92 = NY * PÅ_ETTER91 / OÅ;                          
                    TP_WORK = (TP_WORK + TP_WORK_92) * OPT;                     
                                                                                
                    TP2_WORK = GAMMEL * PÅ_FØR92 / 40;                          
                    TP_WORK_92 = NY * PÅ_ETTER91 / 40;                          
                    TP2_WORK = (TP2_WORK + TP_WORK_92) * (SPT - OPT);           
                    TP_WORK = (TP_WORK + TP2_WORK) * UFG /100;                  
                    TP_WORK = TP_WORK * TP_PROSENT / 100;                       
              IF B02.PENSJONSTYPE2(BER_SØ_IND) = 'A' THEN                       
 /*KONSTS*/      DIV_PERIODE.ATTFØRING_TP_ÅRSBELØP = TP_WORK + 0.5;             
                    TP_WORK = TP_WORK / 12;                                     
                 END;                                                           
              ELSE                                                              
                 DO;                                                            
                                                                                
                    TP_WORK = GAMMEL * PÅ_FØR92 / OÅ;                           
                    TP_WORK_92 = NY * PÅ_ETTER91 / OÅ;                          
                    TP_WORK = (TP_WORK + TP_WORK_92) * SPT;                     
                    TP_WORK = TP_WORK * UFG /100;                               
                    TP_WORK = TP_WORK * TP_PROSENT / 100;                       
              IF B02.PENSJONSTYPE2(BER_SØ_IND) = 'A' THEN                       
 /*KONSTS*/      DIV_PERIODE.ATTFØRING_TP_ÅRSBELØP = TP_WORK + 0.5;             
                    TP_WORK = TP_WORK / 12;                                     
                 END;                                                           
           END;                                                                 
        IF FORHØY_IND ^= 'N' THEN                                               
           TP_WORK = TP_WORK + 0.5;                                             
     END;                                                                       
  ELSE                                                                          
     TP_WORK = 0;                                                               
  TP = TP_WORK;                                                                 
                                                                                
 GAMLE_REGLER: PROC;                                                            
                                                                                
                                                                                
  IF   B02.FRAVIK_BOSATT_KONVENSJON(BER_SØ_IND) = 'J'      !                    
       B02.KONVENSJON_TP(BER_SØ_IND)  = 'J'  !    /*9909*/                      
       B02.KONVENSJON(BER_SØ_IND)  = 'J'  !       /* PENSJON UTLAND  */         
       PÅ > 2               !                                                   
     (FÅ = 1899 & PÅ = 2)   !                    /*2000*/                       
     (FÅ = 1898 & PÅ = 1)         THEN          /*2000*/                        
     DO;                                                                        
        IF FÅ > 1936  /*& FÅ < 98*/        THEN     /*2000*/                    
           DO;                                                                  
              TP_WORK = G * 45 /100;                                            
              TP_WORK = TP_WORK * SPT;                                          
              TP_WORK = TP_WORK * PÅ / 40;                                      
              TP_WORK = TP_WORK * UFG /100;                                     
              TP_WORK = TP_WORK * TP_PROSENT / 100;                             
              IF B02.PENSJONSTYPE2(BER_SØ_IND) = 'A' THEN                       
 /*KONSTS*/      DIV_PERIODE.ATTFØRING_TP_ÅRSBELØP = TP_WORK + 0.5;             
              TP_WORK = TP_WORK / 12;                                           
           END;                                                                 
        ELSE                                                                    
           DO;                                                                  
              IF FÅ > 1917  /*& FÅ < 98*/     THEN      /*2000*/                
                 OÅ = 20 + FÅ - 1917;              /*2000*/                     
              ELSE                                                              
                 OÅ = 20;                                                       
              IF SPT > OPT THEN                                                 
                 DO;                                                            
                                                                                
                    TP_WORK = G * 45 /100;                                      
                    TP_WORK = TP_WORK * OPT;                                    
                    TP_WORK = TP_WORK * PÅ / OÅ;                                
                    TP_WORK_2 = G * 45 / 100;                                   
                    TP_WORK_2 = TP_WORK_2 * (SPT - OPT);                        
                    TP_WORK_2 = TP_WORK_2 * PÅ / 40;                            
                    TP_WORK = (TP_WORK + TP_WORK_2) * UFG /100;                 
                    TP_WORK = TP_WORK * TP_PROSENT / 100;                       
              IF B02.PENSJONSTYPE2(BER_SØ_IND) = 'A' THEN                       
 /*KONSTS*/      DIV_PERIODE.ATTFØRING_TP_ÅRSBELØP = TP_WORK + 0.5;             
                    TP_WORK = TP_WORK / 12;                                     
                 END;                                                           
              ELSE                                                              
                 DO;                                                            
                                                                                
                                                                                
                    TP_WORK = G * 45 / 100;                                     
                    TP_WORK = TP_WORK * SPT;                                    
                    TP_WORK = TP_WORK * PÅ / OÅ;                                
                    TP_WORK = TP_WORK * UFG /100;                               
                    TP_WORK = TP_WORK * TP_PROSENT / 100;                       
              IF B02.PENSJONSTYPE2(BER_SØ_IND) = 'A' THEN                       
 /*KONSTS*/      DIV_PERIODE.ATTFØRING_TP_ÅRSBELØP = TP_WORK + 0.5;             
                    TP_WORK = TP_WORK / 12;                                     
                 END;                                                           
           END;                                                                 
        IF FORHØY_IND ^= 'N' THEN                                               
           TP_WORK = TP_WORK + 0.5;                                             
     END;                                                                       
  ELSE                                                                          
     TP_WORK = 0;                                                               
  TP = TP_WORK;                                                                 
                                                                                
                                                                                
 END GAMLE_REGLER;                                                              
                                                                                
 SLUTT :                                                                        
   /* IKKE GI KONV 'F' TILLEGGSPENSJON, TRUDE 060192  */                        
                                                                                
   IF B02.BOSATT(BER_SØ_IND)  = 000  &                                          
      B02.TT_16_66(BER_SØ_IND) = 0     &                                        
      B02.TT_ETTER_1966(BER_SØ_IND) = 0 &                                       
      B02.TT_FØR_1967(BER_SØ_IND)   = 0 &                                       
      B02.FRAVIK_FLYKTN(BER_SØ_IND)   = ' ' &                                   
      B02.ALDERSP.KONV_P_KODE(BER_SØ_IND)  = 'F'     &                          
      B02.ALDERSP.KONV_GRAD(BER_SØ_IND)  = 0     THEN                           
        TP  = 0 ;                                                               
                                                                                
                                                                                
  RETURN(TP);                                                                   
                                                                                
 END F_TP92;                                                                    
