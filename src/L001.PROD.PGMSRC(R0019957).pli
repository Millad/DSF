 /*       SIST ENDRET 09/06-98 08.50.21 AV   SPA7339                  */00000000
 /* ***************************************************************** */00000010
 /* IDENTIFIKASJON                                                    */00000020
 /*     R0019957 - P900_NOR_DATA         PROCEDURE I  PLI             */00000030
 /*                                                                   */00000040
 /*     PROGRAMMERER: S. LUNDEBY  FRA    DATA-UTVIKLING A/S  JAN 1985 */00000050
 /*                                                                   */00000060
 /* HENSIKT                                                           */00000070
 /*     SJEKKER OM OPPGITT TKNR ER ET NOR-KONTOR. ER DET TILFELLE     */00000080
 /*     SKRIVES FNR , TKNR OG DATO + TID UT PÅ EN VSAMFILE .          */00000090
 /*     DENNE BRUKES SAMMEN MED DE ANDRE SPØRRE-TRANSENE FRA NORTRYGD */00000100
 /*     OM KVELDEN.                                                   */00000110
 /*                                                                   */00000120
 /* PARAMETRE                                                         */00000130
 /*     FNR OG TKNR                                                   */00000140
 /*                                                                   */00000150
 /*     VED ONLINE INNTEKTS-OPPDATERING REG. KOMMUNENR. ISTEDENFOR    */00000160
 /*     TKNR. FOR NOEN FÅ KOMMUNER ER DISSE FORSKELLIGE. DE ER :      */00000170
 /*                                                                   */00000180
 /*        0703  HORTEN       GÅR   TIL   0701   HORTEN               */00000190
 /*        0717  BORRE            ------   "     ------               */00000200
 /*                                                                   */00000210
 /*        0705  TØNSBERG     GÅR   TIL   0704   TØNSBERG             */00000220
 /*        0721  SEM              ------   "     ------               */00000230
 /*                                                                   */00000240
 /*        0707  LARVIK       GÅR   TIL   0709   LARVIK               */00000250
 /*        0708  STAVERN          ------   "     ------               */00000260
 /*        0725  TJØLLING         ------   "     ------               */00000270
 /*        0726  BRUNLANES        ------   "     ------               */00000280
 /*        0727  HEDRUM           ------   "     ------               */00000290
 /*                                                                   */00000300
 /*                                                                   */00000310
 /*                                                                   */00000320
 /* PROGRAMTILKNYTNING                                                */00000330
 /*     INCLUDE MODUL.                                                */00000340
 /*                                                                   */00000350
 /* ***************************************************************** */00000360
                                                                        00000370
    P900_NOR_DATA:                                                      00000380
       PROC                (W900_FNR , W900_TKNR);                      00000390
                                                                        00000400
                                                                        00000410
       DCL W900_FNR         FIXED DEC (11) UNAL,                        00000420
           W900_TKNR        FIXED DEC ( 4) UNAL;                        00000430
                                                                        00000440
                                                                        00000450
      /*--------------------------------------------------------------*/00000460
      /*  RECORD FOR DATA TIL NORTRYGD OG FOR SJEKK OM NOR-SENTRAL    */00000470
      /*--------------------------------------------------------------*/00000480
                                                                        00000490
                                                                        00000500
       %INCLUDE P0019957;     /***                 NOR_REC          ***/00000510
                                                                        00000520
                                                                        00000530
       DCL 1 NOR_SENTRAL_REC                            ,               00000540
             2 TKNR         PIC      '( 4)9'  INIT ( 0 ),               00000550
             2 KD_SENTRAL   FIXED DEC ( 3)    INIT ( 0 ),               00000560
             2 NOR_SENTRAL  FIXED DEC ( 3)    INIT ( 0 ),               00000570
             2 FILLER1      CHAR      (31)    INIT ( '');               00000580
                                                                        00000590
                                                                        00000600
                                                                        00000610
      /*--------------------------------------------------------------*/00000620
      /*  DIV. HJELPE-OMRÅDER                                         */00000630
      /*--------------------------------------------------------------*/00000640
                                                                        00000650
                                                                        00000660
      DCL I                FIXED BIN (15)     INIT ( 0 ),               00000670
          NOR_REC_RBA      POINTER                      ,               00000680
         (DATE , STG)      BUILTIN                      ,               00000690
          TID              PIC      '( 4)9'   INIT ( 0 ),               00000700
          TID_TIM DEF TID  CHAR      ( 2)     POS  ( 1 ),               00000710
          TID_MIN DEF TID  CHAR      ( 2)     POS  ( 3 ),               00000720
          D                CHAR      ( 6)     INIT ( ''),               00000730
          D_ÅR    DEF D    CHAR      ( 2)     POS  ( 1 ),               00000740
          D_MND   DEF D    CHAR      ( 2)     POS  ( 3 ),               00000750
          D_DAG   DEF D    CHAR      ( 2)     POS  ( 5 ),               00000760
          KEY_FNR          PIC      '(11)9'   INIT ( 0 ),               00000770
          KEY_TKNR         PIC      '( 4)9'   INIT ( 0 );               00000780
                                                                        00000790
                                                                        00000800
                                                                        00000810
                                                                        00000820
      /* ************************************************************ */00000830
      /* *******    EKSEKVERING STARTER HER                ********** */00000840
      /* ************************************************************ */00000850
                                                                        00000860
      PROGRAM_ID              =        'R0019957'                     ; 00000870
                                                                        00000880
                                                                        00000890
      /*--------------------------------------------------------------*/00000900
      /*  ADDRESSERER EIB-OMR.                                        */00000910
      /*  SJEKKER OM DET ER ET NORTRYGD-KONTOR                        */00000920
      /*--------------------------------------------------------------*/00000930
                                                                        00000940
      EXEC CICS  ADDRESS EIB(DFHEIPTR)                                ; 00000950
                                                                        00000960
      SELECT   ( W900_TKNR   )                                        ; 00000970
                                                                        00000980
         WHEN  ( 0703 ! 0717 )                                          00000990
            W900_TKNR         =       0701                            ; 00001000
                                                                        00001010
         WHEN  ( 0705 ! 0721 )                                          00001020
            W900_TKNR         =       0704                            ; 00001030
                                                                        00001040
         WHEN  ( 0707 ! 0708 ! 0725 ! 0726 ! 0727 )                     00001050
            W900_TKNR         =       0709                            ; 00001060
                                                                        00001070
         OTHERWISE                                                    ; 00001080
      END;     /* SELECT   (W900_TKNR)                               */ 00001090
                                                                        00001100
                                                                        00001110
      KEY_TKNR                =          W900_TKNR                    ; 00001120
                                                                        00001130
      EXEC CICS HANDLE CONDITION ERROR  (L900)                        ; 00001140
      EXEC CICS HANDLE CONDITION NOTFND (ALT_OK)                      ; 00001150
      EXEC CICS READ   DATASET ('REGION') INTO   ( NOR_SENTRAL_REC )    00001160
                                          RIDFLD ( KEY_TKNR        )  ; 00001170
                                                                        00001180
                                                                        00001190
      D                 =  DATE                                       ; 00001200
      TID               =  EIBTIME / 100                              ; 00001210
                                                                        00001220
      NOR_REC           =  ''                                         ; 00001230
      NOR_REC.TKNR      =  W900_TKNR                                  ; 00001240
      NOR_REC.RECTYPE   = 'PO'                                        ; 00001250
      NOR_REC.TRANSTYPE =  05                                         ; 00001260
      NOR_REC.ÅR        =  D_ÅR                                       ; 00001270
      NOR_REC.MND       =  D_MND                                      ; 00001280
      NOR_REC.DAG       =  D_DAG                                      ; 00001290
      NOR_REC.TIM       =  TID_TIM                                    ; 00001300
      NOR_REC.MIN       =  TID_MIN                                    ; 00001310
      NOR_REC.FNR       =  W900_FNR                                   ; 00001320
                                                                        00001330
      IF NOR_SENTRAL          >  0                          THEN        00001340
         DO;                                                            00001350
            /*--------------------------------------------------------*/00001360
            /* DET VAR ET NORTRYGD-KONTOR . AKT. DATA SKRIVES         */00001370
            /*--------------------------------------------------------*/00001380
                                                                        00001390
            EXEC CICS HANDLE CONDITION ERROR  (L910)                  ; 00001400
            EXEC CICS HANDLE CONDITION DUPREC (ALT_OK)                ; 00001410
                                                                        00001420
                                                                        00001430
            KEY_FNR           =  W900_FNR                             ; 00001440
            EXEC CICS WRITE DATASET ('NORINFO'    ) FROM ( NOR_REC )    00001450
                            RIDFLD  ( KEY_FNR     )                   ; 00001460
                                                                        00001470
         END;                                                           00001480
                                                                        00001490
         ELSE DO;                                                       00001500
            /*--------------------------------------------------------*/00001510
            /* DET VAR ET INFOTRYGD-KONTOR.  AKT. DATA SKRIVES        */00001520
            /*--------------------------------------------------------*/00001530
                                                                        00001540
            EXEC CICS HANDLE CONDITION ERROR  (L910)                  ; 00001550
            EXEC CICS HANDLE CONDITION DUPREC (ALT_OK)                ; 00001560
                                                                        00001570
                                                                        00001580
            KEY_FNR           =  W900_FNR                             ; 00001590
            EXEC CICS WRITE DATASET ('INFOINFO' ) FROM ( NOR_REC )      00001600
                            RIDFLD  ( KEY_FNR   )                   ;   00001610
                                                                        00001620
         END;                                                           00001630
                                                                        00001640
      GOTO ALT_OK;                                                      00001650
                                                                        00001660
  L900:                                                                 00001670
      FEIL_MELD_NR                =    503 ;                            00001680
      FEIL_VED_LABEL              =   '900';                            00001690
                                                                        00001700
      GOTO ALT_OK;                                                      00001710
                                                                        00001720
  L910:                                                                 00001730
      FEIL_MELD_NR                =    504 ;                            00001740
      FEIL_VED_LABEL              =   '910';                            00001750
                                                                        00001760
  ALT_OK:                                                               00001770
                                                                        00001780
                                                                        00001790
    END P900_NOR_DATA;                                                  00001800
