 /*   SIST ENDRET PÅ PROD   2004.01.27 11.28.38 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2000.04.12 10.11.36 AV   JDA7339          */        
 /*       SIST ENDRET 04/05-98 12.40.53 AV   JDA7339                  */        
 /* ***************************************************************** */        
 /* IDENTIFIKASJON                                                    */        
 /*     R0014134 - F_BEREGN_KOMPENSASJONSTILLEGG - PROSEDYRE I PLI.   */        
 /*     PROGRAMMERER: TOM JØRGENSEN, JUNI-82.                         */        
 /*     SIST ENDRET AV KARIN I JULI 82                                */        
 /* HENSIKT                                                           */        
 /*     BEREGNE KOMPENSASJONSTILLEGG.                                 */        
 /* PROGRAMTILKNYTNING                                                */        
 /*     PROSEDYREN BLIR INKLUDERT I OVERLIGGENDE PROGRAM.             */        
 /* BRUK                                                              */        
 /*     KT = F_BEREGN_KOMPENSASJONSTILLEGG(PENSJONSTYPE1,             */        
 /*             PENSJONSTYPE2,TT_ANV,ANTALL_BARN,KOMPFAKTOR,          */        
 /*             FORH_INDIKATOR)                                       */        
 /* ENDRET FRA :                                                      */        
 /*      PROC (PTYPE2,TT_A,A_B,KOMPFAKTOR,FORH_IND)                   */        
 /*  TIL :                                                            */        
 /*      PROC (PTYPE1,PTYPE2,TT_A,A_B,KOMPFAKTOR,FORH_IND)            */        
 /*  MARS 88 - SATISH                                                 */        
 /* ***************************************************************** */        
 F_BEREGN_KOMPENSASJONSTILLEGG:                                                 
  PROC (PTYPE1,PTYPE2,TT_A,A_B,KOMPFAKTOR,FORH_IND)                             
        RETURNS (FIXED DEC (5,2));                                              
   DCL PTYPE1         CHAR(1);                                                  
   DCL PTYPE2         CHAR(1);                                                  
   DCL TT_A   UNAL    FIXED DEC (3);                                            
   DCL A_B            FIXED DEC (3);                                            
   DCL W_KT           FIXED DEC (9,4);                                          
   DCL KT             FIXED DEC (5,2)   INIT (0);                               
   DCL KOMPFAKTOR     FIXED DEC(3,1);                                           
   DCL FORH_IND       CHAR(1);                                                  
   DCL SATS1          FIXED DEC (3);                                            
   DCL SATS2          FIXED DEC (3);                                            
   DCL SATS3          FIXED DEC (3);                                            
                                                                                
                                                                                
   IF G > 35500               THEN                                              
      GOTO SLUTT;                                                               
                                                                                
                                                                                
   IF G < 35500 THEN                                                            
      DO;                                                                       
         SATS1 = 375;                                                           
         SATS2 = 500;                                                           
         SATS3 = 750;                                                           
      END;                                                                      
   ELSE                                                                         
      DO;                                                                       
         SATS1 = 188;                                                           
         SATS2 = 250;                                                           
         SATS3 = 375;                                                           
      END;                                                                      
                                                                                
                                                                                
   IF G > 6799                           THEN                                   
                                                                                
      DO;                                                                       
        IF PTYPE2 = 'S' THEN                                                    
           DO;                                                                  
                                                                                
                                                                                
               W_KT = SATS1 * TT_A;                                             
               W_KT = W_KT * KOMPFAKTOR;                                        
               W_KT = W_KT / (40 * 12);                                         
           END;                                                                 
        ELSE                                                                    
           IF PTYPE2 = 'F' THEN                                                 
              DO;                                                               
                                                                                
                                                                                
                  W_KT = SATS3 * TT_A;                                          
                  W_KT = W_KT * KOMPFAKTOR;                                     
                  W_KT = W_KT / (40 * 12);                                      
              END;                                                              
           ELSE                                                                 
              IF PTYPE2 = 'P' &                                                 
                 (PTYPE1 = 'B' !                                                
                  PTYPE1 = 'N')  THEN                                           
                 DO;                                                            
                                                                                
                                                                                
                    W_KT = SATS2 * TT_A;                                        
                    W_KT = W_KT / (40 * 12);                                    
                    W_KT = W_KT / A_B;                                          
                 END;                                                           
              ELSE                                                              
                 DO;                                                            
                                                                                
                                                                                
                    W_KT = SATS2 * TT_A;                                        
                    W_KT = W_KT * KOMPFAKTOR;                                   
                    W_KT = W_KT / (40 * 12);                                    
                 END;                                                           
        IF FORH_IND = 'J' THEN                                                  
           KT = W_KT + 0.5;                                                     
        ELSE                                                                    
           KT = W_KT;                                                           
      END;                                                                      
   ELSE                                                                         
      KT = 0;                                                                   
                                                                                
                                                                                
                                                                                
                                                                                
   IF B02.SPESOPPL.BOSATT(BER_SØ_IND) ^= 0          !                           
      B02.RF0PERSN.TKNR  (BER_SØ_IND)  > 2100      THEN                         
      DO;                                                                       
        W_KT = 0  ;                                                             
        KT = W_KT ;                                                             
      END;                                                                      
                                                                                
  SLUTT :                                                                       
                                                                                
   RETURN (KT);                                                                 
 END F_BEREGN_KOMPENSASJONSTILLEGG;                                             
