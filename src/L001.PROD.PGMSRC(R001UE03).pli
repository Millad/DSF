 /*   SIST ENDRET PÅ PROD   2004.03.18  9.21.18 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.03.08 12.45.49 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2003.02.27 12.27.18 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2002.11.21 13.14.57 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2002.04.19 12.14.40 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2000.07.26 12.35.51 AV   JDA7339          */        
 /*   SIST ENDRET PÅ PROD   2000.02.18 14.34.27 AV   JDA7339          */        
 /*       SIST ENDRET 30/11-98 08.55.58 AV   JDA7339                  */        
 /*       SIST ENDRET 30/11-98 08.50.29 AV   JDA7339                  */        
 /*       SIST ENDRET 22/06-98 12.41.09 AV   JDA7339                  */        
 /*       SIST ENDRET 15/06-98 08.00.08 AV   RFA9991                  */        
 /* ***************************************************************** */        
 /*IDENTIFIKASJON:                                                    */        
 /*    R001UE03 - UNDERPROGRAM                                        */        
 /*    PROGRAMMERER: HERMAN, FEB 94                                   */        
 /*HENSIKT:                                                           */        
 /*    PROSEDYREN FORETAR KONTROLL AV DATA FRA ENDRINGSBLANKETTEN     */        
 /*    FELTENE 1,5,6,7,8 OG 9  (ENS,EN_INNTEKT OG EN_SPES)            */        
 /*PROGRAMTILKNYTNING:                                                */        
 /*    PROSEDYREN INNGÅR SOM EN DEL AV BEHANDLING AV ENDRINGS-        */        
 /*    BLANKETTEN.                                                    */        
 /*    INCLUDES I  R001UE01.                                          */        
 /*BRUK:                                                              */        
 /* CALL KONTROLL_EN_SØKER(FEIL_I_EN_SØKER);                          */        
 /*                                                                   */        
 /* ***************************************************************** */        
 /*                                                                   */        
 /*   MODULEN KONTROLLERER EN_SØKER, OG RETURNERER                    */        
 /*   MED FEIL_FUNNET = '1'B HVIS FEIL, '0'B HVIS RIKTIG              */        
 /*                                                                   */        
 /* ***************************************************************** */        
                                                                                
                                                                                
 KONTROLL_EN_SØKER:                                                             
   PROC (FEIL_FUNNET);                                                          
                                                                                
                                                                                
   DCL                                                                          
      FEIL_FUNNET BIT (1);                                                      
                                                                                
 /* ***************************************************************** */        
 /* DIVERSE DEKLARASJONER                                             */        
 /* ***************************************************************** */        
                                                                                
   DCL                                                                          
      DAGENS_DATO_ÅMD                                  PIC '99999999'  ,        
      DAGENS_DATO_ÅM DEF DAGENS_DATO_ÅMD               PIC '999999'    ,        
      DAGENS_DATO_Å  DEF DAGENS_DATO_ÅMD               PIC '9999'      ,        
      DAGENS_DATO_M  DEF DAGENS_DATO_ÅMD POS (3)       PIC '99'        ,        
      DAGENS_DATO_D  DEF DAGENS_DATO_ÅMD POS (5)       PIC '99'        ;        
                                                                                
                                                                                
   DCL                                                                          
      KEY_BIT                    BIT(32) BASED (KEY_PEKER),                     
      KEY_PEKER                  POINTER,                                       
      TK_RECL                    CHAR (101);                                    
                                                                                
 /* ***************************************************************** */        
 /*  HJELPE DCL FOR Å SNU VIRK.DATO                                   */        
 /* ***************************************************************** */        
                                                                                
   DCL                                                                          
      HJ_VIRK_DATO_ÅMD PIC '99999999',                                          
      VIRK_DATO_Å DEF HJ_VIRK_DATO_ÅMD POS(1) PIC '9999',                       
      VIRK_DATO_M DEF HJ_VIRK_DATO_ÅMD POS(5) PIC '99';                         
                                                                                
                                                                                
   DCL                                                                          
      1 FNR_REG,                                                                
        2 FNR1        FIXED DEC(11),                                            
        2 FNR2        FIXED DEC(11),                                            
        2 BRUKERID    CHAR     ( 4);                                            
                                                                                
                                                                                
                                                                                
      FNR_REG.BRUKERID =  DIV_PARAM_OMR.CICS_IND;                               
      ANT_FEIL_SKREVET =  0;                                                    
      FEIL_FUNNET      = '0'B;                                                  
      KEY_PEKER        =  ADDR(ENS.TKNR);                                       
                                                                                
 /* ***************************************************************** */        
 /* DATE () ER EN BUILT-IN-FUNCTION I PLI SOM RETURNERER DAGENS DATO  */        
 /* I FORMEN ÅR/MÅNED/DAG.                                            */        
 /* ***************************************************************** */        
                                                                                
      DAGENS_DATO_ÅMD = DATO_2000;                                              
                                                                                
                                                                                
    HJ_VIRK_DATO_ÅMD = ENS.VIRK_DATO_ÅMD;                                       
                                                                                
                                                                                
                                                                                
 /* ***************************************************************** */        
 /* KONTROLL AV FELT 1 (OPPLYSNINGER OM STØNADSMOTTAKEREN)            */        
 /*                                                                   */        
 /* F_NUMERISK KONTROLLERER OM ET OVERFØRT CHAR FELT ER NUMERISK.     */        
 /* DERSOM FELTET ER NUMERISK RETURNERES '1' B, HVIS IKKE '0'B.       */        
 /* ***************************************************************** */        
                                                                                
      IF ^ F_NUMERISK(F_FELT_PIC_CHAR11(ENS.FNR)) THEN                          
                                                                                
 /* ***************************************************************** */        
 /* F_FELT_PIC_CHAR ER EN FUNKSJON SOM KONVERTERER PIC-FELT TIL CHAR  */        
 /* ***************************************************************** */        
                                                                                
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
                                                                                
 /* ***************************************************************** */        
 /* FEILMELDING 200: TEGN SOM IKKE ER TALL FOREKOMMER I TALLFELTET    */        
 /* ***************************************************************** */        
                                                                                
            ENS.FNRNR   = 200;                                                  
         END;                                                                   
      ELSE                                                                      
                                                                                
      IF ^ F_GYLDIG_FNR(ENS.FNR) THEN                                           
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
                                                                                
 /* ***************************************************************** */        
 /* FEILMELDING 1: FEIL I FØDSELSNUMMER                               */        
 /* ***************************************************************** */        
                                                                                
            ENS.FNRNR   = 1;                                                    
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
            FNR_REG.FNR1 = ENS.FNR;                                             
            EXEC CICS LINK PROGRAM ('R0019906') COMMAREA (FNR_REG);             
            IF FNR_REG.FNR2 > 0 THEN                                            
              DO;                                                               
                                                                                
 /* ***************************************************************** */        
 /* FEILMELDING 203: DETTE FØDSELSNUMMERER ER ENDRET                  */        
 /* ***************************************************************** */        
                                                                                
                ENS.FNRNR   = 203;                                              
                ENS.FNR_GML = ENS.FNR;                                          
                ENS.FNR     = FNR_REG.FNR2;                                     
              END;                                                              
         END;                                                                   
                                                                                
      IF ^ F_NUMERISK(F_FELT_PIC_CHAR4(ENS.TKNR)) THEN                          
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            ENS.TKNRNR  = 200;                                                  
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
            EXEC CICS HANDLE CONDITION NOTFND(NOTFND);                          
            EXEC CICS READ DATASET ('TKNRTAB') RIDFLD(KEY_BIT)                  
                                                     INTO(TK_RECL);             
                  LEAVE;                                                        
   NOTFND:                                                                      
            FEIL_FUNNET = '1'B;                                                 
                                                                                
                  ENS.TKNRNR  = 3;                                              
         END;                                                                   
                                                                                
    /* ************************************************** */                    
    /* KONTROLL AV ACF2 FOR TILLGANGE TIL TKNR            */                    
    /*                                                    */                    
          /* ************************************************** */              
    BO_TKNR  = TKNR;                                                            
 /*  CALL ATK_TILGANG_KONTROLL(FNRNR,TKNRNR); */                                
     CALL ATK_TILGANG_KONTROLL;                                                 
                                                                                
      IF ^ F_NUMERISK(F_FELT_PIC_CHAR8(ENS.VIRK_DATO_ÅMD)) THEN                 
         DO;                                                                    
            FEIL_FUNNET        = '1'B;                                          
            ENS.VIRK_DATO_ÅMDNR= 200;                                           
         END;                                                                   
      ELSE                                                                      
                                                                                
      IF ^ F_GYLDIG_DATO(ENS.VIRK_DATO_ÅMD) THEN                                
         DO;                                                                    
            FEIL_FUNNET        = '1'B;                                          
                                                                                
 /* **************************************************************** */         
 /* FEILMELD 9: UGYLDIG VIRKNINGSDATO                                */         
 /* **************************************************************** */         
                                                                                
            ENS.VIRK_DATO_ÅMDNR = 9;                                            
         END;                                                                   
                                                                                
 /* **************************************************************** */         
 /* INNTEKTSKONTROLL   :                                             */         
 /* **************************************************************** */         
      IF PENSJONSINNT_SØKER > 0 THEN                                            
      IF ^F_NUMERISK(F_FELT_PIC_CHAR7(PENSJONSINNT_SØKER)) THEN                 
         DO;                                                                    
            FEIL_FUNNET        = '1'B;                                          
            PENSJONSINNT_SØKERNR = 200;                                         
         END;                                                                   
                                                                                
      IF ARBEIDSINNT_SØKER > 0 THEN                                             
      IF ^ F_NUMERISK(F_FELT_PIC_CHAR7(ARBEIDSINNT_SØKER)) THEN                 
         DO;                                                                    
            FEIL_FUNNET        = '1'B;                                          
            ARBEIDSINNT_SØKERNR = 200;                                          
         END;                                                                   
                                                                                
      IF PENSJONSINNT_EK    > 0 THEN                                            
      IF ^ F_NUMERISK(F_FELT_PIC_CHAR7(PENSJONSINNT_EK)) THEN                   
         DO;                                                                    
            FEIL_FUNNET        = '1'B;                                          
            PENSJONSINNT_EKNR = 200;                                            
         END;                                                                   
                                                                                
      IF ARBEIDSINNT_EK    > 0 THEN                                             
      IF ^ F_NUMERISK(F_FELT_PIC_CHAR7(ARBEIDSINNT_EK)) THEN                    
         DO;                                                                    
            FEIL_FUNNET        = '1'B;                                          
            ARBEIDSINNT_EKNR = 200;                                             
         END;                                                                   
                                                                                
 /* **************************************************************** */         
 /* EKTESKAPSFORHOLD   :                                             */         
 /* **************************************************************** */         
      IF VERIFY(ENS.SKILSMISSE,'X ') ^= 0 THEN                                  
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
                                                                                
 /* ***************************************************************** */        
 /* FEILMELD 264: FEIL I SKILSMISSE                                   */        
 /* ***************************************************************** */        
                                                                                
            ENS.SKILSMISSENR = 264;                                             
         END;                                                                   
                                                                                
                                                                                
 /* **************************************************************** */         
 /* SIVILSTAND         :                                             */         
 /* **************************************************************** */         
      IF VERIFY(ENS.SIVILSTAND,'GPWV ') ^= 0 THEN   /*0797 HL*/                 
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            ENS.SIVILSTANDNR = 397;                                             
         END;                                                                   
                                                                                
      IF ENS.SIVILSTAND ^= ' ' THEN   /*0797 HL*/                               
         DO;                                                                    
            IF (ENS.FNR_EK ^> 0)   THEN                                         
               DO;                                                              
                  FEIL_FUNNET = '1'B;                                           
                  ENS.FNR_EKNR = 327;                                           
               END;                                                             
            IF ENS.NAVN_EK = (25)' '        THEN                                
               DO;                                                              
                  FEIL_FUNNET = '1'B;                                           
                  ENS.NAVN_EKNR = 154;                                          
               END;                                                             
   /*9802 HL : */                                                               
            IF ENS.SIVILSTAND = 'P'         THEN                                
               DO;                                                              
                  IF F_KJØNN(ENS.FNR) ^= F_KJØNN(ENS.FNR_EK)  THEN              
                     DO;                                                        
                        FEIL_FUNNET = '1'B;                                     
                        ENS.FNR_EKNR = 392;                                     
                     END;                                                       
               END;                                                             
            ELSE                                                                
               DO;                                                              
                  IF (F_KJØNN(ENS.FNR) = F_KJØNN(ENS.FNR_EK) &                  
                     ENS.SIVILSTAND ^= 'V')    THEN                             
                     DO;                                                        
                        FEIL_FUNNET = '1'B;                                     
                        ENS.FNR_EKNR = 16;                                      
                     END;                                                       
               END;                                                             
   /*HIT 9802*/                                                                 
         END;                                                                   
                                                                                
                                                                                
      IF VERIFY(ENS.FLYTTET_SAMMEN,'X ') ^= 0 THEN                              
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
                                                                                
 /* ***************************************************************** */        
 /* FEILMELD 264: FEIL I FLYTTET SAMMEN.                              */        
 /* ***************************************************************** */        
                                                                                
            ENS.FLYTTET_SAMMENNR = 264;                                         
         END;                                                                   
                                                                                
                                                                                
                                                                                
      IF VERIFY(ENS.FLYTTET_FRA,'X ') ^= 0 THEN                                 
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            ENS.FLYTTET_FRANR = 264;                                            
         END;                                                                   
                                                                                
      IF VERIFY(ENS.OPPHØR_SAMBO,'X ') ^= 0 THEN                                
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
            ENS.OPPHØR_SAMBONR = 264;                                           
         END;                                                                   
                                                                                
      IF HJ_VIRK_DATO_ÅMD > 19980100      THEN                                  
         DO;                                                                    
            IF VERIFY (ENS.EK_INNT_OVER_2G,'JN ') ^= 0 THEN                     
               DO;                                                              
                  FEIL_FUNNET = '1'B;                                           
                  ENS.EK_INNT_OVER_2GNR = 1541;                                 
               END;                                                             
            IF ENS.EK_INNT_OVER_2G = ' '       THEN                             
               DO;                                                              
                  IF (ENS.SIVILSTAND = 'G' !                                    
                      ENS.SIVILSTAND = 'W' !                                    
                      ENS.SIVILSTAND = 'P')         THEN                        
                      DO;                                                       
                         FEIL_FUNNET = '1'B;                                    
                         ENS.EK_INNT_OVER_2GNR = 272;                           
                      END;                                                      
               END;                                                             
   /* NYTT FELT 200107 : */                                                     
            IF VERIFY (ENS.RED_GP_3_2_5,'JN ') ^= 0 THEN                        
               DO;                                                              
                  FEIL_FUNNET = '1'B;                                           
                  ENS.RED_GP_3_2_5NR = 1541;                                    
               END;                                                             
            IF ENS.RED_GP_3_2_5 ='J'          THEN                              
               DO;                                                              
                  IF (ENS.SIVILSTAND = 'G' !                                    
                      ENS.SIVILSTAND = 'P' !                                    
                      ENS.SIVILSTAND = 'W'  )       THEN                        
                      DO;                                                       
                         FEIL_FUNNET        = '1'B;                             
                         ENS.RED_GP_3_2_5NR = 616 ;                             
                      END;                                                      
               END;                                                             
            ELSE                                                                
            IF ENS.RED_GP_3_2_5 =' '          THEN                              
               DO;                                                              
                  IF (ENS.SIVILSTAND = 'U' !                                    
                      ENS.SIVILSTAND = 'S' !                                    
                      ENS.SIVILSTAND = 'V' !                                    
                      ENS.SIVILSTAND = 'A' !                                    
                      ENS.SIVILSTAND = 'E'  )       THEN                        
                      DO;                                                       
                         FEIL_FUNNET        = '1'B;                             
                         ENS.RED_GP_3_2_5NR = 618 ;                             
                      END;                                                      
               END;                                                             
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
            ENS.EK_INNT_OVER_2G = ' ';                                          
            ENS.RED_GP_3_2_5 = ' ';                                             
            IF ENS.SIVILSTAND = 'V'       THEN                                  
               DO;                                                              
                  FEIL_FUNNET = '1'B;                                           
                  ENS.SIVILSTANDNR = 445;                                       
               END;                                                             
         END;                                                                   
                                                                                
 /* **************************************************************** */         
 /* KONTROLL AV FELT 7 (ENDRING I EKTESKAPSFORHOLD)                  */         
 /* **************************************************************** */         
                                                                                
      IF ENS.FNR_EK  > 0 THEN                                                   
         DO;                                                                    
            IF    ENS.SKILSMISSE     = 'X'                                      
                                            THEN                                
               DO;                                                              
                  FEIL_FUNNET          = '1'B;                                  
                                                                                
 /* **************************************************************** */         
 /* FEILMELD 69: FLERE ENN ET FELT ER UTFYLT                         */         
 /* **************************************************************** */         
                                                                                
                  ENS.SKILSMISSENR     = 69;                                    
                  ENS.FLYTTET_FRANR    = 999;                                   
                  ENS.FLYTTET_SAMMENNR = 999;                                   
               END;                                                             
                                                                                
            IF ^ F_NUMERISK(F_FELT_PIC_CHAR11(ENS.FNR_EK)) THEN                 
              DO;                                                               
                 FEIL_FUNNET  = '1'B;                                           
                 ENS.FNR_EKNR = 200;                                            
              END;                                                              
            ELSE                                                                
               IF ^ F_GYLDIG_FNR(ENS.FNR_EK) THEN                               
                  DO;                                                           
                     FEIL_FUNNET  = '1'B;                                       
                                                                                
 /* **************************************************************** */         
 /* FEILMELD 15: FEIL I EKTEFELLENS FØDSELSNUMMER                    */         
 /* **************************************************************** */         
                                                                                
                     ENS.FNR_EKNR = 15;                                         
                  END;                                                          
               ELSE                                                             
               IF ENS.FNR_EK > 0 &  ENS.FNR = ENS.FNR_EK THEN                   
                  DO;                                                           
                     FEIL_FUNNET  = '1'B;                                       
                     ENS.FNR_EKNR = 16;                                         
                  END;                                                          
                ELSE                                                            
                  DO;                                                           
                     FNR_REG.FNR1 = ENS.FNR_EK;                                 
                     EXEC CICS LINK PROGRAM ('R0019906')                        
                     COMMAREA (FNR_REG);                                        
                     IF FNR_REG.FNR2 > 0 THEN                                   
                       DO;                                                      
                         ENS.FNR_EKNR   = 203;                                  
                         ENS.FNR_GML_EK = ENS.FNR_EK;                           
                         ENS.FNR_EK     = FNR_REG.FNR2;                         
                       END;                                                     
                  END;                                                          
            IF ENS.NAVN_EK = (25)' ' THEN                                       
               DO;                                                              
                  FEIL_FUNNET = '1'B;                                           
                                                                                
 /* **************************************************************** */         
 /* FEILMELD 154: EKTEFELLENS NAVN ER IKKE UTFYLT                    */         
 /* **************************************************************** */         
                                                                                
                  ENS.NAVN_EKNR = 154;                                          
               END;                                                             
            IF ENS.SIVILSTAND = ' '           THEN                              
               DO;                                                              
                  FEIL_FUNNET = '1'B;                                           
                                                                                
 /* **************************************************************** */         
 /* FEILMELD 397: SIVILSTAND SKAL VÆRE G,P,W ELLER V   9802 HL       */         
 /* **************************************************************** */         
                                                                                
                  ENS.SIVILSTANDNR = 397;                                       
               END;                                                             
                                                                                
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
            IF (ENS.SKILSMISSE  = 'X' & ENS.FLYTTET_FRA    = 'X') !             
               (ENS.SKILSMISSE  = 'X' & ENS.FLYTTET_SAMMEN = 'X') !             
               (ENS.FLYTTET_FRA = 'X' & ENS.FLYTTET_SAMMEN = 'X') THEN          
               DO;                                                              
                  FEIL_FUNNET          = '1'B;                                  
                  ENS.SKILSMISSENR     = 69;                                    
                  ENS.FLYTTET_FRANR    = 999;                                   
                  ENS.FLYTTET_SAMMENNR = 999;                                   
               END;                                                             
         END;                                                                   
                                                                                
 /* ***************************************************************** */        
 /* KONTROLL AV FELT 7 (ENDRING I SPRÅKFORM)                          */        
 /* ***************************************************************** */        
                                                                                
      IF VERIFY(ENS.SPRÅK,'NBE ') ^= 0 THEN                                     
         DO;                                                                    
            FEIL_FUNNET = '1'B;                                                 
                                                                                
 /* ***************************************************************** */        
 /* FEILMELD 6: FEIL I SPRÅK                                          */        
 /* ***************************************************************** */        
                                                                                
            ENS.SPRÅKNR = 6;                                                    
         END;                                                                   
                                                                                
 /* ***************************************************************** */        
 /*   KRYSSKONTROLLER .  TESTEN ER UTVIDET TIL Å GODTA VIRK.DATO      */        
 /*   40 DAGER FØR DAGENS DATO.                                       */        
 /* ***************************************************************** */        
                                                                                
      IF ^FEIL_FUNNET THEN                                                      
                                                                                
          DO;                                                                   
                                                                                
              HJ_VIRK_DATO_ÅMD   =  ENS.VIRK_DATO_ÅMD   ;                       
                                                                                
 /* ENDRET 22.6.88 HL - VI AKSEPTERER VIRKDATO 6 MNDR FREM I TID   */           
                                                                                
                  IF ^ F_6MDR(HJ_VIRK_DATO_ÅMD)     THEN                        
                                                                                
                     DO;                                                        
                        FEIL_FUNNET           = '1'B;                           
                        ENS.VIRK_DATO_ÅMDNR   = 205;                            
                     END;                                                       
                                                                                
                                                                                
          END;                                                                  
                                                                                
                                                                                
    IF EN_INNTEKT.MINST_4G = 'J'   THEN                                         
       DO;                                                                      
          IF ENS.VIRK_DATO_ÅMD  < 19840700 THEN                                 
             DO;                                                                
                FEIL_FUNNET = '1'B;                                             
                ENS.VIRK_DATO_ÅMDNR= 273;                                       
             END;                                                               
          ELSE                                                                  
          IF ENS.VIRK_DATO_ÅMD >19910400 THEN                                   
             DO;                                                                
                FEIL_FUNNET = '1'B;                                             
                ENS.VIRK_DATO_ÅMDNR= 487;                                       
             END;                                                               
       END;                                                                     
                                                                                
 /* ***************************************************************** */        
 /* LEGGER FNR OG VIRK_DATO INN I MENY_OMR I KOM_OMR.                 */        
 /* ***************************************************************** */        
                                                                                
     FØDSNUMMER    = ENS.FNR;                                                   
     VIRKNINGSDATO_ÅMD = ENS.VIRK_DATO_ÅMD;                                     
   END KONTROLL_EN_SØKER;                                                       
