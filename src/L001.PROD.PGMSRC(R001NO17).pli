 /*   SIST ENDRET PÅ PROD   2000.02.18 13.05.00 AV   JDA7339          */        
 /*       SIST ENDRET 28/08-98 15.56.57 AV   SPA7339                  */        
 /*       SIST ENDRET 17/06-98 14.40.51 AV   HLA7339                  */        
 /* ***************************************************************** */        
 /*IDENTIFIKASJON:                                                    */        
 /* **********************                                            */        
 /*  PROGRAM-IDENT : R001NO17  UNDERPROGRAM SOM BRUKES AV R001NO10.   */        
 /*                            KONTROLL O1-BLANKETT.                  */        
 /*  PROGRAM-SPRÅK : PLI                                              */        
 /*  PROGRAMMERER  : BRITT FOSSUM                                     */        
 /*  PROGRAMMET BLE LAGET : OKTOBER 1986                              */        
 /*  ENDRINGERSDATO :                                                 */        
 /*  ENDRINGEN GJELDER:                                               */        
 /*  ENDRINGEN BLE UTFØRT AV :                                        */        
 /*                                                                   */        
 /* ***************************************************************** */        
 /*BRUK:                                                              */        
 /* ********                                                          */        
 /*                                                                   */        
 /*  %INCLUDE R001NO17                                                */        
 /*                                                                   */        
 /*  INCLUDE PÅ FØLGENDE PROSEDYRER :                                 */        
 /*                                                                   */        
 /*    R0011602   BLANK O1 TRANSOMR                                   */        
 /*    R0011603   KONTR O1 SØKER                                      */        
 /*    FUNKSJONER SOM BRUKES ER INKLUDERT I HOVEDPROGRAMMET.          */        
 /*                                                                   */        
 /* ***************************************************************** */        
 /*                                                                   */        
 /*  PROSEDYREN BLANKER TRANSOMRÅDE FOR O1 OG FLYTTER DERETTER        */        
 /*  VERDIER FRA OVERFØRT RECORD TIL TRANSOMRÅDET.  TRANSEN BLIR      */        
 /*  KONTROLLERT.  DERSOM DET BLIR FUNNET FEIL, BLIR FEILMELDINGS-    */        
 /*  NUMRE TATT VARE PÅ.                                              */        
 /*                                                                   */        
 /* ***************************************************************** */        
                                                                                
 P116_BEHANDLE_O1: PROC;                                                        
                                                                                
                                                                                
 %INCLUDE P0011601;                               /* TRANSOMR O1 */             
                                                                                
                                                                                
          FEIL_I_BOSATT = '0'B;                                                 
          FEIL_BLANKETT = '0'B;                                                 
          FEIL_FUNNET   = '0'B;                                                 
          CALL BLANK_O1_OPPHØR;                  /* R0011602 */                 
          CALL BLANK_O1_MELDNR;                  /* R0011602 */                 
          CALL OVERFØR_O1_BLANKETT;                                             
          CALL KONTROLL_O1(FEIL_FUNNET);         /* R0011603 */                 
                                                                                
          IF FEIL_FUNNET     THEN                                               
             DO;                                                                
                CALL FINN_FEIL_O1;                                              
             END;                                                               
                                                                                
                                                                                
                                                                                
 /* ***************************************************************** */        
 /*   PROSEDYRE FOR Å OVERFØRE FELT FRA OVERFØRINGSRECORD             */        
 /*   TIL O1_TRANS_OMR I KOM_OMR FOR OPPHØRSBLANKETT 1.               */        
 /* ***************************************************************** */        
 OVERFØR_O1_BLANKETT: PROC;                                                     
                                                                                
                                                                                
          O1.REGDATO_ÅMD  = BLANKETT_RECORD.TRANS_DATO_ÅMD;                     
          O1.TERMID       = DIV_PARAM_OMR.TERMINAL_NR;                          
          O1.FNR_DØD      = BLANKETT_RECORD.FNR;                                
          O1.TKNR_DØD     = BLANKETT_RECORD.TKNR;                               
          O1.DØDSDATO_ÅMD = O1_BLANKETT.DØDSDATO_ÅMD;  /*2000*/                 
                                                                                
          IF F_FELT_PIC_CHAR11(O1_BLANKETT.FNR_EK) ^= (11)' '    THEN           
             O1.FNR_EK    = O1_BLANKETT.FNR_EK;                                 
                                                                                
          IF F_FELT_PIC_CHAR5(O1_BLANKETT.PI_DØD) ^= (5)' '      THEN           
             O1.PI_DØD    = O1_BLANKETT.PI_DØD * 100;                           
                                                                                
                                                                                
   END OVERFØR_O1_BLANKETT;                                                     
                                                                                
                                                                                
 /* ***************************************************************** */        
 /*   PROSEDYRE SOM FINNER OG TAR VARE PÅ FEILMELDINGSNUMMER HVIS DET */        
 /*   ER FEIL I DEN OVERFØRTE REKORDEN.                               */        
 /* ***************************************************************** */        
 FINN_FEIL_O1:PROC;                                                             
                                                                                
                                                                                
          FEIL_IND = 1;                                                         
                                                                                
          IF O1.FNR_DØDNR ^= 0   &                                              
             O1.FNR_DØDNR ^= 999 THEN                                           
             DO;                                                                
                W01_FEIL_NR(FEIL_IND) = O1.FNR_DØDNR;                           
                FEIL_IND = FEIL_IND + 1;                                        
             END;                                                               
                                                                                
          IF O1.TKNR_DØDNR ^= 0   &                                             
             O1.TKNR_DØDNR ^= 999 THEN                                          
             DO;                                                                
                W01_FEIL_NR(FEIL_IND) = O1.TKNR_DØDNR;                          
                FEIL_IND = FEIL_IND + 1;                                        
             END;                                                               
                                                                                
          IF O1.DØDSDATO_ÅMDNR ^= 0   &                                         
             O1.DØDSDATO_ÅMDNR ^= 999 THEN                                      
             DO;                                                                
                W01_FEIL_NR(FEIL_IND) = O1.DØDSDATO_ÅMDNR;                      
                IF FEIL_IND ^= 3 THEN                                           
                   FEIL_IND  = FEIL_IND + 1;                                    
             END;                                                               
                                                                                
          IF O1.FNR_EKNR ^= 0   &                                               
             O1.FNR_EKNR ^= 999 THEN                                            
             DO;                                                                
                W01_FEIL_NR(FEIL_IND) = O1.FNR_EKNR;                            
                IF FEIL_IND ^= 3 THEN                                           
                   FEIL_IND  = FEIL_IND + 1;                                    
             END;                                                               
                                                                                
          IF O1.PI_DØDNR ^= 0   &                                               
             O1.PI_DØDNR ^= 999 THEN                                            
             DO;                                                                
                W01_FEIL_NR(FEIL_IND) = O1.PI_DØDNR;                            
                IF FEIL_IND ^= 3 THEN                                           
                   FEIL_IND  = FEIL_IND + 1;                                    
             END;                                                               
                                                                                
                                                                                
    END FINN_FEIL_O1;                                                           
                                                                                
                                                                                
                                                                                
 %PAGE;                                                                         
 /* ***************************************************************** */        
 /*          E K S T E R N E    P R O C E D U R E R                   */        
 /* ***************************************************************** */        
                                                                                
                                                                                
 %PAGE;                                                                         
 %INCLUDE R0011602;          /*     BLANK O1 TRANSOMR   */                      
                                                                                
 %PAGE;                                                                         
 %INCLUDE R0011603;          /*     KONTR O1 OPPHØR     */                      
                                                                                
                                                                                
 END P116_BEHANDLE_O1;                                                          
                                                                                
                                                                                
