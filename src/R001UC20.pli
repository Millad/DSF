 /*   SIST ENDRET PÅ PROD   2004.06.30 13.33.32 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.03.04  9.21.49 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.07.19  7.29.51 AV   HLA2970          */        
 /*   SIST ENDRET PÅ PROD   2000.03.31  7.40.53 AV   JDA7339          */        
 /*       SIST ENDRET 07/05-99 08.41.03 AV   JDA7339                  */        
 /*       SIST ENDRET 07/05-99 08.38.02 AV   JDA7339                  */        
 /*       SIST ENDRET 22/04-99 10.16.13 AV   JDA7339                  */        
 /*       SIST ENDRET 22/04-99 10.15.23 AV   JDA7339                  */        
 /*       SIST ENDRET 26/03-99 13.24.33 AV   JDA7339                  */        
 /*       SIST ENDRET 26/02-99 09.13.46 AV   JDA7339                  */        
 /*       SIST ENDRET 23/11-98 12.42.49 AV   JDA7339                  */        
 /*       SIST ENDRET 23/11-98 12.40.59 AV   JDA7339                  */        
 /*       SIST ENDRET 09/06-98 08.41.09 AV   SPA7339                  */        
 /*       SIST ENDRET 26/05-98 08.48.28 AV   HLA7339                  */        
 /*IDENTIFIKASJON:                                                    */        
 /*    R001UC20 - HOVED-PROSEDYRE I PLI                               */        
 /*    PROGRAMMERER: HERMAN         NOV 93                            */        
 /*HENSIKT:                                                           */        
 /*    FORSØRGINGSTILLEGG EKTEFELLE / BARN  NY BLANKETT FO            */        
 /*    RUTINEN KONTROLLERER ALLE PERSONER I TRANSAKSJONEN MOT         */        
 /*    INFORMASJON I REGISTERET.  NÅR DET OPPDAGES UOVERENSSTEMMELSER */        
 /*    BLIR FEIL-MELD_NR SATT OG VI GÅR UT AV PGM.  FOR HVER FAMILIE- */        
 /*    PERSON SOM GÅR FEILFRI GJENNOM KONTROLLENE BLIR DET OPPRETTET  */        
 /*    NY STATUS FOR PERSONEN, OG SØKERENS STATUS OPPDATERES.         */        
 /* ****************************************************************  */        
 /*PROGRAMTILKNYTNING:                                                */        
 /*    KALLES OPP AV PROGRAM R0013520                                 */        
 /*BRUK:                                                              */        
 /*    EXEC CICS XCTL PROGRAM ('R001UC20') COMMAREA (KOM_OMR)         */        
 /*                                                                   */        
 /* ***************************************************************** */        
 %SKIP;                                                                         
 KON_FO:                                                                        
   PROC (COMMAREA_PEKER) OPTIONS (MAIN);                                        
 %SKIP;                                                                         
      %INCLUDE P0019908;   /* KOM_OMR  BASED(COMMAREA_PEKER)        */          
      %INCLUDE P0019906;   /* TRANS_OPPL_OMR BASED(TRANS_OPPL_PEKER)*/          
      %INCLUDE P001UC01;                                                        
      %INCLUDE P0019910;   /* STYRINGS_OMR  BASED(STYRINGS_PEKER)   */          
      %INCLUDE P0019912;   /* DIV_PARAM_OMR  BASED(DIV_PARAM_PEKER) */          
                                                                                
      DCL 1 B01 BASED(B01_PEKER), %INCLUDE P0019921;                            
      DCL 1 B02 BASED(B02_PEKER), %INCLUDE P0019921;                            
      DCL COMMAREA_PEKER PTR;                                                   
 /*------------------------------------------------------------*/               
 /*  HJELPE DCL FOR Å SNU DATO                                 */               
 /*------------------------------------------------------------*/               
                                                                                
   DCL  /*2000 : */                                                             
      HJ_VIRK_DATO_ÅMD       PIC '99999999',                                    
      VIRK_DATO_ÅM DEF HJ_VIRK_DATO_ÅMD POS(1) PIC '999999';                    
                                                                                
   DCL ALDER          FIXED DEC (5) INIT (0);                                   
   DCL ALDER_EK       FIXED DEC (5) INIT (0);                                   
   DCL FNR_EK_FIX     FIXED DEC (11) INIT (0);                                  
   DCL                                                                          
      (                                                                         
      BARN_IND,                                                                 
      I,                                                                        
      J                                                                         
      )               FIXED    BIN (15),                                        
      (                                                                         
      ADDR,                                                                     
      CSTG,                                                                     
      MOD                                                                       
      )               BUILTIN;                                                  
   DCL W_TT_MND       FIXED DEC (3) INIT (0);                                   
                                                                                
   DCL W_FNR11        PIC '(11)9';                                              
   DCL W_FNR13        PIC '(13)9';                                              
   DCL  1  FNR_R13    DEF W_FNR13,                                              
          2 DAG       PIC   '99',                                               
          2 MND       PIC   '99',                                               
          2 ÅR        PIC   '9999',                                             
          2 PERSNR,                                                             
            3 ÅRHUNDRE PIC '999',                                               
            3 REST     PIC '99';                                                
                                                                                
   DCL W_FNR11_EK     PIC '(11)9';                                              
   DCL W_FNR13_EK      PIC '(13)9';                                             
   DCL  1  FNR_R13_EK  DEF W_FNR13_EK,                                          
          2 DAG   PIC   '99',                                                   
          2 MND   PIC   '99',                                                   
          2 ÅR    PIC   '9999',                                                 
          2 PERSNR,                                                             
            3 ÅRHUNDRE PIC '999',                                               
            3 REST     PIC '99';                                                
                                                                                
    /* == UC20 == START ========================================== */           
     PROGRAM_ID = 'R001UC20';                                                   
     BARN_IND   = 0;                                                            
     TRANSTYPE  = '6';                                                          
                                                                                
     W_FNR11    = B02.FNR(SØKER_IND);                                           
     W_FNR11_EK = B02.FNR(EKTEF_IND);                                           
     W_FNR13    = KONV_FNR11_FNR13(W_FNR11);                                    
     W_FNR13_EK = KONV_FNR11_FNR13(W_FNR11_EK);                                 
                                                                                
         HJ_VIRK_DATO_ÅMD = VIRKNINGSDATO_ÅMD           ; /*2000*/              
         IF B01.STATUS_KODE_HIST(SØKER_IND) ^= ' '     THEN                     
            DO;                                                                 
 L090:                                                                          
               FEIL_VED_LABEL = '090';                                          
               FEIL_MELD_NR   = 1501;                                           
               GO TO L999;                                                      
            END;                                                                
                                                                                
      IF  B01.PENSJONSTYPE2(SØKER_IND) = 'A'       THEN                         
            DO;                 /* 9905 */                                      
 L092:                                                                          
               FEIL_VED_LABEL = '092';                                          
               FEIL_MELD_NR   = 1010;                                           
               GO TO L999;                                                      
            END;                                                                
                                                                                
 /* ***************************************************************** */        
 /* SØKEREN STÅR REGISTRERT SOM ALDERS- ELLER UFØREPENSJONIST         */        
 /* ELLER AFP-PENSJONIST                                              */        
 /* ***************************************************************** */        
      IF (B01.PENSJONSTYPE1(SØKER_IND) = 'A' !                                  
          B01.PENSJONSTYPE1(SØKER_IND) = 'K' !                                  
          B01.PENSJONSTYPE1(SØKER_IND) = 'U' !                                  
          B01.PENSJONSTYPE1(SØKER_IND) = 'Y') THEN                              
         DO;                                                                    
                                                                                
         IF B01.TKNR(SØKER_IND) ^= FOS.TKNR THEN                                
            B02.TKNR(SØKER_IND) = FOS.TKNR;                                     
 /*         DO                                                                  
 L100: FJERNA 170701 MARTIN                                                     
               FEIL_VED_LABEL = '100'                                           
               FEIL_MELD_NR   = 1702                                            
               GO TO L999                                                       
            END  */                                                             
                                                                                
  /* MÅ HA INN OPPL   (FOS.TILL_EK            = ' ' &                           
     OM BARN !!!       FOS.BT_ANT             =  0 )  THEN                      
                      GO TO L999;    */                                         
                                                                                
 /* ***************************************************************** */        
 /* DERSOM DET ER EKTEFELLE I TRANSEN.                                */        
 /* ***************************************************************** */        
                                                                                
            IF FOS.FNR_EK > 0 THEN                                              
               DO;                                                              
                                                                                
                                                                                
 /* ***************************************************************** */        
 /* DERSOM SØKERS SIVILSTAND ER UGIFT.                                */        
 /* ***************************************************************** */        
                                                                                
                  SELECT (B01.SIVILSTAND(SØKER_IND));                           
                     WHEN ('U')                                                 
                        DO;                                                     
 L120:                                                                          
                           FEIL_VED_LABEL = '120';                              
                           FEIL_MELD_NR   = 1001;                               
                           GO TO L999;                                          
                        END;                                                    
                                                                                
 /* ***************************************************************** */        
 /* DERSOM SØKER ER SKILT.                                            */        
 /* ***************************************************************** */        
                                                                                
                     WHEN ('S')                                                 
                        DO;                                                     
 L130:                                                                          
                           FEIL_VED_LABEL = '130';                              
                           FEIL_MELD_NR   = 1002;                               
                           GO TO L999;                                          
                        END;                                                    
                                                                                
 /* ***************************************************************** */        
 /* DERSOM SØKER ER ETTERLATT.                                        */        
 /* ***************************************************************** */        
                                                                                
                     WHEN ('E')                                                 
                        DO;                                                     
 L140:                                                                          
                           FEIL_VED_LABEL = '140';                              
                           FEIL_MELD_NR   = 1003;                               
                           GO TO L999;                                          
                        END;                                                    
                                                                                
 /* ***************************************************************** */        
 /* ELLERS ER SØKEREN GIFT.                                           */        
 /* ***************************************************************** */        
                                                                                
                     OTHERWISE                                                  
                        DO;                                                     
                                                                                
 /* ***************************************************************** */        
 /* DERSOM DET ER SAMME EKTEFELLE SOM I SØKERENS PESTATUS.            */        
 /* ***************************************************************** */        
    DCL HL_B01_FNR PIC'(11)9';                                                  
    DCL HL_FOS_FNR PIC '(11)9';                                                 
    HL_B01_FNR = B01.FNR(EKTEF_IND);                                            
    HL_FOS_FNR = FOS.FNR_EK;                                                    
                                                                                
                           IF B01.FNR(EKTEF_IND) = FOS.FNR_EK THEN              
                              DO;                                               
 /* 9926 */                      CALL FLYTT_B01_TIL_B02(SØKER_IND);             
                                                                                
                                 IF FEIL_MELD_NR > 0 THEN                       
                                    GO TO L999;                                 
                                 ELSE                                           
                                    PROGRAM_ID = 'R001UC20';                    
                                                                                
 /* 9926 */                      CALL FLYTT_B01_TIL_B02(EKTEF_IND);             
                                                                                
                                 IF FEIL_MELD_NR > 0 THEN                       
                                    GO TO L999;                                 
                                 ELSE                                           
                                    PROGRAM_ID = 'R001UC20';                    
                                                                                
 /* ***************************************************************** */        
 /* DERSOM DET SØKES OM TILLEGG FOR EKTEFELLE.                        */        
 /* ***************************************************************** */        
                                                                                
                                 IF FOS.TILL_EK = 'J' THEN                      
                                                                                
 /* ***************************************************************** */        
 /* DERSOM EKTEFELLE HAR PENSJON.                                     */        
 /* ***************************************************************** */        
                                                                                
                            IF (B01.PENSJONSTYPE1(EKTEF_IND) = 'A'  &           
   /*TILLEGG 150796 HL*/        B01.P67_KODE(EKTEF_IND) ^= '5')       !         
                                       B01.PENSJONSTYPE1(EKTEF_IND) =           
                                                              'U'     !         
                                       B01.PENSJONSTYPE1(EKTEF_IND) =           
                                                              'K'     !         
                                       B01.PENSJONSTYPE1(EKTEF_IND) =           
                                                              'Y' THEN          
                                       DO;                                      
 L150:                                                                          
                                          FEIL_VED_LABEL = '150';               
                                          FEIL_MELD_NR   = 1011;                
                                          GO TO L999;                           
                                       END;                                     
                                                                                
                                 CALL                                           
 /* UC22 */                          AJOURFØR_B02_MED_FO_TRANS;                 
                                                                                
                              END;                                              
                                                                                
 /* ***************************************************************** */        
 /* IKKE SAMME EKTEFELLE.                                             */        
 /* ***************************************************************** */        
                                                                                
                           ELSE                                                 
                              DO;                                               
 L160:                                                                          
                                 FEIL_VED_LABEL = '160';                        
                                 FEIL_MELD_NR   = 1004;                         
                                 GO TO L999;                                    
                              END;                                              
                        END;                                                    
                  END;  /*SELECT SIVILSTAND */                                  
               END;     /*IF FOS.FNR_EK > 0*/                                   
                                                                                
 /* ***************************************************************** */        
 /* INGEN EKTEFELLE I TRANS.                                          */        
 /* ***************************************************************** */        
                                                                                
            ELSE                                                                
               DO;                                                              
                                                                                
 /* ***************************************************************** */        
 /* DERSOM SØKEREN ER REGISTRERT SOM GIFT.   + P/W 0195 HL :          */        
 /* ***************************************************************** */        
                                                                                
                                                                                
                  IF B01.SIVILSTAND(SØKER_IND) = 'G' !                          
                     B01.SIVILSTAND(SØKER_IND) = 'P' !                          
                     B01.SIVILSTAND(SØKER_IND) = 'W' !                          
                     B01.SIVILSTAND(SØKER_IND) = 'V' !                          
                     B01.SIVILSTAND(SØKER_IND) = 'A' THEN                       
                                                                                
                     DO;                                                        
                        IF B01.FNR(EKTEF_IND)  >  0  &                          
                           FOS.TILL_EK        ^= 'J' THEN                       
                           DO;                                                  
                              FOS.FNR_EK = B01.FNR(EKTEF_IND);                  
 /* 9926 */                   CALL FLYTT_B01_TIL_B02(SØKER_IND);                
                                                                                
                              IF FEIL_MELD_NR > 0 THEN                          
                                 GO TO L999;                                    
                              ELSE                                              
                                 PROGRAM_ID = 'R001UC20';                       
                                                                                
 /* 9926 */                   CALL FLYTT_B01_TIL_B02(EKTEF_IND);                
                                                                                
                              IF FEIL_MELD_NR > 0 THEN                          
                                 GO TO L999;                                    
                              ELSE                                              
                                 PROGRAM_ID = 'R001UC20';                       
                           END;                                                 
                        ELSE                                                    
                           DO;                                                  
 L170:                                                                          
                              FEIL_VED_LABEL = '170';                           
                              FEIL_MELD_NR = 1102;                              
                              GO TO L999;                                       
                           END;                                                 
                     END;                                                       
                                                                                
                                                                                
                  ELSE                                                          
                     DO;                                                        
                                                                                
 /* 9926 */             CALL FLYTT_B01_TIL_B02(SØKER_IND);                      
                                                                                
                        IF FEIL_MELD_NR > 0 THEN                                
                           GO TO L999;                                          
                        ELSE                                                    
                           PROGRAM_ID = 'R001UC20';                             
                                                                                
                        IF B01.PENSJONSTYPE2 (SØKER_IND) =  'E'    THEN         
                         DO;                                                    
                           B02.PERSON(EKTEF_IND)= B01.PERSON(EKTEF_IND);        
 /* RULLERING AV FORVENTET            - 9803 HL */                              
        IF B02.ETTEPENS.FORVENTET(SØKER_IND) > 0   THEN                         
           CALL RULL_FORVENTET(B02.G_DATO_ÅMD(SØKER_IND), /*2000*/              
                    B02.VIRK_DATO_ÅMD(SØKER_IND),         /*2000*/              
                    B02.ETTEPENS.FORVENTET(SØKER_IND));                         
                                                                                
                         END;                                                   
                                                                                
 /* UC22 */             CALL AJOURFØR_B02_MED_FO_TRANS;                         
                                                                                
                     END;                                                       
               END;                                                             
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
 L175:                                                                          
            FEIL_VED_LABEL = '175';                                             
            FEIL_MELD_NR   = 1010;                                              
            GO TO L999;                                                         
         END;                                                                   
                                                                                
 /* ***************************************************************** */        
 /* BARNEDELEN                                                        */        
 /* ***************************************************************** */        
                                                                                
        IF FOS.BT_ANT > 0 &                                                     
           B01.PENSJONSTYPE1(SØKER_IND) = 'K'  THEN                             
                                                                                
 /* ***************************************************************** */        
 /* SØKER HAR AVTALEFESTET PENSJON - BARNETILLEGG KAN IKKE TILSTÅS    */        
 /* ***************************************************************** */        
                                                                                
           DO;                                                                  
  L177:                                                                         
              FEIL_VED_LABEL = '177';                                           
              FEIL_MELD_NR   = 1010;                                            
              GO TO L999;                                                       
           END;                                                                 
                                                                                
 /* ***************************************************************** */        
 /* YP MED GRAD UNDER 50. BT KAN IKKE TILSTÅS.  9705 HL               */        
 /* FEILM 428 FJERNET DES 2003  JD  (SE REMEDY 334)                             
 /* ***************************************************************** *         
        IF FOS.BT_ANT                    > 0 &                                  
           B01.PENSJONSTYPE1(SØKER_IND)  = 'Y'    &                             
           VIRK_DATO_ÅM               > 199705   &                              
           B01.UFØRPENS.UFG(SØKER_IND) < 50     THEN                            
                                                                                
                                                                                
           DO;                                                                  
  L178:                                                                         
              FEIL_VED_LABEL = '178';                                           
              FEIL_MELD_NR   =  428;                                            
              GO TO L999;                                                       
           END; */                                                              
                                                                                
      DO I = 1 TO FOS.BT_ANT;                                                   
         BARN_IND = 0;                                                          
                                                                                
         DO J = 3 TO 14 WHILE (B02.FNR(J) > 0);                                 
                                                                                
            IF FOB.FNR_BARN(I) = B01.FNR(J) THEN                                
               DO;                                                              
                  BARN_IND = J;                                                 
                  J        = 14;                                                
               END;                                                             
         END;                                                                   
                                                                                
 /* **************************************************************** */         
 /*  DERSOM BARNET ER I B01(TILKNYTTET SØKERENS FAMILIE).            */         
 /* **************************************************************** */         
                                                                                
         IF BARN_IND > 0 THEN                                                   
            DO;                                                                 
              IF VIRK_DATO_ÅM             > 199104  THEN /*2000*/               
                 DO;                                                            
                    IF FOB.FELLES_ETTER0591(I) = 'J'  THEN                      
                       B02.PENSJONSTYPE2(BARN_IND) = 'W';                       
                    ELSE                                                        
                       B02.PENSJONSTYPE2(BARN_IND) = 'V';                       
                 END;                                                           
              ELSE                                                              
                 DO;                                                            
                                                                                
                    IF FOB.FELLES_FØR0591 (I) = 'J'       &                     
                       VIRK_DATO_ÅM > 198406       THEN /*9904*/                
                                                                                
                       B02.PENSJONSTYPE2(BARN_IND) = 'R';                       
                    ELSE                                                        
                       B02.PENSJONSTYPE2(BARN_IND) = ' ';                       
                 END;                                                           
 /* **************************************************************** */         
 /* DERSOM BARNET ER TILKNYTTET EKTEFELLE.                           */         
 /* **************************************************************** */         
                                                                                
               IF B01.FNR_TILKN(BARN_IND,1) = B01.                              
                                                  FNR(EKTEF_IND) THEN           
                  DO;                                                           
                     IF B01.PENSJONSTYPE1(BARN_IND) = 'L' THEN                  
                        DO;                                                     
 L180:                                                                          
                           FEIL_VED_LABEL = '180';                              
                           FEIL_MELD_NR   = 1201;                               
                           GO TO L999;                                          
                        END;                                                    
                     ELSE                                                       
                                                                                
 /* **************************************************************** */         
 /* TILKNYTNINGSKODE = NOE ANNET.                                    */         
 /* **************************************************************** */         
                                                                                
                        DO;                                                     
 L190:                                                                          
                           FEIL_VED_LABEL = '190';                              
                           FEIL_MELD_NR   = 1208;                               
                           GO TO L999;                                          
                        END;                                                    
                  END;                                                          
               ELSE                                                             
                                                                                
 /* **************************************************************** */         
 /* BARNET ER TILKNYTTET SØKER.                                      */         
 /* **************************************************************** */         
                                                                                
                  DO;                                                           
                     IF B01.PENSJONSTYPE1(BARN_IND) ^= 'L'    THEN              
                        DO;                                                     
 L200:                                                                          
                           FEIL_VED_LABEL = '200';                              
                           FEIL_MELD_NR   = 1207;                               
                           GO TO L999;                                          
                        END;                                                    
                  END;                                                          
            END;                                                                
         ELSE                                                                   
                                                                                
 /* *************************************************************** */          
 /* BARNET ER IKKE I B01.                                           */          
 /* *************************************************************** */          
                                                                                
            DO;                                                                 
               BARN_IND        = J;                                             
               SEARCH_FNR      = FOB.FNR_BARN(I);                               
               POS_I_B01       = BARN_IND;                                      
                                                                                
               EXEC CICS LINK PROGRAM ('R0019928') COMMAREA                     
                                                     (KOM_OMR);                 
                                      /* OPPRETT_STATUS_FOR_PERSON */           
                                                                                
               IF FEIL_MELD_NR > 0 THEN                                         
                  GO TO L999;                                                   
               ELSE                                                             
                  PROGRAM_ID = 'R001UC20';                                      
                                                                                
 /* ************************************************************** */           
 /* DERSOM BARNET ER I BASEN.                                      */           
 /* ************************************************************** */           
                                                                                
               IF B01.FNR(BARN_IND) > 0 THEN                                    
                  DO;                                                           
                                                                                
 /* ************************************************************** */           
 /* BARNET HAR IKKE PESTATUS.                                      */           
 /* ************************************************************** */           
                                                                                
                     IF STATUS_TYPE = 'A' !                                     
                        STATUS_TYPE = 'C' !                                     
                        STATUS_TYPE = 'I' !     /* TILLEGGSTEST I,K */          
                        STATUS_TYPE = 'K' THEN  /* 14.2.85  HL      */          
                        DO;                                                     
                                                                                
  /* 9926 */               CALL FLYTT_B01_TIL_B02(BARN_IND);                    
  /* KS 6/6-84   */                                                             
                     IF VIRK_DATO_ÅM  > 199104  THEN /*2000*/                   
                        DO;                                                     
                           IF FOB.FELLES_ETTER0591 (I) = 'J' THEN               
                              B02.PENSJONSTYPE2(BARN_IND) = 'W';                
                           ELSE                                                 
                              B02.PENSJONSTYPE2(BARN_IND) = 'V';                
                        END;                                                    
                     ELSE                                                       
                        DO;                                                     
                           IF FOB.FELLES_FØR0591 (I) = 'J'   &                  
                             VIRK_DATO_ÅM > 198406 THEN /*9904*/                
                              B02.PENSJONSTYPE2(BARN_IND) = 'R';                
                           ELSE                                                 
                              B02.PENSJONSTYPE2(BARN_IND) = ' ';                
                        END;                                                    
                           CALL                                                 
  /* 9923 */                   OPPRETT_STATUS_FORS_BARN(BARN_IND,               
                                                       SØKER_IND);              
                                                                                
                           CALL                                                 
  /* 9924 */                   KOBLE_TO_PERSONER(SØKER_IND,BARN_IND);           
                        END;                                                    
                     ELSE                                                       
                                                                                
 /* ************************************************************** */           
 /* BARNET HAR PESTATUS.                                           */           
 /* ************************************************************** */           
                                                                                
                        DO;                                                     
                           IF STATUS_TYPE ^= 'F'                                
                                                           THEN                 
                              DO;                                               
                                                                                
 /* ************************************************************** */           
 /* BARNET ER FORSØRGET AV ANDRE.                                  */           
 /* ************************************************************** */           
                                                                                
                                 IF B01.                                        
                                    PENSJONSTYPE1(BARN_IND) = 'L' THEN          
                                    DO;                                         
  L210:                                                                         
                                       FEIL_VED_LABEL = '210';                  
                                       FEIL_MELD_NR   = 1203;                   
                                       GO TO L999;                              
                                    END;                                        
                                 ELSE                                           
                                                                                
 /* ************************************************************** */           
 /* BARNET ER TILKNYTTET ANDRE PÅ EN ANNEN MÅTE.                   */           
 /* ************************************************************** */           
                                                                                
                                    IF B01.                                     
                                       FNR_TILKN(BARN_IND,1) > 0 THEN           
                                       DO;                                      
  L220:                                                                         
                                          FEIL_VED_LABEL = '220';               
                                          FEIL_MELD_NR   = 1206;                
                                          GO TO L999;                           
                                       END;                                     
                                    ELSE                                        
                                                                                
 /* ************************************************************** */           
 /* BARNET HAR PENSJON.                                            */           
 /* ************************************************************** */           
                                                                                
                                       DO;                                      
  L240:                                                                         
                                          FEIL_VED_LABEL = '240';               
                                          FEIL_MELD_NR   = 1204;                
                                          GO TO L999;                           
                                       END;                                     
                              END;                                              
                           ELSE                                                 
                              DO;                                               
  L250:                                                                         
                                 FEIL_VED_LABEL = '250';                        
                                 FEIL_MELD_NR   = 0601;                         
                                 GO TO L999;                                    
                              END;                                              
                        END;                                                    
                  END;                                                          
               ELSE                                                             
                                                                                
 /* *********************************************************** */              
 /* BARNET ER IKKE I BASEN.                                     */              
 /* *********************************************************** */              
                                                                                
                  DO;                                                           
                     B02.FNR   (BARN_IND) = FOB.FNR_BARN(I);                    
                     B02.NAVN  (BARN_IND) = (25)' ';                            
                     B02.TKNR  (BARN_IND) = FOS.TKNR;                           
                     IF VIRK_DATO_ÅM  > 199104  THEN /*2000*/                   
                        DO;                                                     
                           IF FOB.FELLES_ETTER0591 (I) = 'J' THEN               
                              B02.PENSJONSTYPE2(BARN_IND) = 'W';                
                           ELSE                                                 
                              B02.PENSJONSTYPE2(BARN_IND) = 'V';                
                        END;                                                    
                     ELSE                                                       
                        DO;                                                     
                           IF FOB.FELLES_FØR0591 (I) = 'J'   &                  
                             VIRK_DATO_ÅM > 198406 THEN /*9904*/                
                              B02.PENSJONSTYPE2(BARN_IND) = 'R';                
                           ELSE                                                 
                              B02.PENSJONSTYPE2(BARN_IND) = ' ';                
                        END;                                                    
                                                                                
                     CALL                                                       
                          OPPRETT_STATUS_FORS_BARN(BARN_IND,                    
                                                  SØKER_IND);                   
                          /* 9923 */                                            
                                                                                
  /* 9924 */         CALL KOBLE_TO_PERSONER(SØKER_IND,BARN_IND);                
                                                                                
                  END;                                                          
            END;                                                                
      END;                                                                      
                                                                                
 /* *********************************************************** */              
 /* BARN TILKNYTTET SØKER       (FORSØRGEDE BARN)               */              
 /* *********************************************************** */              
                                                                                
      B02.ANTALL_BARN(SØKER_IND) = 0;                                           
      DO I = 1 TO 13 WHILE                                                      
                       (B02.TILKN.FNR_TILKN(SØKER_IND,I) > 0);                  
         IF B02.TILKN.TILKNYTNINGSKODE(SØKER_IND,I) = 'V'  !                    
            B02.TILKN.TILKNYTNINGSKODE(SØKER_IND,I) = 'W'  !                    
            B02.TILKN.TILKNYTNINGSKODE(SØKER_IND,I) = 'L'   THEN                
            DO;                                                                 
               B02.ANTALL_BARN(SØKER_IND) =                                     
                                   B02.ANTALL_BARN(SØKER_IND) + 1;              
               IF VIRK_DATO_ÅM  > 199104  THEN  /*2000*/                        
               DO J = 3 TO 14 UNTIL(B02.FNR(J) =                                
                                       B02.FNR_TILKN(SØKER_IND,I));             
                  IF B02.PENSJONSTYPE2(J) = 'V'      THEN                       
                     B02.TILKN.TILKNYTNINGSKODE(SØKER_IND,I) = 'V';             
                  ELSE                                                          
                     DO;                                                        
                        B02.TILKN.TILKNYTNINGSKODE(SØKER_IND,I)='W';            
                        B02.PENSJONSTYPE2(J) = 'W';                             
                     END;                                                       
               END;                                                             
            END;                                                                
      END;                                                                      
                                                                                
 /* *********************************************************** */              
 /* OPPDATERE TILKN-SEGMENTET, FT_FØR_91 :                      */              
 /* *********************************************************** */              
                                                                                
                                                                                
   IF VIRK_DATO_ÅM  > 199104  THEN               /*2000*/                       
   IF B01.FT_SKAL_BEREGNES(SØKER_IND) ^= 'J'      THEN                          
      DO;                                                                       
         IF B01.PENSJONSTYPE1(EKTEF_IND) = 'F'    THEN                          
            DO J = 1 TO 13 WHILE(B02.FNR_TILKN(SØKER_IND,J) > 0);               
               IF B01.FNR(EKTEF_IND)= B02.FNR_TILKN(SØKER_IND,J) THEN           
                  DO;                                                           
    /*FEIL 11 - 9802 HL: */                                                     
                     IF B01.SUM_YTELSE(SØKER_IND) > 0    THEN                   
                        B02.FT_FØR_91(SØKER_IND,J)   = 'F';                     
                     J=13;                                                      
                  END;                                                          
            END;                                                                
         DO I = 3 TO 14 WHILE(B01.FNR(I) > 0);                                  
            DO J = 1 TO 13 WHILE(B02.FNR_TILKN(SØKER_IND,J) > 0);               
               IF B01.FNR(I) = B02.FNR_TILKN(SØKER_IND,J)     &                 
                  B01.STATUS_KODE_HIST(I) = ' '           THEN                  
                  DO;                                                           
                     IF (B01.PENSJONSTYPE2(I)         = 'R' !                   
                         B01.FT_FØR_91(SØKER_IND,J)   = 'R')  &                 
                        B01.PENSJONSTYPE2(EKTEF_IND) = 'M'   THEN               
                        B02.FT_FØR_91(SØKER_IND,J)   = 'R';                     
                     ELSE                                                       
                     IF B01.PENSJONSTYPE1(I)         = 'L'    &                 
                        B01.STATUS_KODE_HIST(I) = ' '           THEN            
                        B02.FT_FØR_91(SØKER_IND,J)   = 'L';                     
                     J=13;                                                      
                  END;                                                          
            END;                                                                
         END;                                                                   
      END;                                                                      
 /* *********************************************************** */              
 /* OPPDATERE FORSI-SEGMENTET :                                 */              
 /* *********************************************************** */              
                                                                                
   IF VIRK_DATO_ÅM             > 199104  THEN     /*2000*/                      
     DO;                                                                        
      IF FOS.PENSJONSINNTEKT   > 0   THEN                                       
         B02.PENSJONSINNTEKT(SØKER_IND) = FOS.PENSJONSINNTEKT/100;              
      ELSE                                                                      
         B02.PENSJONSINNTEKT(SØKER_IND) = 0;                                    
                                                                                
      IF FOS.ARBEIDSINNTEKT    > 0   THEN                                       
         DO;  /* ARBEIDSINNTEKT REGISTRERT */                                   
            B02.ARBEIDSINNTEKT(SØKER_IND) = FOS.ARBEIDSINNTEKT/100;             
            IF (B02.PENSJONSTYPE1(SØKER_IND) = 'A') THEN                        
               DO;  /* ALDERSPENSJON */                                         
                  ALDER = F_ALDER((B02.FNR(SØKER_IND)),                         
                                  HJ_VIRK_DATO_ÅMD);                            
                  IF (ALDER < 7001) THEN                                        
                     DO;                                                        
                        B02.FAI(SØKER_IND) =                                    
                           B02.ARBEIDSINNTEKT(SØKER_IND);                       
                        B02.FAI_DATO_ÅMD(SØKER_IND) =                           
                           B02.VIRK_DATO_ÅMD(SØKER_IND);                        
                     END;                                                       
               END; /* ALDERSPENSJON */                                         
            ELSE                                                                
            IF (B02.PENSJONSTYPE1(SØKER_IND) = 'K') THEN                        
               DO;  /* AFP */                                                   
                  IF (B01.UTTAKSDATO_ÅMD(SØKER_IND)    > 20000799 &             
                      B02.ARBEIDSINNTEKT(SØKER_IND)    < 150      &             
                      TRANS_OPPL_OMR.VIRKNINGSDATO_ÅMD > 20020000 ) THEN        
                     DO;  /* OPPDRAG 04021 - AVVIS FAI < 15.100 */              
                        IF (B01.PENSJONSTYPE1(EKTEF_IND) = 'F' !                
                            FOS.TILL_EK                  = 'J' ) THEN           
                           DO;  /* DA NULLSTILLER VI FAI */                     
                              B02.FAI(SØKER_IND) = 0;                           
                              B02.FAI_DATO_ÅMD(SØKER_IND) =                     
                                 B02.VIRK_DATO_ÅMD(SØKER_IND);                  
                           END;                                                 
                        ELSE                                                    
                           DO;  /* VI AVVISER BARE DE UTEN ET */                
 L270:                                                                          
                              FEIL_VED_LABEL = '270';                           
                              FEIL_MELD_NR   = 627;                             
                              GO TO L999;                                       
                           END; /* VI AVVISER BARE DE UTEN ET */                
                     END; /* OPPDRAG 04021 - AVVIS FAI < 15.100 */              
                  ELSE                                                          
                     DO;                                                        
                        B02.FAI(SØKER_IND) =                                    
                           B02.ARBEIDSINNTEKT(SØKER_IND);                       
                        B02.FAI_DATO_ÅMD(SØKER_IND) =                           
                           B02.VIRK_DATO_ÅMD(SØKER_IND);                        
                     END;                                                       
               END; /* AFP */                                                   
         END; /* ARBEIDSINNTEKT REGISTRERT */                                   
      ELSE                                                                      
         B02.ARBEIDSINNTEKT(SØKER_IND) = 0;                                     
                                                                                
      IF B02.PENSJONSTYPE2(SØKER_IND) ^= 'F'          THEN                      
         DO;                                                                    
            IF FOS.ARBEIDSINNTEKT_EK > 0   THEN                                 
               B02.ARBEIDSINNTEKT_EK(SØKER_IND) =                               
                                    FOS.ARBEIDSINNTEKT_EK/100;                  
            ELSE                                                                
               B02.ARBEIDSINNTEKT_EK(SØKER_IND) = 0;                            
                                                                                
 /*9803 HL : */                                                                 
                                                                                
      IF B02.PENSJONSTYPE1(EKTEF_IND) = 'A'    !                                
         B02.PENSJONSTYPE1(EKTEF_IND) = 'K'      THEN                           
         DO;  /*2000*/                                                          
            ALDER_EK = F_ALDER((B02.FNR(EKTEF_IND)),                            
                               HJ_VIRK_DATO_ÅMD);                               
            IF ALDER_EK < 7001                 THEN                             
               DO;                                                              
               B02.FAI(EKTEF_IND) = B02.ARBEIDSINNTEKT_EK(SØKER_IND);           
                  B02.FAI_DATO_ÅMD(EKTEF_IND) =                                 
                      B02.VIRK_DATO_ÅMD(SØKER_IND);   /*2000*/                  
               END;                                                             
         END;                                                                   
  /*HIT 9803*/                                                                  
            IF FOS.PENSJONSINNTEKT_EK > 0   THEN                                
               B02.PENSJONSINNTEKT_EK(SØKER_IND) =                              
                                     FOS.PENSJONSINNTEKT_EK/100;                
            ELSE                                                                
               B02.PENSJONSINNTEKT_EK(SØKER_IND) = 0;                           
         END;                                                                   
      /* FEILMELD 1108 L280 LAGT INN 08.94   JD */                              
      IF B02.PENSJONSTYPE2(SØKER_IND) = 'F'           THEN                      
            IF FOS.ARBEIDSINNTEKT_EK > 0  !                                     
               FOS.PENSJONSINNTEKT_EK > 0             THEN                      
            DO;                                                                 
  L280:                                                                         
                                 FEIL_VED_LABEL = '280';                        
                                 FEIL_MELD_NR   = 1108;                         
                                 GO TO L999;                                    
            END;                                                                
            ELSE                                                                
               DO;                           /*9808     KONS RF*/               
                  B02.ARBEIDSINNTEKT_EK(SØKER_IND)  = 0;                        
                  B02.PENSJONSINNTEKT_EK(SØKER_IND) = 0;                        
               END;                                                             
      B02.FORSI.FT_SKAL_BEREGNES(SØKER_IND) = 'J';                              
    END;                                                                        
                                                                                
    /*OPPDATERING AV EØSINFO : */                                               
                                                                                
    W_TT_MND = FOS.TT_EØS_ANT_ÅR * 12 + FOS.TT_EØS_ANT_MND;                     
    IF W_TT_MND > 0    &                                                        
       B02.PENSJONSTYPE1(SØKER_IND) ^= 'A'         THEN                         
          DO;                                                                   
  L281:                                                                         
              FEIL_VED_LABEL = '281';                                           
              FEIL_MELD_NR   = 1821;                                            
              GO TO L999;                                                       
                                                                                
          END;                                                                  
    IF FOS.INNT_PRØVET_EØS_PENS = 'J'        THEN                               
       DO;                                                                      
          IF B02.PENSJONSTYPE1(SØKER_IND) = 'A'    THEN                         
            DO;                                                                 
               IF W_TT_MND = 0     THEN                                         
                  DO;                                                           
  L282:                                                                         
                      FEIL_VED_LABEL = '282';                                   
                      FEIL_MELD_NR   = 1822;                                    
                      GO TO L999;                                               
                                                                                
                  END;                                                          
               ELSE   /*BARE FOR AP : */                                        
                  B02.EØSINFO.TT_PRO_RATA_MND (SØKER_IND) = W_TT_MND;           
            END;                                                                
          ELSE                                                                  
          IF B02.EØSINFO.TT_PRO_RATA_MND (SØKER_IND) = 0   THEN                 
                DO;                                                             
  L283:                                                                         
                    FEIL_VED_LABEL = '283';                                     
                    FEIL_MELD_NR   = 1827;                                      
                    GO TO L999;                                                 
                                                                                
                END;                                                            
          /*BÅDE UP OG AP : */                                                  
          B02.EØSINFO.INNT_PRØVET_EØS_PENS(SØKER_IND) = 'J';                    
          IF B02.BEREGN_FOLKETRYGD(SØKER_IND)  = ' '       THEN                 
             B02.BEREGN_FOLKETRYGD(SØKER_IND)  = 'J';                           
       END;                                                                     
    ELSE                                                                        
    IF FOS.INNT_PRØVET_EØS_PENS = 'N'        THEN                               
       B02.EØSINFO.INNT_PRØVET_EØS_PENS(SØKER_IND) = 'N';                       
                                                                                
  L999:                                                                         
      EXEC CICS RETURN;                                                         
    /* -------------------------------------------------------------- */        
    %INCLUDE R001UC22;  /* AJOURFØR_B02_MED_FO_TRANS          */                
    %INCLUDE R0019905;  /* F_ALDER                            */                
    %INCLUDE R0019923;  /* OPPRETT_STATUS_FORS_BARN           */                
    %INCLUDE R0019924;  /* KOBLE_TO_PERSONER                  */                
    %INCLUDE R0019926;  /* FLYTT_B01_TIL_B02                  */                
    %INCLUDE R0019954;  /* RULL_FORVENTET                     */                
    %INCLUDE R0019995;  /* KONV_FNR11_FNR13                   */                
    /* -------------------------------------------------------------- */        
 END KON_FO;                                                                    
