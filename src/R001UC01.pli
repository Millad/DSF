 /*   SIST ENDRET PÅ PROD   2004.12.17 15.40.22 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2004.10.26 11.42.46 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2003.02.27 12.37.49 AV   JDA2970          */        
 /*   SIST ENDRET PÅ TEST   2002.11.21 13.29.30 AV   JDA2970          */        
 /*   SIST ENDRET PÅ PROD   2001.10.22 13.03.39 AV   JDA2970          */        
 /*       SIST ENDRET 09/06-98 08.40.59 AV   SPA7339                  */        
 /*       SIST ENDRET 25/05-98 14.17.43 AV   HLA7339                  */        
 /*IDENTIFIKASJON:                                                    */        
 /* **********************                                            */        
 /*  PROGRAM-IDENT : R001UC01 KONTROLL_GRUNNBLANKETT_FO  HOVEDPROG    */        
 /*  PROGRAM-SPRÅK : PLI - CICS                                       */        
 /*  PROGRAMMERER  : SATISH PATHAK                                    */        
 /*  PROGRAMMET BLE LAGET : NOV        1993                           */        
 /* ***************************************************************** */        
 /*HENSIKT:                                                           */        
 /* **********                                                        */        
 /*  PROGRAMMET KONTROLLERER GRUNNBLANKETT FORSØRGINGSTILLEGG.        */        
 /*  BLANKETTEN KONTROLLERES ISOLERT - IKKE MOT STATUS.               */        
 /*  OPERATØREN KAN BRUKE PF-TASTENE :                                */        
 /*    PF1  FØRER TIL OPPBEVARING AV TRANSEN I VENTEREGISTERET.       */        
 /*    PF2  FØRER TIL AT TRANSAKSJONEN FORKASTES -                    */        
 /*    I BEGGE TILFELLER SKRIVES DET UT MELDING PÅ SKJERM, OG         */        
 /*    PROGRAM R0010401 SØRGER FOR Å LEGGE UT NYTT BILDE FOR          */        
 /*    REGISTRERING AV A1 - TRANSKODE RN41.                           */        
 /*  NÅR INGEN FEIL MER FINNES SETTES TRANSKODE = R042, OG DET        */        
 /*  RETURNERES TIL R0010401.                                         */        
 /*                                                                   */        
 /* ***************************************************************** */        
 /*PROGRAMTILKNYTTING:                                                */        
 /* ********************                                              */        
 /*  PROGRAMMET KALLES FRA R001C401                                   */        
 /*  PROGRAMMET HAR EXEC CICS XCTL TIL :                              */        
 /*             R0010301 - VALG_AV_FUNKSJON                           */        
 /*             R0010401 - REGISTRERING                               */        
 /*  PROGRAMMET HAR EXEC CICS LINK TIL :                              */        
 /*             R0010490 - SKRIV_UT_PÅ_VENTEREG                       */        
 /* ***************************************************************** */        
 R001UC:                                                                        
   PROC(COMMAREA_PEKER) OPTIONS(MAIN);                                          
 %PAGE;                                                                         
 %INCLUDE S001UC;    /*  FO-MAPSETTET   */                                      
 %PAGE;                                                                         
 %INCLUDE P001UC01;  /*  TRANSOMR FOR FORSØRGINGSTILL.EKTEF/BARN */             
 %PAGE;                                                                         
 %INCLUDE P0019906;  /*  TRANS-OPPL-OMRÅDE (BASED)      */                      
 %PAGE;                                                                         
 %INCLUDE P0019908;  /*  KOM-OMRÅDE (BASED)             */                      
 %PAGE;                                                                         
 %INCLUDE P0019910;  /*  STYRINGS-OMRÅDE (BASED)        */                      
 %PAGE;                                                                         
 %INCLUDE P0019912;  /*  DIV-PARAM-OMRÅDE (BASED)       */                      
 %PAGE;                                                                         
 %INCLUDE DFHBMSCA;                                                             
 %PAGE;                                                                         
   DCL                                                                          
      (BMSMAPBR,COMMAREA_PEKER) PTR;                                            
   DCL                                                                          
      (ADDR,UNSPEC,CSTG,ONCODE,VERIFY,SUBSTR) BUILTIN;                          
   DCL                                                                          
      FEIL_FUNNET  BIT (1),                                                     
      ONKODE PIC'9999',                                                         
      CURSOR_POS FIXED BIN(15) INIT(-1),                                        
      ONK DEF ONKODE CHAR(4),                                                   
      FEILKODE       CHAR(4),                                                   
      DSNAVN         CHAR(8),                                                   
      ANT_FEIL_SKREVET FIXED DEC (3);                                           
 %SKIP(1);                                                                      
 %PAGE;                                                                         
      ON ERROR SNAP BEGIN       ;                                               
         ON ERROR SYSTEM        ;                                               
         ONKODE=ONCODE          ;                                               
         FEILKODE = ONK         ;                                               
         DSNAVN   = EIBDS       ;                                               
         GO TO FEILBEH          ;                                               
      END;                                                                      
                                                                                
 FEILKODE   = 'FEIL'    ;                                                       
 DSNAVN     = '        ';                                                       
                                                                                
 EXEC CICS HANDLE CONDITION ERROR(FEILBEH);                                     
                                                                                
 EXEC CICS HANDLE AID PF1(PF1) PF2(PF2);                                        
                                                                                
 KOM_OMR.PEKER_LISTE.DIV_PARAM_PEKER  = ADDR(KOM_OMR.DIV_PARAM_OMR);            
                                                                                
                                                                                
 IF FRA_CICS THEN                                                               
  DO;                                                                           
    KOM_OMR.PEKER_LISTE.TRANS_OPPL_PEKER = ADDR(KOM_OMR.TRANS_OPPL_OMR);        
    KOM_OMR.PEKER_LISTE.TRANS_PEKER      = ADDR(KOM_OMR.TRANS_OMR);             
    KOM_OMR.PEKER_LISTE.STYRINGS_PEKER   = ADDR(KOM_OMR.STYRINGS_OMR);          
    ATK_KOM_PTR           = ADDR  (KOM_OMR.ATK_COM_OMR  ) ;                     
  END;                                                                          
                                                                                
 IF HENT_FRAM_MAP  THEN                                                         
   DO;                                                                          
     EXEC CICS SEND MAP('S001UC1') MAPSET('S001UC3') MAPONLY                    
                                                   SET (BMSMAPBR);              
     ALLOCATE S001UC1O;                                                         
     S001UC1O.CICS_INFOO = DIV_PARAM_OMR.CICS_NAVN;                             
   END;                                                                         
                                                                                
 RECEIVE_MAP:                                                                   
                                                                                
                                                                                
 IF ^HENT_FRAM_MAP THEN                                                         
    DO;                                                                         
       EXEC CICS RECEIVE MAP('S001UC1')                                         
                                    MAPSET('S001UC3') SET(BMSMAPBR);            
       CALL BLANK_FO_MELDNR;                                                    
       FEIL_MELD_NR = 0;                                                        
    END;                                                                        
 IF FUNKSJONSKODEL > 0 THEN                                                     
    DO;                                                                         
       FUNKSJONSKODE = FUNKSJONSKODEI;                                          
       EXEC CICS XCTL PROGRAM('R0010301') COMMAREA(KOM_OMR);                    
    END;                                                                        
                                                                                
 IF STYRE_KODEL > 0 THEN                                                        
    DO;                                                                         
       STYREKODE = STYRE_KODEI;                                                 
       TRANSKODE = 'R041';                                                      
       EXEC CICS XCTL PROGRAM('R0010401') COMMAREA(KOM_OMR);                    
    END;                                                                        
                                                                                
                                                                                
 IF STYREKODE ^= 'FO'  &  FRA_CICS THEN                                         
    TRANSKODE = 'R041';                                                         
                                                                                
 ELSE                                                                           
  DO;                                                                           
   TRANS_RETURKODE = TRANSKODE;                                                 
   ANT_FEIL_SKREVET = 0;                                                        
   FEIL_FUNNET = '0'B;                                                          
                                                                                
                                                                                
   SELECT (TRANSKODE);                                                          
    WHEN('RUC0')                                                                
       DO;                                                                      
          CALL BLANK_FO_SØKER;                           /*UC02*/               
          CALL BLANK_FO_SØKER2;                          /*UC02*/               
          CALL BLANK_FO_MELDNR;                          /*UC02*/               
          CALL BLANK_FO_BARN;                           /*UC02*/                
          CALL OVERFØR_FO_SØKER;                                                
          CALL KONTROLL_FO_SØKER(FEIL_FUNNET);          /*UC03*/                
       END;                                                                     
    WHEN('RUC1')                                                                
       DO;                                                                      
         CALL BLANK_FO_MELDNR;                           /*UC02*/               
         IF FUNKSJONSKODE ^= 'V' THEN                                           
            CALL BLANK_FO_BARN;                          /*UC02*/               
         CALL OVERFØR_FO_SØKER;                         /*UC07*/                
         CALL KONTROLL_FO_SØKER(FEIL_FUNNET);                                   
       END;                                                                     
    WHEN('RUC2')                                                                
       DO;                                                                      
          CALL BLANK_FO_MELDNR;                                                 
          CALL OVERFØR_FO_SØKER;                                                
          CALL OVERFØR_FO_SØKER2;                     /*UC09*/                  
          CALL BLANK_FO_BARN_REST;                                              
          CALL OVERFØR_FO_BARN;                       /*UC10*/                  
          CALL KONTROLL_FO_SØKER(FEIL_FUNNET);                                  
          IF F_FELT_PIC_CHAR2(FOS.BT_ANT) = '00' THEN                           
             CALL BLANK_FO_BARN;                                                
          ELSE                                                                  
             CALL KONTROLL_FO_BARN(FEIL_FUNNET);      /*UC04*/                  
                                                                                
                                                                                
          IF FOS.TILL_EK = 'J'                      THEN                        
          CALL KONTROLL_ARBEIDSINNTEKT (FOS.VIRK_DATO_ÅMD);/*2000*/             
                                                                                
       END;                                                                     
    WHEN('RUC3')                                                                
       DO;                                                                      
          CALL BLANK_FO_MELDNR;                                                 
          CALL OVERFØR_FO_SØKER;                                                
          CALL OVERFØR_FO_SØKER2;                                               
          CALL KONTROLL_FO_SØKER(FEIL_FUNNET);                                  
                                                                                
          IF FOS.TILL_EK = 'J'                      THEN                        
          CALL KONTROLL_ARBEIDSINNTEKT (FOS.VIRK_DATO_ÅMD);/*2000*/             
       END;                                                                     
    OTHERWISE;                                                                  
       /* INGEN UTGANG DA CICS IKKE GODTAR UGYLDIGE TRANSAKSJONSKODER */        
   END;  /*   SELECT   */                                                       
                                                                                
    IF F_FELT_PIC_CHAR2(FOS.BT_ANT) ^= '00' & ^FEIL_FUNNET THEN                 
       TRANSKODE = 'RUC2';                                                      
    IF F_FELT_PIC_CHAR2(FOS.BT_ANT) = '00' & ^FEIL_FUNNET THEN                  
       TRANSKODE = 'RUC3';                                                      
    IF TRANSKODE = 'RUC2' & F_FELT_PIC_CHAR2(FOS.BT_ANT) = '00' THEN            
       TRANSKODE = 'RUC1';                                                      
                                                                                
                                                                                
 IF FEIL_FUNNET ! HENT_FRAM_MAP ! FRA_UTEN_DIALOG !                             
    TRANS_RETURKODE ^= TRANSKODE THEN                                           
    DO;                                                                         
       IF TRANSKODE = 'RUC0' THEN                                               
          TRANSKODE = 'RUC1';                                                   
       CALL BLANK_FO_MAP;                                                       
       CALL OVERFØR_FO_SØKER_MAP;                                               
                                                                                
       SELECT (TRANSKODE);                                                      
          WHEN ('RUC1')                                                         
             DO;                                                                
                CALL FJERN_TEKST_BARN;                                          
                CALL FJERN_TEKST_SØKER2;                                        
                CALL BLOKKERE_BARN;                                             
             END;                                                               
          WHEN ('RUC2')                                                         
             DO;                                                                
                CALL TEKST_BARN;                                                
                CALL BLANK_FO_BARN_REST;                                        
                CALL OVERFØR_FO_BARN_MAP;                                       
                CALL TEKST_SØKER2;                                              
                CALL OVERFØR_FO_SØKER2_MAP;                                     
             END;                                                               
          WHEN ('RUC3')                                                         
             DO;                                                                
                CALL FJERN_TEKST_BARN;                                          
                CALL BLOKKERE_BARN;                                             
                CALL TEKST_SØKER2;                                              
                CALL OVERFØR_FO_SØKER2_MAP;                                     
             END;                                                               
          OTHERWISE;                                                            
       END;                                                                     
                                                                                
      IF HENT_FRAM_MAP THEN                                                     
        EXEC CICS SEND MAP('S001UC1') MAPSET('S001UC3') ERASE CURSOR;           
      ELSE                                                                      
        EXEC CICS SEND MAP('S001UC1') MAPSET('S001UC3') ERASEAUP                
                                                     DATAONLY CURSOR;           
      HENT_FRAM_MAP = '0'B;                                                     
      FEIL_MELD_NR  = 666;                                                      
                                                                                
      IF  FRA_CICS THEN                                                         
        EXEC CICS RETURN TRANSID(TRANSKODE) COMMAREA(KOM_OMR);                  
      ELSE                                                                      
        GOTO RECEIVE_MAP;                                                       
    END;                                                                        
                                                                                
   ELSE                                                                         
                                                                                
    DO;                                                                         
                                                                                
      IF TRANSKODE = 'RUC0' THEN                                                
        TRANS_RETURKODE = 'RUC1';                                               
      ELSE                                                                      
        TRANS_RETURKODE = TRANSKODE;                                            
      IF  FRA_CICS  THEN                                                        
        DO;                                                                     
          BLANKETTYPE = STYREKODE;                                              
          TRANSKODE = 'R042';                                                   
        END;                                                                    
    END;                                                                        
  END;                                                                          
                                                                                
  CALL UTGANG;                                                                  
                                                                                
  UTGANG: PROC;                                                                 
    IF  FRA_CICS THEN                                                           
      EXEC CICS XCTL PROGRAM('R0010401') COMMAREA(KOM_OMR);                     
    ELSE                                                                        
      EXEC CICS RETURN;                                                         
  END UTGANG;                                                                   
                                                                                
  FEILBEH:                                                                      
                                                                                
     EXEC CICS HANDLE CONDITION ERROR(ABEND);                                   
     IF FEIL_MELD_NR = 0 THEN /* AB 01.08.15 */                                 
        PROGRAM_ID = 'R001UC01';                                                
     S001UC1O.CICS_INFOO = DIV_PARAM_OMR.CICS_NAVN;                             
                                                                                
     S001UC1O.MELDING2O =                                                       
              'F E I L  H A R  O P P S T Å T T ! ! !.';                         
                                                                                
     S001UC1O.MELDING3O =                                                       
              'EIBRCODE : ' !! UNSPEC(EIBRCODE);                                
                                                                                
     S001UC1O.MELDING4O =                                                       
          'EIBFN    : ' !! UNSPEC(EIBFN) !! '. FEILKODE : ' !! FEILKODE         
                                         !! '. DATASETT : ' !! DSNAVN;          
                                                                                
     S001UC1O.MELDING5O =                                                       
                   'PROGRAMNAVN :  ' !! PROGRAM_ID    !!                        
                   '.INGEN OPPDATERING ER BLITT UTFØRT.';                       
                                                                                
     S001UC1O.MELDING6O =                                                       
                   'TA UTSKRIFO!!!. TRYKK DERETTER "ENTER".';                   
                                                                                
                                                                                
     EXEC CICS SEND MAP('S001UC1')                                              
                                                 MAPSET('S001UC3');             
                                                                                
     EXEC CICS RECEIVE MAP('S001UC1')                                           
                                    MAPSET('S001UC3') SET(BMSMAPBR);            
                                                                                
                                                                                
     EXEC CICS SYNCPOINT ROLLBACK;                                              
                                                                                
                                                                                
     TERMINERINGS_IND = 'F';                                                    
                                                                                
     TRANSKODE  = 'R041';                                                       
                                                                                
     CALL UTGANG;                                                               
                                                                                
  ABEND:                                                                        
     EXEC CICS ABEND ABCODE(FEIL);                                              
                                                                                
  PF1:                                                                          
                                                                                
 /* LEGGER UNDERKJENT TRANS UT PÅ VENTEDATABASE HVIS GYLDIG     */              
 /* FNR OG VIRKNINGSDATO,ELLERS FORKASTES TRANSAKSJONEN.        */              
                                                                                
     IF ^(FOS.FNRNR = 1          !                                              
          FOS.FNRNR = 200        !                                              
          FOS.VIRK_DATO_ÅMDNR = 9 !               /*2000*/                      
          FOS.VIRK_DATO_ÅMDNR = 200) THEN         /*2000*/                      
        DO;                                                                     
                                                                                
           IF FØDSNUMMER    = 0   THEN                                          
                                                                                
 /* SKRIVER MELDING OM AT TRANSAKSJONEN ER FORKASTET.           */              
                                                                                
              DO;                                                               
                 FEIL_MELD_NR  = 0;                                             
                 S001UC1O.MELDING6O =                                           
                         'REGISTERINGEN ER IKKE AVSLUTTET MED "ENTER"'!!        
                         '.TRANSAKSJONEN ER FORKASTET.';                        
                                                                                
              END;                                                              
           ELSE                                                                 
              DO;                                                               
                                                                                
                 SEARCH_FNR  = TRANS_OPPL_OMR.FØDSNUMMER;                       
                 IF FRA_CICS THEN                                               
                    BLANKETTYPE = STYREKODE;                                    
                                                                                
                                                                                
                 IF FEIL_MELD_NR = 666 &      /*BLANKETTKONTROLLFEIL*/          
                    ^FRA_MED_DIALOG     THEN  /*IKKE BASEKONTROLLFEIL*/         
                    FUNKSJONSKODE = 'B';                                        
                                                                                
                 IF F_FELT_PIC_CHAR2(FOS.BT_ANT) ^= '00'  THEN                  
                    DO;                                                         
                       CALL BLANK_FO_MELDNR;                                    
                       CALL BLANK_FO_BARN_REST;                                 
                       CALL OVERFØR_FO_BARN_MAP;                                
                    END;                                                        
                                                                                
                 FEIL_MELD_NR  = 0;                                             
                                                                                
                 EXEC CICS LINK PROGRAM('R0010490') COMMAREA(KOM_OMR);          
                                                                                
                 IF FEIL_MELD_NR > 0 THEN                                       
                    DO;                                                         
                       DSNAVN   = 'R0010490';                                   
                       FEILKODE = DB_STATUS_KODE;                               
                       GO TO FEILBEH;                                           
                    END;                                                        
                                                                                
 /* SKRIVER MELDING OM AT TRANSAKSJONEN ER LAGT UT PÅ VENTEBASE.*/              
                                                                                
                                                                                
                 S001UC1O.MELDING6O =                                           
                         'TRANSAKSJONEN ER LAGT UT PÅ VENTEREGISTER.';          
              END;                                                              
                                                                                
                                                                                
           EXEC CICS SEND MAP('S001UC1') MAPSET('S001UC3')                      
                                                           DATAONLY;            
     /* DUMMY SEND-KALL.                              */                        
                                                                                
           EXEC CICS SEND MAP('S001UC1')                                        
                                                  MAPSET('S001UC3');            
           EXEC CICS DELAY INTERVAL(005);                                       
                                                                                
           TERMINERINGS_IND = 'F';                                              
                                                                                
           TRANSKODE  = 'R041';                                                 
                                                                                
           CALL UTGANG;                                                         
                                                                                
        END;                                                                    
     ELSE                                                                       
        DO;                                                                     
           TERMINERINGS_IND = 'F';                                              
                                                                                
           TRANSKODE = 'R041';                                                  
                                                                                
           S001UC1O.MELDING6O =                                                 
                                                                                
   'NB** TRANSAKSJONEN ER FORKASTET P.G.A. UGYLDIG FNR EL. VDATO **NB';         
                                                                                
           EXEC CICS SEND MAP('S001UC1') MAPSET('S001UC3')                      
                                                           DATAONLY;            
     /* DUMMY SEND-KALL.                            */                          
                                                                                
           EXEC CICS SEND MAP('S001UC1')                                        
                                    MAPSET('S001UC3');                          
                                                                                
           EXEC CICS DELAY INTERVAL(005);                                       
           CALL UTGANG;                                                         
        END;                                                                    
                                                                                
                                                                                
  PF2:                                                                          
     TERMINERINGS_IND   = 'F';                                                  
                                                                                
     S001UC1O.MELDING6O =                                                       
      'NBNBNBNBNB***    TRANSAKSJONEN ER FORKASTET.    ***NBNBNBNBNB';          
                                                                                
     EXEC CICS SEND MAP('S001UC1') MAPSET('S001UC3')                            
                                                      DATAONLY ;                
     /* DUMMY SEND-KALL.                                     */                 
                                                                                
     EXEC CICS SEND MAP('S001UC1')                                              
                                    MAPSET('S001UC3');                          
     EXEC CICS DELAY INTERVAL(005);                                             
                                                                                
                                                                                
     TRANSKODE = 'R041';                                                        
                                                                                
     CALL UTGANG;                                                               
 %PAGE;                                                                         
      %INCLUDE R001UC02;               /* BLANK  ARBEIDSOMRÅDE */               
      %INCLUDE R001UC03;               /* KONTROLL AV SØKERDELEN */             
      %INCLUDE R001UC04;               /* KONTROLL AV BARNEDATA */              
      %INCLUDE R001UC05;               /* KONTROLL AV EK_INNTEKT*/              
      %INCLUDE R001UC06;               /* ARBEIDSOMRÅDE -> MAP */               
      %INCLUDE R001UC07;               /* FORTEKSTER OG ATTRIBUTTER*/           
                                       /* SØKER-DEL2       */                   
      %INCLUDE R001UC09;               /* MAP -> ARBEIDSOMRÅDE */               
      %INCLUDE R001UC10;               /* FLYTTING AV BARNEDATA */              
 %PAGE;                                                                         
      %INCLUDE R0019901;               /*  TESTER OM DATO ER GYLDIG */          
      %INCLUDE R0019902;               /*  FINNE KJØNN     */                   
      %INCLUDE R0019904;               /* FØDSELSNUMMERKONTROLL */              
      %INCLUDE R0019905;               /* F_ALDER               */              
      %INCLUDE R0019910;               /* NUMERISK KONTROLL */                  
      %INCLUDE R0019912;  /* KONVERTERING CHAR ==> PIC / PIC ==> CHAR */        
      %INCLUDE R0019944;               /* SKRIV_FEIL      */                    
      %INCLUDE R0019959;               /* DAGENS DATO + 2 MDR */                
   /* %INCLUDE R0019964;                  DAGENS DATO + 6 MDR */                
      %INCLUDE R0019956;          /*     P9956_BER_G_CICS    */                 
      %INCLUDE R0019983;               /* KONV_ÅMD_HÅMD   */                    
      %INCLUDE R0019984;               /* KONV_MÅ_HÅMD   */                     
      %INCLUDE R0019988;               /* KONV_HÅMD_ÅMD  */                     
      %INCLUDE R0019989;               /* KONV_HÅMD_MÅ   */                     
      %INCLUDE R0019995;               /* KONV_FNR11_FNR13(FNR)*/               
      %INCLUDE R0019945;               /* ATK_TILLGANG_KONTROLL   */            
      %INCLUDE R0019999;               /* KONTROLL_ACF2           */            
   END R001UC;                                                                  
